SUBTTL MULTIPLE PG21-180
BEGIN MPG21

;THE TRANSFER VECTOR FOR THE AUGUAT 8136 PG21-180 BOARD

^LMP21V:
	JRST LCINIT		;INITIALIZE LETTER/NUMBER CONVERSION TABLES
	JRST LOCOUT
MDPC,<
	JRST CSLTLP
	JRST MSLTLN		;SPECIAL ROUTINE FOR PANEL NUMBER INITIALLY
MD,<
	JRST GTCONP
	JRST CSLTLD
>;MD
>;MDPC
MWL,<
	JRST WLOCOUT
	JRST LOCPNC
	JRST LOCPNW
	JRST PRCNPN
	JRST GETLOC
	JRST PADPRT
	JRST RAYDIP
	JRST RAYCON
>;MWL
	[ASCIZ/#LL#/]
MDPC,<	[ASCIZ/#LJ#-#/]
	[ASCIZ/#L#/]
>;MDPC
MWL,<	[ASCIZ/#L#/]
	[ASCIZ/#LJ#/]
>;MWL
CHECK LMP21V,LTVLEN

L2NSUB:	BLOCK L2NLEN
N2LSUB:	REPEAT N2LLEN, < "?"
>
EN2L__.

NNN__1
FOR I IN(A,B,C,D,E,F)
<	L2N2L I,0
>
FOR I IN (G,I,O,Q)
<	L2N2L I,1B0
>

N2LMAX__NNN-1

ORG EN2L

MXPNL__5	;MAXIMUM PANELS WRAPPABLE SIDE BY SIDE
NGRPS__6

COMMENT 

AUGAT-8136-PG21 CONNECTOR PIN FORMAT PRINTS AS #LJ#(#)
WHERE L IS THE GROUP.  THE J IS LITERAL.  THE FIRST DIGIT IS THE PANEL
AND THE SECOND IS THE JACK.  PIN IS LAST

______|_____|_____|_____|_____|_____|
|        2223                      35
|     5   |1|          12           |
|_____ ___|_|_______________________|
     |     |		|
<    |     |		|------------>PIN
     |     |
<    |     |------------------------->JACK 0=>1, 1=>2
     |
<    |-------------------------------># <PANEL-1>*NGRPS+GROUP



LCINIT:	MOVE T,[L2NSUB,,L2N]
	BLT T,L2N+L2NLEN+N2LLEN-1
	MOVEI T,N2LMAX
	MOVEM T,MAXN2L
	POPJ P,

LOCOUT:	TRNN A,7777
	JRST CNLOC
	LDB TTT,[POINT 6,A,23]
	JUMPE TTT,CPOPJ			;JUMP IF ILLEGAL
	LDB T,[POINT 3,A,20]		;PANEL NUMBER
	PUSHJ P,PUTDEC			;PRINT IT OUT
	LDB T,[POINT 3,A,23]		;GROUP LETTER
	PUTBYT @N2L(T)			;CONVERT TO LETTER AND PRINT
	MOVEI T,2			;SETUP FOR 2 DIGIT NUMBER PRINT
	MOVEM T,NDIG
	LDB T,[POINT 12,A,35]	;SLOT NUMBER WITHIN GROUP
	JRST NPUTDEC			;PRINT IT OUT

CNLOC:	LDB T,[POINT 5,A,22]
	JUMPE T,CPOPJ
	PUSH P,T+1
	SOS T
	IDIVI T,NGRPS
	AOS T+1
	EXCH T+1,(P)
	AOS T
	PUSHJ P,PUTDEC		;PANEL NUMBER
	POP P,T
	PUTBYT @N2L(T)		;GROUP LETTER
	PUTBYT "J"
	LDB T,[POINT 1,A,23]
	AOS T
	JRST PUTDEC

MSLTLN:	SETZM DESTIN
	GETNUM				;GET PANEL NUMBER
	CAILE NUMREG,MXPNL			;MAX PANELS
	POPJ P,
	DPB NUMREG,[POINT 3,DESTIN,20]	;STORE AS PANEL NUMBER
	CAIN CHRREG,12			;END OF LINE?
	JUMPE NUMREG,CPOPJ1		;YES, AND NULL NUMBER, JUST RETURN
	JUMPE NUMREG,CPOPJ		;OTHERWISE ZERO IS IN ERROR
	PUSHJ P,CHKLET			;LETTER TYPED NEXT?
	POPJ P,				;NO, ERROR
	SKIPG D,L2N-"A"(CHRREG)		;GET NUMBER CORRESPONDING TO LETTER
	POPJ P,				;NON-EX OR GIOQ!!
	DPB D,[POINT 3,DESTIN,23]	;DEPOSIT AS GROUP LETTER
	GETNUM				;GET NEXT NUMBER
	JUMPE NUMREG,CPOPJ		;ZERO, ERROR
	CAILE NUMREG,=30		;EXCEEDS MAX SLOT NUMBER WITHIN GROUP?
	POPJ P,				;YES
	DPB NUMREG,[POINT 12,DESTIN,35]	;DEPOSIT AS SLOT NUMBER
	JRST CPOPJ2			;RETURN HAPPINESS


MDPC,<
CSLTLP:
MD,<
	PUSHJ P,CSLTOUT
CSLTLD:	HLRZS A
	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
>;MD
	PUTBYT "-"
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 12,A,35]
	JRST NPUTDEC
MD,<
GTCONP:	SETZM DESTIN
	GETNUM
	CAIN CHRREG,12
	JUMPE NUMREG,CPOPJ1		;CARRIAGE RETURN ONLY
	JUMPE NUMREG,CPOPJ		;ERROR IF NO NUMBER
	CAILE NUMREG,MXPNL		;MAX PANELS
	POPJ P,				;ERROR OTHERWISE
	SOS NUMREG
	IMULI NUMREG,NGRPS
	MOVEM NUMREG,DESTIN
	PUSHJ P,CHKLET			;CHECK FOR LETTER
	POPJ P,				;BAD LETTER, ERROR
	SKIPN CHRREG,L2N-"A"(CHRREG)
	POPJ P,
	ADD CHRREG,DESTIN
	SETZM DESTIN
	DPB CHRREG,[POINT 5,DESTIN,22]
	GETNUM
	JUMPN NUMREG,CPOPJ		;BETTER NOT BE A NUMBER HERE
	CAIE CHRREG,"J"			;AND LETTER BETTER BE J
	POPJ P,
	GETNUM
	JUMPE NUMREG,CPOPJ		;BETTER BE A NUMBER HERE
	CAIN CHRREG,"-"			;TERMINATED BY A -
	CAILE NUMREG,2			;AND LESS THAN OR EQUAL TO 2
	POPJ P,
	SOS NUMREG
	DPB NUMREG,[POINT 1,DESTIN,23]	;TEMP STORAGE FOR CONNECTOR NUMBER
	GETNUM
	JUMPE NUMREG,CPOPJ		;BETTER BE A NON ZERO NUMBER
	CAILE NUMREG,=26		;LESS THAN 27
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
	CAIE CHRREG,12			;TERMINATION HAD BETTER BE HERE AND EOL
	POPJ P,
	JRST CPOPJ2			;ALL'S WELL THAT ENDS WELL	
>;MD
>;MDPC

MWL,<
WLOCOUT:JRST LOCOUT

GETLOC:	GETNUM
	JUMPE NUMREG,CPOPJ		;NON ZERO NUMBER
	CAILE NUMREG,MXPNL			;PANEL # MAX OF FOUR
	POPJ P,
	LSH NUMREG,=15			;INTO LEFT 3 BITS OF RIGHT HALF
	HRLM NUMREG,(P)			;INTO LH OF PDL
	PUSHJ P,CHKLET
	POPJ P,				;NOT A LETTER
	SKIPN CHRREG,L2N-"A"(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 3,(P),5]	;DEPOSIT GROUP LETTER INTO LH OF PDL
	GETNUM
	JUMPN NUMREG,[	CAILE NUMREG,=30	;HERE FOR DIP LOC
			POPJ P,			;ONLY 30 DIP SLOTS PER GROUP
			HLRZ TT,(P)
			TRO TT,(NUMREG)
			JRST CPOPJ2]
	LDB NUMREG,[POINT 3,(P),2]
	SOS NUMREG
	IMULI NUMREG,NGRPS
	LDB CHRREG,[POINT 3,(P),5]
	ADD NUMREG,CHRREG
	DPB NUMREG,[POINT 5,(P),4]
	PUSHJ P,CHKLET			;HERE TO DO CONNECTOR PIN
	POPJ P,
	CAIE CHRREG,"J"
	POPJ P,
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAIN CHRREG,"-"
	CAILE NUMREG,2			;CHECK FOR LEGAL JACK #
	POPJ P,
	SOS NUMREG
	DPB NUMREG,[POINT 1,(P),5]	;DEPOSIT CONNECTOR NUMBER INTO RH OF LH OF PDL
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,=26		;ONLY 26 PINS PER JACK
	POPJ P,
	HLRZ TT,(P)
	TRZ TT,7777
	TRO TT,(NUMREG)
	JRST CPOPJ1	

LOCPNW:
LOCPNC:	PUSHJ P,LOCOUT
	HLRZ A,A
	PUTBYT "-"
PRCNPN:	LDB T,[POINT 12,A,35]
RAYDP1:	MOVEI TTT,2
	MOVEM TTT,NDIG
	JRST NPUTDEC

RAYCON:	PUSHJ P,LOCOUT
	PUTSTR [ASCIZ /   /]		;3 SPACES
	HLRZS A
	JRST PRCNPN

PADPRT:	MOVEI T,(G)
	LSH T,-1
	IDIVI T,NGRPS		;T PANEL #-1, TT GROUP LETTER-1
	ADDI T,1
	PUSH P,TT
	PUSHJ P,PUTDEC
	POP P,T
	PUTBYT @N2L+1(T)
	PUTBYT "J"
	MOVEI T,1
	TRNE G,1
	MOVEI T,2
	JRST PUTDEC

RAYDIP:	PUSH P,T			;HAS SOME BITS
	PUSHJ P,RAYLOC
	PUTSTR [ASCIZ /   /]		;3 SPACES
	POP P,T
	TRZE T,L1416
	ADD A,[1,,0]
	LDB T,[POINT 6,A,17]	;DIP PIN NUMBER
	JRST RAYDP1


RAYLOC:	CAIN B,=16	;16 PIN DIP?
	JRST RAY16	;YES
ITS,<
	CAIN B,=24	;24 PIN DIP?
	JRST RAY24	;YES
	CAIN B,=28
	JRST RAY28
	CAIN B,=40
	JRST RAY40
>;ITS
	CAIE B,=14	;14 PIN DIP?
	JSP TTT,RAYERR	;NO, ERROR!!
	TRNN T,L1416	;BETTER HAVE THE ADD 1 TO PIN BIT ON!
	JSP TTT,RAYERR
	JRST LOCOUT	;YES, WIN

RAY16:	TRNE T,L1416	;BETTER NOT HAVE ADD 1 TO PIN BIT ON!
	JSP TTT,RAYERR
	JRST LOCOUT	;WIN

ITS,<
RAY24:	LDB T,[POINT 12,A,35]	;SLOT NUMBER
	IDIVI T,5		;RIGHT 3 SLOTS OF GROUP ONLY!
	CAILE TT,2		;ELSE ERROR
	JSP TTT,RAYERR
	LDB T,[POINT 6,A,17]	;PIN NUMBER OF CHIP
	CAIG T,=24
	JUMPN T,RAY24A
	JSP TTT,RAYERR

RAY24A:	HLRZ T,MAP24-1(T)	;SLOT OFFSET FOR THIS PIN
	LDB TT,[POINT 12,A,35]
	ADD TT,T
	DPB TT,[POINT 12,A,35]
	HLRZ T,A		;PIN NUMBER
	MOVE T,MAP24-1(T)	;MAP THE PIN NUMBER TO NEW VALUE
	DPB T,[POINT 6,A,17]
	JRST LOCOUT		;GO PRINT LOCATION

RAY28:	LDB T,[POINT 12,A,35]	;SLOT NUMBER
	IDIVI T,5		;RIGHT 3 SLOTS OF GROUP ONLY!
	CAILE TT,2		;ELSE ERROR
	JSP TTT,RAYERR
	LDB T,[POINT 6,A,17]	;PIN NUMBER OF CHIP
	CAIG T,=28
	JUMPN T,RAY28A
	JSP TTT,RAYERR

RAY28A:	HLRZ T,MAP28-1(T)	;SLOT OFFSET FOR THIS PIN
	LDB TT,[POINT 12,A,35]
	ADD TT,T
	DPB TT,[POINT 12,A,35]
	HLRZ T,A		;PIN NUMBER
	MOVE T,MAP28-1(T)	;MAP THE PIN NUMBER TO NEW VALUE
	DPB T,[POINT 6,A,17]
	JRST LOCOUT		;GO PRINT LOCATION

RAY40:	LDB T,[POINT 12,A,35]	;SLOT NUMBER
	IDIVI T,5		;RIGHT 2 SLOTS OF GROUP ONLY!
	CAILE TT,1		;ELSE ERROR
	JSP TTT,RAYERR
	LDB T,[POINT 6,A,17]	;PIN NUMBER OF CHIP
	CAIG T,=40
	JUMPN T,RAY40A
	JSP TTT,RAYERR

RAY40A:	HLRZ T,MAP40-1(T)	;SLOT OFFSET FOR THIS PIN
	LDB TT,[POINT 12,A,35]
	ADD TT,T
	DPB TT,[POINT 12,A,35]
	HLRZ T,A		;PIN NUMBER
	MOVE T,MAP40-1(T)	;MAP THE PIN NUMBER TO NEW VALUE
	DPB T,[POINT 6,A,17]
	JRST LOCOUT		;GO PRINT LOCATION

RAYERR:	OUTSTR [ASCIZ /RAYTHEON DECK CONVERSION ERROR!
/]
	JRST ERRET

RADIX =10
DEFINE MAP(SLOT,PIN)
<	SLOT-1,,PIN
>

MAP24:	MAP(1,15)	;1
	MAP(1,14)	;2
	MAP(1,13)	;3
	MAP(1,2)	;4
	MAP(1,3)	;5
	MAP(2,15)	;6
	MAP(2,14)	;7
	MAP(2,3)	;8
	MAP(2,2)	;9
	MAP(2,4)	;10
	MAP(3,15)	;11
	MAP(3,14)	;12
	MAP(3,4)	;13
	MAP(3,11)	;14
	MAP(3,12)	;15
	MAP(2,6)	;16
	MAP(2,5)	;17
	MAP(2,12)	;18
	MAP(2,11)	;19
	MAP(2,13)	;20
	MAP(1,6)	;21
	MAP(1,5)	;22
	MAP(1,12)	;23
	MAP(1,11)	;24
CHECK MAP24,=24

MAP28:	MAP(1,15)	;1
	MAP(1,14)	;2
	MAP(1,13)	;3
	MAP(1,2)	;4
	MAP(1,3)	;5
	MAP(2,15)	;6
	MAP(2,14)	;7
	MAP(2,3)	;8
	MAP(2,2)	;9
	MAP(2,4)	;10
	MAP(3,15)	;11
	MAP(3,14)	;12
	MAP(3,3)	;13
	MAP(3,2)	;14

	MAP(3,6)	;15
	MAP(3,5)	;16
	MAP(3,4)	;17
	MAP(3,11)	;18
	MAP(3,12)	;19
	MAP(2,6)	;20
	MAP(2,5)	;21
	MAP(2,12)	;22
	MAP(2,11)	;23
	MAP(2,13)	;24
	MAP(1,6)	;25
	MAP(1,5)	;26
	MAP(1,12)	;27
	MAP(1,11)	;28
CHECK MAP28,=28

MAP40:	MAP(1,15)	;1
	MAP(1,14)	;2
	MAP(1,4)	;3
	MAP(1,2)	;4
	MAP(1,3)	;5
	MAP(2,15)	;6
	MAP(2,14)	;7
	MAP(2,4)	;8
	MAP(2,2)	;9
	MAP(2,3)	;10
	MAP(3,15)	;11
	MAP(3,14)	;12
	MAP(3,4)	;13
	MAP(3,2)	;14
	MAP(3,3)	;15
	MAP(4,15)	;16
	MAP(4,14)	;17
	MAP(4,4)	;18
	MAP(4,2)	;19
	MAP(4,3)	;20

	MAP(4,5)	;21
	MAP(4,6)	;22
	MAP(4,13)	;23
	MAP(4,12)	;24
	MAP(4,11)	;25
	MAP(3,5)	;26
	MAP(3,6)	;27
	MAP(3,13)	;28
	MAP(3,12)	;29
	MAP(3,11)	;30
	MAP(2,5)	;31
	MAP(2,6)	;32
	MAP(2,13)	;33
	MAP(2,12)	;34
	MAP(2,11)	;35
	MAP(1,5)	;36
	MAP(1,6)	;37
	MAP(1,13)	;38
	MAP(1,12)	;39
	MAP(1,11)	;40
CHECK MAP40,=40

RADIX 8
>;ITS
>;MWL
BEND MPG21

MWL,<
SUBTTL WIRE WRAP ROUTINES  --  MULTIPLE PG21'S
BEGIN MPG21

comment 

All calculations are done from the dip side.

(0,0) at lower left hand corner in left handed coordinate system.
Dip sockets are arranged in 5x6 groups.  These 30 dip groups come
in pairs.  Each group comes with a pair of Scotch Flex(R) connectors
labeled "J1" and "J2".  There can be up to 6 30 dip groups on one
board.  The horizontal spacing between groups is 2.700".

There are 5 panels, vertically arranged.  The vertical spacing
is 7.500".  

5A30(8) is at (0,0).  5AJ2-26 is at (500,6100).  5AJ1-26 is at (500,6400).

It follows that 5B30(8) is at (2700,0)


MXPNL__5		;max number of panels wrappable at once
NGRPS__6		;PG21-180 HAS 6 GROUPS
GRPCOL__5		;# COLS IN GROUP
GRPROW__6		;# COLUMNS IN GROUP
UMLCOL__GRPROW		;UML INTERCHANGES ROWS AND COLUMNS
UMLROW__GRPCOL
MAXDIP__GRPROW*GRPCOL	;# DIPS IN GROUP
PNLOFT__=7500		;7.500" VERTICAL SPACING BETWEEN PANELS
GXOFST__=2700		;2.700" GROUP HORIZONTAL SPACING
XDIPSP__ =500		; .500" DIP HORIZONTAL SPACING
YDIPSP__=1000		;1.000" DIP VERTICAL SPACING
XPINSP__ =300		; .300" DIP PIN HORIZONTAL SPACING
YPINSP__ =100		; .100" DIP PIN VERTICAL SPACING
PXOFST__ =500		; .500" X OFFSET FOR POWER PIN
PYOFST__ =800		; .800" Y OFFSET FOR POWER AND GROUND PINS
JXOFST__ =500		; .500" X OFFSET FOR JACKS
JYOFST__=6100		;6.100" Y OFFSET FOR J2
JYOFS1__ =300		; .300" Y OFFSET FROM J2 TO J1
PXPNSP__ =100		; .100"	CONNECTOR PIN VERTICAL SPACING
PYPNSP__ =100		; .100" CONNECTOR PIN HORIZONTAL SPACING

;SOME BYTE POINTERS FOR EXTRACTING FIELDS

DEFINE %DIPP<[POINT  6,A,17]>	;DIP PIN #
DEFINE %DIPPNL<[POINT 3,A,20]>	;PANEL NUMBER
DEFINE %DIPG<[POINT  3,A,23]>	;DIP GROUP
DEFINE %DIPS<[POINT 12,A,35]>	;DIP SLOT

DEFINE %CONP<[POINT 12,A,35]>	;CONNECTOR PIN # (AND JACK NUMBER <PINS 1-2*26>)
DEFINE %CNPG<[POINT 5,A,22]>	;<PANEL-1>*NGRPS+GROUP
DEFINE %CNJK<[POINT 1,A,23]>	;CONNECTOR JACK BIT

^^MP21TV__.	;TRANSFER VECTOR FOR DOUBLE HEIGHT DEC BOARDS

	-1		;ISWW
	JRST CPOPJ	;WWINIT
	JRST MAPRC
	JRST MAPPAD
	JRST PINMAP	
	JRST PINBIT	
	JRST VGCON	
	JRST DISTPP	;POINT TO POINT WIRING
	JRST FPWR
	JRST FGND
	JRST PGPRTM
	JRST PGPRNT
	JRST GNDCLR
	JRST WAGGND
	JRST GNDOUT
	JRST VCCOUT
	JRST RAYGEN
	MXPNL*UMLROW*NGRPS	;NROWS		    (USED FOR UML ONLY)
	UMLCOL		;NCOLS		    (USED FOR UML ONLY)
	0		;NCLPRG		    (USED FOR UML ONLY)
	MXPNL*MAXDIP*NGRPS	;DIPSLT_NROWS*NCOLS (USED FOR UML ONLY)
	SETPAD(MXPNL*NGRPS*2)	;NPADS		    (USED FOR UML ONLY)
	XWD -=26,1	;PADLET		    (USED FOR UML ONLY)
	XWD -1,0	;PADPIN		    (USED FOR UML ONLY)
	=10		;FRACTN
	=200*2		;WRAPMG .200" INSULATION AROUND EACH POST
	=1500		;POSTMG .750" BARE WIRE AROUND EACH POST
CHECK MP21TV,WTVLEN

;TABLES FOR MAPIT

;**************************************** DIP PINS

DEFINE XY(X,Y)
<	X*XPINSP,,Y*YPINSP
>

PINTAB:
FOR Y_7, 0, -1
<	XY(1,Y)
>
FOR Y_0, 7, 1
<	XY(0,Y)
>
;**************************************** DIP LOCATIONS

DEFINE XY(X,Y)
<	X*XDIPSP,,Y*YDIPSP
>

DIPLOC:
 FOR Y_5, 0, -1
<FOR X_4, 0, -1
<	XY(X,Y)
>>
;**************************************** SCOTCH-FLEX PINS

DEFINE XY(X,Y)
<	X*PXPNSP,,Y*PYPNSP
>

FLXPIN:
 FOR Y_1, 0, -1
<FOR X_0, =12, 1
<	XY(X,Y)
>>
MAPIT:	JUMPL A,CONMAP
	LDB TTT,%DIPPNL		;GET PANEL NUMBER
	JUMPE TTT,CPOPJ		;LOSER
	MOVEI TT,MXPNL		;MAX NUMBER OF PANELS
	CAILE TTT,MXPNL		;TOO BIG
	POPJ P,
	SUBM TT,TTT
	IMULI TTT,PNLOFT	;VERTICAL OFFSET OF PANEL	
	LDB TT,%DIPG		;GROUP LETTER
	LDB T,%DIPS		;DIP #
	JUMPE T,CPOPJ
	CAILE T,MAXDIP		;TOO MANY DIPS FOR SLOT
	POPJ P,
	MOVE T,DIPLOC-1(T)	;XY OF DIP SLOT
	IMULI TT,GXOFST		;X FUDGE FOR GROUP
	MOVSI TT,-GXOFST(TT)
	ADD T,TT		;LOCATION OF DIP ON BOARD
	ADD T,TTT		;ADD IN PANEL OFFSET
	LDB TT,%DIPP		;PIN #
	TLNE A,MAPPWR!MAPGND	;WANT POWER OR GROUND?
	JRST PINPGP		;YES, RETURN FIX IT UP
	CAIN B,=16		;16 PIN DIP?
	JRST MAPIT0		;NO, DON'T NEED TO FUTZ AROUND
ITS,<
	CAIN B,=24		;24 PIN CHIP?
	JRST MAP24P
	CAIN B,=28
	JRST MAP28P
	CAIN B,=40
	JRST MAP40P
>;ITS
	CAIE B,=14		;14 PIN CHIP?
	POPJ P,			;??? I GIVE UP
	ADDI TT,1
	ADD A,[1,,0]
	TLO A,PLUS1
MAPIT0:	ADD T,PINTAB-1(TT)	;ADD EXTRA XY FOR PIN
	SETZB TT,TTT		;FLUSH AWAY THOSE GOODIES
	JRST CPOPJ2

;POWER AND GROUND PIN FOR DIP

PINPGP:	JUMPN TT,CPOPJ		;THE PIN # SHOULD BE ZERO
	ADDI T,PYOFST		;FIX THE Y
	TLNN A,MAPPWR
	JRST [	MOVSI TT,GND	;HE WANTS GND
		JRST PINPG1]
	ADD T,[PXOFST,,0]	;HE WANTS PWR
	MOVSI TT,PWR
PINPG1:	SETZ TTT,
	JRST CPOPJ2

ITS,<
;I WOULDN'T WISH THIS ON . . .

MAP24P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,2		;BETTER BE IN A SLOT 1,2,OR 3 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 24 PIN DIP
	ADD T,M24PIN-1(TT)	;ADD X,Y OFFSET FOR 24 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

MAP28P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,2		;BETTER BE IN A SLOT 1,2,OR 3 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 28 PIN DIP
	ADD T,M28PIN-1(TT)	;ADD X,Y OFFSET FOR 28 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

MAP40P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,1		;BETTER BE IN A SLOT 1 OR 2 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 40 PIN DIP
	ADD T,M40PIN-1(TT)	;ADD X,Y OFFSET FOR 40 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

RADIX =10

DEFINE LP(SLOT,PIN)
<	-<SLOT-1>*XDIPSP,,<8-PIN>*YPINSP
>

DEFINE RP(SLOT,PIN)
<	XPINSP-<SLOT-1>*XDIPSP,,<PIN-9>*YPINSP
>

M24PIN:	RP(1,15)	;1
	RP(1,14)	;2
	RP(1,13)	;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,3)		;8
	LP(2,2)		;9
	LP(2,4)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,4)		;13
	RP(3,11)	;14
	RP(3,12)	;15
	LP(2,6)		;16
	LP(2,5)		;17
	RP(2,12)	;18
	RP(2,11)	;19
	RP(2,13)	;20
	LP(1,6)	;21
	LP(1,5)		;22
	RP(1,12)	;23
	RP(1,11)	;24
CHECK M24PIN,=24

M28PIN:	RP(1,15)	;1
	RP(1,14)	;2
	RP(1,13)	;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,3)		;8
	LP(2,2)		;9
	LP(2,4)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,3)		;13
	LP(3,2)		;14

	LP(3,6)		;15
	LP(3,5)		;16
	LP(3,4)		;17
	RP(3,11)	;18
	RP(3,12)	;19
	LP(2,6)		;20
	LP(2,5)		;21
	RP(2,12)	;22
	RP(2,11)	;23
	RP(2,13)	;24
	LP(1,6)		;25
	LP(1,5)		;26
	RP(1,12)	;27
	RP(1,11)	;28
CHECK M28PIN,=28

M40PIN:	RP(1,15)	;1
	RP(1,14)	;2
	LP(1,4)		;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,4)		;8
	LP(2,2)		;9
	LP(2,3)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,4)		;13
	LP(3,2)		;14
	LP(3,3)		;15
	RP(4,15)	;16
	RP(4,14)	;17
	LP(4,4)		;18
	LP(4,2)		;19
	LP(4,3)		;20

	LP(4,5)		;21
	LP(4,6)		;22
	RP(4,13)	;23
	RP(4,12)	;24
	RP(4,11)	;25
	LP(3,5)		;26
	LP(3,6)		;27
	RP(3,13)	;28
	RP(3,12)	;29
	RP(3,11)	;30
	LP(2,5)		;31
	LP(2,6)		;32
	RP(2,13)	;33
	RP(2,12)	;34
	RP(2,11)	;35
	LP(1,5)		;36
	LP(1,6)		;37
	RP(1,13)	;38
	RP(1,12)	;39
	RP(1,11)	;40
CHECK M40PIN,=40

RADIX 8
>;ITS
;HERE FOR CONNECTOR PINS

CONMAP:	AOS (P)
	LDB TT,%CNPG		;PANEL AND GROUP NUMBER
	JUMPE TT,CPOPJ		;ILLEGAL PANEL NO
	PUSH P,TT+1
	SOS TT
	IDIVI TT,NGRPS
	CAIL TT,MXPNL
	JRST [	POP P,TT+1
		POPJ P,]			;TOO BIG A PANEL NUMBER
	MOVEI T,MXPNL-1
	SUBM T,TT		;0 FOR PANEL <MXPNL>
	IMULI TT,PNLOFT		;OFFSET BETWEEN PANELS
	MOVE T,TT+1
	POP P,TT+1
	IMULI T,GXOFST
	HRLZ T,T	;XY OF GROUP
	ADD T,[JXOFST,,JYOFST]	;LOC OF J2
	ADD T,TT		;ADD IN PANEL OFFSET
	LDB TT,%CNJK
	SKIPG TT
	ADDI T,JYOFS1		;ADD OFFSET FOR JACK 1
	LDB TT,%CONP		;PIN #
	JUMPE TT,CPOPJ
	CAILE TT,=26		;REASONABLE # OF PINS
	POPJ P,
	ADD T,FLXPIN-1(TT)
	SETZB TT,TTT
	JRST CPOPJ1

;THE ROUTINES WHICH CALL MAPIT

PINBIT:	PUSHJ P,MAPIT
	JFCL			;DIP ERROR
	SETZ TT,		;CONNECTOR ERROR
	POPJ P,			;HAPPINESS

PINMAP:	PUSHJ P,MAPIT
	JRST PINERR
	JRST CONERR
	JRST CPOPJ1

CONERR:	LDB T,%CNPG		;PANEL NO.
	PUSH P,T+1
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS T+1
	EXCH T+1,(P)
	PUSHJ P,NUMPNT
	POP P,T		;CONNECTOR GROUP
	AOS T
	OUTCHR N2L(T)
	OUTCHR ["J"]
	PUSH P,TT
	LDB TT,%CNJK
	AOS TT
	PUSHJ P,NUMPNT
	OUTCHR ["-"]
	LDB T,%CONP
	PUSHJ P,NUMPNT		;PIN
CONER1:	OUTSTR [ASCIZ / MAPPING ERROR
/]
	POP P,TT
	POPJ P,
		
PINERR:	LDB T,%DIPPNL		;PANEL NO.
	PUSHJ P,NUMPNT
	LDB T,%DIPG		;FORM A#(#)
	OUTCHR N2L(T)
	LDB T,%DIPS		;SLOT #
	PUSHJ P,NUMPNT
	OUTCHR["("]
	LDB T,%DIPP		;PIN #
	PUSHJ P,NUMPNT
	OUTCHR [")"]
	JRST CONER1

MAPRC:	HLRZ TT,T		;(1,1) IS DIP IN UPPER LEFT HAND CORNER
	SOJL TT,CPOPJ
	CAML TT,NROWS
	POPJ P,
	HRRZS T			;ROW IN TT, COL IN T
	SOJL T,CPOPJ
	CAML T,NCOLS		;(0,0) IS NOW IN UPPER LEFT
	POPJ P,
	IDIVI TT,NGRPS*GRPCOL	;FUDGE RIGHT THING INTO TT FOR MPG21
	ADDI TT,1
	LSH TT,3
	PUSH P,TT
	MOVE TT,TTT
	IDIVI TT,GRPCOL
	ADDI TT,1
	IOR TT,(P)
	LSH TT,14
	POP P,(P)
	SUBI TTT,UMLROW-1	;TRANSLATE
	MOVNS TTT		;REFLECT
	IMULI T,UMLROW
	ADDI T,1(TTT)
	IOR T,TT		;OR IN GROUP
	JRST CPOPJ1

MAPPAD:	ADDI T,1		;T HAS CONNECTOR #, STARTING AT 1 FOR FIRST
	LSH T,14
	ADDI T,(TT)		;TT HAS CONNECTOR PIN, STARTING AT 1
	POPJ P,


FPWR:	TLZ A,PLUS1!PLUS2!77
	SKIPGE A
	PUSHJ P,FPGC
	TLO A,MAPPWR
	POPJ P,

FGND:	TLZ A,PLUS1!PLUS2!77
	SKIPGE A
	PUSHJ P,FPGC
	TLO A,MAPGND
	POPJ P,

FPGC:	LDB T,[POINT 5,A,22]
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS TT
	DPB T,[POINT 3,A,20]
	DPB TT,[POINT 3,A,23]
	LDB T,[POINT 12,A,35]
	SOS T
	IDIVI T,=13
	MOVE T,[	REPEAT 4,<2>
			REPEAT 5,<3>
			REPEAT 4,<4>](TT)
	DPB T,[POINT 12,A,35]
	HRRZ A,A
	POPJ P,


PGPRTM:	SETOM PMINUS#
	CAIA
PGPRNT:	SETZM PMINUS
	JUMPL A,PGCPNT		;JUMP FOR CONNECTOR
	LDB T,%DIPPNL		;PANEL NO.
	PUSHJ P,DECOUT
	LDB T,%DIPG		;LETTER PART OF SLOT LOCATION
	PUTBYT @N2L(T)
	MOVEI T,2		;TWO DIGITS FOR SLOT #
	MOVEM T,NDIG
	LDB T,%DIPS
	PUSHJ P,NDECOUT		;SLOT #
	PUTBYT "("
	TLNN A,MAPPWR!MAPGND
	JRST NPGPNT
	TLNN A,MAPPWR
	SKIPA T,["G"]
	MOVEI T,"V"
	PUTBYT @T
PGPN1:	PUTBYT ")"
	POPJ P,

NPGPNT:	LDB T,%DIPP		;PIN #
	SKIPN PMINUS
	JRST NPGPN1
	TLNE A,PLUS1
	JRST [	PUSHJ P,DECOUT
		PUTBYT "-"
		JRST PGPN1]
NPGPN1:	TLNE A,PLUS1
	SUBI T,1		;SUBTRACT 1 FOR 14 PIN DIP #
	PUSHJ P,DECOUT
	TLNE A,PLUS1
	PUTBYT "+"
	TLNE A,PLUS2
	PUTSTR [ASCIZ /+2??????????/]
	JRST PGPN1

;PRINTS CONNECTOR PIN

PGCPNT:	LDB T,%CNPG	;PANEL NO.AND GROUP
	PUSH P,T+1
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS T+1
	EXCH T+1,(P)
	PUSHJ P,DECOUT
	POP P,T
	AOS T
	PUTBYT @N2L(T)
	PUTBYT "J"
	LDB T,%CNJK
	PUSHJ P,DECOUT
	PUTBYT "-"
	LDB T,%CONP
	JRST DECOUT

RAYGEN:	JUMPL A,RAYRP1
	TLNN A,MAPPWR!MAPGND	;ONLY DO GENERATED POWER AND GROUNDS HERE
	JRST RAYRP3		;PALM OFF THE REST ON RAYDIP!
	LDB T,%DIPPNL
	PUSHJ P,DECOUT
	LDB T,%DIPG
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,%DIPS		;SLOT #
	PUSHJ P,NDECOUT
	PUTSTR [ASCIZ /  /]	;2 SPACES
	MOVEI T,2		;ASSUME NOT POWER OR GROUND
	MOVEM T,NDIG
	LDB T,%DIPP
	PUTBYT "0"
	MOVEI T,"V"
	TLNN A,MAPPWR
	MOVEI T,"G"
	PUTBYT (T)
	POPJ P,

RAYRP3:	SETZM T		;CLEAR BODY BITS (14 TO 16 PIN CONVERSION ALREADY DONE
					;IN PNAMEP (WLROUT) CROCK!!!)
	JRST RAYDIP

RAYRP1:	OUTSTR [ASCIZ /CONNECTOR BECAME GENERATED PIN
/]
	JRST ERRET

VGCON:	POPJ P,

FOR NAME (GNDCLR,WAGGND,GNDOUT,VCCOUT)
<NAME:	OUTSTR [ASCIZ /NAME NOT IMPLEMENTED FOR THE MPG21 BOARD
/]
	JRST ERRET
>
BEND MPG21
>;MWL
    