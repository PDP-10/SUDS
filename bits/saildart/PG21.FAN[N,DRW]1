00100	SUBTTL PG21, PG21-60, PG21-180
00200	BEGIN PG21
00300	
00400	;THE TRANSFER VECTOR FOR THE AUGUAT 8136 PG21-180 BOARD
00500	
00600	^LP21TV:
00700		JRST LCINIT		;INITIALIZE LETTER/NUMBER CONVERSION TABLES
00800		JRST LOCOUT
00900	MDPC,<
01000		JRST CSLTLP
01100		JRST SLTLN		;USE THE CANONICAL ONE
01200	MD,<
01300		JRST GTCONP
01400		JRST CSLTLD
01500	>;MD
01600	>;MDPC
01700	MWL,<
01800		JRST WLOCOUT
01900		JRST LOCPNC
02000		JRST LOCPNW
02100		JRST PRCNPN
02200		JRST GETLOC
02300		JRST PADPRT
02400		JRST RAYDIP
02500		JRST RAYCON
02600	>;MWL
02700		[ASCIZ/#LL#/]
02800	MDPC,<	[ASCIZ/LJ#-#/]
02900		[ASCIZ/L#/]
03000	>;MDPC
03100	MWL,<	[ASCIZ/L#/]
03200		[ASCIZ/LJ#/]
03300	>;MWL
03400	CHECK LP21TV,LTVLEN
03500	
03600	L2NSUB:	BLOCK L2NLEN
03700	N2LSUB:	REPEAT N2LLEN, < "?"
03800	>
03900	EN2L__.
04000	
04100	NNN__1
04200	FOR I IN(A,B,C,D,E,F)
04300	<	L2N2L I,0
04400	>
04500	FOR I IN (G,I,O,Q)
04600	<	L2N2L I,1B0
04700	>
04800	
04900	N2LMAX__NNN-1
05000	
05100	ORG EN2L
     	00100	
00200	COMMENT 
00300	
00400	AUGAT-8136-PG21 CONNECTOR PIN FORMAT PRINTS AS LJ#(#)
00500	WHERE L IS THE GROUP.  THE J IS LITERAL.  THE FIRST DIGIT IS THE JACK
00600	AND THE SECOND IS THE PIN
00700	
00800	______|_____|_____|_____|_____|_____|
00900	|        2223                      35
01000	|    5    |1|          12           |
01100	|_________|_|_______________________|
01200	     |     |		|
01300	<    |     |		|------------># PIN
01400	     |     |
01500	<    |     |------------------------->J# JACK
01600	     |
01700	<    |------------------------------->L GROUP
01800	
01900	
02000	
02100	LCINIT:	MOVE T,[L2NSUB,,L2N]
02200		BLT T,L2N+L2NLEN+N2LLEN-1
02300		MOVEI T,N2LMAX
02400		MOVEM T,MAXN2L
02500		POPJ P,
02600	
02700	LOCOUT:	LDB TTT,[POINT 6,A,23]
02800		JUMPE TTT,CPOPJ			;JUMP IF ILLEGAL
02900		MOVEI T,2
03000		TRNE A,7777			;CONNECTOR?
03100		JRST [	MOVEM T,NDIG		;    NO, PRINT DIP LOC
03200			PUTBYT @N2L(TTT)
03300			LDB T,[POINT 12,A,35]
03400			JRST NPUTDEC]
03500		LSH TTT,-1			;YES, PRINT JACK
03600		PUTBYT @N2L(TTT)		;GROUP
03700		PUTBYT "J"
03800		TRNN A,10000			;0 IS JACK 1, 1 IS JACK 2
03900		SUBI T,1
04000		JRST PUTDEC			;JACK #
04100	
04200	MDPC,<
04300	CSLTLP:
04400	MD,<
04500		PUSHJ P,CSLTOUT
04600	CSLTLD:	HLRZS A
04700		PUSH P,A
04800		TRZ A,7777
04900		PUSHJ P,LOCOUT
05000		POP P,A
05100		PUTBYT "-"
05200	>;MD
05300		MOVEI T,2
05400		MOVEM T,NDIG
05500		LDB T,[POINT 12,A,35]
05600		JRST NPUTDEC
05700	MD,<
05800	GTCONP:	SETZM DESTIN
05900		GETNUM
06000		CAIN CHRREG,12
06100		JUMPE NUMREG,CPOPJ1		;CARRIAGE RETURN ONLY
06200		JUMPN NUMREG,CPOPJ
06300		PUSHJ P,CHKLET			;CHECK FOR LETTER
06400		POPJ P,
06500		SKIPN CHRREG,L2N-"A"(CHRREG)
06600		POPJ P,
06700		DPB CHRREG,[POINT 5,DESTIN,22]
06800		GETNUM
06900		JUMPN NUMREG,CPOPJ
07000		CAIE CHRREG,"J"
07100		POPJ P,
07200		GETNUM
07300		JUMPE NUMREG,CPOPJ
07400		CAIN CHRREG,"-"
07500		CAILE NUMREG,2
07600		POPJ P,
07700		SUBI NUMREG,1
07800		DPB NUMREG,[POINT 1,DESTIN,23]
07900		GETNUM
08000		JUMPE NUMREG,CPOPJ
08100		CAILE NUMREG,=26
08200		POPJ P,
08300		DPB NUMREG,[POINT 12,DESTIN,35]
08400		CAIE CHRREG,12
08500		POPJ P,
08600		JRST CPOPJ2			;ALL'S WELL THAT ENDS WELL	
08700	>;MD
08800	>;MDPC
     	00100	
00200	MWL,<
00300	WLOCOUT:JRST LOCOUT
00400	
00500	GETLOC:	GETCH
00600		PUSHJ P,CHKLET
00700		POPJ P,				;NOT A LETTER
00800		SKIPN CHRREG,L2N-"A"(CHRREG)
00900		POPJ P,
01000		HRLM CHRREG,(P)
01100		GETNUM
01200		JUMPN NUMREG,[	CAILE NUMREG,=30	;HERE FOR DIP LOC
01300				POPJ P,			;ONLY 30 DIP SLOTS PER GROUP
01400				HLRZ TT,(P)
01500				LSH TT,14
01600				TRO TT,(NUMREG)
01700				JRST CPOPJ2]
01800		PUSHJ P,CHKLET			;HERE TO DO CONNECTOR PIN
01900		POPJ P,
02000		CAIE CHRREG,"J"
02100		POPJ P,
02200		GETNUM
02300		JUMPE NUMREG,CPOPJ
02400		CAIN CHRREG,"-"
02500		CAILE NUMREG,2			;CHECK FOR LEGAL JACK #
02600		POPJ P,
02700		HLRZ CHRREG,(P)
02800		LSH CHRREG,1			;MAKE ROOM FOR THE JACK BIT
02900		SUBI NUMREG,1			;REDUCE TO ONE BIT
03000		IOR CHRREG,NUMREG
03100		LSH CHRREG,14
03200		HRLM CHRREG,(P)
03300		GETNUM
03400		JUMPE NUMREG,CPOPJ
03500		CAILE NUMREG,=26		;ONLY 26 PINS PER JACK
03600		POPJ P,
03700		HLRZ TT,(P)
03800		TRO TT,(NUMREG)
03900		JRST CPOPJ1	
04000	
04100	LOCPNW:
04200	LOCPNC:	PUSHJ P,LOCOUT
04300		HLRZ A,A
04400		PUTBYT "-"
04500	PRCNPN:	LDB T,[POINT 12,A,35]
04600	RAYDP1:	MOVEI TTT,2
04700		MOVEM TTT,NDIG
04800		JRST NPUTDEC
04900	
05000	RAYCON:	PUSHJ P,LOCOUT
05100		PUTSTR [ASCIZ /   /]		;3 SPACES
05200		HLRZS A
05300		JRST PRCNPN
05400	
05500	PADPRT:	MOVEI T,(G)
05600		LSH T,-1
05700		PUTBYT @N2L+1(T)
05800		PUTBYT "J"
05900		MOVEI T,1
06000		TRNE G,1
06100		MOVEI T,2
06200		JRST PUTDEC
06300	
06400	RAYDIP:	PUSH P,T			;HAS SOME BITS
06500		PUSHJ P,LOCOUT
06600		PUTSTR [ASCIZ /   /]		;3 SPACES
06700		POP P,T
06800		TRZE T,L1416
06900		ADD A,[1,,0]
07000		HLRZ T,A
07100		JRST RAYDP1
07200	>;MWL
07300	BEND PG21
     	00100	
00200	MWL,<
00300	SUBTTL WIRE WRAP ROUTINES  --  PG21, PG21-60, AND PG21-180
00400	BEGIN WPG21
00500	
00600	comment 
00700	
00800	All calculations are done from the dip side.
00900	
01000	(0,0) at lower left hand corner in left handed coordinate system.
01100	Dip sockets are arranged in 5x6 groups.  These 30 dip groups come
01200	in pairs.  Each group comes with a pair of Scotch Flex(R) connectors
01300	labeled "J1" and "J2".  There can be up to 6 30 dip groups on one
01400	board.  The horizontal spacing between groups is 2.700".
01500	
01600	A30(8) is at (0,0).  AJ2-26 is at (500,6100).  AJ1-26 is at (500,6400).
01700	
01800	It follows that B30(8) is at (2700,0)
01900	
02000	
02100	NGRPS__6		;PG21-180 HAS 6 GROUPS
02200	GRPCOL__5		;# COLS IN GROUP
02300	GRPROW__6		;# COLUMNS IN GROUP
02400	UMLCOL__GRPROW		;UML INTERCHANGES ROWS AND COLUMNS
02500	UMLROW__GRPCOL
02600	MAXDIP__GRPROW*GRPCOL	;# DIPS IN GROUP
02700	GXOFST__=2700		;2.700" GROUP HORIZONTAL SPACING
02800	XDIPSP__ =500		; .500" DIP HORIZONTAL SPACING
02900	YDIPSP__=1000		;1.000" DIP VERTICAL SPACING
03000	XPINSP__ =300		; .300" DIP PIN HORIZONTAL SPACING
03100	YPINSP__ =100		; .100" DIP PIN VERTICAL SPACING
03200	PXOFST__ =500		; .500" X OFFSET FOR POWER PIN
03300	PYOFST__ =800		; .800" Y OFFSET FOR POWER AND GROUND PINS
03400	JXOFST__ =500		; .500" X OFFSET FOR JACKS
03500	JYOFST__=6100		;6.100" Y OFFSET FOR J2
03600	JYOFS1__ =300		; .300" Y OFFSET FROM J2 TO J1
03700	PXPNSP__ =100		; .100"	CONNECTOR PIN VERTICAL SPACING
03800	PYPNSP__ =100		; .100" CONNECTOR PIN HORIZONTAL SPACING
03900	
04000	;SOME BYTE POINTERS FOR EXTRACTING FIELDS
04100	
04200	DEFINE %DIPP<[POINT  6,A,17]>	;DIP PIN #
04300	DEFINE %DIPG<[POINT  6,A,23]>	;DIP GROUP
04400	DEFINE %DIPS<[POINT 12,A,35]>	;DIP SLOT
04500	
04600	DEFINE %CONP<[POINT 12,A,35]>	;CONNECTOR PIN #
04700	DEFINE %CONG<[POINT  5,A,22]>	;CONNECTOR GROUP
04800	DEFINE %CONJ<[POINT  1,A,23]>	;CONNECTOR JACK
04900	
05000	^^WP21TV__.	;TRANSFER VECTOR FOR DOUBLE HEIGHT DEC BOARDS
05100	
05200		-1		;ISWW
05300		JRST WWINIT	;WWINIT FIGURES OUT IF PG21, PG21-60, OR PG21-180
05400		JRST MAPRC
05500		JRST MAPPAD
05600		JRST PINMAP	
05700		JRST PINBIT	
05800		JRST VGCON	
05900		JRST DISTPP	;POINT TO POINT WIRING
06000		JRST FPWR
06100		JRST FGND
06200		JRST PGPRTM
06300		JRST PGPRNT
06400		JRST GNDCLR
06500		JRST WAGGND
06600		JRST GNDOUT
06700		JRST VCCOUT
06800		JRST RAYGEN
06900		JRST LCPNM	;PRINTS DIP LOC AND PIN # ON WIRE-WRAP SIDE
07000		UMLROW*NGRPS	;NROWS		    (USED FOR UML ONLY)
07100		UMLCOL		;NCOLS		    (USED FOR UML ONLY)
07200		0		;NCLPRG		    (USED FOR UML ONLY)
07300		MAXDIP*NGRPS	;DIPSLT_NROWS*NCOLS (USED FOR UML ONLY)
07400		SETPAD(NGRPS*2)	;NPADS		    (USED FOR UML ONLY)
07500		XWD -=26,1	;PADLET		    (USED FOR UML ONLY)
07600		XWD -1,0	;PADPIN		    (USED FOR UML ONLY)
07700		=10		;FRACTN
07800		=200*2		;WRAPMG .200" INSULATION AROUND EACH POST
07900		=1500		;POSTMG .750" BARE WIRE AROUND EACH POST
08000	CHECK WP21TV,WTVLEN
     	00100	
00200	;TABLES FOR MAPIT
00300	
00400	;**************************************** DIP PINS
00500	
00600	DEFINE XY(X,Y)
00700	<	X*XPINSP,,Y*YPINSP
00800	>
00900	
01000	PINTAB:
01100	FOR Y_7, 0, -1
01200	<	XY(1,Y)
01300	>
01400	FOR Y_0, 7, 1
01500	<	XY(0,Y)
01600	>
01700	;**************************************** DIP LOCATIONS
01800	
01900	DEFINE XY(X,Y)
02000	<	X*XDIPSP,,Y*YDIPSP
02100	>
02200	
02300	DIPLOC:
02400	 FOR Y_5, 0, -1
02500	<FOR X_4, 0, -1
02600	<	XY(X,Y)
02700	>>
02800	;**************************************** SCOTCH-FLEX PINS
02900	
03000	DEFINE XY(X,Y)
03100	<	X*PXPNSP,,Y*PYPNSP
03200	>
03300	
03400	FLXPIN:
03500	 FOR Y_1, 0, -1
03600	<FOR X_0, =12, 1
03700	<	XY(X,Y)
03800	>>
     	00100	;WWINT FIGURES OUT IF THIS IS A 60 OR 180 SLOT BOARD
00200	
00300	WWINIT:	MOVE T,BRDTYP		;GET THE BOARD TYPE
00400		MOVE T,BNAMES(T)
00500		MOVE TT,1(T)
00600		TRNN TT,1771		;CHECK FOR NULL FIFTH CHARACTER
00700		JRST IS180		;PG21 IS AN ALIAS FOR PG21-180
00800		MOVE TT,2(T)		;GET THE LAST CHARCTERS OF BOARD NAME
00900		CAMN TT,[ASCIZ /60/]
01000		JRST IS60
01100		CAMN TT,[ASCIZ /180/]
01200		JRST IS180
01300		NOCMU,<.VALUE	>;IT FOULS UP CMU!!		;I DON'T KNOW ABOUT THIS ONE
01400	
01500	IS180:	POPJ P,			;180 IS THE DEFAULT
01600	
01700	IS60:	MOVEI T,UMLROW*2
01800		MOVEM T,NROWS
01900		MOVEI T,MAXDIP*2
02000		MOVEM T,DIPSLT
02100		MOVEI T,2*2
02200		MOVEM T,NPADS
02300		POPJ P,
02400	
02500	MAPIT:	JUMPL A,CONMAP
02600		LDB TT,%DIPG		;GROUP LETTER
02700		LDB T,%DIPS		;DIP #
02800		JUMPE T,CPOPJ
02900		CAILE T,MAXDIP		;TOO MANY DIPS FOR SLOT
03000		POPJ P,
03100		MOVE T,DIPLOC-1(T)	;XY OF DIP SLOT
03200		IMULI TT,GXOFST		;X FUDGE FOR GROUP
03300		MOVSI TT,-GXOFST(TT)
03400		ADD T,TT		;LOCATION OF DIP ON BOARD
03500		LDB TT,%DIPP		;PIN #
03600		TLNE A,MAPPWR!MAPGND	;WANT POWER OR GROUND?
03700		JRST PINPGP		;YES, RETURN FIX IT UP
03800		CAIE B,=14		;14 PIN DIP?
03900		JRST MAPIT0		;NO, DON'T NEED TO FUTZ AROUND
04000		ADDI TT,1
04100		ADD A,[1,,0]
04200		TLO A,PLUS1
04300	MAPIT0:	ADD T,PINTAB-1(TT)	;ADD EXTRA XY FOR PIN
04400		SETZB TT,TTT		;FLUSH AWAY THOSE GOODIES
04500		JRST CPOPJ2
04600	
04700	;POWER AND GROUND PIN FOR DIP
04800	
04900	PINPGP:	JUMPN TT,CPOPJ		;THE PIN # SHOULD BE ZERO
05000		ADDI T,PYOFST		;FIX THE Y
05100		TLNN A,MAPPWR
05200		JRST [	MOVSI TT,GND	;HE WANTS GND
05300			JRST PINPG1]
05400		ADD T,[PXOFST,,0]	;HE WANTS PWR
05500		MOVSI TT,PWR
05600	PINPG1:	SETZ TTT,
05700		JRST CPOPJ2
05800	
05900	;HERE FOR CONNECTOR PINS
06000	
06100	CONMAP:	AOS (P)
06200		LDB T,%CONG		;GROUP
06300		IMULI T,GXOFST
06400		MOVSI T,-GXOFST(T)	;XY OF GROUP
06500		ADD T,[JXOFST,,JYOFST]	;LOC OF J2
06600		TRNN A,10000
06700		ADDI T,JYOFS1		;FUDGE FOR J1
06800		LDB TT,%CONP		;PIN #
06900		JUMPE TT,CPOPJ
07000		CAILE TT,=26		;REASONABLE # OF PINS
07100		POPJ P,
07200		ADD T,FLXPIN-1(TT)
07300		SETZB TT,TTT
07400		JRST CPOPJ1
07500	
07600	;THE ROUTINES WHICH CALL MAPIT
07700	
07800	PINBIT:	PUSHJ P,MAPIT
07900		JFCL			;DIP ERROR
08000		SETZ TT,		;CONNECTOR ERROR
08100		POPJ P,			;HAPPINESS
08200	
08300	PINMAP:	PUSHJ P,MAPIT
08400		JRST PINERR
08500		JRST CONERR
08600		JRST CPOPJ1
08700	
08800	CONERR:	LDB T,%CONG		;CONNECTOR GROUP
08900		OUTCHR N2L(T)
09000		OUTCHR ["J"]
09100		LDB T,%CONJ
09200		ADDI T,1
09300		PUSHJ P,NUMPNT		;JACK
09400		OUTCHR ["-"]
09500		LDB T,%CONP
09600		PUSHJ P,NUMPNT		;PIN
09700	CONER1:	OUTSTR [ASCIZ / Mapping error
09800	/]
09900		POPJ P,
10000			
10100	PINERR:	LDB T,%DIPG		;FORM A#(#)
10200		OUTCHR N2L(T)
10300		LDB T,%DIPS		;SLOT #
10400		PUSHJ P,NUMPNT
10500		OUTCHR["("]
10600		LDB T,%DIPP		;PIN #
10700		PUSHJ P,NUMPNT
10800		OUTCHR [")"]
10900		JRST CONER1
     	00100	
00200	MAPRC:	HLRZ TT,T		;(1,1) IS DIP IN UPPER LEFT HAND CORNER
00300		SOJL TT,CPOPJ
00400		CAML TT,NROWS
00500		POPJ P,
00600		HRRZS T			;ROW IN TT, COL IN T
00700		SOJL T,CPOPJ
00800		CAML T,NCOLS		;(0,0) IS NOW IN UPPER LEFT
00900		POPJ P,
01000		IDIVI TT,GRPCOL		;GROUP IN TT, SUBROW IN TTT
01100		ADDI TT,1
01200		LSH TT,14
01300		SUBI TTT,UMLROW-1	;TRANSLATE
01400		MOVNS TTT		;REFLECT
01500		IMULI T,UMLROW
01600		ADDI T,1(TTT)
01700		IOR T,TT		;OR IN GROUP
01800		JRST CPOPJ1
01900	
02000	MAPPAD:	MOVEI T,1(T)
02100		LSH T,14
02200		IORI T,(TT)
02300		POPJ P,
     	00100	
00200	FPWR:	TLZ A,PLUS1!PLUS2!77
00300		SKIPGE A		;POWER AND GROUND NOT AVAILABLE OF CONNECTORS
00400		PUSHJ P,FPGC		;FIND POWER AND GROUND FOR CONNECTOR
00500		TLO A,MAPPWR
00600		POPJ P,
00700	
00800	FGND:	TLZ A,PLUS1!PLUS2!77
00900		SKIPGE A
01000		PUSHJ P,FPGC
01100		TLO A,MAPGND
01200		POPJ P,
01300	
01400	FPGC:	MOVEI T,(A)		;GET SCOTCH FLEX PIN #
01500		TRZ T,770000
01600		SOS T
01700		IDIVI T,=13
01800		ANDI A,760000		;GET GROUP IN A
01900		LSH A,-1
02000		IOR A,[	REPEAT 4,<2>	;THIS IS CLOSET SLOT FOR THIS CONNECTOR
02100			REPEAT 5,<3>
02200			REPEAT 4,<4>](TT)
02300		POPJ P,
02400	
02500	PGPRTM:	SETOM PMINUS#
02600		CAIA
02700	PGPRNT:	SETZM PMINUS
02800		JUMPL A,PGCPNT		;JUMP FOR CONNECTOR
02900		LDB T,%DIPG		;LETTER PART OF SLOT LOCATION
03000		PUTBYT @N2L(T)
03100		MOVEI T,2		;TWO DIGITS FOR SLOT #
03200		MOVEM T,NDIG
03300		LDB T,%DIPS
03400		PUSHJ P,NDECOUT		;SLOT #
03500		PUTBYT "("
03600		TLNN A,MAPPWR!MAPGND
03700		JRST NPGPNT
03800		TLNN A,MAPPWR
03900		SKIPA T,["G"]
04000		MOVEI T,"V"
04100		PUTBYT @T
04200	PGPN1:	PUTBYT ")"
04300		POPJ P,
04400	
04500	NPGPNT:	LDB T,%DIPP		;PIN #
04600		SKIPN PMINUS
04700		JRST NPGPN1
04800		TLNE A,PLUS1
04900		JRST [	PUSHJ P,DECOUT
05000			PUTBYT "-"
05100			JRST PGPN1]
05200	NPGPN1:	TLNE A,PLUS1
05300		SUBI T,1		;SUBTRACT 1 FOR 14 PIN DIP #
05400		PUSHJ P,DECOUT
05500		TLNE A,PLUS1
05600		PUTBYT "+"
05700		TLNE A,PLUS2
05800		PUTSTR [ASCIZ /+2??????????/]
05900		JRST PGPN1
06000	
06100	;PRINTS CONNECTOR PIN
06200	
06300	PGCPNT:	LDB T,%CONG
06400		PUTBYT @N2L(T)
06500		PUTBYT "J"
06600		LDB T,%CONJ
06700		PUSHJ P,DECOUT
06800		PUTBYT "-"
06900		LDB T,%CONP
07000		JRST DECOUT
07100	
07200	RAYGEN:	JUMPL A,RAYRP1
07300		LDB T,%DIPG
07400		PUTBYT @N2L(T)
07500		MOVEI T,2
07600		MOVEM T,NDIG
07700		LDB T,%DIPS		;SLOT #
07800		PUSHJ P,NDECOUT
07900		PUTSTR [ASCIZ /   /]	;3 SPACES
08000		MOVEI T,2		;ASSUME NOT POWER OR GROUND
08100		MOVEM T,NDIG
08200		LDB T,%DIPP
08300		TLNN A,MAPPWR!MAPGND	;ONLY GROUND AND POWER CAN BE GENERATED
08400		JRST NPUTDEC		;NOT POWER OR GROUND
08500		PUTBYT "0"
08600		MOVEI T,"V"
08700		TLNN A,MAPPWR
08800		MOVEI T,"G"
08900		PUTBYT (T)
09000		POPJ P,
09100	
09200	RAYRP1:	OUTSTR [ASCIZ /Connector became generated pin
09300	/]
09400		JRST ERRET
09500	
09600	VGCON:	POPJ P,
09700	
09800	FOR NAME (GNDCLR,WAGGND,GNDOUT,VCCOUT)
09900	<NAME:	OUTSTR [ASCIZ /NAME not implemented for PG21
10000	/]
10100		JRST ERRET
10200	>
10300	BEND WPG21
10400	>;MWL
 