VERSION(CLOSE,1)
GETCLS:	TRNE STBOX
	POPJ P,
	MOVE A,CURSE
	EXCH A,LSTCUR		;SET CURRENT CURSOR POS AS LAST FOUND!
	TRZN NEEDCL
	CAME A,CURSE		;MOVED SINCE LAST FIND?
	JRST GETCL1
	JRST GETCL2		;NO
GETCL1:	PUSHJ P,PUSHIT
	PUSHJ P,FNDCLS
	PUSHJ P,POPIT
	SETOM CLOSUP
GETCL2:	SKIPN A,CLOSES
	POPJ P,
	JRST CPOPJ1

SCLOSP:	PUSHJ P,SCLOSL
	MOVE T,1(T)
	TDZ T,[1,,1]		;CLEAR THESE BITS
MD,<	MOVE TTT,MODE
	CAIE TTT,BTXTM
	JRST SCLSP1
	MOVE TTT,BTBODY
	PUSH P,F
	HLRZ F,(TTT)
	HLRZ F,(F)
	PUSHJ P,ORIENT
	POP P,F
	ADJUST(ADD,T,<1(TTT)>)
SCLSP1:
>;MD
	MOVEM T,LSTCUR		;THIS IS NOW CURSOR POS OF LAST CLOSEST
	JRST CHKON

SCLOSL:	PUSH P,TT
	MOVE TT,MODE
	XCT CLOSLT(TT)
SCLOS1:	HRL T,TT
	HRRZ TT,(TT)
	CAIN TT,(T)
	JRST SCLOSX
	JUMPN TT,SCLOS1
	POP P,TT
	PUSHJ P,FUCKUP
	POPJ P,

CLOSLT:	MOVEI TT,SETPNT	;S
	MOVEI TT,DBODPN	;B
MD,<	MOVEI TT,PONPNT	>	;P
MPC,<	PUSHJ P,PONSID	>	;P
	PUSHJ P,FUCKUP	;L
MD,<	MOVEI TT,PONPNT	>	;T
MPC,<	PUSHJ P,PONSID	>	;T
	PUSHJ P,FUCKUP	;A
	PUSHJ P,FUCKUP	;MA
	PUSHJ P,FUCKUP	;SP
MD,<	PUSHJ P,BTLAST	;BT
	PUSHJ P,FUCKUP	;BTA
	PUSHJ P,FUCKUP	;E
	PUSHJ P,FUCKUP	;EI
	JRST SCLOSX	;ET  DOESN'T USE CLAST
	JRST SCLOSX	;EP	"
	PUSHJ P,FUCKUP	;EG
	PUSHJ P,FUCKUP	;EA
>;MD
REPEAT NUMODES+<CLOSLT-.>,<PUSHJ P,FUCKUP>

MD,<
BTLAST:	MOVE TT,BTBODY
	HLRZ TT,(TT)
	HLRZ TT,1(TT)
	ADDI TT,1
	POPJ P,			;RETURN POINTER TO LIST HEAD
>;MD

MPC,<
PONSID:	HLRZ TT,(T)
	HRRZ TT,1(TT)
	MOVE TT,1(TT)
	EQV TT,SID
	SKIPL TT		;ON CURRENT SIDE?
	SKIPA TT,[PONPN2]	;NO
	MOVEI TT,PONPNT		;YES
	POPJ P,
>;MPC

SCLOSX:	POP P,TT
SCLOSE:	HRRZM T,CLOSES
	HLRZM T,CLAST
	PUSH P,T
	MOVE T,CURSE
	MOVEM T,LSTCUR			;NOTE LAST PLACE WE SET CLOSEST
	POP P,T
	SETOM CLOSUP
	TRZ NEEDCL			;IF SETTING CLOSEST, DON'T NEED NEW ONE
	POPJ P,

FNDCLS:	TRNE INMOV
	POPJ P,			;DON'T GAFROTZ CLOSES
	MOVE T,MODE
	JRST @FNDTAB(T)

FNDTAB:	FNDSET		;S
	FNDBOD		;B
	FNDPNT		;P
	FNDLIN		;L
	FNDPNT		;T
	NOFNDS		;A
	NOFNDS		;MA
	CPOPJ		;SP
MD,<	BTXFND		;BT
	NOFNDS		;BTA
	EDIFND		;E
	NOFNDS		;EI
	EDTFND		;ET
	EDPFND		;EP
>
REPEAT NUMODES+<FNDTAB-.>,<NOFNDS
>

NOFNDS:	SETZM CLOSES	;NO CLOSEST ANYTHING
	POPJ P,
FNDLIN:	SETZM CLOSES	;CLEAR CLOSEST POINTER
	PUSHJ P,CLSLIN	;FIND CLOSEST LINE
	MOVEM A,CLOSES	;SAVE AS CLOSEST LINE (DON'T USE CLAST HERE)
	POPJ P,

CLSLIN:	SETZ A,		;NO CLOSEST YET
	SKIPN B,PONPNT	;NO POINTS?
	POPJ P,
	MOVSI T,177777	;FAR ENOUGH
	MOVEM T,DIFSOF
FLLOP:	HLRZ C,(B)	;GET POINTER TO DATA BLOCK
	HLRZ D,1(C)	;GET POINTER TO ^_ BLOCK
MD,<
	HRRZ E,(D)	;GET POINTER
	JUMPE E,.+2	;NONE?
	PUSHJ P,ISCLLN	;SEE IF CLOSER
	HRRZ E,1(D)	;GET ANOTHER POINTER
	JUMPE E,.+2	;NONE?
	PUSHJ P,ISCLLN	;SEE IF CLOSER
>;MD
MPC,<
	JUMPE D,FLLOP2
FLLOP1:	HLRZ E,(D)
	JUMPE E,.+2
	PUSHJ P,ISCLLN
	HRRZ E,1(D)
	JUMPE E,.+2
	PUSHJ P,ISCLLN
	HLRZ E,1(D)
	JUMPE E,.+2
	PUSHJ P,ISCLLN
	HRRZ D,(D)
	JUMPN D,FLLOP1
FLLOP2:
>;MPC
	HRRZ B,(B)	;GO TO NEXT POINT
	JUMPN B,FLLOP
	POPJ P,

ISCLLN:	MOVE C,1(B)	;GET X,Y OF FIRST POINT
MPC,<	CAMGE C,1(E)	;DO SEGMENT ONCE ONLY
	POPJ P,
>;MPC
	MOVE F,1(E)	;GET X,Y OF SECOND POINT
	MOVE TTT,CURSE	;GET CURSOR X,Y
	MOVE T,F
	XOR T,C		;GET DIFFERENCE
	TLNN T,-1	;LEFT HALF ZERO?
	JRST [TRNN T,-1	;YES, RIGHT HALF ZERO TOO?
		POPJ P,	;YES
		MOVEI TT,1 ;NO, LEFT HALF ONLY, ZERO. VERT. LINE
		HRRZM TTT,TPOINT
		HLLM F,TPOINT
		JRST LRTCON]
	TRNE T,-1	;RIGHT HALF ZERO?
	JRST LRSLAN	;NO, SLANTY LINE
	MOVEI TT,2	;YES, HORIZ. LINE
	HLLZM TTT,TPOINT
	HRRM F,TPOINT
	MOVSS F
	MOVSS C		;SWAP HALVES
	MOVSS TTT
LRTCON:	HLRE T,C	;GET THE PART THAT'S THE SAME (X OR Y)
	HRRES F
	HRRES C
	HRRE G,TTT
MD,<	CAME F,G	;WELL IF PHIL SAYS SO!
	CAMN C,G	;EITHER END SAME AS CURSOR?
	POPJ P,		;YES, CAN'T BE CLOSE
>;MD
	CAML F,G	;GREATER THAN ONE BUT LESS THAN THE OTHER?
	JRST [CAMG C,G
		JRST ISPOSS
		POPJ P,];NO, NOT CLOSE
	CAMG C,G
	POPJ P,		;NO, NOT CLOSE
ISPOSS:	HLRES TTT	;GET OTHER THING
	SUB TTT,T	;GET DIFFERENCE
	IMUL TTT,TTT	;SQUARE IT
	CAML TTT,DIFSOF	;DISTANCE SMALLER?
	POPJ P,		;NO
ITCLSS:	TLNE M,XWINDOW!MACACT!DSKACT
	JRST ITCLSX
	MOVE T,TPOINT	;TAKE INTERSECTION POINT
	PUSH P,TTT
	PUSHJ P,ONSCR
	JRST [	POP P,(P)
		POPJ P,]	;NOT ON
	POP P,TTT
ITCLSX:	MOVEM TTT,DIFSOF;YES, DEPOSIT NEW DISTANCE
	MOVE A,B	;GET POINTERS
	HRL A,E
	MOVE T,TPOINT
	TDZ T,[1,,1]
	MOVEM T,IPOINT
	POPJ P,
LRSLAN:	HLRE T,CURSE
	MOVEM T,DX1		;STORE HERE TERMPORARILY
	HRRE T,CURSE
	PUSHJ P,CALINE		;RETURN LENGTH IN TTT
	POPJ P,			;OFF ONE END OR OTHER
	DIV TT,L2		;DO FINAL CALC
	MOVM TTT,TT
	CAML TTT,DIFSOF
	POPJ P,
	CAML T,L2
	JRST [	MOVE T,1(E)
		MOVEM T,TPOINT
		JRST ITCLSS]
	PUSH P,TTT
	DIV T,L2		;CALC SCALE
	HRRE TTT,1(B)
	HRRE TT,1(E)
	SUB TT,TTT
	MULM T,TT
	ADD TT,TTT
	MOVEM TT,TPOINT
	HLRE TTT,1(B)
	HLRE TT,1(E)
	SUB TT,TTT
	MULM T,TT
	ADD TT,TTT
	HRLM TT,TPOINT
	POP P,TTT
	JRST ITCLSS

CALINE:	HRRE TT,1(B)
	SUB T,TT
	MOVEM T,DY1
	HRRE T,1(E)
	SUB T,TT
	MOVEM T,DY3
	HLRE TT,1(B)
	MOVE T,DX1
	SUB T,TT
	MOVEM T,DX1
	HLRE T,1(E)
	SUB T,TT
	MOVEM T,DX3
	MOVE T,DX3
	IMUL T,DX3
	MOVE TT,DY3
	IMUL TT,DY3
	ADD T,TT
	MOVEM T,L2
	MOVE T,DX1
	IMUL T,DX3
	MOVE TT,DY1
	IMUL TT,DY3
	ADD T,TT
	JUMPLE T,CPOPJ		;NEGATIVE MEANS OFF ONE END
	MOVMS T
	CAML T,L2
	POPJ P,
	MOVE TT,DX1		;INSIDE LINE, CALC LENGTH
	IMUL TT,DY3
	MOVE TTT,DX3
	IMUL TTT,DY1
	SUB TT,TTT
	MUL TT,TT		;PRODUCE DOUBLE LENGTH RESULT
	JRST CPOPJ1
;SQRT:	FINDS THE SQUARE ROOT OF THE INTEGER IN T.  USES TT AND TTT
SQRT:	JFFO T,.+2	;FIND THE HIGH ORDER BIT
	POPJ P,		;ZERO?
	SUBI TT,=37
	MOVNS TT
	LSH TT,-1
	MOVEI TTT,1
	LSH TTT,-1(TT)	;MAKE WORD WITH HALF AS MANY LOW ORDER BITS
	SETOM SQRFLG	;TURN ON TEMP (COUNT) FLAG
SLOP1:	MOVE TT,TTT	;GET APROXIMATE SQRT
	IDIVM T,TT	;DIVIDE IT INTO THE SQUARE
	ADD TT,TTT	;ADD APROX.
	ASH TT,-1	;DIVIDE BY 2. THIS IS THE NEW APPROX.
	MOVE TTT,TT	;GET APPROX.
	IDIVM T,TTT	;DIVIDE IT INTO THE SQUARE
	ADD TTT,TT	;ADD APPROX.
	ASH TTT,-1	;DIVIDE BY 2. THIS IS THE NEW APPROX.
	AOSG SQRFLG	;GONE THROUGH TWICE?
	JRST SLOP1	;NO
	MOVE T,TTT
	POPJ P,
;FNDSET, FNDBOD, FNDPNT, EDTFND, EDPFND, EDIFND
FNDSET:	MOVEI F,SETPNT	;GET SET POINTER
	JRST CLFNDA	;FIND CLOSEST SET.

FNDBOD:	MOVEI F,DBODPN	;GET POINTER TO LIST OF BODIES
	JRST CLFNDA	;FIND BODY WHICH IS CLOSEST TO CURSOR

FNDPNT:	MOVEI F,PONPNT	;GET POINTER TO POINTS WHICH ARE ON THE SCREEN
	JRST CLFNDA	;FIND POINT WHICH IS CLOSEST OT CURSOR

MD,<
BTXFND:	MOVE F,BTBODY
	HLRZ F,(F)
	HLRZ F,1(F)
	ADDI F,1
	JRST CLFNDA

EDTFND:	MOVE A,CURBOD	;GET POINTER TO CURRENT BODY (TYPE) DEFINITION
	HLRZ C,1(A)	;GET DATA BLOCK POINTER
	HLRZ B,1(C)	;GET TEXT POINTER
	JRST CLFND	;FIND PIECE OF TEXT CLOSEST TO CURSOR

EDIFND:	MOVE A,CURBOD
	HLRZ C,1(A)
	HRRZ B,1(C)
	JRST CLFND	;FIND CLOSEST POINT IN LINES

EDPFND:	MOVE A,CURBOD	;GET POINTER TO CURRENTLY-BEING-EDITED TYPE DEF.
	HLRZ F,1(A)	;GET POINTER TO DATA BLOCK
	JRST CLFNDA
>;MD
CLFNDA:	HRRZ B,(F)
CLFND:	HRLOI TT,177777	;MAKE A LARGE NUMBER(LARGE ENOUGH)
	MOVE E,TT
	SETZB A,CLOSES	;CLEAR CLOSEST POINTERS
	JUMPE B,CPOPJ
CLOP1:	MOVE T,1(B)	;GET X,Y WORD
	TDZ T,[1,,1]
	TRNE INLIN	;DRAWING A LINE?
	CAME B,LINING	;YES, IS THIS THE POINT FROM WHICH WE ARE DRAWING?
	CAIA
	JRST CLOP2	;YES, IGNORE IT
	MOVE D,MODE
	CAIE D,TXTM
	JRST NOTXTM	;NOT TEXT MODE
	HLRZ D,(B)
	HRRZ D,1(D)
	HLRZ D,(D)
	JUMPE D,CLOP2
NOTXTM:
MD,<	MOVEI D,1
	LSH D,@MODE
	TDNN D,[1EDTTM!1BTXTM]
	JRST NOETXM
	TDNN D,[1BTXTM]
	JRST NADJBT
	MOVE TTT,BTBODY
	PUSH P,F
	HLRZ F,(TTT)
	HLRZ F,(F)
	PUSHJ P,ORIENT
	POP P,F
	ADJUST(ADD,T,<1(TTT)>)	;CALC SCREEN POS OF TEXT
NADJBT:	HLRZ D,(B)
	JUMPE D,CLOP2		;DON'T FIND INDIRECT POINTERS
	HRRZ D,(D)
	HLRZ D,(D)
	TLNN M,%IDENT
	JUMPE D,CLOP2
NOETXM:
>;MD
	MOVEM T,CLXY	;SAVE X,Y HERE
	SUB T,CURSE	;SUBTRACT CURSOR POSITION
	MOVMS T		;GET ABS OF DIFFERENCE
	CAMLE T,TT	;THIS ONE SMALLER X DISTANCE?
	JRST CLOP2	;YES, THINK ABOUT IT MORE
TRYIT:	MOVE T,1(B)
	CAMN T,1(A)
	JUMPN A,CKCOIN	;IF ALREADY HAVE CLOSEST AT SAME PLACE, CHECK
	TLNE M,DSKACT!MACACT!XWINDOW
	JRST WINX	;NO TEST IF NO WINDOW
	MOVE T,CLXY
	PUSHJ P,ONSCR
	JRST CLOP2
WINX:	HLRE T,CLXY	;GET X OF POINT
	HLRE D,CURSE	;GET X OF CURSOR
	SUB T,D		;GET DIFF.
	MOVMS T		;ABS VAL.
	HRRE D,CLXY	;GET Y OF POINT
	SUB D,CURSE	;SUBTRACT CURSOR (Y IN RIGHT HALF)
	HRRES D		;GET Y PART
	MOVMS D		;ABS. VAL.
	IMUL D,D	;Y^2
	IMUL T,T	;X^2
	ADD T,D		;X^2+Y^2
	CAMLE T,E	;SMALLER OR EQUAL DISTANCE?
	JRST CLOP2	;NO
	MOVE A,B	;YES, SHORTEST SO FAR. HOLD POINTER TO IT
	HRL A,F		;ALSO LAST POINTER
	MOVE E,T	;REPLACE COMPARE WITH NEW ONE
	PUSHJ P,SQRT	;GET SQRT OF DISTANCE
	ADDI T,20	;SAFETY FACTOR
	MOVS TT,T	;REPLACE THE Y-PART COMPARE WITH THE DISTANCE
CLOP2:	MOVE F,B
	HRRZ B,(B)	;NO, GO TO NEXT
	JUMPN B,CLOP1	;LOOP IF MORE TO LOOK AT
	HRRZM A,CLOSES	;DEPOSIT POINTER TO CLOSEST
	HLRZM A,CLAST	;ALSO LAST
	POPJ P,

CKCOIN:	MOVEI TTT,1
	LSH TTT,@MODE
	TDNN TTT,[1PNTM!1TXTM]
	JRST WINX		;ONLY CHECK POINTS
	HLRZ TTT,(A)
	HRRZ TTT,1(TTT)
	MOVE TTT,1(TTT)
	TLNE TTT,MPC,<CPIN!>ISPIN	;DON'T FIND PIN IF COINCEDENT
	JRST WINX			;CONTINUE CHECK
	JRST CLOP2			;LOSE IMMEDIATELY
;UPCLOS
UPCLOS:	TLNE DSPACT	;DISPLAYING AT ALL?
	TRNE INMOV!STBOX;DOING ANYTHING OBVIOUSLY WRONG?
	POPJ P,		;YES, DON'T INDICATE CLOSEST
	SKIPN CLOSUP		;NEED TO UPDATE CLOSES DISPLAY?
	POPJ P,			;NO
	SETZM CLOSUP
	MOVE T,MODE
	SKIPE A,CLOSES
	JRST @UPTAB(T)
UPNONE:	MOVEI T,BIGPG
	JRST HYDPOG

UPTAB:	.UPSET		;S
	.UPBOD		;B
	.UPPNT		;P
	.UPLIN		;L
	.UPPNT		;T
	UPNONE		;A
	UPNONE		;MA
	UPNONE		;SP
MD,<	.UPBTX		;BT
	UPNONE		;BTA
	.UPED		;E
	UPNONE		;EI
	.UPED		;ET
	.UPEDP		;EP
>
REPEAT NUMODES+<UPTAB-.>,<UPNONE
>

SPSETUP:
	SKIPA T,[ANGLPG]
.SETUP:	MOVEI T,BIGPG
	MOVEM T,PGLASS
	PUSHJ P,DPYSET
	JRST SETBRT

.UPVCT:	MOVE T,1(A)	;X,Y OF THING
.UPT:	TDZ T,[1,,1]
	PUSHJ P,ONSCR	;ON SCREEN?
	POPJ P,
	PUSHJ P,VHLINE	;MAKE CROSS HAIRS (MAYBE)
	PUSHJ P,BIGAIV	;PUT IN VECTOR TO THERE
	JRST CPOPJ1

MD,<
.UPEDP:	PUSHJ P,.SETUP
	MOVE T,1(A)
	PUSHJ P,.UPT
	JRST UPNONE
	MOVEI T,"*"+"*"+1
	PUSHJ P,TXTDPB
	JRST BLINKO

.UPBTX:	PUSHJ P,.SETUP
	MOVE TT,BTBODY
	MOVE T,1(A)
	PUSH P,F
	HLRZ F,(TT)
	HLRZ F,(F)
	PUSHJ P,ORIENT
	POP P,F
	ADJUST(ADD,T,<1(TT)>)
	PUSHJ P,.UPT
	JRST UPNONE
	HLRZ T,(A)
	HLRZ T,(T)
	SKIPE T			;SKIP IF NOT PROPERTY
	SKIPA T,[ASCID/BTP/]
	MOVE T,[ASCID/BT/]
	PUSHJ P,TXTDPB
	JRST BLINKO
>;MD

MD,<
.UPED:	SKIPA T,[ASCID/*/]
>;MD
.UPBOD:	MOVEI T,"B"+"B"+1
	PUSH P,T
	PUSHJ P,.SETUP
	PUSHJ P,.UPVCT
	JRST [	POP P,(P)
		JRST UPNONE]
	POP P,T
	PUSHJ P,TXTDPB
	JRST BLINKO	;OUTPUT IT

SETBRT:
MPC,<	TRNE BTHSDS
	SKIPA T,BTHBRT	;ALWAYS USE 7 IF BOTH SIDES ARE ON
>;MPC
	MOVE T,DEFBRT	;GET CURRENT BRIGHTNESS
	ADDI T,1
	JRST DPYBRT
;.UPPNT
.UPPNT:	PUSHJ P,.SETUP
	PUSHJ P,.UPVCT
	JRST UPNONE
	MOVSI T,(<ASCII/*/>)
	TRNE INLIN	;DRAWING A LINE?
	JRST GTPNTC	;YES, JUST STAR
	MOVSI T,(<ASCII/P/>)
	HLRZ B,(A)	;GET DATA BLOCK POINTER
	HRRZ B,1(B)
	HLRZ TT,(B)	;GET TEXT POINTER
	JUMPE TT,NOTEX	;ANY TEXT?
	MOVSI T,(<ASCII/T/>)
	HRRZ TT,(TT)	;GET SIZE BLOCK
	HLRZ TTT,(TT)
	TRNE TTT,400000
	TLO T,(<BYTE(7),"V">)
NOTEX:	MOVE TT,1(B)
	TLNE TT,ISPIN	;IS THIS POINT A PIN
	TRO T,"A"+"A"	;YES, TACK ON AN A
	TLNE TT,CPIN	;CONNECTOR PIN?
	TRO T,"C"+"C"	;TACK ON A "C"
	HLRZ TTT,(A)
	HLRZ TTT,1(TTT)
MD,<	SKIPN (TTT)
	SKIPE 1(TTT)	;ANY LINES ATTACHED HERE?
	TRO T,"L"10
>;MD
MPC,<
	JUMPE TTT,UPPNTA
UPPNTB:	SKIPE 1(TTT)
	JRST UPPNTC
	SKIPN TTT,(TTT)
	JRST UPPNTA
	TLNN TTT,-1
	JRST UPPNTB
UPPNTC:	TRO T,"L"10
UPPNTA:	LDB TTT,[POINT 3,TT,6]
	IOR T,(TTT)[	0
			"G"17
			"1"17
			"2"17
			"3"17
			0
			0
			0	]
>;MPC
GTPNTC:	TRO T,1		;MAKE IT ASCID
	PUSHJ P,TXTDPB
	JRST BLINKO	;OUTPUT THE THING
;.UPSET
.UPSET:	PUSHJ P,.SETUP
	PUSHJ P,.UPVCT
	SKIPA T,[1]		;FLASH THE ONES ON SCREEN ANY WAY
	MOVEI T,"S"+"S"+1	;ASCID S
	PUSHJ P,DCLSET
	JRST BLINKO		;AND BLINK IT

DCLSET:	PUSHJ P,TXTDPB
	HLRZ A,(A)	;POINTER TO THIS SET
STPNTR:	HRRZ TT,1(A)	;POINTER TO FIRST POINT IN SET
	JUMPE TT,SNOPNT	;NO POINT?
	MOVE T,1(TT)
	PUSHJ P,ONSCR
	JRST SNOPNT
MPC,<
	HLRZ T,(TT)
	HRRZ T,1(T)
	MOVE T,1(T)
	EQV T,SID	;ON THIS SIDE?
	TLNN T,FRONT
	TRNE BTHSDS	;OR BOTH SIDES ON?
	CAIA
	JRST SNOPNT	;NO
>;MPC
	MOVE T,1(TT)
	PUSHJ P,BIGAIV
	MOVEI T,"P"+"P"+1	;ASCID P
	PUSHJ P,TXTDPB
SNOPNT:	HLRZ T,1(A)		;POINTER TO FIRST BODY IN SET
	JUMPE T,SNOBOD		;NO BODY?
	MOVE T,1(T)		;X,Y
	PUSHJ P,BIGAIV
	MOVEI T,"B"+"B"+1	;ASCID B
	PUSHJ P,TXTDPB
SNOBOD:	HRRZ A,(A)		;NEXT WORD OF SET
	JUMPN A,STPNTR
	POPJ P,
;.UPLIN
.UPLIN:	PUSHJ P,.SETUP
	MOVE T,IPOINT
	PUSHJ P,ONSCR
	JRST [	MOVEI T,BIGPG
		JRST HYDPOG]
	PUSHJ P,VHLINE	;MAKE CROSS HAIRS (MAYBE)
	PUSHJ P,BIGAIV 	;PUT IN VECTOR TO THERE
	MOVEI T,"L"1+1	;ASCID L
	PUSHJ P,TXTDPB
	JRST BLINKO
;LINES, BOXES, BODY
DOSLPB:	MOVE T,MODE
	JRST @SLPBTB(T)

SLPBTB:	DRWBOX		;S
	NOSLPB		;B
	DOANGL		;P
	NOSLPB		;L
	DOANGL		;T
	ARROW		;A
	ARROW		;MA
	DOSPM		;SP
MD,<
	NOSLPB		;BT
	ARROW		;BTA
	NOSLPB		;E
	NOSLPB	;EI
	NOSLPB		;ET
	NOSLPB		;EP
	NOSLPB		;EG
	ARROW		;EA
>;MD
REPEAT NUMODES+<SLPBTB-.>,<NOSLPB
>

NOSLPB:	MOVEI T,ANGLPG
	JRST HYDPOG
DOSPM:	JRST @SPDISP

UPSTAR:	PUSHJ P,SPSETUP
	MOVE T,STARLOC
	PUSHJ P,.UPT
	JRST UPNONE
	MOVEI T,"*"+"*"+1
	PUSHJ P,TXTDPB
	JRST BLINKO

;UPREP
UPREP:	TRZ MCHG		;NOOP THE MAIN DISPLAY
	MOVEI T,CPOPJ
	MOVEM T,DSPDSP
	PUSHJ P,NOSLPB
	MOVEI T,MAINPG
	MOVEM T,PGLASS
	PUSHJ P,DPYSET
	PUSHJ P,SETBRT
	SETZ F,
	MOVE A,OLDTYP
	MOVEI D,0
	UNSCAL(D)
	ADD D,YOFF
	MOVE T,[(3*%LEFT+%RIGHT)/4]
	UNSCAL(T)
	ADD T,XOFF
	HRL D,T
	PUSHJ P,PUTBDL
	MOVE T,[(3*%RIGHT+%LEFT)/4]
	UNSCAL(T)
	ADD T,XOFF
	HRL D,T
	MOVE A,NEWTYP
	PUSHJ P,PUTBDL
	JRST MAINOU
;ANGLE (LINE OR SETBOX)
DOANGL:	TRNE INLIN	;ARE WE IN A LINE?
	SKIPN LINING	;REALLY????
	JRST NOSLPB	;NO
	MOVEI T,ANGLPG
	MOVEM T,PGLASS	;YES, USE PIECE OF GLASS 1
	PUSHJ P,DPYSET	;SET UP BUFFER
	PUSHJ P,SETBRT
	MOVE A,LINING	;GET POINTER TO CURRENT POINT WE ARE DRAWING FROM
	MOVE T,1(A)	;GET X,Y
	PUSHJ P,AIVECT	;DRAW INVIS. TO IT
MD,<	MOVE T,CURSE	;GET CURSOR POSITION
	TRNN ZIGZAG	;OTHER WAY?
	MOVE T,1(A)	;YES, GET POINT
	HLL T,CURSE	;GET CURSOR X
	TRNE ZIGZAG	;OTHER WAY?
	HLL T,1(A)	;NO, GET POINT X
	PUSHJ P,AVECT	;DRAW LINE TO IT
>;MD
	MOVE T,CURSE	;GET CURSOR POSITION
	PUSHJ P,AVECT	;DRAW LINE TO IT
	PUSHJ P,BLINKO	;OUTPUT IT
	POPJ P,
;SETBOX
DRWBOX:	TRNN STBOX
	JRST NOSLPB
	MOVEI T,ANGLPG
	MOVEM T,PGLASS
	PUSHJ P,DPYSET
	MOVE A,SETBOX
	MOVE T,1(A)
	PUSHJ P,AIVECT
	JRST NXTDRW
NXTDR1:	MOVE T,1(A)
	PUSHJ P,AVECT
NXTDRW:	HRRZ A,(A)
	JUMPN A,NXTDR1
	MOVE A,ENDBOX
	MOVE T,CURSE	;GET CURSOR POSITION
	TRNN ZIGZAG	;OTHER WAY?
	MOVE T,1(A)	;YES, GET POINT
	HLL T,CURSE	;GET CURSOR X
	TRNE ZIGZAG	;OTHER WAY?
	HLL T,1(A)	;NO, GET POINT X
	PUSHJ P,AVECT	;DRAW LINE TO IT
	MOVE T,CURSE	;GET CURSOR POSITION
	PUSHJ P,AVECT	;DRAW LINE TO IT
	JRST BLINKO
;ARROW
ARROW:	MOVE T,TXTCNT
	CAMN T,LPNTR
	POPJ P,
	MOVEM T,LPNTR
	MOVN T,ALTLIN
	MOVEM T,LINCNT		;INITIALIZE TO - NUMBER OF LINES WE HAVE MOVED UP
	SETZM CHRCNT
	MOVE T,TXTPNT
	ADD T,[POINT 7,1]
	MOVE TTT,TXTCNT
	MOVEM TTT,TOTCNT
ARROW1:	TLNN T,760000
	JRST [	HRR T,-1(T)
		TRNE T,-1
		JRST .+1
		JRST ARROW2]
	ILDB TT,T
	JUMPE TT,ARROW1
	SOSG TOTCNT
	JRST ARROW2
	AOS TTT,CHRCNT
	CAIL TTT,MAXLIN			;FORCED CRLF?
	JRST ARROW3			;YES
	CAMN TT,EOLCHR			;END OF LINE CHARACTER?
	JRST ARROW3
	JRST ARROW1

ARROW3:	SETZM CHRCNT
	AOS LINCNT
	JRST ARROW1

ARROW2:	MOVEI T,ANGLPG
	SKIPL TT,LINCNT		;IS TEXT ON SCREEN?
	CAILE TT,MAXPAG		;CHECK TOP ALSO
	JRST HYDPOG		;NO, HIDE CURSOR
	MOVEM T,PGLASS
	PUSHJ P,DPYSET
	MOVEI TT,106
	TDO TT,BSOR
	MOVN T,LINCNT
	IMUL T,CMUL+ACHRSZ
	IMULI T,CHGHT
IFN AYPOS,<	ADD T,[AYPOS]	>
	DPB T,[POINT 11,TT,21]		;Y PART
	MOVN T,CMUL+ACHRSZ
	IMULI T,CWIDTH
IFN AXPOS,<ADD T,[AXPOS]>
	DPB T,[POINT 11,TT,10]	;X PART
	PUSH P,TT
	TRO TT,40
	PUSHJ P,INSOU2			;DEPOSIT
	MOVE TT,CHRCNT
	IMUL TT,CMUL+ACHRSZ
	IMULI TT,CWIDTH
IFN AXPOS,<ADD TT,[AXPOS]>
	DPB TT,[POINT 11,(P),10]
	MOVE TT,(P)
	PUSHJ P,INSOUT			;STUFF IT
	POP P,TT
	LDB T,[POINT 11,TT,21]
	MOVE TTT,CMUL+ACHRSZ
	IMULI TTT,CHGHT
	ASH TTT,-2
	ADD T,TTT
	DPB T,[POINT 11,TT,21]
	PUSHJ P,INSOUT
	JRST DPYOUT
  