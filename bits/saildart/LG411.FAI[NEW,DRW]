;<WIRELIST>LG411.FAI.5, 14-NOV-75 14:40:02, EDIT BY HELLIWELL
SUBTTL LG411  --  PLUGS INTO DEC STYLE BACKPLANE
.ADD(ALLLOCS,LG411,LG411V)
BEGIN LG411

;TRANSFER VECTOR

^LG411V:	JRST LCINIT
	JRST LOCOUT
	JRST QUPIN
	JRST SLTOUT
	JRST GETSLT
	JRST BPINPN
	JRST RPNAM
MDPC,<
	JRST CSLTLP
	JRST GTSLTL
	JRST SLTLPN
	JRST PINPLS
MD,<
	JRST CSLTLT
	JRST GTCONP
	JRST CSLTLD
>;MD
>;MDPC
MWL,<
	JRST PINPN5
	JRST TRPNAM
	JRST RPNAMA
	JRST WLOCOUT
	JRST LOCPNC
	JRST LOCPNW
	JRST PRCNPN
	JRST GETLOC
	JRST PADPRT
	JRST RAYDIP
	JRST RAYCON
	JRST LOCPIN
	JRST LOCPNM
	JRST LOCPNP
	JRST CPNSEP
	JRST CPNMER
	JRST BPNPIN
	JRST CPARTP
	JRST PRBCN
	JRST SEQLOC
	JRST CONGIN
	JRST GTSLTT
>;MWL
	[ASCIZ/#LL#/]
	[ASCIZ/#/]
MDPC,<	[ASCIZ/CONNECTOR PIN ((#LL#)LL# OR (#LL#)"J"#-#)?/]
	[ASCIZ/DIP LOCATION ((#LL#)"Z"#?/]
	[ASCIZ/Z01/]
>;MDPC
MWL,<	[ASCIZ/DIP LOCATION "Z"#?/]
	[ASCIZ/CONNECTOR LOC (L OR "J"#)?/]
>;MWL

CHECK LG411V,LTVLEN


L2NSUB:	BLOCK L2NLEN
N2LSUB:	REPEAT N2LLEN, <	"?"
>
EN2L_.
NNN_1
FOR I IN(A,B,C,D,E,F,H,J,K,L,M,N,P,R,S,T,U,V,W,X,Y,Z)
<	L2N2L I,0
>
FOR I IN(G,I,O,Q)
<	L2N2L I,1B0
>
N2LMAX_NNN-1

ORG EN2L
LCINIT:	MOVE T,[L2NSUB,,L2N]			;BLT PNTR
	BLT T,L2N+L2NLEN+N2LLEN-1		;BLT IN THOSE LETTER-NUMBER CONVERSION TABLES
	MOVEI T,N2LMAX
	MOVEM T,MAXN2L				;BIGGEST # WE CAN CHANGE TO A LETTER
	POPJ P,

;PUT PIN ON END OF BODY LOCATION

BPINPN:	MOVE T,A
	PUSHJ P,PUTDEC
	POPJ P,

MWL,<
PINPN5:	MOVEI TT,5
	MOVEM TT,NDIG
	JRST NPUTDEC

TRPNAM:	TGETNUM
	JRST CPOPJ1

RPNAMA:	GETCH
	CAIL CHRREG,"0"
	CAILE CHRREG,"9"
	TDZA A,A
	PUSHJ P,TRPNAM
	JFCL
	JRST CPOPJ1
>;MWL

RPNAM:	GETNUM
	JRST CPOPJ1

MDPC,<
SLTLPN:PUSHJ P,SLTOUT
	HLRZ A,A
	JRST LOCOUT

PINPLS:HLRZ T,A
	PUSHJ P,PUTDEC
MD,<	TRNE A,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		POPJ P, ]
	TRNE A,L1416
	PUTBYT "+"
>;MD
	POPJ P,
>;MDPC
SUBTTL	LOCPIN -- PRINTS LOCATION AND PIN #
MWL,<

LOCPIN:PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	PUTBYT ")"
	POPJ P,

;LOCPNM -- PRINTS DIP LOC AND PIN # ON WIRE-WRAP SIDE

LOCPNM:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	MOVE TT,(P)
	TRNE TT,R1416
	JRST [	CAIL T,8
		ADDI T,2
		JRST LCPNM3 ]
	TRNE TT,L1416
	ADDI T,1
LCPNM3:	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/-2/]
		JRST LCPNM2 ]
	TRNE T,L1416
	PUTBYT "-"
LCPNM2:	PUTBYT ")"
	POPJ P,

LOCPNP:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		JRST LCPNP2 ]
	TRNE T,L1416
	PUTBYT "+"
LCPNP2:	PUTBYT ")"
	POPJ P,
;MORE WL ROUTINES

;SEPARATE CONNECTOR PIN BODY LOC (PADDLE) FROM PIN # (PIN, SIDE)
CPNSEP:	MOVE TT,T
	ANDI T,770000
	ANDI TT,7777
	POPJ P,

;MERGE CONNECTOR PIN BODY LOC WITH PIN #
CPNMER:	DPB TT,[POINT 12,T,35]
	POPJ P,

;PRINT CONNECTOR PIN NAME AS SEEN FROM BACK PANEL
BPNPIN:	PUSH P,T	;SAVE OFFSET HERE
	LDB T,[POINT 4,A,3]
	SKIPE T		;CONVERT 0 TO SPACE
	ADDI T,20	;ALL THE REST TO DIGITS
	ADDI T,40
	PUTBYT (T)
	LDB T,[POINT 5,A,8]
	LDB TT,[POINT 6,A,23]
	ADDI T,-1(TT)
	CAILE T,77
	SETZ T,
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,17]
	POP P,TT
	JUMPE TT,NOOFFS
	ADD T,TT
	JUMPLE T,[OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE ZERO OR NEGATIVE.
/]
		SETZ T,
		JRST NOOFFS]
	CAIG T,=99
	JRST NOOFFS
	OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE MORE THAN 2 DIGITS.
/]
	MOVEI T,=99
NOOFFS:	PUSHJ P,NDECOUT
	LDB T,[POINT 6,A,29]
	PUTBYT @N2L(T)
	LDB T,[POINT 6,A,35]
	JRST DECOUT

;OUTPUT EDGE PIN TO PARTITION FILE AS NUMBER
CPARTP:	LSH T,-14
	PUTBYT "0"(T)
	MOVEI T,2
	MOVEM T,NDIG
	MOVE T,TT
	LSH T,-5
	TRNE TT,1
	SUBI T,1
	JRST NDECOUT

>;MWL
SUBTTL	CSLTLT,LOCPNC,LOCPNW,PRCNPN -- CONNECTOR PIN PRINTING STUFF

MD,<
CSLTLT:	PUSHJ P,SLTOUT
	JRST CSLTLD
>;MD

MWL,<
PRBCN:	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
	JRST PRCNPN
>;MWL
SUBTTL	DETECT IF THIS PIN NAME IS "WILD"
;CALL WITH PIN NAME IN RH(A)
QUPIN:	PUSH P,A
	LSH A,-14
	ANDI A,77
	MOVE A,N2L(A)
	CAIE A,"U"
	JRST [	POP P,A
		POPJ P,]
	POP P,A
	ANDI A,3777
	PUSH P,B
	IDIVI A,=200
	POP P,B
	CAILE A,2
	TDZA A,A
	ADDI A,1
	JRST CPOPJ1

MWL,<
;THESE ROUTINES SHOULD BE MOVED EVENTUALLY, THEY ARE A FUNCTION OF THE BOARD TYPE

;CALL WITH LESSER OF TWO LOCS IN T
;GREATER IN TT
SEQLOC:	XOR T,TT
	TRNE T,770000		;LETTERS SHOULD BE THE SAME
	POPJ P,			;ELSE NOT IN SEQUENCE
	XOR T,TT
	SUB TT,T
	CAIN TT,1
	AOS (P)
	POPJ P,

CONGIN:	PUSH P,A
	HRRZ A,A
	JUMPE A,CONG1
	TRNN A,2
	JRST [	TRC A,3
		JRST CONG3]
	LSH A,-6
	ANDI A,77
	ADDI A,1
	CAIG A,=18
	JRST [	LSH A,6
		TRO A,1
		DPB A,[POINT 12,(P),35]
		POP P,A
		JRST CPOPJ1]
	LDB A,[POINT 6,(P),23]
	ADDI A,1
	PUSH P,T
	LDB T,[POINT 3,-1(P),11]		;GET EXTENT
	CAIG A,1(T)			;DOES THIS PADDLE EXIST?
	JRST [	POP P,T
		JRST CONG2]
	POP P,T
	POP P,A
	POPJ P,

;VARIOUS FLAVORS OF RETURN FOR CONGIN

CONG1:	HRRZ A,L2N+"A"
CONG2:	LSH A,6
	IOR A,L2N+"A"
	HRRZ A,A		;CLEAR ANY LH GARBAGE
	LSH A,6
	TRO A,1
CONG3:	HRRM A,(P)
	POP P,A
	JRST CPOPJ1
>;MWL
SUBTTL	SLTOUT -- PRINTS DIP BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|				   18      21        26    29          35
|                 18		    |   4   |    5    |  3  |     6     |
|___________________________________|_______|_________|_____|___________|
		  |			|	 |	 |	  |
<		  |			|	 |	 |	  |------># SLOT
		  |			|	 |	 |
<		  |			|	 |	 |--------------->L +RACK
		  |			|	 |
<		  |			|	 |----------------------->L RACK
		  |			|
<		  |			|--------------------------------># BAY
		  |
<		  |------------------------------------------------------>NOT USED

SLTOUT:	LDB T,[POINT 4,A,21]
	JUMPE T,.+2
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
MDPC,<	JUMPE TT,CPOPJ	>
	PUTBYT @N2L(TT)
	LDB TTT,[POINT 3,A,29]
	JUMPE TTT,SLTOU1
	ADD TT,TTT
	PUTBYT @N2L(TT)
SLTOU1:	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,35]
	JRST NPUTDEC
SUBTTL	CSLTOUT -- PRINTS CONNECTOR PIN BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|	    |			   18      21        26    29          35
|     6     |          12	    |   4   |    5    |  3  |     6     |
|___________|_______________________|_______|_________|_____|___________|
      |			|		|	 |	 |	  |
<     |			|		|	 |	 |	  |------># SLOT
      |			|		|	 |	 |
<     |			|		|	 |	 |--------------->L +RACK
      |			|		|	 |
<     |			|		|	 |----------------------->L RACK
      |			|		|
<     |			|		|--------------------------------># BAY
      |			|
<     |			|------------------------------------------------>NOT USED
      |
<     |------------------------------------------------------------------>L PADDLE

MD,<
CSLTOUT:
	JRST SLTOUT			;DO SAME AS BODY LOC.
IFN 0,<;REMOVE BY R HELLIWELL 31 MAR 75
;CONFUSING BECAUSE DISPLAYED AND PLOTTED CONNECTOR PIN DESIGNATOR
;WERE DIFFERENT FROM TYPEIN AND TYPEOUT.
	TRNN A,-1
	POPJ P,
	LDB T,[POINT 3,A,29]
	LDB TT,[POINT 6,A,5]
	TLNN A,4000			;"U" PIN?
	CAILE TT,1(T)			;ON BACK PANEL?
	JRST SLTOUT			;NO, TREAT LIKE A BODY
	LDB T,[POINT 4,A,21]		;SHOW ACTUAL BACKPANEL RACK LETTER
	SKIPE T
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
	LDB T,[POINT 6,A,5]
	ADDI TT,-1(T)
	PUTBYT @N2L(TT)
	LDB T,[POINT 6,A,35]
	JRST PUTDEC
>;IFN 0
>;MD
;CHECK TO MAKE SURE CHRREG IS A LETTER (CONVERTS LOWER CASE)

CHKLET:CAIL CHRREG,"a"		;CHECK FOR LOWER CASE
	CAILE CHRREG,"z"
	CAIA			;NOT LOWER CASE
	SUBI CHRREG,<"a"-"A">	;CONVERT TO UPPER CASE
	CAIL CHRREG,"A"
	CAILE CHRREG,"Z"
	POPJ P,
	JRST CPOPJ1		;ITS LEGAL

TWOLET:ROT CHRREG,-7*2	;MAKE IT ASCII
	MOVSI NUMREG,-TWOLEN
TWOLT1:	CAME CHRREG,TWOTAB(NUMREG)
	AOBJN NUMREG,TWOLT1
	JUMPGE NUMREG,CPOPJ
	MOVEI NUMREG,40(NUMREG)
	JRST CPOPJ1

TWOTAB:	ASCII/BT/
	ASCII/CB/
	ASCII/CR/
	ASCII/DL/
	ASCII/FC/
	ASCII/TP/
REPEAT 40-<.-TWOTAB>,<ASCII/??/>
TWOLEN__.-TWOTAB

MDPC,<
;THE CANONICAL DIP LOC READER WHERE DIP LOC IS OF FORM L#
;SLTLN -- READS A BAY/RACK/SLOT/DIPLOC OR JUST A DIPLOC
;CALL IS:
;	PUSHJ P,SLTLN
;	JRST ERROR
;	JRST CRLF.ONLY
;MD,<	JFCL	>	;LOC ONLY
;	(BRS,,LOC IN DESTIN)

SLTLN:	SETZM DESTIN
MPC,<	GETCH	>
MD,<	GETNUM
	CAIL NUMREG,20
	POPJ P,
	DPB NUMREG,[POINT 4,DESTIN,3]
>;MD
	CAIN CHRREG,12
	JUMPE NUMREG,CPOPJ1
	PUSHJ P,CHKLET
	POPJ P,
MD,<	PUSH P,D
	SKIPN D,L2N(CHRREG)
	JRST POPDJ
	DPB D,[POINT 5,DESTIN,8]
	GETNUM
	PUSHJ P,CHKLET
	JRST SLTLN1
	JUMPL D,SLTLN3		;GIOQ ILLEGAL FOR BRS
	POP P,D
	JUMPN NUMREG,SLTLN2
	LDB NUMREG,[POINT 5,DESTIN,8]
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	JUMPL CHRREG,[SUB CHRREG,NUMREG
		JRST SLTLN4]
	SUB CHRREG,NUMREG
	JUMPLE CHRREG,SLTLN4
	CAILE CHRREG,7	
	JRST SLTLN4
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
	JUMPE NUMREG,CPOPJ
	PUSHJ P,CHKLET
	JRST SLTLN8
SLTLN2:	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,17]
>;MD
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPE NUMREG,SLTLN5
SLTLN7:	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
MD,<	HLRZ NUMREG,DESTIN
	JUMPN NUMREG,CPOPJ3	;THIS RETURN IF BRS TYPED
>;MD
	JRST CPOPJ2		;THIS RETURN IF PC OR NO BRS

SLTLN5:	LDB NUMREG,[POINT 6,DESTIN,23]
	MOVE NUMREG,N2L(NUMREG)
	LSH NUMREG,7
	IOR CHRREG,NUMREG
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

MD,<
SLTLN1:	POP P,D
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
	LDB NUMREG,[POINT 9,DESTIN,8]
	CAIL NUMREG,40
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ2

SLTLN3:	MOVE D,N2L(D)		;GET LETTER BACK
	LSH D,7
	IOR CHRREG,D		;PUT 2 LETTERS IN CHRREG
	POP P,D			;RESTORE D
	PUSHJ P,SLTLN6
	POPJ P,
	JRST SLTLN9

SLTLN4:	PUSHJ P,SLTLN0
	POPJ P,
SLTLN9:	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

SLTLN8:	PUSH P,NUMREG
	PUSH P,CHRREG
	LDB NUMREG,[POINT 5,DESTIN,8]
	LDB CHRREG,[POINT 3,DESTIN,11]
	PUSHJ P,SLTLN0
	JRST [	POP P,CHRREG
		POP P,NUMREG
		POPJ P,]
	POP P,CHRREG
	POP P,NUMREG
	JRST SLTLN7

SLTLN0:	ADD CHRREG,NUMREG	;GET NUMBER BACK
	MOVE NUMREG,N2L(NUMREG)	;GET FIRST LETTER BACK
	MOVE CHRREG,N2L(CHRREG)	;GET SECOND LETTER BACK
	LSH NUMREG,7
	IOR CHRREG,NUMREG
SLTLN6:	LDB NUMREG,[POINT 4,DESTIN,3]
	JUMPN NUMREG,CPOPJ
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ1

POPDJ:	POP P,D
	POPJ P,
>;MD
>;MDPC

;GETSLT -- READS A BAY/RACK/SLOT #

;CALL IS:
;	PUSHJ P,GETSLT
;	JRST ERROR
;	JUMPE TT,CRLF.ONLY	;THIS IS OPTIONAL, OBVIOUSLY
;	MOVEM TT,BRS

MWL,<
GTSLTT:	SETZM DESTIN
	TGETNUM
	JRST GTSLT1
>;MWL

GETSLT:	SETZM DESTIN
	GETNUM
GTSLT1:	DPB NUMREG,[POINT 4,DESTIN,3]
	PUSHJ P,CHKLET
	JRST [	JUMPE NUMREG,CPOPJ1
		POPJ P,]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 5,DESTIN,8]
	GETNUM
	JUMPN NUMREG,GTSLT2
	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	LDB NUMREG,[POINT 5,DESTIN,8]
	SUB CHRREG,NUMREG
	JUMPL CHRREG,CPOPJ
	CAILE CHRREG,7
	POPJ P,
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
GTSLT2:	DPB NUMREG,[POINT 6,DESTIN,17]
	JRST CPOPJ1
MD,<
;	PUSHJ P,RDLLN
;	    ERROR RETURN
;	    SUCCESS RETURN

RDLLN:	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 12,DESTIN,29]
	GETNUM
	JUMPN NUMREG,CKCR
	PUSHJ P,CHKLET
	POPJ P,
	LDB TT,[POINT 6,DESTIN,29]
	DPB TT,[POINT 6,DESTIN,23]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,29]
	GETNUM
	JUMPE NUMREG,CPOPJ
CKCR:	LDB TT,[POINT 12,DESTIN,29]
	XOR TT,L2N+"U"
	TRNN TT,-1			;SAME LETTER?
	JRST [	CAILE NUMREG,3777	;YES, LEGAL NUMBER?
		POPJ P,			;NO
		HRRZ TT,L2N+"U"
		DPB TT,[POINT 6,DESTIN,23]
		TRO NUMREG,4000
		DPB NUMREG,[POINT 12,DESTIN,35]
		JRST CPOPJ1]
	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,35]
	JRST CPOPJ1
>;MD

;ARGUEMENT IN A, BASHES T
;FORMAT OF ARG:
;BITS
;  18   23 24   29 30   35
; /-----------------------\	IF N IS NON-ZERO THIS IS A DIP SLOT, K IS AN ARBITRARY LETTER,
; |       |               |	AND N IS THE 3 DIGIT SLOT #. OTHERWISE THIS IS A CONNECTOR
; |   K   |       N       |	LOC FORM: IF K IS GREATER THAN OR EQUAL TO 10 AND NOT THE CODE
; |       |               |	FOR U, THEN THIS REPRESENTS A SCOTCH-FLEX BODY, AND IS PRINTED
; \-----------------------/	AS "J" FOLLOWED BY THE NUMBER K-10. IF IT IS THE CODE FOR U,
;THEN IT IS THE WILD CONNECTOR BODY AND IS PRINTED AS "U". OTHERWISE, IT IS THE EDGE FINGER
;PADDLE LETTER AND IS PRINTED AS THE LETTER IT CONVERTS TO.

MWL,<
WLOCOUT:
	TRNE T,BWILD				;IF IT'S WILD
	PUTBYT WLDCHR				;SAY IT'S WILD
>;MWL
LOCOUT:	LDB T,[POINT 6,A,23]			;HIGH SIXBIT BYTE IN THE LOW HALF
	JUMPE T,CPOPJ				;IF NOTHING THERE, NOTHING
	TRNN A,7777				;CHECK FOR CONNECTOR LOC TYPE
	JRST [	CAME T,L2N+"U"		;CONN LOC, IS IT "U"?
		CAIGE T,10			; OR A LLN TYPE?
		JRST [	PUTBYT @N2L(T)		;YES, CONVERT TO ASCII AND PRINT
			POPJ P,]
		PUTBYT "J"			;NO, ITS A JN-N TYPE, PRINT A J
		PUTBYT "0"-10(T)		;AND THE EXCESS OVER 10 AS THE FIRST N, PRINT IT
		POPJ P,]
	PUTBYT @N2L(T)				;DIP LOC, PRINT THE LETTER HE USED JUST FOR LAUGHS
	MOVEI T,3				;110 DIP SLOTS MAKES FOR 3 DIGIT SLOT NUMBERS
	MOVEM T,NDIG				;TELL THE DECIMAL PRINT ROUTINE ABOUT IT
	LDB T,[POINT 12,A,35]			;GET THE SLOT #
	JRST NPUTDEC				;GO PRINT IT


MDPC,<
GTSLTL:	PUSHJ P,SLTLN
	POPJ P,
	JRST CPOPJ1
	MOVE NUMREG,"Z"+L2N
	DPB NUMREG,[POINT 6,DESTIN,23]
	JRST CPOPJ2
>;MDPC
;CSLTLP, CSLTLD
;TAKES ARG IN A, BASHES A AND T
;ARG FORMAT
;BITS
;  0     5 6    11 12   17 18 21 22 26 27 29 30   35
; /-------------------------------------------------\
; |       |   F   |   D   |     |     |     |       |
; |   C   |---------------|  B  |  R  |  H  |   S   |
; |       |       N       |     |     |     |       |
; \-------------------------------------------------/
;
;LOCPNC, LOCPNW
;ARGS IN A AND T, BASHES BOTH
;ARG IN A FORMAT
;BITS
;  0     5 6    11 12   17 18   23 24           35
; /-----------------------------------------------\
; |       |   F   |   D   |       |               |
; |   C   |---------------|   C   |       0       |
; |       |       N       |       |               |
; \-----------------------------------------------/
;ARG IN T NOT USED IN LOCPNC, AHS BWILD BIT IN RIGHT HALF
;
;PPCNPN
;ARG IN A, BASHES A AND T
;ARG FORMAT
;BITS
;  0                    17 18   23 24           35
; /-----------------------------------------------\
; |                       |       |   F   |   D   |
; |           X           |   C   |---------------|
; |                       |       |       N       |
; \-----------------------------------------------/
;
;X IS A "DON'T CARE" FIELD
;B IS BAY NUMBER
;R IS RACK LETTER, ENCODED
;H IS BOARD HIGTH MINUS ONE
;S IS SLOT NUMBER
;IF THE ABOVE ARE NOT PROVIDED, IT IS AS A GROUP. H IS ALWAYS 3 FOR AN LG411 OTHERWISE
;C IS THE CONNECTOR BODY SPECIFIER, AND DETERMINES HOW THE REMAINING BITS ARE INTERPERTED
;IF C IS THE CODE FOR U, THE FORM IS "U" FOLLOWED BY THE VALUE OF N AS A NUMBER, WITH THE
;	4000 BIT FORCED TO ZERO (THIS BIT SHOULD ALWAYS BE SET IN THIS FORM. (U#)
;IF C IS THE CODE FOR A, B, C OR D, THAT LETTER IS PRINTED FOLLOWED BY THE LETTER FOR WHICH
;	F IS THE CODE, FOLLOWED BY THE VALUE OF D (D SHOULD ALWAYS BE 1 OR 2 IN THIS FORM
;	(LL#)
;IF C IS GREATER THAN OR EQUAL TO 10 (OCTAL) AND IS NOT THE CODE FOR U, THIS IS A J#-# FORM
;WHERE THE J AND THE - ARE LITERALS. THE FIRST # IS THE EXCESS OF 10 WHICH C IS AND THE
;SECOND # IS N
;
;CSLTLP TRIES TO PRINT BAY, RACK, SLOT IF IN D, CSLTLD NEVER DOES

MWL,<
LOCPNW:
	TRNE T,BWILD				;IF IT'S WILD
	PUTBYT WLDCHR				;SAY IT'S WILD
LOCPNC:	PUSHJ P,LOCOUT				;PRINT THE CONNECTOR BODY LOCATION
	MOVS A,A				;PREPARE TO FALL THROUGH TO PRCNPN
>;MWL
MDPC,<
CSLTLP:
MD,<	CAML A,[050000,,0]			;IS IT A EDGECONNECTOR PIN?
	JRST [	TRNE A,-1			;NO, DOES BRS EXIST?
		PUSHJ P,SLTOUT			;YES, PRINT IT LIKE FOR A DIP
		JRST CSLTLD]			;GO PRINT JACK, PIN
	PUSHJ P,CSLTOUT				;YES, PRINT BAY RACK SLOT FOR EDGE FINGER
CSLTLD:
>;MD
	HLRZ A,A				;FLUSH BRS
	PUSH P,A				;SAVE A CLEAN COPY
	ANDI A,770000				;MAKE WILD PIN FORMAT
	PUSHJ P,LOCOUT				;SO THAT LOCOUT WILL PRINT IT LIKE A CONN LOC
	POP P,A	>;MDPC				;GET "ALL" THE BITS BACK
MWL,<
	HRRZS T,A				;COPY INTO T, CLEAR SOME CRUD
	CAIGE T,10000
	JRST [	TRNN T,7700
		JRST CSLSFX
		JRST CSLLDP]			>;MWL
	CAIGE T,100000				;J OR U FORM
	JRST CSLLDP				;NO DO LLN (LIKE PADDLES, MAN)
CSLSFX:
	LSH A,-=12
	CAME A,L2N+"U"				;U FORM
	PUTBYT "-"
CSSFX1:	ANDI T,7777				;MASK TO THE LAST NUMBER PART
	CAIG T,=26
	JRST PUTDEC				;GO PRINT IT
	POPJ P,

PRCNPN:	HRRZS T,A
	CAIGE T,10000
	TRNE T,7700
	CAIL T,100000
	JRST CSSFX1
CSLLDP:	LDB T,[POINT 6,A,29]			;GET SECOND LETTER
	PUTBYT @N2L(T)				;CONVERT TO ASCII AND PRINT
	MOVE T,A				;COPY TO T FOR PUTDEC
	ANDI T,77				;MASK TO TRAILING #
	JRST PUTDEC				;GO PRINT IT
MDPC,<
MD,<
;READS CONNECTOR PIN SPEC IN THE FORM THAT CSLTLP PRINTS IT IN EXCEPT WITH HALVS SWOPPED,
;AS SHOWN BELOW. BASHES NUMREG AND CHRREG. RETURNS ANSWER IN DESTIN.
;0 SKIP ERROR, 1 SKIP ONLY CR OR STARTS WITH "THERE EXISTS"
;2 SKIPS WIN BUT NO BRS, 3 SKIPS WIN AND BRS.
;MUST EXIT WITH A 12 IN CHRREG TO REALLY WIN.
;BITS
;  0   3 4   8 9  11 12   17 18   23 24   29 30   35
; /-------------------------------------------------\
; |     |     |     |       |       |   F   |   D   |
; |  B  |  R  |  H  |   S   |   C   |---------------|
; |     |     |     |       |       |       N       |
; \-------------------------------------------------/
;
GTCONP:	SETZM DESTIN				;CLEAR ALL FIELDS
	GETNUM					;GET FIRST STUFF
LAY,<	CAIE CHRREG,TEXIST>			;IF TERMINATOR WAS THERE EXISTS OR
	CAIN CHRREG,12				;IF TERMINATOR WAS LINEFEED
	JUMPE NUMREG,CPOPJ1			;AND NO NUMBER, TAKE 1 SKIP EXIT
	JUMPE NUMREG,NOBRS			;BRS STARTS WITH A #
	CAIG NUMREG,17				;IF NUMBER TOO BIG FOR FIELD
	PUSHJ P,CHKLET				; OR TERM ISN'T A LETTER
	POPJ P,					;LOSE
	SKIPGE CHRREG,L2N(CHRREG)		;YES, CONVERT AND IS IT A GOOD LETTER
	POPJ P,					;NO, LOSE
	DPB NUMREG,[POINT 4,DESTIN,3]		;BAY
	DPB CHRREG,[POINT 5,DESTIN,8]		;RACK
	GETNUM					;NEXT STUFF
	JUMPN NUMREG,GOTSLT			;IF #, IS SLOT #
	PUSHJ P,CHKLET				;TERMINATOR A LETTER?
	POPJ P,					;LOSE
	SKIPGE CHRREG,L2N(CHRREG)		;CONVERT AND TEST SECOND L
	POPJ P,					;BAD SECOND L
	LDB NUMREG,[POINT 5,DESTIN,8]		;GET BACK FIRST L
	SUB CHRREG,NUMREG			;FIGURE BOARD HIGTH - 1
	CAIE CHRREG,3				;WHICH MUST BE 3 (HIGTH 4)
	POPJ P,					;WRONG HIGTH, LOSE
	GETNUM					;GET NEXT STUFF
	JUMPE NUMREG,CPOPJ			;NLL REQUIRES FOLLOWING N
GOTSLT:	CAILE NUMREG,77				;SLOT # SHOULD FIT THE FIELD
	POPJ P,					;DOSEN'T, LOSE
	DPB NUMREG,[POINT 6,DESTIN,17]		;SLOT
	MOVEI NUMREG,3				;THIS BOARD IS 3+1 HIGH
	DPB NUMREG,[POINT 3,DESTIN,11]		;HIGTH
NOBRS:	PUSHJ P,CHKLET				;LETTER?
	POPJ P,					;LOSE, ALL CPIN SPECS BEGIN WITH A LETTER
	CAIN CHRREG,"J"				;IS IT A J?
	JRST SCOTCH				;YES, GO DO SCOTCH FLEX TYPE INPUT
	CAIN CHRREG,"U"				;IS IT A U?
	JRST UCONN				;YES, DO SPECIAL INPUT FORMAT
	CAILE CHRREG,"D"			;ELSE IT MUST BE A, B, C, OR D
	POPJ P,					;IT'S NOT, LOSE
	SUBI CHRREG,"A"-1			;CONVERT IT
	DPB CHRREG,[POINT 6,DESTIN,23]		;PUT IT TO BED
	GETNUM					;NEXT
	JUMPN NUMREG,CPOPJ			;N IS NOT THE SECOND OF LLN
	PUSHJ P,CHKLET				;A LETTER?
	POPJ P,					;NO, LOSE
	SKIPGE CHRREG,L2N(CHRREG)		;CONVERT, CHECK GIOQ
	POPJ P,					;LOSE
	DPB CHRREG,[POINT 6,DESTIN,29]		;PUT AWAY
	GETNUM
	JUMPE NUMREG,CPOPJ			;THERE MUST BE A #
	CAIG NUMREG,2				;NUMBER MUST BE 1 OR 2
	CAIE CHRREG,12				;TERMINATOR MUST BE <CRLF>
	POPJ P,					;LOSE
	DPB NUMREG,[POINT 6,DESTIN,35]		;PUT AWAY
GTCNPE:	HRROI NUMREG,0				;-1,,0
	TDNE NUMREG,DESTIN			;DID WE GET BRS?
	AOS (P)					;YES, EXTRA SKIP
	JRST CPOPJ2				;A WINNER!

SCOTCH:	GETNUM					;NEXT STUFF
	JUMPE NUMREG,CPOPJ			;NUMBER MUST BE THERE
	CAIG NUMREG,7				;NUMBER MUST BE 1 THRU 7
	CAIE CHRREG,"-"				;TERMINATOR MUST BE -
	POPJ P,					;LOSE
	ADDI NUMREG,10				;CONVERT TO SPECIAL CODE
	DPB NUMREG,[POINT 6,DESTIN,23]		;PUT AWAY
	GETNUM
	JUMPE NUMREG,CPOPJ			;NUMBER MUST BE THERE
	CAIG NUMREG,=26				;MUST BE 1 THRU 26.
	CAIE CHRREG,12				;TERMINATOR MUST BE END OF LINE
	POPJ P,					;LOSE
DEPL12:	DPB NUMREG,[POINT 12,DESTIN,35]		;PUT AWAY
	JRST GTCNPE				;WINNER, BUT GO SEE HOW BIG OF  A ONE


UCONN:	MOVE CHRREG,L2N+"U"			;GET U CODE
	DPB CHRREG,[POINT 6,DESTIN,23]		;PUT AWAY
	GETNUM
	JUMPE NUMREG,CPOPJ			;# MUST BE THERE
	CAIG NUMREG,3777			; MUST BE 1 THRU 3777
	CAIE CHRREG,12				;TERM MUST BE <CRLF>
	POPJ P,					;LOSE
	IORI NUMREG,4000			;SET MAGIC BIT
	JRST DEPL12				;PUT AWAY AND WIN
>;MD
>;MDPC
MWL,<
;ARG IN G IS 0 THROUGH # OF PADDLES + # OF JACKS - 1, AND
;IS AN INDEX INTO THEM IN SOME RANDOM ORDER TO GET THEM PRINTED. BASHES NOTHING.
PADPRT:	TRNN G,777774				;EDGE FINGER?
	JRST [	PUTBYT 40			;YES
		PUTBYT "A"(G)			;PRINT PADDLE
		PUTBYT 40			;MUST BE THREE CHARACTERS, OR AT LEAST USED TO
		POPJ P,]			;DONE
	PUTBYT "J"				;NO, SCORCH-FLACKS, J# FORM, PRINT THE J
	PUTBYT "0"-3(G)				;PRINT THE # AFTER ADJUSTING FOR THE OFFSET OF 3
	PUTBYT 40				;MUST BE THREE CHARACTERS, OR AT LEAST USED TO MUST BE
	POPJ P,					;DONE

;READS DIP LOC OR CONNECTOR BODY LOC INTO TT IN FORM THAT LOCOUT PRINTS.
;ALSO BASHES NUMREG AND CHRREG
GETLOC:	SETZ TT,			;SO ALL THE FEILDS ARE CLEAN
	GETCH
	PUSHJ P,CHKLET			;A LETTER
	POPJ P,				;SORRY, HAS TO BE A LETTER
	CAIN CHRREG,"J"			;A J FORM?
	JRST SCARCH			;YEP, GO TO IT
	CAIN CHRREG,"U"			;A WILD ONE?
	JRST [	MOVE TT,L2N+"U"		;YEP, GET A CODED U
		LSH TT,=12		;PUT IT IN POSITION
		GETCH			;JUST TO CHECK TERMINATOR
		JRST SCARC1]		;GO ACTUALLY CHECK IT
	SKIPGE TT,L2N(CHRREG)		;CONVERT AND CHECK GIOQ
	POPJ P,					;I'M SORRY, YOU DIDN'T BEAT THE REAPER
	GETNUM
	CAIE CHRREG,12				;RIGHT TERMINATOR?
	POPJ P,					;NO, LOSE
	LSH TT,=12				;PUT LETTER CODE IN ITS PLACE
	JUMPE NUMREG,CPOPJ1			;NO NUMBER IS CONNECTOR LOC
	DPB NUMREG,[POINT 12,TT,35]		;PUT NUMBER AWAY
	JRST CPOPJ2				;NUMBER MEANS DIP SLOT

SCARCH:	GETNUM
	JUMPE NUMREG,CPOPJ			;HAVE TO HAVE A NUMBER
	ADDI NUMREG,10				;ENCODE THE #
	DPB NUMREG,[POINT 6,TT,23]		;PUT IT IN ITS PLACE
SCARC1:	CAIN CHRREG,12				;IS THE TERMINATOR <CRLF>?
	AOS (P)					;YES, TURN THE FOLLOWING POPJ INTO A POPJ1
	POPJ P,					;DONE

;RAYDIP
;ARGS IN A AND T, BASHES BOTH.
;PRINTS DIP SLOT # AS 3 DIGITS, THEN 2 SPACES, THEN PIN # AS 2 DIGITS.
;TOTAL OF 7 COLUMNS.
;ARG IN A FORMAT
;BITS
;  0                    17 18   23 24           35
; /-----------------------------------------------\
; !         PIN #         !   X   !    SLOT  #    !
; \-----------------------------------------------/
;T HAS L1416 BIT IN THE RIGHT HALF.
;
RAYDIP:	PUSH P,T				;SAVE FOR LATER
	MOVEI T,3				;RAY FORMAT FOR DIP LOC IS JUST 3 DIGIT #
	MOVEM T,NDIG				;ARRANGE TO PRINT IT THAT WAY
	LDB T,[POINT 12,A,35]			;GET THE DIP LOC #
	PUSHJ P,NPUTDEC				;AND PRINT IT
	PUTSTR [ASCIZ /   /]			;GET TO THE PIN# COLUMN
	MOVEI T,2				;PIN #S ARE 2 DIGITS
	MOVEM T,NDIG				;ARRANGE TO PRINT THEM SO
	LDB T,[POINT 6,A,17]			;GET THE PIN #
	POP P,A					;GET THAT FLAG WORD BACK
	TRNE A,L1416				;IS IT A 14 PIN DIP IN A 16 PIN SLOT
	AOJA T,NPUTDEC				;YES, REAL PIN # IS LOGICAL PIN # + 1, AND PRINT IT
	JRST NPUTDEC				;NO, JUST GO PRINT THE PIN #

;RAYCON
;ARG IN A, BASHES A AND T
;PRINTS "JN    MM" WHERE N IS JACK # (ONE DIGIT) AND MM IS 2 DIGIT PIN #
;OR PRINTS "KI     L" WHERE K IS PADDLE LETTER, L IS FINGER LETTER, AND I IS THE 1 DIGITSIDE OF CARD #.
;I8 COLUMNS EITHER WAY.
;ARG FORMAT IS THE SAME AS FOR PPCNPN, BUT FUNNINESS ABOUT WHICH HALF.
;
RAYCON:	LDB T,[POINT 6,A,23]			;GET THE BODY LOC PART
	CAIG T,4				;EDGE FINGER TYPE?
	JRST [	PUTBYT "A"-1(T)			;YES, PRINT PADDLE LETTER
		LDB T,[POINT 6,A,17]
		PUSHJ P,PUTDEC
		PUTSTR [ASCIZ /     /]
		LDB T,[POINT 6,A,11]
		PUTBYT @N2L(T)
		POPJ P,]			; AND DONE
	PUTSTR [ASCIZ /J/]			;MUST BE SCOTCH-FLEX JACK, PRINT INVARIANT PART
	PUTBYT "0"-10(T)			;PRINT JACK #. (ENCODING IS BY ADDING 10 TO THE VALUE OF THE DIGIT
	PUTSTR [ASCIZ /    /]			;GET TO THE PIN NUMBER FIELD
	MOVEI T,2				;PIN #S ARE 2 DIGITS
	MOVEM T,NDIG				;ARRANGE TO PRINT THEM SO
	HLRZ T,A				;PUT PIN # IN THE RIGHT AC
	ANDI T,7777				;MASK OFF EXTRAENOUS CRUD
	JRST NPUTDEC				;AND PRINT IT
>;MWL
BEND LG411

MDWL,<
.ADD(ALLWW,LG411,WLG411)
MWL,<
SUBTTL WIRE WRAP ROUTINES  --  LG411
BEGIN WLG411


^^WLG411_.

	-1		;ISWW
	JRST CPOPJ	;WWINIT
	JRST MAPRC
	JRST MAPPAD
	JRST PINMAP
	JRST PINBIT
	JRST CPOPJ	;VGCON NOT WRITTEN YET
	JRST DISTPP	;POINT TO POINT DISTANCE
	JRST FPWR
	JRST FGND
	JRST PGPRTM
	JRST PGPRNT
	JRST GNDCLR
	JRST WAGGND
	JRST GNDOUT
	JRST VCCOUT
	JRST RAYGEN
	=6		;NROWS
	=19		;NCOLS
	=0		;NCLPRG
	=110		;DIPSLT, NOT = NROWS*NCOLS BECAUSE FIRST ROW NOT FULL
	SETPAD(=11)	;NPADS
	-=18,,1		;PADLET
	-=2,,1		;PADPIN
	=10		;FRACTN
	=200*2		;0.2" INSULATION AROUND EACH POST
	=750*2		;0.75" BARE WIRE AROUND EACH POST
	0		;NEXTR
CHECK WLG411,WTVLEN
PINTAB:	FOR Y_=700,0,-=100
<	=300,,Y
>
	FOR Y_0,=700,=100
<	0,,Y
>

XDIPSP_=500
YDIPSP_=1000
XDIPOR_=400
YDIPOR_=700

DIPLOC:	FOR X_=14,0,-1
<	X*XDIPSP+XDIPOR,,5*YDIPSP+YDIPOR
>
	FOR Y_4,0,-1
<	FOR X_=18,0,-1
<	X*XDIPSP+XDIPOR,,Y*YDIPSP+YDIPOR
>
>

VFLXPN:	FOR X_=100,0,-=100
<	FOR Y_=1200,0,-=100
<	X,,Y
>
>

HFLXPN:	FOR Y_=100,0,-=100
<	FOR X_0,=1200,=100
<	X,,Y
>
>

FLXLOC:	=9600,,=5900
	=9200,,=5900
	=8800,,=5900
	=8400,,=5900
	=8000,,=5900
	=5900,,=7000
	=3000,,=7000

FLXTYP:	FOR X_1,5,1
<	VFLXPN-1(T)
>
	FOR X_6,7,1
<	HFLXPN-1(T)
>

PADLOC:	=100,,=200
	=2800,,=200
	=5400,,=200
	=8100,,=200

PADPIN:	FOR X1_0,=1400,=700
<	FOR X_0,=500,=100
<	X1+X,,0
>
>
MAPIT:	JUMPL A,CONMAP
	LDB TT,LOW12
	JUMPE TT,CPOPJ
	CAMLE TT,DIPSLT
	POPJ P,
	MOVE T,DIPLOC-1(TT)
	LDB TT,B6AT17
	TLNE A,MAPPWR!MAPGND
	JRST PINPGP
	JUMPE TT,CPOPJ
	CAILE TT,=16
	POPJ P,
	CAIE B,=14
	JRST MAPIT0
	CAILE TT,=14
	POPJ P,
	ADDI TT,1
	ADD A,[1,,0]
	TLO A,PLUS1
MAPIT0:	ADD T,PINTAB-1(TT)
	SETZB TT,TTT
	JRST CPOPJ2

PINPGP:	JUMPN TT,CPOPJ
	SETZ TTT,
	TLNN A,MAPPWR
	JRST [	ADD T,[=300,,=800]
		MOVSI TT,GND
		JRST CPOPJ2]
	ADDI T,=800
	MOVSI TT,PWR
	JRST CPOPJ2

CONMAP:	AOS (P)
	LDB TT,B6AT23
	JUMPE TT,CPOPJ
	CAIG TT,4
	JRST [	LDB T,[POINT 6,A,29]
		JUMPE T,CPOPJ
		CAILE T,=18
		POPJ P,
		MOVE T,PADPIN-1(T)
		ADD T,PADLOC-1(TT)
		LDB TT,[POINT 6,A,35]
		CAIN TT,1
		JRST CNMPWN
		CAIE TT,2
		POPJ P,
		ADDI T,=200
		JRST CNMPWN]
	CAILE TT,10
	CAILE TT,10+=7
	POPJ P,
	LDB T,LOW12
	JUMPE T,CPOPJ
	CAILE T,=26
	POPJ P,
	MOVE T,@FLXTYP-10-1(TT)
	ADD T,FLXLOC-10-1(TT)
CNMPWN:	SETZB TT,TTT
	JRST CPOPJ1
PINBIT:	PUSHJ P,MAPIT
	TDZA TT,TT
	SETZ TT,
	POPJ P,

PINMAP:	PUSHJ P,MAPIT
	JRST PINERR
	JRST CONERR
	JRST CPOPJ1

PINERR:	PUSH P,A
	TLZ A,777700
	PUSHJ P,LOCPIN
	JRST CPNER1

CONERR:	PUSH P,A
	HRL A,A
	TRZ A,7777
	PUSHJ P,LOCPNC
CPNER1:	OUTSTR [ASCIZ/ MAPING ERROR.
/]
	POP P,A
	POPJ P,
MAPRC:	HLRZ TT,T
	CAMG TT,NROWS
	SKIPG TT
	POPJ P,
	HRRE T,T
	CAMG T,NCOLS
	SOSGE T
	POPJ P,
	IMUL TT,NCOLS
	SUB TT,T
	SUBI TT,4
	CAMG TT,DIPSLT
	SKIPG T,TT
	POPJ P,
	MOVE TT,"Z"+L2N
	DPB TT,[POINT 6,T,23]
	JRST CPOPJ1
FPWR:	JUMPL A,FPWRC
	TLZ A,77!PLUS1!PLUS2
	TLO A,MAPPWR
	POPJ P,

FPWRC:	LDB T,B6AT23
	CAIG T,4
	JRST [	JUMPE T,[	OUTSTR [ASCIZ/PAHRGH!!
/]
				JRST ERRET]
		MOVE A,EDGCRD-1(T)
		TLO A,MAPPWR
		POPJ P,]
	CAILE T,10
	CAILE T,10+=7
	JRST [	OUTSTR [ASCIZ/PARGLE!!
/]
		JRST ERRET]
	MOVE A,FLXCRD-10-1(T)
	TLO A,MAPPWR
	POPJ P,

FGND:	JUMPL A,FGNDC
	TLZ A,77!PLUS1!PLUS2
	TLO A,MAPGND
	POPJ P,

FGNDC:	LDB T,B6AT23
	CAIG T,4
	JRST [	JUMPE T,[	OUTSTR [ASCIZ/GAHRGH!!
/]
				JRST ERRET]
		MOVE A,EDGCRD-1(T)
		TLO A,MAPGND
		POPJ P,]
	CAILE T,10
	CAILE T,10+=7
	JRST [	OUTSTR [ASCIZ/GARGLE!!
/]
		JRST ERRET]
	MOVE A,FLXCRD-10-1(T)
	TLO A,MAPGND
	POPJ P,

FLXCRD:	FOR N IN(=16,=17,=17,=18,=19,=3,=9)
<	<<"Z"-"A"+1-4>=12>!N
>

EDGCRD:	FOR N IN(=109,=104,=98,=93)
<	<<"Z"-"A"+1-4>=12>!N
>
PGPRNT:	TDZA TT,TT
PGPRTM:	MOVNI TT,1
	JUMPL A,PGCPNT
	PUTBYT "Z"
	LDB T,LOW12
	PUSHJ P,DECOUT
	PUTBYT "("
	TLNN A,MAPPWR!MAPGND
	JRST NPGPNT
	MOVEI T,"G"
	TLNN A,MAPGND
	MOVEI T,"V"
	PUTBYT (T)
PGPN1:	PUTBYT ")"
	POPJ P,

PGCPNT:	PUSH P,A
	HRL A,A
	TRZ A,7777
	PUSHJ P,LOCPNC
	POP P,A
	POPJ P,

NPGPNT:	LDB T,B6AT17
	JUMPN TT,NPGPN1
	TLNE A,PLUS1
	SUBI T,1
NPGPN1:	TLNE A,PLUS2
	PUTSTR [ASCIZ/BURP!!/]
	PUSHJ P,DECOUT
	TLNE A,PLUS1
	PUTBYT @1+[	"-"
			"+"](TT)
	JRST PGPN1
RAYGEN:	JUMPL A,RAYRP1
	TLNN A,MAPPWR!MAPGND
	JRST RAYDIP
	MOVEI T,3
	MOVEM T,NDIG
	LDB T,LOW12
	PUSHJ P,NDECOUT
	PUTSTR [ASCIZ/   0/]
	MOVEI T,"V"
	TLNN A,MAPPWR
	MOVEI T,"G"
	PUTBYT (T)
	POPJ P,

RAYRP1:	OUTSTR [ASCIZ/CONNECTOR BECAME GENERATED PIN.
/]
	JRST ERRET
MAPPAD:	JUMPE T,[	HRREI T,1(F)
			PUSHJ P,MAPPA1
			ANDCMI T,770000
			POPJ P,]
MAPPA1:	CAIG T,4
	JRST [	LSH T,6
		IORI T,(TT)
		LSH T,6
		IORI T,(TTT)
		POPJ P,]
	ADDI T,10-4
	LSH T,=11
	CAILE TT,0
	IORI T,-1(TT)
	LSH T,1
	ADDI T,(TTT)
	POPJ P,

	FOR NAME IN(GNDCLR,WAGGND,GNDOUT,VCCOUT)
<NAME:	OUTSTR [ASCIZ/NAME IS NOT IMPLEMENTED FOR THE AUGAT LG411 BOARD.
/]
	JRST ERRET
>

BEND WLG411
>;MWL
>;MDWL
