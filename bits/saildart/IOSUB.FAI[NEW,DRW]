;<DRAW>IOSUB.FAI.50, 15-NOV-75 19:10:22, EDIT BY HELLIWELL
VERSION(IOSUB,5)
;FILENAME SCANNER, ENTER WITH DEFAULT EXTENSION IN T

;CHANGE REMEMBERED NAME
CENAME:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/NEW REMEMBERED /]
	SETZ T,
	PUSHJ P,SETNAM
	SETZM FILNAM		;CLEAR IF CR ALONE
	ENTPPN			;GET SOME PPN
	MOVE T,FILNAM
	MOVEM T,LSTNAM
	HLLZ T,FILEXT
	MOVEM T,LSTEXT
	MOVE T,FILPPN
	MOVEM T,LSTPPN
	JRST FILEUP

ZENAME:	MOVEI B,LSTNAM
	JRST CLRSIG

SETLST:
NODEC,<	SETZ TT,
	DSKPPN TT,
	MOVEM TT,FILPPN
>;NODEC
DEC,<	SETZM FILPPN	>	;WRITE ON CURRENT AREA
	MOVEM T,FILEXT
SETLS1:	SKIPN T,LSTNAM
	POPJ P,
	MOVEM T,FILNAM
	MOVSI T,(<BYTE(9)DEFPRO>)
	MOVEM T,FILDAT
	SETOM THEREXISTS
	JRST CPOPJ1

SETNAM:	MOVEM T,DEFEXT
NAMCON:	MOVE T,DEFEXT
	MOVEM T,FILEXT
	SETZM THEREXISTS
	MOVSI T,(<BYTE(9)DEFPRO>)
	MOVEM T,FILDAT
	SETZ T,	;CLEAR THIS WORD
NODEC,<	DSKPPN T,		;HIS OWN PPN
	MOVEM T,FILPPN
>;NODEC
DEC,<	SETZM FILPPN	>
	SETOM NOPPNF		;ASSUME NO PPN WILL BE TYPED
	TLNN M,DSKACT!MACACT
	OUTSTR [ASCIZ/FILENAME?/]
	PUSHJ P,GETNAM		;SCAN FILENAME
	CAIN C,12
	JUMPE T,CPOPJ		;LET HIM OUT IF HE TYPES NOTHING
VIROS,<
	SETZM VSTBUF
	CAIE C,74
	JRST NOVDIR
	JUMPN T,NOVDIR
	PUSHJ P,VSTPPN		;CONVERT STRING TO PPN
	JRST ILLNAM
	SETZM NOPPNF		;FLAG PPN TYPED
	MOVEM T,FILPPN
	PUSHJ P,GETNAM		;NOW GET FILENAME
NOVDIR:
>;VIROS
	JUMPN T,NOLSTG
	CAIE C,TEXIST		;DOES HE WANT SAME AS INPUT?
	JRST NOLSTG
	PUSHJ P,GETNAM
	JUMPN T,ILLNAM
	MOVE T,LSTPPN
	MOVEM T,FILPPN
	SKIPN T,LSTNAM		;YES, GET IT(WILL LOSE IF 0)
	JRST [	PUSHJ P,SCARF
		OUTSTR[ASCIZ/NO REMEBERED NAME!
/]
		JRST NAMCON]
	SETOM THEREXISTS
NOLSTG:
NOITS,<
	MOVEM T,FILNAM		;SAVE FILENAME
	CAIN C,"["		;ANY EXTENSION?
	JRST NOEXT		;NO
	CAIN C,12
	JRST NOEXT		;NO
	CAIE C,"."
	JRST [	ILLNAM:	PUSHJ P,INNERR
			JRST NAMCON]
	PUSHJ P,GETWRD		;SCAN EXTENSION
	HLLZM T,FILEXT		;SAVE IT.
	SETZM THEREXISTS	;RE-ENABLE EXIST CHECK
NOEXT:	CAIN C,12		;HERE TO SCAN PPN
	JRST CPOPJ1		;LEAVE NOW
	SETZM THEREXISTS	;RE-ENABLE EXIST CHECK
VIROS,<	SKIPN VSTBUF	>	;DID WE USE <DIRECTORY> FORMAT?
	CAIE C,"["
	JRST ILLNAM		;MUST BE [
NOCMU,<
	PUSHJ P,GETPPN
	JRST ILLNAM
	HRLM T,FILPPN		;AND SAVE
	CAIE C,","		;BETTER BE COMMA
	JRST NOPRG
	PUSHJ P,GETPPN
	JRST ILLNAM
	HRRM T,FILPPN		;AND SAVE
>;NOCMU
CMU,<	SETZM PPNBUF		;CLEAR OUT A BUFFER FOR THE PPN
	SETZM PPNBUF+1
	SETZM PPNBUF+2
	MOVE A,[POINT 7,PPNBUF]
	MOVEI TTT,=13		;13 CHARACTERS AT MOST!
CMUPP1:	PUSHJ P,GETLIN		;GET A CHAR
	CAIL C,"0"		;0-9 ARE LEGAL IN PPN'S
	CAILE C,"9"
	CAIN C,","		;SO IS COMMA
	JRST CMUPP2		;SO GO STORE IT
	CAIL C,"a"		;CONVERT LOWER CASE TO UPPER CASE
	CAILE C,"z"
	JRST .+2
	SUBI C,40		;CONVERT IT
	CAIL C,"A"		;IS IT A LETTER
	CAILE C,"Z"
	JRST CMUPP3		;NO, MUST BE THE END
CMUPP2:	IDPB C,A
	SOJG TTT,CMUPP1		;AND GO GET ANOTHER CHAR UNLES WE HAVE 13
	PUSHJ P,GETLIN		;IN WHICH CASE WE GET THE "]" (WE HOPE!)
CMUPP3:	MOVE A,[XWD FILPPN,PPNBUF]
	CMUDEC A,		;CONVERT THE PPN TO DEC FORMAT
	JRST ILLNAM		;WHOOPS, BAD PPN
>;CMU
NOPRG:	SETZ T,
	CAIN C,"]"		;BETTER END WITH THIS
	PUSHJ P,GETNAM		;MAKE SURE HE DIDN'T TYPE TO MUCH
	JUMPN T,ILLNAM		;LOSE IF HE DID
	CAIE C,12
	JRST ILLNAM		;DIDN'T END WITH LF
	SETZM NOPPNF		;HE TYPED A PPN!
	JRST CPOPJ1
>;NOITS
ITS,<
	CAIE C,";"
	JRST NOLST1
	SETZM NOPPNF
	MOVEM T,FILPPN
	PUSHJ P,GETNAM
	JUMPE T,CPOPJ	;STILL NOT REASONABLE
NOLST1:	CAIE C,40
	CAIN C,12
	SKIPA
	JRST [	ILLNAM:	PUSHJ P,INNERR
			JRST NAMCON]
	MOVEM T,FILNAM
	CAIN C,12
	JRST CPOPJ1	;DONE
	PUSHJ P,GETNAM	;GET SECOND FILE NAME
	CAIE C,12
	JRST ILLNAM	;EXTRA GARBAGE AT END
	JUMPE T,ILLNAM
	MOVEM T,FILEXT
	JRST CPOPJ1
>;ITS

IFN DECSW!IIISW,<
GETPPN:	PUSHJ P,GETOCT
	JUMPE T,CPOPJ
	CAIG T,777777
	AOS (P)
	POPJ P,

GETOCT:	SETZ T,
GETNU1:	PUSHJ P,GETLIN
	CAIE C,15
	CAIN C,40
	JRST GETNU1
	CAIL C,"0"
	CAILE C,"7"
	POPJ P,
	LSH T,3
	ADDI T,-"0"(C)
	JRST GETNU1
>;IFN DECSW!IIISW

STANFO,<
GETPPN:	PUSHJ P,GETWRD
	JUMPE T,CPOPJ
	TRNE T,-1
	POPJ P,
	HLRZ T,T		;ONLY 3 LETTERS
	TRNN T,7777
	LSH T,-14
	TRNN T,77
	LSH T,-6
	JRST CPOPJ1
>;STANFO
	
NOITS,<GETNAM:>
GETWRD:	SETZ T,			;WORD WILL ACCUMULATE HERE
	MOVE A,[POINT 6,T]	;BYTE POINTER TO DEPOSIT CHARACTERS
CHRGET:	PUSHJ P,GETLIN		;READ A CHAR
ISCHRX:	CAIE C,15		;IGNORE LF'S
	CAIN C,40		;AND SPACES
	JRST CHRGET
	CAIL C,"0"		;NUMBERS ARE LEGAL
	CAILE C,"9"
	CAIA
	JRST CHROK
	CAILE C,"z"
	POPJ P,			;BREAK CHAR.
	CAIL C,"a"
	SUBI C,40		;CHANGE LOWER CASE TO UPPER
	CAIL C,"A"		;NOW ONLY UPPER CASE LETTERS ARE LEGAL
	CAILE C,"Z"
	POPJ P,			;BREAK
CHROK:	SUBI C,40		;NO, MAKE IT SIXBIT
	TLNE A,770000		;END OF WORD?
	IDPB C,A		;STORE
	JRST CHRGET

;ITS NAME SCANNER
ITS,<
GETNAM:	SETZ T,			;WORD WILL ACCUMULATE HERE
	MOVE A,[POINT 6,T]	;BYTE POINTER TO DEPOSIT CHARACTERS
GETNM1:	PUSHJ P,GETLIN		;READ A CHAR
	CAIE C,15		;IGNORE LF'S
	CAIN C,40		;AND SPACES
	JRST GETNM1
	SKIPA
GETNM2:	PUSHJ P,GETLIN
	CAIN C,15
	JRST GETNM2
	CAIL C,"a"
	CAILE C,"z"
	CAIA
	SUBI C,40		;LOWERCASE
	CAILE C,40		;BREAK ON SPACE
	CAILE C,"_"
	POPJ P,			;NOT SIXBIT CHARACTER
	CAIE C,";"
	CAIN C,":"
	POPJ P,
	SUBI C,40		;NO, MAKE IT SIXBIT
	TLNE A,770000		;END OF WORD?
	IDPB C,A		;STORE
	JRST GETNM2
>;ITS
;VIROR DIRECTORY STRING TO PPN CONVERTER

VIROS,<
VSTPPN:	MOVE T,[POINT 7,VSTBUF]
VSTPP1:	PUSHJ P,GETLIN
	CAIL C,"A"+40
	CAILE C,"Z"+40
	CAIA
	SUBI C,40
	CAIN C,240
	JRST RECOGN
	CAIN C,76
	JRST EXACTM
	CAIE C,ALTMOD
	CAIN C,12
	POPJ P,
	CAMN T,[VSTEND]
	POPJ P,
	IDPB C,T
	JRST VSTPP1

EXACTM:	SETZ TT,
	IDPB TT,T
	PUSH P,1
	PUSH P,2
	SETZ 1,
	HRROI 2,VSTBUF
	STDIR
	JRST VSTPPX
	JRST VSTPPX
	POP P,2
	POP P,1
	JRST VSTWIN

RECOGN:	MOVEI C,ALTMOD		;IF ERROR, WE PRINT, NOT INNERR
	SETZ TT,
	MOVE TTT,T
	IDPB TT,TTT
	SETOM TYPFLG
	TLNE M,DSKACT!MACACT
	SETZM TYPFLG
	PUSH P,1
	PUSH P,2
	PUSH P,T
	MOVNI 1,1
	HRROI 2,VSTBUF
	STDIR
	JRST VSTPPY
	JRST VSTPPW
	POP P,1
	SKIPN TYPFLG
	JRST VSTPPB
	PUSH P,3
VSTPPZ:	CAMN 1,2	;DONE YET?
	JRST VSTPPA
	ILDB 3,1
	OUTCHR 3
	JRST VSTPPZ

VSTPPA:	POP P,3
	OUTCHR[76]
VSTPPB:	POP P,2
	POP P,1
	JRST VSTWIN

VSTPPW:	POP P,(P)
	POP P,2
	POP P,1
	SKIPE TYPFLG
	OUTCHR[7]
	JRST VSTPP1

VSTPPY:	POP P,(P)
	SKIPE TYPFLG
	OUTSTR[ASCIZ/? /]
VSTPPX:	POP P,2
	POP P,1
	POPJ P,

VSTWIN:	MOVE T,[POINT 7,VSTBUF]
	MOVEM T,STPTR
	MOVE T,[3,,STPPN]
	SETZM USRPPN
	COMPT. T,
	JRST [	OUTSTR[ASCIZ/COMPT. UUO FAILED ON PREVIOUSLY RECOGNIZED DIRECTORY NAME.
/]
		POPJ P,]
	MOVE T,USRPPN
	JRST CPOPJ1
>;VIROS
;DSKIN
DSKIN:	PUSHJ P,GETDSK
	POPJ P,
	POPJ P,

DSKINH:	PUSHJ P,GETDSK
	POPJ P,
	JRST IBREAK			;NOW TURN IT OFF

GETDSK:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/DISK INPUT /]
	MOVSI T,EXTDSK
	PUSHJ P,SETNAM
	POPJ P,
	INIT IDSK,0
	'DSK   '
	DSKHD
	JRST [	OUTSTR[ASCIZ/CAN'T GET DISK!!!
/]
	CDSK:
	NOLAY,<TLZ M,DSKACT!DSKFLG>
	LAY,<MOVSI T,DSKACT!DSKFLG
		TDZ M,T
		ANDCAM T,LAYM
	>;LAY
	SETOM DSKEND
		JRST DSPSET]
	OUTSTR[ASCIZ/READING /]
	MOVEI A,FILNAM
	JSR FPRINT
DEC,<	MOVE T,FILPPN	>
	LOOKUP IDSK,FILNAM
	JRST [	PUSHJ P,LOOKER
		RELEASE IDSK,
		JRST CDSK]
DEC,<	JSR IDSK,LOOKCK	>
	OUTSTR[ASCIZ/
/]
	MOVEI T,DSKBUF
	EXCH T,.JBFF
	INBUF IDSK,1
	MOVEM T,.JBFF
NOLAY,<TLO M,DSKACT!DSKFLG>
LAY,<MOVSI T,DSKACT!DSKFLG
	TDO M,T
	IORM T,LAYM
>;LAY
	SETZM DSKEND
	SETZM BRKCHR
	AOS (P)
	JRST DSPSET





DSKCHR:	SOSG DSKHD+2
	IN IDSK,
	CAIA
	JRST DSKREL
	IBP DSKHD+1
	MOVE C,@DSKHD+1
	TRNE C,1
	JRST [	MOVNI C,5
		ADDM C,DSKHD+2
		AOS DSKHD+1
		JRST DSKCHR]
	LDB C,DSKHD+1
	JUMPE C,DSKCHR
	CAIE C,15
	CAIN C,14
	JRST DSKCHR
	CAME C,BRKCHR
	JRST CPOPJ1
MAKBRK:
NOLAY,<TLZ M,DSKACT>		;BREAK INPUT
LAY,<MOVSI C,DSKACT
	TDZ M,C
	ANDCAM C,LAYM
>;LAY
	MOVEI C,200+":"		;GENERATE END OF ;T
	JRST CPOPJ1

IBREAK:		;ENTER HERE TO GENERATE INPUT BREAK FOR ERROR
NOLAY,<TLZ M,DSKACT>
LAY,<MOVSI C,DSKACT
	TDZ M,C
	ANDCAM C,LAYM
>;LAY
	TLNE M,MACACT		;STILL IN MACRO?
	POPJ P,			;YES, SKIP PRINTOUT
	OUTSTR[ASCIZ/DISK INPUT BREAK!
/]
	JRST PLEVEL

DSKREL:	STATO IDSK,1B22
	JRST EFLUSH
	PUSHJ P,IFLUSH		;FLUSH INPUT FILE IN NORMAL MANNER
	JRST MAKBRK		;MAKE A <CTRL>:

EFLUSH:	OUTSTR[ASCIZ/DISK INPUT FILE ERROR FINISH!!
/]
IFLUSH:		;ENTER HERE JUST TO FLUSH DSKINPUT
NOLAY,<TLZ M,DSKACT!DSKFLG>
LAY,<MOVSI C,DSKACT!DSKFLG
	TDZ M,C
	ANDCAM C,LAYM
>;LAY
	SETOM DSKEND
	OUTSTR[ASCIZ/END OF DISK INPUT FILE!!
/]
	RELEASE IDSK,
PLEVEL:	PUSHJ P,DSPSET
	PUSH P,T
	HRRZ T,MACPNT
	SUBI T,MACPDL-1
	JUMPE T,NOLEVL
	IDIVI T,5
	OUTSTR[ASCIZ/YOUR ARE NOW AT MACRO LEVEL /]
	PUSHJ P,DECOUT
	OUTSTR[ASCIZ/.
/]
NOLEVL:	POP P,T
	JRST ENDDSP
;DKSOUT
PUTDSK:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/DISK OUTPUT /]
	MOVSI T,EXTDSK
	PUSHJ P,SETNAM
	POPJ P,
	ENTPPN
DEC,<	RELEASE ODSK,	>
	INIT ODSK,0
	'DSK   '
	DSKOHD,,0
	JRST [	OUTSTR[ASCIZ/CAN'T GET DISK!
/]
	DSKCLR:	SETZM DSKOPN
		RELEASE ODSK,
NODEC,<		POPJ P,	>
DEC,<		JRST LOGINI>
				]
	OUTSTR[ASCIZ/WRITING /]
	MOVEI A,FILNAM
	JSR FPRINT
	ENTER ODSK,FILNAM
	JRST [	OUTSTR[ASCIZ/, ENTER FAILED!
/]
		JRST DSKCLR]
	OUTSTR[ASCIZ/
/]
	MOVEI T,DSKOBF
	EXCH T,.JBFF
	OUTBUF ODSK,1
	MOVEM T,.JBFF
	SETOM DSKOPN
	POPJ P,

DSKOCHR:
	PUSH P,C	;SAVE CHAR.
SKEY,<	CAIN C,ALTMOD
	TRO C,600	;MAKE SURE WE GET 4 ALTMODES
>;SKEY
	LSH C,-7
	ANDI C,3
	JUMPE C,NOCM
NOSKEY,<	MOVE C,BITTAB-1(C)
	PUSHJ P,DCHR1
>;NOSKEY
SKEY,<	MOVN C,C
	JRST .+1+3(C)
	PUSHJ P,PUTALT
	PUSHJ P,PUTALT
	PUSHJ P,PUTALT
>;SKEY
NOCM:	MOVE C,(P)
	PUSHJ P,DCHR1
	POP P,C
	POPJ P,

NOSKEY,<
BITTAB:	CTRL
	META
	CTLMTA
>;NOSKEY

SKEY,<
PUTALT:	MOVEI C,ALTMOD
>;SKEY
DCHR1:	ANDI C,177	;ONLY THIS
SKEY,<	CAIN C,10
	MOVEI C,177
>;SKEY
	CAIE C,12
	JRST DCHR2
	MOVEI C,15
	PUSHJ P,DCHR2
	MOVEI C,12
DCHR2:	SKIPN DSKOPN	;INSIDE ERROR MAYBE?
	POPJ P,		;YES
	SOSG DSKOHD+2
	OUT ODSK,
	JRST DCHR4	;ALL OK, DEPOSIT CHAR
	OUTSTR[ASCIZ/DISK OUTPUT ERROR!
/]
	JRST DSKCLR
DCHR4:	IDPB C,DSKOHD+1
	POPJ P,
;PC CARD
MPC,<
DOCARD:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/PC CARD (CRD) /]
	MOVSI T,EXTCRD
	PUSHJ P,SETNAM
	POPJ P,
	PUSHJ P,CRDREL
	MOVE T,FILNAM
	MOVEM T,CRDNAM
	MOVE T,FILEXT
	MOVEM T,CRDEXT
	SKIPE NOPPNF		;SKIP IF PPN TYPED
	SKIPE THEREXISTS	;NO PPN TYPED--FALL THRU IF THEREXISTS WAS TYPED
	SKIPA T,FILPPN		;PPN OR THEREXISTS TYPED-USE PPN FROM SETNAM
	MOVE T,LIBPPN		;NO " - USE LIBRARY
	MOVEM T,CRDPPN
	PUSHJ P,GETCRD
	JRST [	PUSHJ P,ZCARD
		JRST PERRET]
	TRO MCHG
	TLO M,CRDISP
	POPJ P,

ZCARD:	PUSHJ P,CRDREL
	TLZ M,CRDISP
	TRO MCHG
	POPJ P,

GETCRD:	MOVEM P,PERRSAV
	INIT ICARD,10
	'DSK   '
	CARDHD
	JRST [	OUTSTR[ASCIZ/CAN'T GET DISK!!
/]
		POPJ P,]
CRDRD:	OUTSTR[ASCIZ/READING CARD FROM /]
	MOVEI A,CRDNAM
	JSR FPRINT
	MOVE T,CRDPPN
	LOOKUP ICARD,CRDNAM
	JRST [	PUSHJ P,LOOKER
		TLNN M,DSKACT!MACACT
		OUTSTR[ASCIZ/TRY ANOTHER CRD /]
		PUSHJ P,GCRDNM		;GET FILE NAME
		JRST [	RELEASE ICARD,
			POPJ P,]
		SETZM CRDDAT
		JRST CRDRD]
DEC,<	JSR ICARD,LOOKCK	>
NODEC,<	MOVEM T,CRDPPN	>
	OUTSTR[ASCIZ/
/]
	MOVEI TT,CRDBUF
	EXCH TT,.JBFF
	INBUF ICARD,2
	MOVEM TT,.JBFF
	PUSHJ P,CARDIN
	CAIE TTT,CRDVER		;CORRECT VERSION OF FILE?
	JRST [	OUTSTR[ASCIZ/IMPROPER VERSION NUMBER FOR PC CARD DEFINITION FILE.
/]
		POPJ P,]
	PUSHJ P,CRDLP2		;READ CARD OUTLINE INTO CRDLST
	PUSH P,SID
	JUMPL SID,CRDLP5
	SWITCH
CRDLP5:	PUSHJ P,CRDLPP
CRDLP6:	SWITCH			;GET TO BACK SIDE
	PUSHJ P,CRDLPP
	SWITCH			;BACK TO FRONT SIDE
	PUSHJ P,RSHORT		;READ SHORTING BARS
	SWITCH			;AND BACK SIDE
	PUSHJ P,RSHORT
	POP P,T
	EQV T,SID
	JUMPL T,CRDLP7
	SWITCH
CRDLP7:	MOVSS CRDLST
	PUSHJ P,CRDLP2		;PUT TARGET LOCS IN LEFT HALF OF CRDLST
	MOVSS CRDLST
	RELEASE ICARD,
	JRST CPOPJ1

GCRDNM:	MOVSI T,EXTCRD
	PUSHJ P,SETNAM
	POPJ P,
	MOVE T,FILNAM
	MOVEM T,CRDNAM
	MOVE T,FILEXT
	MOVEM T,CRDEXT
	MOVE T,FILPPN
	MOVEM T,CRDPPN
	JRST CPOPJ1
;MORE PC CARD
RSHORT:	SKIPA G,[BARLST]
CRDLP2:	MOVEI G,CRDLST
CRDLP3:	PUSHJ P,CARDIN
	CAIE TTT,400000
	CAMN TTT,[(400000)]	;END OF BOARDER?
	POPJ P,			;YES
	GETFS(B)
	HRRM B,(G)
	MOVE G,B
	MOVEM TTT,1(B)
	SETZM (B)		;CLEAR LINK
	JRST CRDLP3

CRDLPP:	PUSHJ P,CARDIN
	CAMN TTT,[(400000)]
	POPJ P,
	TDZ TTT,[(1)1]
	PUSHJ P,FIND.P		;ATTEMPT TO ATTACH TO EXISTING POINT IF ANY
	MOVE TT,1(T)
	TLNE TT,ISPIN!FEEDTH!CPIN
	PUSHJ P,[MOVE T,1(D)
		JRST PNTPUT]
	PUSHJ P,CARDIN
	TDZ TTT,[(1)1]
	GETFS (TT)
	MOVEM TTT,1(TT)
	HRRM TT,(T)
	MOVSI TTT,CPIN
	IORM TTT,1(T)
	PUSHJ P,CARDIN
	MOVEM TTT,(TT)
	MOVE B,D
	PUSHJ P,REMPNT			;CPIN'S CAN'T BE IN ANY SETS!
	JRST CRDLPP

CRDREL:SETZM CRDNAM	;CLEAR CARD NAME
	HRRZ B,CRDLST
	PUSHJ P,PUTFS
	HLRZ B,CRDLST
	PUSHJ P,PUTFS
	SETZM CRDLST
	HRRZ B,BARLST
	PUSHJ P,PUTFS
	HLRZ B,BARLST
	PUSHJ P,PUTFS
	SETZM BARLST
	MOVEI B,PONPNT
	PUSHJ P,FNGDEL
	MOVEI B,PONPN2
	JRST FNGDEL

FNGDL3:	PUSH P,A		;SAVE LAST POINTER
	HLRZ T,(B)
	HRRZ TT,1(T)
	MOVE A,1(TT)
	TLNN A,CPIN		;CONNECTOR PIN?
	JRST NOCDL1		;NO, SKIP
	TRNE A,-1		;ANY PAD?
	JRST NOCDEL
	HLRZ T,1(T)		;NEIGHBOR POINTER
	JUMPE T,FNGDL2		;OK IF NO NEIGHBORS
FNGDL1:	SKIPE 1(T)		;ANY?
	JRST NOCDEL		;YES, LEAVE HIM
	SKIPN T,(T)		;NEIGHBOR OR NEXT POINTER
	JRST FNGDL2		;NO, PROCEDE
	TLNN T,-1		;NEIGHBOR?
	JRST FNGDL1		;NO, JUST POINTER
	JRST NOCDEL

FNGDL2:	HLRZ T,(TT)		;TEXT POINTER
	JUMPN T,NOCDEL		;YES, NO DELETY
	TRO TFLG		;DELETE PINS OK!
	MOVE A,(P)		;SETUP LAST POINTER
	PUSHJ P,DELPNT
	POP P,B
	JRST FNGDEL

NOCDL1:	POP P,B
	HRRZ B,(B)
FNGDEL:	MOVE A,B
	HRRZ B,(B)
	JUMPN B,FNGDL3
	TRZ TFLG		;NO DELETY PINS NO MORE
	POPJ P,

NOCDEL:	MOVSI T,CPIN		;TURN OFF CPIN
	ANDCAM T,1(TT)		;PUT BACK WITHOUT POINTER TO END BLOCK
	HRRZ A,(TT)		;GET POINTER TO END BLOCK
	HLLZS (TT)		;THEN CLEAR IT
	FSTRET (A)		;AND GIVE BACK BLOCK
	JRST NOCDL1

CARDIN:	SOSG CARDHD+2
	IN ICARD,
	CAIA
	JRST [	OUTSTR[ASCIZ/INPUT ERROR ON CARD!
/]
		MOVE P,PERRSAV
		JRST ZCARD]
	ILDB TTT,CARDHD+1
	POPJ P,
>;MPC
;THERE EXISTS CHECK
EXIST:	PUSH P,FILEXT
	PUSH P,FILPPN
	LOOKUP DAT,FILNAM
	SKIPA T,FILEXT
	SETO T,
	CLOSE DAT,	;GODDAMN BAG-BITING MOTHER-FUCKING CRETINOUS SYSTEM
	POP P,FILPPN
	POP P,FILEXT
	MOVSI TT,777000		;MASK OUT ALL BUT PROTECTION
	ANDM TT,FILDAT
	HRRES T
	JUMPE T,CPOPJ1		;DOESN'T EXIST
	JUMPL T,ITDOES
	HRRM T,FILEXT		;PUT HERE FOR PRINT
	OUTSTR[ASCIZ/SAFETY LOOKUP OF /]
	MOVEI A,FILNAM
	JSR FPRINT
	PUSHJ P,LOOKER
	OUTSTR[ASCIZ/WRITE ANYWAY?/]
	INCHRW C
	CAIN C,15
	INCHRW C
	JRST ITDONT

ITDOES:	SKIPE THEREXISTS
	JRST CPOPJ1
DEC,<	PUSHJ P,CHKPPN		;REALLY SAME PPN?
	JRST CPOPJ1		;NO, TAKE NOT FOUND RETURN
>;DEC
	TLNN M,DSKACT!MACACT
	OUTSTR [ASCIZ/FILE ALREADY EXISTS; REPLACE IT?/]
	PUSHJ P,GETCHR
ITDONT:	OUTSTR [ASCIZ/
/]
	CAIE C,"Y"
	CAIN C,"y"
	AOSA (P)
	RELEASE DAT,
	POPJ P,
;FIND THIS, THAT, AND THE OTHER THING
FFNDID:	TLNE TTT,-1		;PIN?
	JRST FFNDPID		;YES
	ADD TTT,OLDPID		;ADD OFFSET
	MOVE TT,TTT
	ANDI TT,LHASHP-1
	HRRZ D,@PHASHP		;LOOK IN HASH BUCKET
	JUMPN D,FNDID1
	POPJ P,

FNDID:	TLNE TTT,-1		;PIN?
	JRST FNDPID		;YES
	ADD TTT,OLDPID		;ADD OFFSET
	SKIPN D,PONPNT
	POPJ P,
FNDID1:	HLRZ TT,(D)
	HRRZ T,(TT)
	CAIE T,(TTT)
	JRST FNDID2
	HRRZ TT,1(TT)
	MOVE TT,1(TT)
	TLNN TT,ISPIN		;IF IS PIN, NO MATCH
	JRST [	AOS (P)
		JRST FP2]	;PICK VARIABLES
FNDID2:	HRRZ D,(D)
	JUMPN D,FNDID1
	POPJ P,

FFNDPI:	ADD TTT,OLDBID
	HLRZ TT,TTT
	PUSH P,B
	PUSHJ P,FFNDBID
	JRST FBLOS
	JRST FNDBI2

FNDPID:	ADD TTT,OLDBID
	HLRZ TT,TTT
	PUSH P,B
	PUSHJ P,FNDBID
	JRST FBLOS
FNDBI2:	HLRZ D,(B)
	HRRZ D,(D)
	JUMPE D,FBLOS
FNDB1:	HLRZ C,(D)
	HRRZ T,1(C)
MPC,<	MOVE TTT,1(T)
	EQV TTT,SID
	JUMPGE TTT,NOTPID	;SKIP IF WRONG SIDE
>;MPC
	HRRZ T,(T)
	HLRZ T,(T)
MD,<	HRRZ T,(T)	>
	CAIN T,(TT)
	JRST GOTPID
NOTPID:	HRRZ D,(C)
	JUMPN D,FNDB1
	SKIPA D,B
FBLOS:	SETZ D,
FBLOS1:	POP P,B
	POPJ P,

GOTPID:	PUSHJ P,FP2
	AOS -1(P)
	JRST FBLOS1

FFNDBI:	MOVE B,TTT
	ANDI B,LHASHB-1
	SKIPN B,HASHB(B)
	POPJ P,
	JRST FNDBD1

FNDBID:	SKIPN B,DBODPN
	POPJ P,
FNDBD1:	HLRZ T,(B)
	HLRZ T,1(T)
	HLRZ T,(T)
	CAIN T,(TTT)
	JRST CPOPJ1
	HRRZ B,(B)
	JUMPN B,FNDBD1
	POPJ P,
MD,<
TNAME:	PUSH P,T		;SAVE STRING POINTER ON STACK
	MOVEI D,BODPNT-1	;SIMULATE NORMAL LINK
	HRRZ C,BODPNT		;TYPE POINTER
	JUMPE C,RET21		;NONE YET?
TN1:	HRRZ A,(P)		;POINTER TO NAME (ARG)
	HRRZ B,(C)		;POINTER TO TYPE NAME
	PUSHJ P,TXTMAT		;COMPARE
	CAIA			;NOT THE SAME
	JRST RET2		;FOUND IT (POINTER TO TYPE IN C)
	MOVE D,C		;LAGS ONE BEHIND C
	HRRZ C,1(C)		;NEXT TYPE.
	JUMPN C,TN1
RET21:	AOS -1(P)
RET2:	POP P,T
	POPJ P,
>;MD

FIND.B:				;FIND POINTER CROSS-REFERENCE
	HRRZ C,NEWBOD		;POINTER TO NEW BODY CROSS-REFERENCE LIST
	JUMPE C,CPOPJ		;NOT FOUND
FB1:	HLRZ A,1(C)		;EXTERNAL REPRESENTATION
	CAIN A,(T)		;IS THIS THE ONE
	JRST [	HRRZ B,1(C)	;INTERNAL REPRESENTATION
		JRST CPOPJ1]	;SKIP RETURN
	HRRZ C,(C)		;NEXT
	JUMPN C,FB1
	POPJ P,			;NOT FOUND
;SOME OUTPUT PRINTERS
OUTTX1: MOVE TT,1(T)
	MOVEM TT,TTBUF
	OUTSTR TTBUF
	HRRZ T,(T)
OUTTXT:	JUMPN T,OUTTX1
	POPJ P,

OUTTCR:	PUSHJ P,OUTTXT
	OUTSTR[ASCIZ/
/]
	POPJ P,

MD,<
TTYPE:	SETZ C,			;BLANK ONES FIRST
	OUTSTR[ASCIZ/
/]
	SETZM NNAMES
	PUSHJ P,TTYPEW
	SKIPN C,LIBLST
	POPJ P,
TTYPEY:	MOVE B,C
	OUTSTR[ASCIZ/
/]
	PUSHJ P,LIBTYP
	OUTSTR NAMBUF
	SETZM NNAMES
	PUSHJ P,TTYPEW
	HRRZ C,(C)
	JUMPN C,TTYPEY
	OUTSTR[ASCIZ/
/]
	POPJ P,

TTYPEW:	SKIPN A,BODPNT
	POPJ P,
TTYPEX:	HLRZ T,(A)		;LIBRARY POINTER
	CAME T,C		;SAME LIBRARY
	JRST TTYPEZ
	SOSGE NNAMES
	JRST [	MOVEI T,8
		MOVEM T,NNAMES
		OUTSTR[ASCIZ/
/]
		JRST .+2]
	OUTCHR[11]
	HRRZ T,(A)
	PUSHJ P,OUTTXT
TTYPEZ:	HRRZ A,1(A)
	JUMPN A,TTYPEX
	POPJ P,

LIBOUT:	SKIPN LIBPPN
	JRST LBNOPP
	MOVE TTT,[POINT 7,NAMBUF]
ITS,<	MOVE TT,LIBPPN
	PUSHJ P,SIXOUT
>;ITS
NOITS,<	HLLZ TT,LIBPPN
	PUSHJ P,LSIXOUT
	MOVEI TT,","
	IDPB TT,TTT
	HRLZ TT,LIBPPN
	PUSHJ P,LSIXOUT
>;NOITS
	SETZ TT,
	IDPB TT,TTT
	OUTSTR[ASCIZ/LIBRARY AREA: /]
	OUTSTR NAMBUF
	OUTSTR[ASCIZ/
/]
LBNOPP:	SKIPN A,LIBLST
	JRST MAPP0
LIBOU1:	MOVE B,A
	PUSHJ P,LIBTYP
	OUTSTR NAMBUF
	OUTSTR[ASCIZ/
/]
	HRRZ A,(A)
	JUMPN A,LIBOU1
MAPP0:	SKIPN A,MAPLST
	POPJ P,
	OUTSTR[ASCIZ/LIBRARY MAPPINGS:
/]
MAPP1:	MOVE B,A
	PUSHJ P,LIBTYP
	OUTSTR NAMBUF
	HLRZ B,(A)
	HRRZ B,(B)
	PUSHJ P,LIBTAB
	OUTSTR NAMBUF
	OUTSTR[ASCIZ/
/]
	HRRZ A,(A)
	JUMPN A,MAPP1
	POPJ P,

;CALL WITH B POINTING TO BODY DEF
OUTIT:	HRRZ T,(B)		;BODY NAME
	OUTCHR[42]
	PUSHJ P,OUTTXT
	OUTSTR[ASCIZ/" /]
	HLRZ B,(B)		;LIBRARY POINTER
	JUMPE B,CPOPJ
	PUSHJ P,LIBTYP
	OUTSTR NAMBUF
	POPJ P,

LIBTYP:	MOVE TTT,[POINT 7,NAMBUF]
	JRST LIBTY1
LIBTAB:	MOVE TTT,[POINT 7,NAMBUF]
	MOVEI T,11
	IDPB T,TTT
LIBTY1:
ITS,<	HLRZ TT,(B)
	MOVE TT,1(TT)
	PUSHJ P,SIXOUT
	MOVEI T,";"
	IDPB T,TTT
>;ITS
	MOVE TT,1(B)
	PUSHJ P,SIXOUT
	HLRZ B,(B)
	HLLZ TT,(B)
	JUMPE TT,NOLEXT
NOITS,<	MOVEI T,".">
ITS,<	MOVEI T," ">
	IDPB T,TTT
	PUSHJ P,SIXOUT
NOLEXT:
NOITS,<	MOVEI T,"["
	IDPB T,TTT
NOCMU,<
	HLLZ TT,1(B)
	PUSHJ P,LSIXOUT
	MOVEI T,","
	IDPB T,TTT
	HRLZ TT,1(B)
	PUSHJ P,LSIXOUT
>;NOCMU
CMU,<
	SKIPN TT,1(B)
	DSKPPN TT,
	MOVE T,[TT,,PPNBUF]
	DECCMU T,
	JRST [	PUSHJ P,LSIXOUT
		JRST PPNDN3 ]
	SKIPA TT,[POINT 7,PPNBUF]
	IDPB T,TTT
	ILDB T,TT
	JUMPN T,.-2
PPNDN3:
>;CMU
	MOVEI T,"]"
	IDPB T,TTT
>;NOITS
	HRRZ T,(B)
	MOVEI TT,"^"
	TRNN T,TOPLVL
	IDPB TT,TTT
	SETZ T,
	IDPB T,TTT
	POPJ P,

TYPREL:	HRRZ B,(A)
	PUSH P,B
	HLRZ B,(B)
	PUSHJ P,PUTFS		;GIVE BACK DIP TYPE NAME (MAYBE)
	POP P,B
	PUSHJ P,PUTFS		;PUT BACK NAME
	HLRZ C,1(A)		;POINTER TO DATA BLOCK
	FSTRET (A)		;GIVE BACK HEAD
;ENTER HERE TO RELEASE PART OF A BODY DEFINITION
PARREL:	JUMPE C,CPOPJ		;QUIT IF NO DEF HERE
	HLRZ B,(C)
	PUSHJ P,PUTFS		;RETURN BITS,,DIP DEF LIST AND LOC OFFSET
	HRRZ B,1(C)		;POINTER TO LINES
	PUSHJ P,PUTFS		;PUT THEM BACK
	HRRZ B,(C)
	JUMPE B,NGIVPN		;SKIP IF NO PINS
GIVPIN:	HLRZ A,(B)		;GET BITS, NAME, ETC. BLOCK
	FSTRET(A)		;GIVE IT BACK
	HRRZ B,(B)
	JUMPN B,GIVPIN
	HRRZ B,(C)		;GET PIN POINTER
	PUSHJ P,PUTFS		;GIVE BACK PIN LIST
NGIVPN:	MOVE B,C
	HLRZ C,1(C)
	FSTRET(B)
	JUMPE C,CPOPJ		;DONE IF NO TEXT
	MOVE A,C
GIVTXT:	HLRZ B,(A)
	HLRZ B,(B)
	JUMPE B,GVTXT1
	HLRZ B,(B)
	SKIPE B		;CHECK DIP DEF IN CASE THIS IS "DIPTYPE"
	PUSHJ P,PUTFS
	HLRZ B,(A)
	HLRZ B,(B)
	PUSHJ P,PUTFS
GVTXT1:	HLRZ B,(A)		;POINTER TO TEXT
	JUMPE B,.+2
	PUSHJ P,PUTFS		;GIVE IT BACK
	HRRZ A,(A)		;NEXT
	JUMPN A,GIVTXT
	MOVE B,C		;NOW LIST
	JRST PUTFS		;THAT'S ALL
;MORE OUTPUT ROUTINES
LOUTX:	ADD T,[POINT 7,1]
LOUTY:	TLNE T,760000
	JRST LOUTZ
	HRR T,-1(T)
	TRNN T,-1
	POPJ P,
LOUTZ:	ILDB TTT,T
	JUMPE TTT,LOUTY
	PUSHJ P,WORDOUT
	JRST LOUTY

LOUT:	PUSHJ P,LOUTX
LCRLF:	MOVEI TTT,15
	PUSHJ P,WORDOUT
	MOVEI TTT,12
	JRST WORDOUT

LTYPE:	MOVEM P,PERRSAV
	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/BODY DEF LISTING /]
	MOVSI T,EXTBDL
	PUSHJ P,SETNAM
	POPJ P,
	ENTPPN
	INIT DAT,0
	'DSK   '
	XWD IOHD,0
	JRST [	OUTSTR[ASCIZ/CAN'T GET DSK!!
/]
		POPJ P,]
	MOVEI T,IOBUF
	EXCH T,.JBFF
	OUTBUF DAT,2
	MOVEM T,.JBFF
	PUSHJ P,EXIST
	POPJ P,
	MOVEI A,FILNAM
	JSR FPRINT
	ENTER DAT,FILNAM
	JRST [	RELEASE DAT,
		OUTSTR[ASCIZ/, ENTER FAILED.
/]
		POPJ P,]
	OUTSTR[ASCIZ/
/]
FOR I IN(SITE1,SITE2,TAUTHOR,TIT1,TIT2,TMODULE,TVARIABLE,TPREFIX,TREV,TPROJ,TPAGE,TOF,TDCODE)
<	SKIPE T,I
	PUSHJ P,LOUT
>
	MOVE B,[D,,B]		;POINT HIM AT HIMSELF AND D
	SKIPN C,LSTNAM
	JRST LTYPEN
	HLLZ D,LSTEXT
	MOVE E,LSTPPN
	PUSHJ P,LTYPEF
	PUSHJ P,LCRLF
LTYPEN:	MOVEI A,BODPNT-1
	JRST LTYPE1
LTYPE2:	HRRZ T,(A)
	PUSHJ P,LOUTX
	HLRZ B,(A)
	JUMPE B,LTYPE4
	PUSHJ P,LTYPET
LTYPE4:	PUSHJ P,LCRLF
LTYPE1:	HRRZ A,1(A)
	JUMPN A,LTYPE2
	RELEASE DAT,
	POPJ P,

LTYPET:	PUSHJ P,LIBTAB
	CAIA
LTYPEF:	PUSHJ P,LIBTYP
	MOVE T,[POINT 7,NAMBUF]
	ILDB TTT,T
	JUMPE TTT,CPOPJ
	PUSHJ P,WORDOUT
	JRST .-3
>;MD
DECOUT:	SKIPL T
	JRST DECOU1
	MOVM T,T
	OUTCHR ["-"]
DECOU1:	IDIVI T,=10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,DECOU1
	HLRZ T,(P)
	ADDI T,60
	OUTCHR T
	POPJ P,

OCTOUT:	IDIVI T,10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,OCTOUT
	HLRZ T,(P)
	ADDI T,60
	OUTCHR T
	POPJ P,

SIXPNT:	JUMPE TT,CPOPJ
	SETZ T,
	LSHC T,6
	ADDI T,40
	OUTCHR T
	JRST SIXPNT
     