;<WIRELIST>LEBEL.FAI.15, 15-NOV-75 19:04:29, EDIT BY HELLIWELL
.ADD(ALLLOCS,LEBEL,LEBLTV)

BEGIN LEBEL

;THE TRANSFER VECTOR

^LEBLTV:
	JRST LCINIT		;INITIALIZE LETTER/NUMBER CONVERSION TABLES
	JRST LOCOUT
	JRST QUPIN
	JRST SLTOUT
	JRST GETSLT
	JRST BPINPN
	JRST RPNAM
MDPC,<
	JRST CSLTLP
	JRST SLTLN		;USE THE CANONICAL ONE
	JRST SLTLPN
	JRST PINPLS
MD,<
	JRST CSLTLT
	JRST GTCONP
	JRST CSLTLD
>;MD
>;MDPC
MWL,<
	JRST PINPN5
	JRST TRPNAM
	JRST RPNAMA
	JRST WLOCOUT
	JRST LOCPNC
	JRST LOCPNW
	JRST PRCNPN
	JRST GETLOC
	JRST PADPRT
	JRST RAYDIP
	JRST RAYCON
	JRST LOCPIN
	JRST LOCPNM
	JRST LOCPNP
	JRST CPNSEP
	JRST CPNMER
	JRST BPNPIN
	JRST CPARTP
	JRST PRBCN
	JRST SEQLOC
	JRST CONGIN
	JRST GTSLTT
>;MWL
	[ASCIZ/#LL#/]
	[ASCIZ /#/]
MDPC,<	[ASCIZ/P#-#/]
	[ASCIZ/L#/]
	[ASCID /A01/]
>;MDPC
MWL,<	[ASCIZ/L#/]
	[ASCIZ/L/]
>;MWL
CHECK LEBLTV,LTVLEN

L2NSUB:	BLOCK L2NLEN
N2LSUB:	REPEAT N2LLEN, < "?"
>
EN2L__.

NNN__1
FOR I IN(A,B,C,D,E,F,H,J,K,L,M,N,P,R,S,T,U,V,W,X,Y,Z)
<	L2N2L I,0
>
FOR I IN (G,I,O,Q)
<	L2N2L I,1B0
>

N2LMAX__NNN-1

ORG EN2L

LCINIT:	MOVE T,[L2NSUB,,L2N]
	BLT T,L2N+L2NLEN+N2LLEN-1
	MOVEI T,N2LMAX
	MOVEM T,MAXN2L
	POPJ P,
;THIS BLOCK STRUCTURE HIDES ALL THE STANDARD ROUTINES FOR
;ALL BUT III BOARDS FROM III


;PUT PIN ON END OF BODY LOCATION

BPINPN:	MOVE T,A
	PUSHJ P,PUTDEC
	POPJ P,

MWL,<
PINPN5:	MOVEI TT,5
	MOVEM TT,NDIG
	JRST NPUTDEC

TRPNAM:	TGETNUM
	JRST CPOPJ1

RPNAMA:	GETCH
	CAIL CHRREG,"0"
	CAILE CHRREG,"9"
	TDZA A,A
	PUSHJ P,TRPNAM
	JFCL
	JRST CPOPJ1
>;MWL

RPNAM:	GETNUM
	JRST CPOPJ1

MDPC,<
SLTLPN:PUSHJ P,SLTOUT
	HLRZ A,A
	JRST LOCOUT

PINPLS:HLRZ T,A
	PUSHJ P,PUTDEC
MD,<	TRNE A,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		POPJ P, ]
	TRNE A,L1416
	PUTBYT "+"
>;MD
	POPJ P,
>;MDPC
SUBTTL	LOCPIN -- PRINTS LOCATION AND PIN #
MWL,<

LOCPIN:PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	PUTBYT ")"
	POPJ P,

;LOCPNM -- PRINTS DIP LOC AND PIN # ON WIRE-WRAP SIDE

LOCPNM:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	MOVE TT,(P)
	TRNE TT,R1416
	JRST [	CAIL T,8
		ADDI T,2
		JRST LCPNM3 ]
	TRNE TT,L1416
	ADDI T,1
LCPNM3:	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/-2/]
		JRST LCPNM2 ]
	TRNE T,L1416
	PUTBYT "-"
LCPNM2:	PUTBYT ")"
	POPJ P,

LOCPNP:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		JRST LCPNP2 ]
	TRNE T,L1416
	PUTBYT "+"
LCPNP2:	PUTBYT ")"
	POPJ P,
;MORE WL ROUTINES

;SEPARATE CONNECTOR PIN BODY LOC (PADDLE) FROM PIN # (PIN, SIDE)
CPNSEP:	MOVE TT,T
	ANDI T,770000
	ANDI TT,7777
	POPJ P,

;MERGE CONNECTOR PIN BODY LOC WITH PIN #
CPNMER:	DPB TT,[POINT 12,T,35]
	POPJ P,

;PRINT CONNECTOR PIN NAME AS SEEN FROM BACK PANEL
BPNPIN:	PUSH P,T	;SAVE OFFSET HERE
	LDB T,[POINT 4,A,3]
	SKIPE T		;CONVERT 0 TO SPACE
	ADDI T,20	;ALL THE REST TO DIGITS
	ADDI T,40
	PUTBYT (T)
	LDB T,[POINT 5,A,8]
	LDB TT,[POINT 6,A,23]
	ADDI T,-1(TT)
	CAILE T,77
	SETZ T,
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,17]
	POP P,TT
	JUMPE TT,NOOFFS
	ADD T,TT
	JUMPLE T,[OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE ZERO OR NEGATIVE.
/]
		SETZ T,
		JRST NOOFFS]
	CAIG T,=99
	JRST NOOFFS
	OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE MORE THAN 2 DIGITS.
/]
	MOVEI T,=99
NOOFFS:	PUSHJ P,NDECOUT
	LDB T,[POINT 6,A,29]
	PUTBYT @N2L(T)
	LDB T,[POINT 6,A,35]
	JRST DECOUT

;OUTPUT EDGE PIN TO PARTITION FILE AS NUMBER
CPARTP:	LSH T,-14
	PUTBYT "0"(T)
	MOVEI T,2
	MOVEM T,NDIG
	MOVE T,TT
	LSH T,-5
	TRNE TT,1
	SUBI T,1
	JRST NDECOUT

>;MWL
SUBTTL	CSLTLT,LOCPNC,LOCPNW,PRCNPN -- CONNECTOR PIN PRINTING STUFF

MD,<
CSLTLT:	PUSHJ P,SLTOUT
	JRST CSLTLD
>;MD

MWL,<
PRBCN:	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
	JRST PRCNPN
>;MWL
SUBTTL	DETECT IF THIS PIN NAME IS "WILD"
;CALL WITH PIN NAME IN RH(A)
QUPIN:	PUSH P,A
	LSH A,-14
	ANDI A,77
	MOVE A,N2L(A)
	CAIE A,"U"
	JRST [	POP P,A
		POPJ P,]
	POP P,A
	ANDI A,3777
	PUSH P,B
	IDIVI A,=200
	POP P,B
	CAILE A,2
	TDZA A,A
	ADDI A,1
	JRST CPOPJ1

MWL,<
;THESE ROUTINES SHOULD BE MOVED EVENTUALLY, THEY ARE A FUNCTION OF THE BOARD TYPE

;CALL WITH LESSER OF TWO LOCS IN T
;GREATER IN TT
SEQLOC:	XOR T,TT
	TRNE T,770000		;LETTERS SHOULD BE THE SAME
	POPJ P,			;ELSE NOT IN SEQUENCE
	XOR T,TT
	SUB TT,T
	CAIN TT,1
	AOS (P)
	POPJ P,

CONGIN:	PUSH P,A
	HRRZ A,A
	JUMPE A,CONG1
	TRNN A,2
	JRST [	TRC A,3
		JRST CONG3]
	LSH A,-6
	ANDI A,77
	ADDI A,1
	CAIG A,=18
	JRST [	LSH A,6
		TRO A,1
		DPB A,[POINT 12,(P),35]
		POP P,A
		JRST CPOPJ1]
	LDB A,[POINT 6,(P),23]
	ADDI A,1
	PUSH P,T
	LDB T,[POINT 3,-1(P),11]		;GET EXTENT
	CAIG A,1(T)			;DOES THIS PADDLE EXIST?
	JRST [	POP P,T
		JRST CONG2]
	POP P,T
	POP P,A
	POPJ P,

;VARIOUS FLAVORS OF RETURN FOR CONGIN

CONG1:	HRRZ A,L2N+"A"
CONG2:	LSH A,6
	IOR A,L2N+"A"
	HRRZ A,A		;CLEAR ANY LH GARBAGE
	LSH A,6
	TRO A,1
CONG3:	HRRM A,(P)
	POP P,A
	JRST CPOPJ1
>;MWL
SUBTTL	SLTOUT -- PRINTS DIP BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|				   18      21        26    29          35
|                 18		    |   4   |    5    |  3  |     6     |
|___________________________________|_______|_________|_____|___________|
		  |			|	 |	 |	  |
<		  |			|	 |	 |	  |------># SLOT
		  |			|	 |	 |
<		  |			|	 |	 |--------------->L +RACK
		  |			|	 |
<		  |			|	 |----------------------->L RACK
		  |			|
<		  |			|--------------------------------># BAY
		  |
<		  |------------------------------------------------------>NOT USED

SLTOUT:	LDB T,[POINT 4,A,21]
	JUMPE T,.+2
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
MDPC,<	JUMPE TT,CPOPJ	>
	PUTBYT @N2L(TT)
	LDB TTT,[POINT 3,A,29]
	JUMPE TTT,SLTOU1
	ADD TT,TTT
	PUTBYT @N2L(TT)
SLTOU1:	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,35]
	JRST NPUTDEC
SUBTTL	CSLTOUT -- PRINTS CONNECTOR PIN BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|	    |			   18      21        26    29          35
|     6     |          12	    |   4   |    5    |  3  |     6     |
|___________|_______________________|_______|_________|_____|___________|
      |			|		|	 |	 |	  |
<     |			|		|	 |	 |	  |------># SLOT
      |			|		|	 |	 |
<     |			|		|	 |	 |--------------->L +RACK
      |			|		|	 |
<     |			|		|	 |----------------------->L RACK
      |			|		|
<     |			|		|--------------------------------># BAY
      |			|
<     |			|------------------------------------------------>NOT USED
      |
<     |------------------------------------------------------------------>L PADDLE

MD,<
CSLTOUT:
	JRST SLTOUT			;DO SAME AS BODY LOC.
IFN 0,<;REMOVE BY R HELLIWELL 31 MAR 75
;CONFUSING BECAUSE DISPLAYED AND PLOTTED CONNECTOR PIN DESIGNATOR
;WERE DIFFERENT FROM TYPEIN AND TYPEOUT.
	TRNN A,-1
	POPJ P,
	LDB T,[POINT 3,A,29]
	LDB TT,[POINT 6,A,5]
	TLNN A,4000			;"U" PIN?
	CAILE TT,1(T)			;ON BACK PANEL?
	JRST SLTOUT			;NO, TREAT LIKE A BODY
	LDB T,[POINT 4,A,21]		;SHOW ACTUAL BACKPANEL RACK LETTER
	SKIPE T
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
	LDB T,[POINT 6,A,5]
	ADDI TT,-1(T)
	PUTBYT @N2L(TT)
	LDB T,[POINT 6,A,35]
	JRST PUTDEC
>;IFN 0
>;MD
;CHECK TO MAKE SURE CHRREG IS A LETTER (CONVERTS LOWER CASE)

CHKLET:CAIL CHRREG,"a"		;CHECK FOR LOWER CASE
	CAILE CHRREG,"z"
	CAIA			;NOT LOWER CASE
	SUBI CHRREG,<"a"-"A">	;CONVERT TO UPPER CASE
	CAIL CHRREG,"A"
	CAILE CHRREG,"Z"
	POPJ P,
	JRST CPOPJ1		;ITS LEGAL

TWOLET:ROT CHRREG,-7*2	;MAKE IT ASCII
	MOVSI NUMREG,-TWOLEN
TWOLT1:	CAME CHRREG,TWOTAB(NUMREG)
	AOBJN NUMREG,TWOLT1
	JUMPGE NUMREG,CPOPJ
	MOVEI NUMREG,40(NUMREG)
	JRST CPOPJ1

TWOTAB:	ASCII/BT/
	ASCII/CB/
	ASCII/CR/
	ASCII/DL/
	ASCII/FC/
	ASCII/TP/
REPEAT 40-<.-TWOTAB>,<ASCII/??/>
TWOLEN__.-TWOTAB

MDPC,<
;THE CANONICAL DIP LOC READER WHERE DIP LOC IS OF FORM L#
;SLTLN -- READS A BAY/RACK/SLOT/DIPLOC OR JUST A DIPLOC
;CALL IS:
;	PUSHJ P,SLTLN
;	JRST ERROR
;	JRST CRLF.ONLY
;MD,<	JFCL	>	;LOC ONLY
;	(BRS,,LOC IN DESTIN)

SLTLN:	SETZM DESTIN
MPC,<	GETCH	>
MD,<	GETNUM
	CAIL NUMREG,20
	POPJ P,
	DPB NUMREG,[POINT 4,DESTIN,3]
>;MD
	CAIN CHRREG,12
	JUMPE NUMREG,CPOPJ1
	PUSHJ P,CHKLET
	POPJ P,
MD,<	PUSH P,D
	SKIPN D,L2N(CHRREG)
	JRST POPDJ
	DPB D,[POINT 5,DESTIN,8]
	GETNUM
	PUSHJ P,CHKLET
	JRST SLTLN1
	JUMPL D,SLTLN3		;GIOQ ILLEGAL FOR BRS
	POP P,D
	JUMPN NUMREG,SLTLN2
	LDB NUMREG,[POINT 5,DESTIN,8]
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	JUMPL CHRREG,[SUB CHRREG,NUMREG
		JRST SLTLN4]
	SUB CHRREG,NUMREG
	JUMPLE CHRREG,SLTLN4
	CAILE CHRREG,7	
	JRST SLTLN4
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
	JUMPE NUMREG,CPOPJ
	PUSHJ P,CHKLET
	JRST SLTLN8
SLTLN2:	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,17]
>;MD
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPE NUMREG,SLTLN5
SLTLN7:	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
MD,<	HLRZ NUMREG,DESTIN
	JUMPN NUMREG,CPOPJ3	;THIS RETURN IF BRS TYPED
>;MD
	JRST CPOPJ2		;THIS RETURN IF PC OR NO BRS

SLTLN5:	LDB NUMREG,[POINT 6,DESTIN,23]
	MOVE NUMREG,N2L(NUMREG)
	LSH NUMREG,7
	IOR CHRREG,NUMREG
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

MD,<
SLTLN1:	POP P,D
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
	LDB NUMREG,[POINT 9,DESTIN,8]
	CAIL NUMREG,40
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ2

SLTLN3:	MOVE D,N2L(D)		;GET LETTER BACK
	LSH D,7
	IOR CHRREG,D		;PUT 2 LETTERS IN CHRREG
	POP P,D			;RESTORE D
	PUSHJ P,SLTLN6
	POPJ P,
	JRST SLTLN9

SLTLN4:	PUSHJ P,SLTLN0
	POPJ P,
SLTLN9:	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

SLTLN8:	PUSH P,NUMREG
	PUSH P,CHRREG
	LDB NUMREG,[POINT 5,DESTIN,8]
	LDB CHRREG,[POINT 3,DESTIN,11]
	PUSHJ P,SLTLN0
	JRST [	POP P,CHRREG
		POP P,NUMREG
		POPJ P,]
	POP P,CHRREG
	POP P,NUMREG
	JRST SLTLN7

SLTLN0:	ADD CHRREG,NUMREG	;GET NUMBER BACK
	MOVE NUMREG,N2L(NUMREG)	;GET FIRST LETTER BACK
	MOVE CHRREG,N2L(CHRREG)	;GET SECOND LETTER BACK
	LSH NUMREG,7
	IOR CHRREG,NUMREG
SLTLN6:	LDB NUMREG,[POINT 4,DESTIN,3]
	JUMPN NUMREG,CPOPJ
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ1

POPDJ:	POP P,D
	POPJ P,
>;MD
>;MDPC

;GETSLT -- READS A BAY/RACK/SLOT #

;CALL IS:
;	PUSHJ P,GETSLT
;	JRST ERROR
;	JUMPE TT,CRLF.ONLY	;THIS IS OPTIONAL, OBVIOUSLY
;	MOVEM TT,BRS

MWL,<
GTSLTT:	SETZM DESTIN
	TGETNUM
	JRST GTSLT1
>;MWL

GETSLT:	SETZM DESTIN
	GETNUM
GTSLT1:	DPB NUMREG,[POINT 4,DESTIN,3]
	PUSHJ P,CHKLET
	JRST [	JUMPE NUMREG,CPOPJ1
		POPJ P,]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 5,DESTIN,8]
	GETNUM
	JUMPN NUMREG,GTSLT2
	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	LDB NUMREG,[POINT 5,DESTIN,8]
	SUB CHRREG,NUMREG
	JUMPL CHRREG,CPOPJ
	CAILE CHRREG,7
	POPJ P,
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
GTSLT2:	DPB NUMREG,[POINT 6,DESTIN,17]
	JRST CPOPJ1
MD,<
;	PUSHJ P,RDLLN
;	    ERROR RETURN
;	    SUCCESS RETURN

RDLLN:	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 12,DESTIN,29]
	GETNUM
	JUMPN NUMREG,CKCR
	PUSHJ P,CHKLET
	POPJ P,
	LDB TT,[POINT 6,DESTIN,29]
	DPB TT,[POINT 6,DESTIN,23]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,29]
	GETNUM
	JUMPE NUMREG,CPOPJ
CKCR:	LDB TT,[POINT 12,DESTIN,29]
	XOR TT,L2N+"U"
	TRNN TT,-1			;SAME LETTER?
	JRST [	CAILE NUMREG,3777	;YES, LEGAL NUMBER?
		POPJ P,			;NO
		HRRZ TT,L2N+"U"
		DPB TT,[POINT 6,DESTIN,23]
		TRO NUMREG,4000
		DPB NUMREG,[POINT 12,DESTIN,35]
		JRST CPOPJ1]
	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,35]
	JRST CPOPJ1
>;MD

LOCOUT:	LDB TTT,[POINT 6,A,23]
	JUMPE TTT,CPOPJ		;NOTHING HERE, MEANS NOTHING AT ALL
	MOVEI T,2
	MOVEM T,NDIG
	TRNN A,7777		;CON?
	JRST [	PUTBYT "P"	;YES
		MOVEI T,(TTT)
		JRST NPUTDEC ]
	PUTBYT @N2L(TTT)
	LDB T,[POINT 12,A,35]	;NO, PRINT NUMBER
	JRST NPUTDEC

MDPC,<
;CALL IS LIKE GTSLTL EXCEPT:
;LAY,<	CPOPJ1 RETURN MAY BE TAKEN ON TEXIST CHAR ALSO >

MD,<
GTCONP:
	SETZM DESTIN
	PUSHJ P,GETLOC
	POPJ P,
	JRST CPOPJ1
	CAIE CHRREG,"-"
	POPJ P,
	HRRZM TT,DESTIN
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,7777
	POPJ P,
	IORM NUMREG,DESTIN
	JRST CPOPJ2
>;MD

CSLTLP:	
MD,<
	PUSHJ P,CSLTOUT
CSLTLD:	HLRZ A,A
	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
	PUTBYT "-"
>;MD
	LDB T,[POINT 12,A,35]
	MOVEI TTT,3
	MOVEM TTT,NDIG
	JRST NPUTDEC
>;MDPC

GETLOC:	GETCH
MDPC,<
	CAIN CHRREG,12
	JRST CPOPJ1
>;MDPC
	PUSHJ P,CHKLET
	POPJ P,
	HRLM CHRREG,(P)
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,7777
	POPJ P,
	HLRZ TT,(P)
	CAIN TT,"P"
	JRST [	LSH NUMREG,14
		MOVEI TT,(NUMREG)
MDPC,<		AOS (P) >
		JRST CPOPJ1 ]
	SKIPN TT,L2N(TT)		;CONVERT LETTER TO NUMBER
	POPJ P,
	LSH TT,14
	TRO TT,(NUMREG)
MDPC,<	AOS (P) >
	JRST CPOPJ2
MWL,<
WLOCOUT:JRST LOCOUT

LOCPNC:
LOCPNW:	PUSHJ P,LOCOUT
	HLRZ A,A
	PUTBYT "-"
PRCNPN:	LDB T,[POINT 12,A,35]
RAYDP1:	MOVEI TTT,3
	MOVEM TTT,NDIG
	JRST NPUTDEC

RAYCON:	PUSHJ P,LOCOUT		;PRINTS CONNECTOR SPEC FOR RAYTHEON WIRE WRAP
	PUTSTR [ASCIZ /  /]	;TWO SPACES
	HLRZ A,A
	JRST PRCNPN

PADPRT:	PUTSTR [ASCIZ / P/]	;MUST BE 3 CHARACTERS
	MOVEI T,1(G)
	JRST PUTDEC

RAYDIP:	PUSH P,T		;PRINT DIP PIN SPEC FOR RAYTHEON WIRE WRAP
	PUSHJ P,LOCOUT	
	PUTSTR [ASCIZ /  /]
	POP P,T
	TRZE T,L1416
	ADD A,[1,,0]		;14 PIN DIP IN 16 PIN SLOT
	HLRZ T,A
	JRST RAYDP1
>;MWL

BEND LEBEL
;LEBEL WIREWRAP BOARD
MDWL,<
.ADD(ALLWW,LEBEL,WEBLTV)
MWL,<

BEGIN WLEBEL

comment 

The two formats for the Lebel board are

dip pin spec:	A#-#
The A must literally be there.  The first # is the slot # and the second # is
the dip pin #.


______|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|                      11          17          23                      35
|           12		|     6	    |     6	|           12		|
|_______________________|___________|___________|_______________________|
	     |		      |		  |		     |
<	     |		      |		  |		     |----------->loc #
	     |		      |		  |
<	     |		      |		  |------------------------------>loc letter
	     |		      |
<	     |		      |------------------------------------------>dip pin #
	     |
<	     |----------------------------------------------------------->flags

connector pin spec:	P#-#
The P must literally be there.  The first # specfies the connector.  (Connectors 1-8
are the scotch-flex connectors.  Connector 9 is the edge connector.)  The second
# is the pin #.

______|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|                      11          17          23                      35
|           12		|     6	    |     6	|           12		|
|_______________________|___________|___________|_______________________|
	     |		      |		  |		     |
<	     |		      |		  |		     |----------->pin #
	     |		      |		  |
<	     |		      |		  |------------------------------>connector #
	     |		      |
<	     |		      |------------------------------------------>not used
	     |
<	     |----------------------------------------------------------->flags



^WEBLTV__.	;TRANSFER VECTOR FOR DOUBLE HEIGHT DEC BOARDS

	-1		;this is a wirewrap board
	JRST CPOPJ	;WWINIT
	JRST MAPRC
	JRST MAPPAD
	JRST PINMAP	
	JRST PINBIT	
	JRST VGCON	
	JRST DISTPP	;POINT TO POINT WIRING
	JRST FPWR
	JRST FGND
	JRST PGPRTM
	JRST PGPRNT
	JRST GNDCLR
	JRST WAGGND
	JRST GNDOUT
	JRST VCCOUT
	JRST RAYGEN
	=10		;NROWS		    (USED FOR UML ONLY)
	=8		;NCOLS		    (USED FOR UML ONLY)
	0		;NCLPRG
	=80		;DIPSLT_NROWS*NCOLS (USED FOR UML ONLY)
	SETPAD(=9)	;NPADS		    (USED FOR UML ONLY)
	XWD -=50,0	;PADLET		    (USED FOR UML ONLY)
	XWD -2,1	;PADPIN		    (USED FOR UML ONLY)
	=10		;FRACTN
	=200*2		;WRAPMG .200" INSULATION AROUND EACH POST
	=1500		;POSTMG .750" BARE WIRE AROUND EACH POST
	0		;NEXTR
CHECK WEBLTV,WTVLEN

COMMENT 

ALL CALCULATIONS DONE FROM PIN SIDE

(0,0) AT LOWER LEFT HAND CORNER SLOTS NUMBERED FROM 1-80
DIP SOCKETS ARE ARRANGED IN A 8X10 MATRIX (8 ROWS OF 10 DIPS)
HORIZONTAL SPACING:  .500"	VERTICAL SPACING:  1.000"
EACH DIP SOCKET IS 18 PINS (16 PINS FOR DIP 2 PINS FOR POWER AND GROUND)
DIP PINS 8 AND 9 ARE FACING THE BACKPLANE CONNECTOR FINGERS
GROUND AND POWER PINS ARE IN LINE AND OPPOSITE PINS 1 AND 16 RESPECTIVELY

THERE ARE 100 BACKPLANE CONNECTOR PINS AS WELL AS CONNECTOR PINS TO RECEIVE
8 SCOTCH-FLEX (R) CABLES AT THE OPPOSITE END OF THE BOARD.  THE SCOTCH-FLEX
CONNECTORS ARE LABEL P1-P8 WHILE THE BACKPLANE CONNECTOR IS LABELED P9.



;DIP SOCKET GEOMETRY

XOFSET__=1000	;(XOFSET,YOFSET) IS LOCATION OF A71(9)
YOFSET__=1000
XDIPSP__=500	; .500" HORIZONTAL SPACING
YDIPSP__=1000	;1.000" VERTICAL SPACING
XPINSP__=300	; .300" HORIZONTAL SPACING BETWEEN PINS
YPINSP__=100	; .100" VERTICAL SPACING BETWEEN PINS

;SCOTCH FLEX CONNECTOR GEOMETRY

PXOFST__=100	;(PXOFST,PYOFST) IS LOCATION OF P5-14
PYOFST__=9200
PXSPAC__=1800	;1.800" HORIZONTAL SPACING
PYSPAC__=300	; .300" VERTICAL SPACING
PXPNSP__=100	; .100"	VERTICAL SPACING BETWEEN PINS
PYPNSP__=100	; .100" HORIZONTAL SPACING BETWEEN PINS

;BACKPLANE CONNECTOR GEOMETRY

BXOFST__=0	;(BXOFST,BYOFST) IS LOCATION OF P9-99
BYOFST__=300
BXPNSP__=125	; .125" HORIZONTAL SPACING BETWEEN PINS
BYPNSP__=200	; .200" VERTICAL SPACING BETWEEN PINS
;PIN LOCATION TABLES (INDEX BY PIN #)

;********************************************* DIP PINS

DEFINE XY(X,Y)
<	X*XPINSP,,Y*YPINSP
>

PINTAB:	XY(1,7)	;PIN 1
	XY(1,6)
	XY(1,5)
	XY(1,4)
	XY(1,3)
	XY(1,2)
	XY(1,1)
	XY(1,0)	;PIN 8
	XY(0,0)	;PIN 9
	XY(0,1)
	XY(0,2)
	XY(0,3)
	XY(0,4)
	XY(0,5)
	XY(0,6)
	XY(0,7)	;PIN 16

;********************************************* DIP LOCATIONS

DEFINE XY(X,Y)
<	<XOFSET+X*XDIPSP>,,<YOFSET+Y*YDIPSP>
>

DIPLOC:
 FOR Y_ 7,  0, -1
<FOR X_ 0, =9,  1
<	XY(X,Y)
>>

;********************************************* SCOTCH-FLEX PINS

DEFINE XY(X,Y)
<	PXPNSP*X,,PYPNSP*Y
>

FLXPIN:
 FOR Y_ 1,   0, -1
<FOR X_ 0, =12,  1
<	XY(X,Y)
>>

;********************************************* SCOTCH-FLEX CONNECTORS

DEFINE XY(X,Y)
<	<PXOFST+X*PXSPAC>,,<PYOFST+Y*PYSPAC>
>

FLXLOC:
 FOR Y_ 1, 0, -1
<FOR X_ 0, 3,  1
<	XY(X,Y)
>>

;********************************************* BACK PLANE CONNECTOR PINS

DEFINE XY(X,Y)
<	<BXOFST+X*BXPNSP>,,<BYOFST+Y*BYPNSP>
>

BAKPIN:
 FOR X_ =49, 0, -1
<FOR Y_   0, 1,  1
<	XY(X,Y)
>>

;TAKES	A	LOCATION IN A
;	B	# PINS ON DIP
;RETURN	T	X,,Y OF PIN
;	TT	PWR AND GND FLAGS
;	TTT	DETAB FOR DIP PIN, 0 FOR CONNECTOR PIN ( WE ALWAYS RETURN 0 BECAUSE
;		WE ALWAYS DO POINT TO POINT WIRING)
;NON-SKIP	DIP PIN ERROR
;SKIP		CONNECTOR PIN ERROR
;DOUBLE SKIP	SUCCESS

MAPIT:	JUMPL A,CONMAP		;IS IT A CONNECTOR PIN?
	LDB TT,LOW12		;GET LOC NUMBER
	JUMPE TT,CPOPJ		;0 IS A NO,NO
	CAMLE TT,DIPSLT		;THATS TOO BIG
	POPJ P,
	MOVE T,DIPLOC-1(TT)	;PUT XY IN T
	LDB TT,[POINT 6,A,17]	;GET THE PIN #
	TLNE A,MAPPWR!MAPGND	;POWER OR GROUND PIN?
	JRST PINPGP
	CAIE B,=14		;I KNOW WHICH WAY 14 PIN DIPS GO IN TOO!
	JRST MAPIT0		;SIXTEEN DIP
	ADDI TT,1		;YES, FUDGE PIN # BY 1
;I THINK I STILL NEED THIS!
	ADD A,[1,,0]		;ALSO RETURN CORRECTED PIN # FOR DSTCLC
	TLO A,PLUS1		;AND INDICATE IT IS A PLUS 1 VERSION
MAPIT0:	ADD T,PINTAB-1(TT)	;ADD EXTRA XY FOR PIN
	SETZB TT,TTT		;NO AUTOMATIC GND/PWR, AND NO DETAB WITH POINT TO POINT
	JRST CPOPJ2

;FIND CARD POWER OR GROUND FOR DIP

PINPGP:	JUMPN TT,CPOPJ		;PIN FIELD BETTER HAD BE CLEAR
	ADDI T,=8*YPINSP	;BOTH GET THIS
	TLNN A,MAPPWR		;POWER?
	JRST [	ADD T,[XPINSP,,0]	;YES, IT IS OVER THIS MUCH
		MOVSI TT,GND	;NO, RETURN GND BIT
		SETZ TTT,	;AT END
		JRST CPOPJ2]
	MOVSI TT,PWR
	SETZ TTT,
	JRST CPOPJ2

;HERE TO RETURN LOCATION OF CONNECTOR PIN

CONMAP:	AOS (P)			;TAKE CONNECTOR PIN RETURN
	LDB T,B6AT23		;GET PIN GROUP
	JUMPE T,CPOPJ		;CHECK LEGAL GROUP
	CAILE T,=9		;LEGAL?
	POPJ P,
	LDB TT,LOW12		;GET PIN #
	JUMPLE TT,CPOPJ
	CAIN T,=9		;SCOTCH-FLEX CONNECTOR?
	JRST CONBAC		;NO, BACK PLANE CONNECTOR
	MOVE T,FLXLOC-1(T)	;GET LOCATION OF CONNECTOR
	CAILE TT,=26		;ONLY 26 PINS PER CONNECTOR
	POPJ P,
	ADD T,FLXPIN-1(TT)	;LOC OF THIS PIN
	SETZB TTT,TT
	JRST CPOPJ1		;CONNECTOR PIN RETURN

CONBAC:	CAILE TT,=100		;HERE FOR BACK PLANE CONNECTOR
	POPJ P,			;ILLEGAL
	MOVE T,BAKPIN-1(TT)	;LOCATION OF BACK PLANE CONNECTOR
	SETZB TTT,TT
	JRST CPOPJ1

VGCON:	POPJ P,			;NO CONNECTOR GROUND PINS

;TAKES UML ROW,,COLUMN IN T AND RETURNS DIP LOCATION IN T
;	NON-SKIP RETURN ERROR
;	SKIP RETURN FOR HAPPINESS

MAPRC:	TDNE T,[777700,,770000]
	POPJ P,
	HLRZ TT,T
	CAMLE TT,NROWS
	POPJ P,
	HRRZ T,T
	CAMLE T,NCOLS
	POPJ P,
	SUBI T,1		;WE GOT ROW IN TT, COL IN T
	IMUL T,NROWS
	SUB TT,NROWS		;RESULT SHOULD BE NEGATIVE
	SUB T,TT
	ADDI T,1+<1=12>	;ADD ONE AND TURN ON THAT EXTRA BIT
	JRST CPOPJ1

MAPPAD:	LSH T,13
	IORI T,(TT)
	LSH T,1
	ADDI T,(TTT)
	POPJ P,

PINBIT:	PUSHJ P,MAPIT
	JFCL
	SETZ TT,
	POPJ P,

PINMAP:	PUSHJ P,MAPIT
	JRST PINERR
	JRST CONERR
	JRST CPOPJ1

CONERR:	PUTBYT	"P"		;PRINT FORM P#-#
	LDB T,B6AT23
	PUSHJ P,NUMPNT		;CONNECTOR #
	OUTCHR ["-"]
	LDB T,LOW12
	PUSHJ P,NUMPNT		;PIN #
CONER1:	OUTSTR [ASCIZ / MAPPING ERROR	
/]
	POPJ P,

PINERR:	LDB T,B6AT23		;FORM A#(#)
	OUTCHR N2L(T)
	LDB T,LOW12		;SLOT #
	PUSHJ P,NUMPNT
	OUTCHR["("]
	LDB T,[POINT 6,A,17]	;PIN #
LSTNUM:	PUSHJ P,NUMPNT
	OUTCHR [")"]
	JRST CONER1

FPWR:	JUMPL A,FPWRC
	TLZ A,77!PLUS1!PLUS2	;CLEAR PIN FIELD
	TLO A,MAPPWR
	POPJ P,

FPWRC:	LDB T,B6AT23		;GET CONNECTOR
	CAIGE T,=9		;SCOTCH FLEX?
	JRST [	MOVE A,FLXCRD(T)
		TLO A,MAPPWR
		POPJ P,]
	PUSH P,B
	PUSH P,C
	PUSH P,G		;SAVE ALL THOSE AC'S
	PUSH P,W
	PUSH P,[MAPPWR,,0]
FGPCON:	MOVE W,GPCAND		;GET THE GROUND PIN CANDIDATES
	PUSH P,A		;SAVE THIS PIN
	PUSH P,[300000,,0]	;A VERY LARGE # (BEST DISTANCE SO FAR)
	PUSH P,[0]		;BEST PIN SO FAR
FGPCLP:	MOVE A,-2(P)		;THE CONNECTOR PIN
	MOVE C,(W)		;THE CARD PIN TO TRY
	IOR C,-3(P)		;WILL BE MAPPWR OR MAPGND
	PUSHJ P,DISTPP		;CALCULATE THE DISTANCE
	JUMPE G,[JRST 4,.]	;ZERO DISTANCE IS AN ERROR
	CAML G,-1(P)		;IS THIS ONE BETTER?
	JRST FGPCL1		;NO, TRY NEXT
	MOVEM G,-1(P)		;BEST SO FAR
	MOVE C,(W)
	MOVEM C,(P)		;PIN
FGPCL1:	AOBJN W,FGPCLP		;TRY NEXT CANDIDATE
	MOVE A,(P)		;THE CLOSEST CARD PIN
	IOR A,-3(P)		;SET MAPPWR OR MAPGND
	SUB P,[4,,4]
	POP P,W
	POP P,G
	POP P,C
	POP P,B
	POPJ P,

FGND:	JUMPL A,FCGND		;FOR GROUND FOR CONNECTOR PIN
	TLZ A,77!PLUS1!PLUS2
	TLO A,MAPGND
	POPJ P,

FCGND:	LDB T,B6AT23		;GET CONNECTOR #
	CAIGE T,=9		;IS IT AN EDGE CONNECTOR
	JRST [	MOVE A,FLXCRD(T)
		TLO A,MAPGND
		POPJ P,]
	PUSH P,B
	PUSH P,C
	PUSH P,G
	PUSH P,W
	PUSH P,[MAPGND,,0]
	JRST FGPCON

	RADIX =10
GPCANP:	FOR A  (1,2,3,4,5,6,7,8,9,10)
<	<112>!A
>

	RADIX 8
GPCAND:	GPCANP-.,,GPCANP	;AOBJN POINTER TO GPCANP

;TABLE INDEXED BY SCOTCH FLEX CONNECTOR, GIVES NEAREST CARD POWER AND GROUND

FLXCRD:	0		;0
	10001		;1
	10003		;2
	10008		;3
	10011		;4
	10002		;5
	10004		;6
	10007		;7
	10012		;8

;PGPRTM AND PGPRNT PRINT OUT PIN SPECIFICATION FOR WIRE LIST

PGPRTM:	SETOM PMINUS#
	CAIA
PGPRNT:	SETZM PMINUS
	JUMPL A,PGCPNT		;JUMP FOR CONNECTOR
	LDB T,B6AT23		;LETTER PART OF SLOT LOCATION
	PUTBYT @N2L(T)
	LDB T,[POINT 12,A,35]
	PUSHJ P,DECOUT		;SLOT #
	PUTBYT "("
	TLNN A,MAPPWR!MAPGND
	JRST NPGPNT
	TLNN A,MAPPWR
	SKIPA T,["G"]
	MOVEI T,"V"
	PUTBYT @T
PGPN1:	PUTBYT ")"
	POPJ P,

NPGPNT:	LDB T,B6AT17		;PIN #
	SKIPN PMINUS
	JRST NPGPN1
	TLNE A,PLUS1
	JRST [	PUSHJ P,DECOUT
		PUTBYT "-"
		JRST PGPN1]
NPGPN1:	TLNE A,PLUS1
	SUBI T,1		;SUBTRACT 1 FOR 14 PIN DIP #
	PUSHJ P,DECOUT
	TLNE A,PLUS1
	PUTBYT "+"
	TLNE A,PLUS2
	PUTSTR [ASCIZ /+2??????????/]
	JRST PGPN1

;PRINTS CONNECTOR PIN

PGCPNT:	PUTBYT "P"
	LDB T,B6AT23		;CONNECTOR #
	PUSHJ P,DECOUT
	PUTBYT "-"
	LDB T,LOW12		;SLOT
	JRST DECOUT

RAYGEN:	JUMPL A,RAYRP1
	TLNN A,MAPPWR!MAPGND	;ONLY GROUND AND POWER CAN BE GENERATED
	JRST RAYRP0
	PUTBYT "A"
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,LOW12		;SLOT #
	PUSHJ P,NDECOUT
	PUTSTR [ASCIZ /  /]
	PUTSTR [ASCIZ /00/]
	MOVEI T,"V"
	TLNN A,MAPPWR
	MOVEI T,"G"
	PUTBYT (T)
	POPJ P,

RAYRP0:	LDB T,B6AT23		;LETTER PART OF SLOT
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,LOW12		;SLOT #
	PUSHJ P,NDECOUT
	PUTSTR [ASCIZ /  /]
	MOVEI T,3
	MOVEM T,NDIG
	LDB T,B6AT17
	JRST NDECOUT

RAYRP1:	OUTSTR [ASCIZ /CONNECTOR BECAME GENERATED PIN
/]
	JRST ERRET

FOR NAME (GNDCLR,WAGGND,GNDOUT,VCCOUT)
<NAME:	OUTSTR [ASCIZ /NAME NOT IMPLEMENTED FOR THE LEBEL BOARD
/]
	JRST ERRET
>
BEND WLEBEL
>;MWL
>;MDWL
 