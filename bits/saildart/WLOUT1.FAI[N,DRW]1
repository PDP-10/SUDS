SUBTTL	'TTY'		PRINT ONE SIGNAL ON TTY!
TLSTBP:	TRO ISBACK
	OUTSTR[ASCIZ/
SIGNAL NAME?/]
	JRST TLIST1

TLISTA:	OUTSTR[ASCIZ/
SIGNAL NAME?/]
	TRZ ISBACK
	TRO DOALLC
	JRST TLIST2

TLISTB:	OUTSTR[ASCIZ/
SIGNAL NAME?/]
TLIST:	TRZ ISBACK
TLIST1:	TRZ DOALLC
TLIST2:
ROUTE,<	TRZ DOROUT	>
	MOVE T,[PUSHJ P,TTYIN]
	MOVEM T,GETCHR
	PUSHJ P,TTYSIG
	POPJ P,			;NULL STRING OR ALTMODE
	LDB T,[POINT 7,STRTAB,6];GET FIRST CHAR
	CAIE T,FORALL		;USE FOR ALL CHAR AS "BOTH" SIGS
	JRST TLISTX
	MOVEI T," "
	DPB T,[POINT 7,STRTAB,6]
	PUSHJ P,TLISTX
	MOVEI T,NOTCHR
	DPB T,[POINT 7,STRTAB,6]
TLISTX:	MOVE A,[POINT 7,STRTAB]	;POINTER TO BEGINNING OF STRING
	PUSHJ P,PERMUT
	MOVEI T,CMPWRD
	MOVEM T,SAVET
	MOVE T,[PUSHJ P,TTYOUT]	;THIS GUY WILL COUNT ALL THE RIGHT THINGS
	MOVEM T,PUTCHR
	SETOM TTYFLG		;FLAG TTY OUTPUT
	SETZM LCOUNT		;NORMALLY DONE BY OUTSET
	SETZM CDSKPC
	MOVE L,DEFLST
	TRNE ISBACK
	JRST [	FETCH(W,L,BLST)
		JUMPE W,XBAK
		PUSHJ P,ISBP	;CALL SUBR
		SKIPG CDSKPC
		OUTSTR[ASCIZ/SIGNAL NOT FOUND!
/]
		POPJ P,]
	TRNN DOALLC
	JRST DOONE
	FETCH(W,L,WIRL)
	SKIPN USEDWL
	FETCH(W,L,PCWL)
	JUMPE W,NXCARD
DOALL:	PUSHJ P,ISBP
	FETCH(W,W,NXTWL)
	JUMPN W,DOALL
	POPJ P,

DOONE:	PUSHJ P,LSTGET		;NOW GET RIGHT CARD
	POPJ P,
	PUSHJ P,ISBP
	SKIPG CDSKPC
	OUTSTR[ASCIZ/SIGNAL NOT FOUND!
/]
	POPJ P,

ISBP:	LDB TTT,[POINT 7,PERMTB,6];FIRST CHAR OF NAME
	CAIN TTT,GENCHR		;IS IT GENERATED?
	JRST REDGEN		;READ # FROM STRING
NOTGEN:	PUSHJ P,TXTFND		;FIND THIS SIGNAL NAME
	POPJ P,
	MOVE C,E		;EQUIVALENT, USE E
ISGENP:	AOS CDSKPC		;COUNT SIGS SEEN
	FETCH(T,C,WBIT)
	TRNN T,NAM2ND
	SKIPA G,C
	FETCH(G,C,WPIN)
	TRNN ISBACK
	TRNN DOALLC		;NEED CARD LOC?
	JRST TLISTT
	FETCH(T,W,FCRD)
	HRLZM T,SLICEL
	SKIPE SLICEL
	PUSHJ P,PSLICL
	CRLF
TLISTT:	SKIPA F,G		;ALL RIGHT, SO F CAN'T BE AC 0
TLISTS:	CRLF
	PUTSIG ADDR(F,SPNT)
	FETCH(F,F,NNAM)
	JUMPN F,TLISTS
	TRNE ISBACK
	JRST BAKLOP
	JRST WIRLOP

REDGEN:	MOVEI T,CMPWRD
	MOVEM T,SAVET
	PUSHJ P,GENFND
	JRST NOTGEN		;NOT FOUND, TRY AS REGULAR SIGNAL
	MOVE C,E
	JRST ISGENP
SUBTTL	'WL'	'BL'	SETUP, CALL BODY LIST SUBR
;LIST BODIES CROSS-REFERENCE AND WIRE LISTS

ROUTE,<
ROUTIT:	SKIPN ISWW
	JRST NXWW
	TRO DOROUT
	TRZ ISBACK!FLAG
	SETZM TINCHS
	SETZM NWIRES
	SETZM NRUNS
	JRST LIST0
>;ROUTE

WRTWCI:	TRZ ISBACK
	TRO FLAG		;FLAG TO SET SIMTAB LATER
	JRST LIST0

BAKLST:	TROA ISBACK
LIST:	TRZ ISBACK
	TRZ FLAG
LIST0:	MOVE T,[PUSHJ P,TTYIN]
	MOVEM T,GETCHR
	MOVE L,DEFLST
	TRNE ISBACK
	JRST [	FETCH(W,L,BLST)
		JUMPE W,[XBAK:OUTSTR[ASCIZ/NO BACK PANEL WIRELIST!
/]
			POPJ P,]
		MOVSI T,'BL '
		JRST BLSTNM]
	PUSHJ P,LSTGET	;NOW ASK FOR PARTICULAR CARD!
	POPJ P,
ROUTE,<	TRNN DOROUT
	JRST NROUT1
	PUSHJ P,CLEARP	;CLEAR MARK BITS FOR ROUTER
	MOVSI T,'WLR'
	JRST ISROUT

NROUT1:
>;ROUTE
	MOVSI T,'WL '	;WIRE LIST FILE EXTENSION
ISROUT:	TRNE FLAG
	MOVSI T,'WCI'	;THIS IS WLCOMP INPUT EXTENSION
BLSTNM:	MOVEI TT,0	;DATA MODE
	MOVSI TTT,'DSK'
	PUSHJ P,OUTSET
	POPJ P,
	TRNE FLAG	;IF FLAG SET,
	TLO SIMTAB	;INDICATE TO SIMULATE TABS
	PUSHJ P,FILLST
	 PUTHDR			;ROUTINE TO CALL AFTER HEADER SETUP
	TRNE ISBACK
	SKIPA T,[[ASCIZ/SLOTS IN USE/]]
	MOVEI T,[ASCIZ/LOC	DIPTYPE	BODY	FILE	POS/]
	MOVEM T,HEADER
	SETZM LINCNT
	MOVEI T,WLBMAR
	MOVEM T,MARGIN
	MOVEI H,RADDR(W,WBDY,NXTB)
	SETZM SAVET		;LAST LOC PRINTED
	TRNE ISBACK
	JRST SLTLST
	SETZM NBODS		;LAST BODY NAME PRINTED
	SETZM LSTDIP		;LAST DIP NAME PRINTED
	JRST BSTR1

BSTR:	PUSHJ P,LSTLOP
BSTR1:	FETCH(H,H,NXTB)
	JUMPN H,BSTR
	JRST NOBODS

SLTLS1:	SKIPL LINCNT
	PUSHJ P,PUTHDR
	FETCH(A,H,BLOC)
	PUSHJ P,SLTOUT
	MOVE T,LCOUNT
	CAIGE T,=64		;MORE THAN 9 COLUMNS?
	JRST [	PUTBYT 11	;NO, TAB NOT CRLF
		JRST SLTLST]
	CRLF
SLTLST:	FETCH(H,H,NXTB)
	JUMPN H,SLTLS1
	JRST NOSLTS

;		BODY LIST SUBR
LSTLOP:	FETCH(A,H,BLOC)
	CAMN A,SAVET		;DIFFERENT LOC FROM LAST?
	JRST [	JUMPN A,SAMLOC	;IF NOT ZERO GO ON
		SKIPGE LINCNT	;NEED A HEADER
		JRST SAMLOC	;NO
		PUSHJ P,PUTHDR	;YES
		JRST SAMLC1]	;CONTINUE
	MOVEM A,SAVET
	SKIPL LINCNT
	PUSHJ P,PUTHDR
	FETCH(T,H,BBIT)
	PUSHJ P,WLOCOUT		;PRINT LOC (IN A, BBIT IN T)
SAMLC1:	SETZM NBODS		;CLEAR LAST BODY NAME POINTER, SO WILL PRINT ON NEW PAGE
	SETZM LSTDIP		;ALSO DIP TYPE
SAMLOC:	PUTBYT 11
	FETCH(T,H,BBIT)
	TRNE T,CBODY		;CON?
	SKIPA A,[-1]
	FETCH(A,H,DIPT)		;DIP POINTER
	SKIPN A
	MOVNI A,2
	CAMN A,LSTDIP
	JRST SAMDIP
	MOVEM A,LSTDIP
	JUMPL A,[PUTSTR (A)2+[ASCIZ/NONE/
			      ASCIZ/CON/]
		JRST SAMDIP]
	FETCH(A,A,DNAM)		;NAME POINTER
	PUSHJ P,PRINT7		;PRINT 7 CHARS OF NAME
SAMDIP:	PUTBYT 11
	MOVEI T,ADDR(H,BNAM)
	EXCH T,NBODS
	JUMPE T,NSAMBD
	MOVE TT,NBODS
	PUSHJ P,SEQMAT
	JFCL
	CAIA
	JRST SAMBOD
NSAMBD:	PUTSTR ADDR(H,BNAM)
SAMBOD:	PUTBYT 11
	FETCH(A,H,FILB)
	PUTSIX ADDR(A,FILN)	;FILENAME
	PUTBYT 11
	FETCH(A,H,BBIT)
	PUSHJ P,BPPNT
	CRLF
	POPJ P,

BPPNT:	TRNN A,BANYPOS		;ANY POSITIONS?
	POPJ P,
	LDB T,[POINT BYPOSL,A,35-BYPOSB]
	PUTBYT @N2L+1(T)
	LDB T,[POINT BXPOSL,A,35-BXPOSB]
	ADDI T,1
	JRST DECOUT

CNPNT:	TRNN A,CANYPOS		;ANY POSITIONS?
	POPJ P,
	LDB T,[POINT CYPOSL,A,35-CYPOSB]
	PUTBYT @N2L+1(T)
	LDB T,[POINT CXPOSL,A,35-CXPOSB]
	ADDI T,1
	JRST DECOUT
;		PRINT SIGNAL NAMES, CALL WIRE SUBR
NOSLTS:	MOVEI T,[ASCIZ/SIGNAL NAME			  INPUT		  OUTPUT
	SLOT	PIN   Q	TYPES	LOW	HI	LOW	HI	FILE	POS/]
	JRST GWHDR

NOBODS:	MOVEI T,[ASCIZ/SIGNAL NAME
	LOC(PIN#) TYPE	LOW	HI	USE	DIPTYPE	BODY	FILE	POS/]
ROUTE,<	TRNE DOROUT
	MOVEI T,[ASCIZ/SIGNAL NAME
	LOC(PIN#)   Z	TYPE	LOW	HI	INCHES	USE	DIPTYPE	BODY	FILE	POS/]
>;ROUTE
GWHDR:	MOVEM T,HEADER
	SETZM LINCNT			;FORCE NEW HEADER OUT (USE SAME MARGIN)
	MOVEI T,WLWMAR
	MOVEM T,MARGIN
ROUTE,<	TRNN DOROUT			;ROUTING?
	JRST NOVGRN
	MOVE T,[RPGY,,RPGN]		;THIS IS ROUTINE TO CALL
	PUSHJ P,PGSPEC			;CALL SPECIAL ROUTINE TO DO PWR AND GND
NOVGRN:
>;ROUTE
	FETCH(H,W,WIRES)
	JUMPE H,NOSTRS
STRLOP:	MOVE G,H
	FETCH(A,H,WBIT)
	PUSHJ P,GWIRBT			;GET WIRE BITS IN LEFT HALF OF A
ROUTE,<	TRNN DOROUT			;IF ROUTING
	JRST ROUSIN
	TLNE A,SPWR!SGND		;DID PWR AND GND, SO
	JRST WIRNON			;SKIP IT NOW
	JRST ROUGEN
ROUSIN:
>;ROUTE
	TLC A,GENSIG!WSINGL		;SINGLE UNNAMED PIN?
	TLNN A,WNULL			;OR NULL RUN?
	TLCN A,GENSIG!WSINGL
	JRST WIRNON			;YES, DO IT LATER
ROUGEN:	PUSHJ P,DOSTRS			;CHECK AND MAYBE PRINT SIGNAL NAMES
	JRST WIRNON			;NO PRINT WIRE
ISWIRE:	MOVE G,H			;WIRE POINTER
	TRNN ISBACK
	JRST WIRSOM
	PUSHJ P,BAKLOP
	JRST WIRNON

WIRSOM:
ROUTE,<	TRNE DOROUTE
	JRST WIRROU
ROUSIM:					;SIMPLE RUNS JUMP BACK HERE
	SETOM ZLEVN			;DISABLE Z LEVEL PRINT
	SETZM WIRELN			;CLEAR WIRE LENGTH CELL
>;ROUTE
	PUSHJ P,WIRLOP
WIRNON:	FETCH(H,H,ALPH)
	JUMPN H,STRLOP
ROUTE,<	TRNE DOROUT
	JRST NOSTRS
>;ROUTE
	MOVEI T,[ASCIZ/UNUSED PINS			  INPUT		  OUTPUT
	SLOT	PIN	TYPES	LOW	HI	LOW	HI	FILE/]
	TRNN ISBACK
	MOVEI T,[ASCIZ/UNUSED PINS
	LOC(PIN#) TYPE	LOW	HI	USE	DIPTYPE	BODY	FILE	POS/]
	MOVEM T,HEADER
	SETZM LINCNT
	FETCH(H,W,WIRES)
GENSIN:	FETCH(A,H,WBIT)
	MOVE G,H
	PUSHJ P,GWIRBT
	TLNE A,GENSIG!WNULL
	TLNN A,WSINGL!WNULL
	JRST NOGNSN
	PUSHJ P,DOSTRS
	JRST NOGNSN
	MOVE G,H
	TRNN ISBACK
	JRST WIRGSN
	PUSHJ P,BAKLOP
	JRST NOGNSN

WIRGSN:	PUSHJ P,WIRLOP
NOGNSN:	FETCH(H,H,ALPH)
	JUMPN H,GENSIN
NOSTRS:
ROUTE,<	TRNN DOROUTE
	JRST NOWIRS
	SKIPL LINCNT
	PUSHJ P,PUTHDR
	CRLF
	CRLF
	PUSHJ P,PTOTS			;TOTALS TO FILE
	MOVE T,[OUTCHR TTT]
	MOVEM T,PUTCHR
	PUSHJ P,PTOTS			;AND TO TTY
NOWIRS:
>;ROUTE
	RELEASE LST,
	POPJ P,

GWIRBT:	TRNN A,NAM2ND			;IS IT HEADER BLOCK?
	JRST [	HRL A,A			;YES, JUST COPY BITS
		POPJ P,]
	FETCH(T,H,WPIN)
	FETCH(T,T,WBIT)
	HRL A,T
	POPJ P,

DOSTRS:	TRNE REFLAG			;DOING REFERENCE HACK?
	JRST STRLP0			;YES, SKIP TEST
	TRNE A,NAM2ND			;SECOND NAME?
	POPJ P,				;YES, SKIP IT
STRLP0:	SKIPL LINCNT
	PUSHJ P,PUTHDR
STANFO,<	CAIA	>			;SOME PEOPLE WANT TO WASTE SPACE
STRLP1:	CRLF
	PUTSIG ADDR(G,SPNT)
	FETCH(G,G,NNAM)			;NEXT ONE
	JUMPN G,STRLP1
	FETCH(G,H,WBIT)
	TRNN G,NAM2ND			;DID WE START WITH FIRST?
	JRST CPOPJ1			;YES, DONE
	FETCH(G,H,WPIN)			;NO, GET FIRST
	CRLF
	PUTSIG ADDR(G,SPNT)
	PUTSTR[ASCIZ/	____ REFERENCE/]
	JRST STRLP3

STRLP2:	CRLF
	PUTSIG ADDR(G,SPNT)
STRLP3:	FETCH(G,G,NNAM)			;NEXT
	CAIE G,(H)			;BACK TO OURSELVES YET?
	JUMPN G,STRLP2
	CRLF
	CRLF				;MAKE SURE IT IS SEPARATED
	POPJ P,

ROUTE,<
WIRROU:	FETCH(T,H,WBIT)			;WE KNOW NOW THAT THIS IS A HEADER BLOCK
	TRNE T,SPWR!SGND
	JRST WIRNON			;DONE ALREADY
	TRNE T,WNULL
	JRST [	ERRSTR 0,[ASCIZ/NULL RUN/]
		JRST ROUSIM]
	TRNE T,WSINGL
	JRST [	ERRSTR 0,[ASCIZ/ONE PIN RUN/]
		JRST ROUSIM]
	PUSHJ P,ROUWR1			;ROUTE AND PRINT
	JRST WIRNON

PTOTS:	MOVE T,NWIRES
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/ TOTAL WIRES
/]
	MOVE T,NRUNS
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/ TOTAL RUNS
/]
	MOVE T,TINCHS
	MOVEM T,WIRELN
	PUSHJ P,DOWLEN
	PUTSTR[ASCIZ/ INCHES OF WIRE
/]
	POPJ P,
>;ROUTE
;		WIRE SUBR
WIRLOP:	PUSHJ P,LOPSET
	JUMPN G,WIRLP1			;IS THERE ONE?
	ERRSTR 0,[ASCIZ/NULL WIRE/]
	POPJ P,

LOPSET:	SETZM CNTTAB			;ZERO COUNTS AND LOADING
	MOVE T,[CNTTAB,,CNTTAB+1]
	BLT T,LODTAB+LODLEN-1
	MOVEM G,WIRTMP			;SAVE WIRE HEADER POINTER
	FETCH(G,G,WPIN)			;GET FIRST PIN
	POPJ P,

;COME BACK HERE IF CAN'T ROUTE
WIRLP1:	PUSHJ P,WIRLP0			;PRINT A PIN
	FETCH(G,G,NXTP)
	JUMPN G,WIRLP1
	JRST WIRERR			;END ROUTINE

WIRLP0:
NODEC,<	MOVE T,LCOUNT	;WELL, IF THE SIGNAL NAME IS ON THE
	CAIL T,8	;SAME LINE AS A PIN, COBOL WILL LOSE!!
>;NODEC
DEC,<	SKIPE LCOUNT	>
	CRLF
	PUTBYT 11
	FETCH(B,G,PBIT)		;SETUP B FOR DUP CHECKS
	FETCH(F,G,PBDY)		;F GETS BODY POINTER
	SETZM TCOUNT
	FETCH(T,F,BBIT)
	FETCH(A,F,BLOC)
	FETCH(T,G,PINN)
	HRLI A,(T)
	FETCH(T,F,BBIT)
	TRNN T,CBODY		;CONNECTOR PIN?
	JRST NCONPN		;NO
	PUSHJ P,LOCPNW
	TRNN B,DUP
	AOS NCONS
	FETCH(T,G,PBIT)
	PUSHJ P,PTAB
	SKIPN ISWW
	JRST NCGND
	FETCH(T,F,BLOC)
	FETCH(TT,G,PINN)
	PUSHJ P,CPNMER
	MOVE A,T
	TLO A,MAPCON
	PUSHJ P,PINBIT
	TLNN TT,PWR
	JRST NCPWR
	TRNN B,DUP
	AOS NPWR
	PUTBYT PWRCHR
NCPWR:	TLNN TT,GND
	JRST NCGND
	TRNN B,DUP
	AOS NGND
	PUTBYT GNDCHR
NCGND:	FETCH(T,G,PBIT)			;GET PIN BITS
	TRNN T,TRMBTS			;ANY TERMINATOR RULE?
	JRST NTRMRL			;NO
	PUTBYT "("
	LDB T,[POINT TRMBSZ,T,TRMBPS+=18]
	PUSHJ P,DECOUT
	PUTBYT ")"
NTRMRL:	SETZ A,			;CLEAR PIN TYPE POINTER
	PUTSTR[ASCIZ/				CON	/]
	JRST NOTNAM
 
NCONPN:	PUSHJ P,LOCPNP
	MOVE T,B
	PUSHJ P,PTAB
	FETCH(A,G,PTYP)
	JUMPN A,GOTPIN
	TRNN B,DUP
	AOS NULLS
	PUTSTR[ASCIZ/UN	/]
	JRST WASNUL

GOTPIN:	FETCH(TT,A,DPBIT)
	TRNN TT,NULLD		;NULL LOAD?
	JRST NOTNUL
	TRNN B,DUP
	AOS NULLS
	PUTSTR[ASCIZ/NC	/]
	JRST WASNUL

NOTNUL:	TRNE TT,INLD!GND!PWR!TERM!OUTLD		;DO WE RECOGNIZE THIS PIN?
	JRST WILPNT				;YES, WILL PRINT
	PUTSTR[ASCIZ/OOPS	/]
	JRST WASNUL

WILPNT:	EXCH A,TT
	PUSHJ P,PNTYPE				;PRINT TYPE CHARACTERS
	EXCH A,TT
	PUTBYT 11
	FETCH(T,A,LLOW)
	FETCH(TTT,A,LHI)
	HRL T,TTT
	TRNN TT,INLD
	JRST NINLD
	TRNN B,DUP
	AOS NIN
	PUSH P,T
	FETCH(TTT,G,PBIT)
	TRNE TTT,PSHARE			;SHARED LOAD?
	JRST SHARIT			;YES, PRINT AS SUCH
	HLRE T,(P)
	TRNN B,DUP
	ADDM T,LINH
	HRRE T,(P)
	TRNN B,DUP
	ADDM T,LINL
	JRST WASNEG

SHARIT:	PUTBYT "("
	HRRE T,(P)
	PUSHJ P,LDOUT
	PUTSTR[ASCIZ/)	/]
	POP P,T
	HLRZ T,T
	ADDM T,LINH
	PUSHJ P,LDOUT
	JRST WASSHR

NINLD:	TRNN TT,GND
	JRST NTGND
	TRNN B,DUP
	AOS NGND
	JRST WASNUL

NTGND:	TRNN TT,PWR
	JRST NTPWR
	HLRE TT,T
	HRRE T,T
	TRNE B,DUP
	JRST WASPWR
	AOS NPWR
	ADDM TT,PMAMPS
	JUMPE T,WASPWR
	EXCH T,PVOLTS
	JUMPE T,WASPW1
	CAME T,PVOLTS
	SETOM ILVOLT
WASPW1:	MOVE T,PVOLTS
WASPWR:	PUSH P,TT
	SKIPLE T
	PUTBYT "+"
	PUSHJ P,LDOUT
	PUTSTR[ASCIZ/V	/]
	POP P,T
	PUSHJ P,LDOUT
	PUTSTR[ASCIZ/MA/]
	JRST WASSHR

NTPWR:	TRNN TT,TERM		;TERMINATOR?
	JRST NTTERM
	PUSH P,T
	TRNE B,DUP
	JRST WASNEG
	AOS NTERM
	HRRE T,(P)
	ADDM T,LINL
	HLRE T,(P)
	TRNE TT,ECL
	JRST [	ADDM T,LOUTH
		JRST WASNEG]
	ADDM T,LINH
	JRST WASNEG

;HERE WE KNOW IT IS AN OUTPUT (THAT'S ALL THAT'S LEFT).
NTTERM:	TRNN B,DUP
	AOS ANYOUT		;COUNT SOME OUTPUT
	PUSH P,T
	TRNN TT,OPENC
	JRST NOPENC
	TRNN B,DUP
	AOS NOC
	POP P,T
	HRRE T,T
	TRNE B,DUP
	JRST [	PUTBYT "("
		PUSHJ P,LDOUT
		PUTBYT ")"
		JRST WASNUL]
	MOVEI TTT,LOCL
	PUSHJ P,LOACAL
	JRST WASLCL

NOPENC:	TRNN TT,PULL		;OUTPUT PULLUP?
	JRST NTPULL
	TRNN B,DUP
	AOS NPULL
	HLRE T,(P)
	TRNN B,DUP
	ADDM T,LPULLH
	JRST WASPUL

NTPULL:	TRNN TT,TRI		;TRI-STATE?
	JRST NOTTRI
	TRNN B,DUP
	AOS NTRI
	TRNE B,DUP
	JRST WASNEG
	HLRE T,(P)
	MOVEI TTT,LOUTH
	PUSHJ P,LOACAL
	HRRE T,(P)
	MOVEI TTT,LOUTL
	PUSHJ P,LOACAL
	JRST WASNEG

NOTTRI:	AOS NOUT
	HLRE T,(P)
	ADDM T,LOUTH
	HRRE T,(P)
	ADDM T,LOUTL
WASNEG:	TRNE B,DUP
	PUTBYT "("
	HRRE T,(P)
	PUSHJ P,LDOUT
	TRNE B,DUP
	PUTBYT ")"
WASPUL:	PUTBYT 11
	TRNE B,DUP
	PUTBYT "("
	POP P,T
	HLRE T,T
	PUSHJ P,LDOUT
	TRNE B,DUP
	PUTBYT ")"
	JRST WASSHR

WASLCL:	PUSHJ P,LDOUT
WASNUL:	PUTBYT 11
WASSHR:
ROUTE,<	TRNN DOROUT			;NEED INCHES COLUMN?
	JRST NOWLEN
	PUTBYT 11
	SKIPN WIRELN
	JRST NOWLEN			;NO
	PUSHJ P,DOWLEN
NOWLEN:
>;ROUTE
	PUTBYT 11
	JUMPE A,NOUSE1			;ANY TYPE POINTER
	FETCH(A,A,DUSE)
	PUTSIX A
NOUSE1:	PUTBYT 11
	FETCH(T,F,DIPT)			;DIP TYPE POINTER
	SETZM TCOUNT
	JUMPE T,WNONAM
	FETCH(A,T,DNAM)			;DIP NAME
	PUSHJ P,STROUT
WNONAM:	PUTBYT 11
	PUTSTR ADDR(F,BNAM)
NOTNAM:	PUTBYT 11
	FETCH(A,G,FILP)			;PIN FILE POINTER
	PUTSIX ADDR(A,FILN)
	PUTBYT 11
	FETCH(A,F,BBIT)			;APPROX POS
	TRNN A,CBODY
	JRST BPPNT			;AND PRINT
	FETCH(A,G,PBIT)
	JRST CNPNT

PTAB:	MOVE TT,TCOUNT
	SUBI TT,=16-6
	PUTBYT " "
	AOJL TT,.-1
ROUTE,<	TRNN DOROUT			;ARE WE ROUTING? (NEED Z LEVEL)
	JRST NOZLEV
	MOVE TT,ZLEVN
	JUMPL TT,NOZLV1
	PUSH P,T
STANFO,<	PUTSTR (TT)[	ASCIZ/  |/	;0	MIDDLE OF RUN LEVEL 1
			ASCIZ/  |/	;1		"	     2
			ASCIZ/  \/	;2	BEGIN RUN     LEVEL 1 TO 2
			ASCIZ/  ./	;3	BEGIN RUN     LEVEL 1 TO 1
			ASCIZ/  |/	];4	END RUN
>;STANFO
NOSTANFO,<	PUTSTR (TT)[	ASCIZ/  !./	;0	MIDDLE OF RUN LEVEL 1
			ASCIZ/  .!/	;1		"	     2
			ASCIZ/  \/	;2	BEGIN RUN     LEVEL 1 TO 2
			ASCIZ/  ./	;3	BEGIN RUN     LEVEL 1 TO 1
			ASCIZ/  !/	];4	END RUN
>;NOSTANFO
	TRNE TT,1
	TDZA TT,TT
	MOVEI TT,1
	MOVEM TT,ZLEVN
	POP P,T
NOZLV1:	PUTBYT 11
NOZLEV:
>;ROUTE
	TRNE T,PIDPIN
	SKIPA TTT,["*"]
	MOVEI TTT," "
	XCT PUTCHR
	POPJ P,

LOACAL:	SKIPN TT,(TTT)
	JRST LOASTO
	JUMPG TT,LOACL1
	CAMGE TT,T
LOASTO:	MOVEM T,(TTT)
	POPJ P,

LOACL1:	CAMLE TT,T
	JRST LOASTO
	POPJ P,
;		GATHER LOADING AND TYPE BITS
;CALL WITH WIRE POINTER IN G, GATHER CONTROL BITS IN LH AC 0
;RETURNS ACCUMULATED BITS IN A, LH=WBIT  RH=TBIT
;LOADING AND COUNTS ARE KEPT IN STANDARD PLACES
;CLOBBERS T, TT, TTT
GATHER:	FETCH(T,G,WBIT)
	TLO A,(T)
	FETCH(T,G,TBIT)
	TRO A,(T)
	FETCH(G,G,WPIN)
	JUMPE G,CPOPJ
	TLNE BAKGAT
	JRST GDOBAK
	FETCH(T,G,TBIT)
	TRNE T,ANYCON
	JRST GATHR1
GATHRZ:	TLNN DOLC
	POPJ P,
GATHR1:	FETCH(TT,G,PBIT)
	TRNE TT,DUP
	JRST GATHR5
	FETCH(T,G,PBDY)
	FETCH(T,T,BBIT)
	TRNE T,CBODY
	JRST GATC
	TLNN DOLC
	JRST GATHR5
	FETCH(T,G,PTYP)
	JUMPN T,GATHR4
	AOS NULLS
	JRST GATHR5

GATC:	SKIPE ISWW
	TLNE DOLC
	AOS NCONS
	SKIPN ISWW
	JRST GATHR5
	PUSH P,A
	FETCH(A,G,PBDY)
	FETCH(T,A,BLOCK)
	FETCH(TT,G,PINN)
	PUSHJ P,CPNMER
	MOVE A,T
	TLO A,MAPCON
	PUSHJ P,PINBIT
	POP P,A
	JUMPE TT,GATHR5
	TSO A,TT
	TLNN DOLC
	JRST GATHR5
	TLNE TT,PWR
	AOS NPWR
	TLNE TT,GND
	AOS NGND
	JRST GATHR5

GATHR4:	FETCH(TT,T,LHI)
	FETCH(TTT,T,DPBIT)
	FETCH(T,T,LLOW)
	TRNE TTT,NULLD
	JRST [	AOS NULLS
		JRST GATHR5]
	TRNE TTT,GND
	JRST [	AOS NGND
		JRST GATHR5]
	TRNE TTT,PWR
	JRST [	AOS NPWR
		ADDM TT,PMAMPS
		JUMPE T,GATHR5
		EXCH T,PVOLTS
		JUMPE T,GATHR5
		CAME T,PVOLTS
		SETOM ILVOLT
		JRST GATHR5]
	TRNE TTT,INLD
	JRST [	AOS NIN
		FETCH(TTT,G,PBIT)
		ADDM TT,LINH
		TRNN TTT,PSHARE
		ADDM T,LINL
		JRST GATHR5]
	TRNE TTT,TERM
	JRST [	AOS NTERM
		ADDM T,LINL
		TRNE TTT,ECL
		ADDM TT,LOUTH
		TRNN TTT,ECL
		ADDM TT,LINH
		JRST GATHR5]
	TRNN TTT,OUTLD
	JRST GATHR5
	AOS ANYOUT
	TRNE TTT,OPENC
	JRST [	AOS NOC
		MOVEI TTT,LOCL
		PUSHJ P,LOACAL
		JRST GATHR5]
	TRNE TTT,TRI
	JRST [	AOS NTRI
		MOVEI TTT,LTRIL
		PUSH P,TT
		PUSHJ P,LOACAL
		POP P,T
		MOVEI TTT,LTRIH
		PUSHJ P,LOACAL
		JRST GATHR5]
	TRNE TTT,PULL
	JRST [	AOS NPULL
		ADDM TT,LPULLH
		JRST GATHR5]
	AOS NOUT
	ADDM TT,LOUTH
	ADDM T,LOUTL
GATHR5:	FETCH(G,G,NXTP)
	JUMPN G,GATHR1
	POPJ P,

GDOBAK:	FETCH(T,G,PBIT)
	TRNE T,DUP
	JRST GATBKN
	FETCH(TTT,G,CBIT)		;CON/CARD BITS
	TRNN TTT,INLD!TERM			;ANY INPUTS?
	JRST GATBKO			;NO, CHECK OUT
	TRCE TTT,INLD
	AOS NIN
	TRCE TTT,TERM
	AOS NTERM
	TRCN TTT,TERM!INLD
	AOS NTRMIN
	FETCH(T,G,HILD)
	ADDM T,LINH
	FETCH(T,G,LILD)
	ADDM T,LINL
GATBKO:	FETCH(TT,G,HOLD)
	FETCH(T,G,LOLD)
	TRNN TTT,OUTLD			;ANY OUTPUTS?
	JRST GATBKX			;NO, LOOP
	AOS ANYOUT
	TRNE TTT,INLD
	AOS NINOUT
	TRNE TTT,TERM
	AOS NTRMOUT
	TRNE TTT,TRI
	JRST [	AOS NTRI
		MOVEI TTT,LTRIL
		PUSH P,TT
		PUSHJ P,LOACAL
		POP P,T
		MOVEI TTT,LTRIH
		PUSHJ P,LOACAL
		JRST GATBKN]
	TRNN TTT,OPENC!PULL
	JRST [	AOS NOUT
		JRST GATBKX]
	TRNN TTT,PULL
	JRST GATBKC
	AOS NPULL
	ADDM TT,LPULLH
GATBKC:	TRNN TTT,OPENC
	JRST GATBKN
	AOS NOC
	MOVEI TTT,LOCL
	PUSHJ P,LOACAL
	JRST GATBKN

GATBKX:	ADDM T,LOUTL
	ADDM TT,LOUTH
GATBKN:	FETCH(G,G,NXTP)
	JUMPN G,GDOBAK
	POPJ P,
;		END WIRE SUBR, PRINT ERRORS
WIRERR:	MOVE G,WIRTMP
	FETCH(T,G,WBIT)
	TRNE T,SNC		;IS THIS AN NC WIRE?
	JRST ERDONE		;YES, NOTHING CAN BE WRONG!!!!
	SKIPE NULLS		;ANY NULLS IN RUN?
	ERRSTR 0,[ASCIZ/UN OR NC PIN(S)/]
	TRNN T,CABRUN
	JRST ISSIG1
	MOVEI A,8
	CAMG A,LCOUNT
	CRLF
	PUSHJ P,FILL
	PUTSTR[ASCIZ/  "CABLE RUN"/]
ISSIG1:	SKIPE NTERM
	JRST ISSIG
	SKIPN NIN
	SKIPE ANYOUT
	JRST ISSIG
	SKIPN NPWR
	SKIPE NGND
	JRST ISSIG
ERDONE:	SKIPE LCOUNT		;AT LEFT?
	CRLF			;NO, GET THERE
	POPJ P,

ISSIG:	FETCH(T,G,WBIT)
	TRNE T,WSINGL!WNULL	;THESE DON'T NEED LOADING SUMMARY
	JRST NOTLEN
	MOVEI A,=24
	CAMG A,LCOUNT
	CRLF
	PUSHJ P,FILL
	PUSHJ P,LODMER		;CALC PROPER LOADING
	SKIPE T,NPULL
	JRST [	CAME T,ANYOUT	;ALL OUTPUTS PULLUPS?
		JRST .+1
		TLO TFLG
		PUTBYT "("
		MOVE T,LINH
		PUSHJ P,LDOUT
		PUTBYT ")"
		PUTBYT "/"
		JRST PULNLY]
	TLZ TFLG		;ASSUME NOT PRINT HI
	SKIPN T,LINL
	JRST [	SKIPN NPWR
		JRST NOTPWR
		PUTSTR [ASCIZ/PWR/]
		JRST NOHIPN
	NOTPWR:	SKIPN NGND
		JRST .+1
		PUTSTR [ASCIZ/GND/]
		JRST NOHIPN]
	PUSHJ P,LDOUT
	SKIPN NTERM
	SKIPE NIN
	SKIPN ANYOUT
	JRST NOHIPN
	SKIPE LINH
	SKIPN LOUTH
	JRST NOHIPN
	SKIPE LINL
	SKIPN LOUTL
	JRST NOHIPN
	MOVE T,LINL
	ADD T,LOUTL
	JUMPE T,MUSTHI
	EQV T,LOUTL
	JUMPGE T,MUSTHI
	JRST MUSTHI		;BETTER PRINT HI
	HRLZ T,LINL
	IDIV T,LOUTL
	MOVM T,T
	HRLZ TT,LINH
	IDIV TT,LOUTH
	MOVM TT,TT
	CAMGE TT,T
	JRST NOHIPN
MUSTHI:	TLO TFLG
	PUTBYT "("
	MOVE T,LINH
	PUSHJ P,LDOUT
	PUTBYT ")"
NOHIPN:	PUTBYT "/"
	MOVE T,LOUTL
	PUSHJ P,LDOUT
	TLNN TFLG
	JRST NOTOUT
PULNLY:	PUTBYT "("
	MOVE T,LOUTH
	PUSHJ P,LDOUT
	PUTBYT ")"
NOTOUT:
ROUTE,<	TRNN DOROUTE
	JRST NOTLEN
	SKIPE T,INCHES			;ANY TOTAL WIRE LENGTH?
	CAMN T,WIRELN			;ONLY PRINT IF DIFFERENT FROM LAST
	JRST NOTLEN
	MOVEM T,WIRELN
	MOVEI A,=48
	PUSHJ P,FILL
	PUSHJ P,DOWLEN
>;ROUTE
NOTLEN:	FETCH(T,G,TBITS)
	TRC T,ECL!TTL
	TRCN T,ECL!TTL
	ERRSTR 3,[ASCIZ/ECL PINS CONNECTED TO TTL PINS/]
	TRNN T,ECL
	JRST NOZERR		;NO CHECK UNLESS ECL RUN
	MOVE T,NTERM
	CAILE T,1
	ERRSTR 2,[ASCIZ/MULTIPLE TERMINATORS/]
	SKIPN NCONS
	JRST INTZCK
	SKIPN ANYOUT		;OUTPUTS?
	JRST EXTZIN
	SKIPE NTERM
	ERRSTR 2,[ASCIZ/ILLEGAL TERMINATOR/]
	JRST NOZERR

EXTZIN:	SKIPN NIN		;INPUTS?
	JRST NOZERR		;NO, OK
	SKIPN NTERM
	ERRSTR 0,[ASCIZ/QUESTIONABLE TERMINATION/]
	JRST NOZERR

INTZCK:	SKIPN ANYOUT		;OUTPUTS?
	JRST NOZERR		;OTHER ERRORS WILL CATCH REST
	SKIPN NIN
	JRST INTNIN
	SKIPN NTERM
	ERRSTR 1,[ASCIZ/TERMINATOR NEEDED/]
	JRST NOZERR

INTNIN:	SKIPE NTERM
	ERRSTR 2,[ASCIZ/QUESTIONABLE TERMINATION/]
NOZERR:	MOVE T,NPULL
	CAILE T,1
	ERRSTR 2,[ASCIZ/MORE THAN 1 PULLUP/]
	MOVE T,NCONS
	CAILE T,1
	ERRSTR 0,[ASCIZ/MORE THAN 1 CONNECTOR PIN/]
	SKIPN NPWR
	SKIPE NGND
	JRST PWRGND
	SKIPN T,ANYOUT
	JRST NOOVLD
	CAMN T,NOUT			;ALL NORMAL OUTPUTS?
	JRST [	CAIN T,1
		JRST WOROK
		JRST WORNOK]
	CAMN T,NTRI
	JRST WOROK
	SUB T,NPULL
	CAME T,NOC
WORNOK:	ERRSTR 2,[ASCIZ/"WIRE-OR" WARNING/]
WOROK:	SKIPN NIN
	JRST [	SKIPN ANYOUT
		JRST ERDONE
		SKIPE NCONS
		JRST ERDONE
		PUSHJ P,EXTRAO
		JRST [	ERRSTR 1,[ASCIZ/UNUSED SIGNAL/]
			JRST ERDONE]
		ERRSTR 0,[ASCIZ/UNUSED EXTRA OUTPUT/]
		JRST ERDONE]
	SKIPE NCONS
	JRST NOOVLD		;DONt COMPLAIN ABOUT THESE IF LEAVES CARD
	SKIPN LINH
	JRST NOOVHI
	SKIPN LOUTH
	ERRSTR 3,[ASCIZ/NO HI DRIVE/]
NOOVHI:	MOVE T,ANYOUT
	SKIPE LINL			;IF NO INPUT LOADS,
	CAMN T,NPULL			;OR ALL OUTPUTS PULLUPS?
	JRST NOOVLD			;DON'T CARE ABOUT LOW IF ONLY PULLUPS
	SKIPN LOUTL
	ERRSTR 3,[ASCIZ/NO LOW DRIVE/]
NOOVLD:	SKIPN T,ANYOUT
	JRST [	SKIPN NIN
		JRST ERDONE
		FETCH(T,G,WBIT)
		TRNE T,SPWR!SGND
		JRST ERDONE		;WELL, THIS IS SOME SORT OF DRIVE
		SKIPE NCONS
		JRST [	MOVM T,LINL
			CAIL T,=1000	;IF .GE. 10 MA
			ERRSTR 0,[ASCIZ/HEAVILY LOADED/]
			JRST ERDONE]
		FETCH(TT,G,TBIT)
		TRC T,WSINGL!GENSIG
		TRCN T,WSINGL!GENSIG	;SINGLE UNNAMED PINS ONLY
		TRNE TT,DRVREQ		;WHICH REQUIRE NO DRIVE
		CAIA			;LOSE
		JRST ERDONE		;NO MESSAGE IF NO DRIVE REQUIRED
		PUSHJ P,EXTRAI
		JRST [	ERRSTR 1,[ASCIZ/NO DRIVE/]
			JRST ERDONE]
		ERRSTR 0,[ASCIZ/INACTIVE INPUT/]
		JRST ERDONE]
	CAMN T,NPULL
	JRST CKHI
	SKIPN T,LOUTL
	JRST CKHI
	PUSHJ P,LOLOAD
	JUMPL T,CKHI
	ERRSTR 2,[ASCIZ/OUTPUT LOW OVERLOAD/]
CKHI:	PUSHJ P,HILOAD
	JUMPL T,ERDONE
	ERRSTR 2,[ASCIZ/OUTPUT HI OVERLOAD/]
	JRST ERDONE

LOLOAD:	MOVE T,LOUTL
	IMUL T,LOADMG	;LOAD MARGIN
	PUSH P,T
	MOVE T,LINL
	IMULI T,=100
	ADDM T,(P)
	POP P,T
	SKIPN T
	SKIPA T,[-1]	;IF IT COMES OUT EXACT
	EQV T,LOUTL
	POPJ P,

HILOAD:	MOVE T,LOUTH
	IMUL T,LOADMG	;LOAD MARGIN
	PUSH P,T
	MOVE T,LINH
	IMULI T,=100
	ADDM T,(P)
	POP P,T
	SKIPN T
	SKIPA T,[-1]
	EQV T,LOUTH
	POPJ P,

PWRGND:	SKIPE NPWR
	SKIPN NGND
	CAIA
	ERRSTR 3,[ASCIZ/POWER CONNECTED TO GROUND!/]
	SKIPE ANYOUT
	ERRSTR 3,[ASCIZ\OUTPUTS CONNECTED TO POWER AND/OR GROUND!\]
	SKIPE ILVOLT
	ERRSTR 3,[ASCIZ/MIXED POWER VOLTAGES/]
	SKIPE NPWR
	SKIPN NIN
	JRST ERDONE
	ERRSTR 3,[ASCIZ/INPUT CONNECTED TO POWER!/]
	JRST ERDONE

EXTRAI:	FETCH(T,G,WBIT)
	TRNE T,GENSIG		;MUST NOT HAVE NAME OF ITS OWN
	TRNN T,WSINGL		;MUST BE SINGLE PIN
	POPJ P,
	FETCH(TTT,G,WPIN)	;GET FIRST (AND ONLY) PIN
	FETCH(TT,TTT,PTYP)
	JUMPE TT,CPOPJ		;NO TYPE, NO SINGLE OUTPUT
	FETCH(T,TT,DPBIT)	;TYPE BITS
	TRNN T,SHARE		;ARE WE THE RIGHT GUY?
	POPJ P,			;NO
	FETCH(TT,TT,PSWP)
	JUMPE TT,CPOPJ		;IF NO SHARE #, NO POSSIBLE MATCH
	MOVEM TT,SHRNUM
	FETCH(T,TTT,PBDY)	;BODY POINTER
	FETCH(T,T,BPIN)		;AND FIRST PIN
EXTRI1:	CAIN T,(TTT)		;DON'T LOOK AT US!
	JRST EXTRI2
	FETCH(TT,T,PTYP)
	JUMPE TT,EXTRI2
	FETCH(TT,TT,PSWP)	;WE KNOW THE # WON'T MATCH IF NOT INPUT
	CAME TT,SHRNUM		;SAME GROUP?
	JRST EXTRI2		;NO
	PUSH P,T
	HRLM G,(P)
	FETCH(G,T,HPNT)
	TLZ DOLC!BAKGAT
	SETZ A,
	PUSHJ P,GATHER
	POP P,T
	HLRZ G,T
	TLNE A,SGND		;IS THIS RUN EVER CALLED "GND"?
	JRST CPOPJ1		;YES, INACTIVE INPUT
EXTRI2:	FETCH(T,T,NXBP)
	JUMPN T,EXTRI1
	POPJ P,

EXTRAO:	FETCH(T,G,WBIT)
	TRNN T,WSINGL		;MUST BE SINGLE PIN
	POPJ P,
	FETCH(TTT,G,WPIN)	;GET FIRST (AND ONLY) PIN
	FETCH(TT,TTT,PTYP)
	JUMPE TT,CPOPJ		;NO TYPE, NO SINGLE OUTPUT
	FETCH(T,TT,DPBIT)	;TYPE BITS
	TRNN T,OUTLD		;ARE WE THE RIGHT GUY?
	POPJ P,			;NO
	FETCH(TT,TT,PSWP)
	JUMPE TT,CPOPJ		;IF NO SHARE #, NO POSSIBLE MATCH
	MOVEM TT,SHRNUM
	FETCH(T,TTT,PBDY)	;BODY POINTER
	FETCH(T,T,BPIN)		;AND FIRST PIN
EXTRO1:	CAIN T,(TTT)		;DON'T LOOK AT US!
	JRST EXTRO2
	FETCH(TT,T,PTYP)
	JUMPE TT,EXTRO2
	FETCH(TT,TT,PSWP)	;WE KNOW THE # WON'T MATCH IF NOT OUTPUT
	CAME TT,SHRNUM		;SAME GROUP?
	JRST EXTRO2		;NO
	PUSH P,T
	HRLM G,(P)
	FETCH(G,T,HPNT)
	TLZ DOLC!BAKGAT
	SETZ A,
	PUSHJ P,GATHER
	POP P,T
	HLRZ G,T
	TRNE A,ANYCON!INLD	;IS THIS OUTPUT USED, OR DOES IT GO TO BACK PANEL?
	JRST CPOPJ1		;YES, EXTRA UNUSED OUTPUT
EXTRO2:	FETCH(T,T,NXBP)
	JUMPN T,EXTRO1
	POPJ P,

LODMER:	PUSHJ P,LODMR1
	MOVEM T,LOUTL
	MOVEM TT,LOUTH
	POPJ P,

LODMR1:	SKIPE T,LOUTL
	JRST LODMRH
	SKIPN T,LTRIL
	SKIPA T,LOCL
	SKIPN TT,LOCL
	JRST LODMRH
	JUMPG T,[CAMLE T,TT
		MOVE T,TT
		JRST LODMRH]
	CAMGE T,TT
	MOVE T,TT
LODMRH:	SKIPN TT,LOUTH
	MOVE TT,LTRIH
	ADD TT,LPULLH
	POPJ P,
;		HEADER SUBRS
PUTHDR:	TLZN NOFF
	PUTSTR[BYTE(7)15,14]
	PUSH P,A
	MOVN TTT,PAGLEN
	ADD TTT,MARGIN
	MOVEM TTT,LINCNT
	FETCH(A,L,WLNM)			;NAME OF WIRE LIST
	SETZM TCOUNT
	PUSHJ P,STROUT
	PUSHJ P,PNTBRW		;PRINT BAY-RACK-SLOT
	POP P,A
	CRLF
	PUTSTR @HEADER
	PUTSTR[ASCIZ/

/]
	POPJ P,

;TITLE PARAMETERS
FNPOS__=32		;4 COLUMNS FOR WIRE LIST TITLE
DTPOS__FNPOS+=24	;SO THE LINE OVERFLOWS IF DEC HAS LARGEST NAME
CLPOS__DTPOS+=24

PNTBRW:	FETCH(A,W,FCRD)
PNTBRS:	PUSH P,A
	PUSHJ P,PNTFDT
	SKIPN (P)
	JRST [	POP P,(P)
		POPJ P,]
	MOVEI A,CLPOS
	PUSHJ P,FILL
	POP P,A
	JRST SLTOUT

PNTFDT:	SKIPE TTYFLG
	POPJ P,
	SKIPN PNTNAM
	JRST PNTDT
	MOVEI A,FNPOS
	PUSHJ P,FILL
	MOVEI A,PNTNAM
	PUSHJ P,FILPNT
PNTDT:	MOVEI A,DTPOS
	PUSHJ P,FILL
	DATE A,			;DATE
	HRLZ A,A
	MSTIME T,
	IDIVI T,=60*=1000	;TIME IN MINUTES
	HRR A,T
PRNTDT:	HLRZ T,A
	IDIVI T,=31
	HRLM T,(P)
	MOVEI T,1(TT)
	IDIVI T,=10
	PUTBYT 60(T)
	PUTBYT 60(TT)
	PUTBYT "-"
	HLRZ T,(P)
	IDIVI T,=12
	HRLM T,(P)
	PUTSTR DATES(TT)
	HLRZ T,(P)
	ADDI T,=64
	IDIVI T,=10
	PUTBYT 60(T)
	PUTBYT 60(TT)
	PUTSTR[ASCIZ/  /]
	HRRZ T,A
	IDIVI T,=60*=10
	PUTBYT 60(T)
	MOVE T,TT
	IDIVI T,=60
	PUTBYT 60(T)
	MOVE T,TT
	IDIVI T,=10
	PUTBYT 60(T)
	PUTBYT 60(TT)
	POPJ P,

DATES:	ASCIZ/JAN-/
	ASCIZ/FEB-/
	ASCIZ/MAR-/
	ASCIZ/APR-/
	ASCIZ/MAY-/
	ASCIZ/JUN-/
	ASCIZ/JUL-/
	ASCIZ/AUG-/
	ASCIZ/SEP-/
	ASCIZ/OCT-/
	ASCIZ/NOV-/
	ASCIZ/DEC-/

FILLST:	SKIPE TTYFLG		;NOT TO TTY
	JRST CPOPJ1
TFILES:	PUSH P,@(P)		;SAVE HEADER DISPATCH ON STACK
	PUSH P,B
	TRNN ISBACK
	FETCH(B,W,FILS)
	TRNE ISBACK
	FETCH(B,L,BKNM)
	JUMPE B,FILLS2
	MOVEI T,[ASCIZ/FILNAM	P,PN		DATE	   TIME	MODULE(DWG NUM)	REV	AUTHOR
	TITLE 1				PROJECT		BOARD TYPE
	TITLE 2				SHEET n OF m
	CARD LOC(VARIABLE SETTINGS)/]
	MOVEM T,HEADER
ITS,<
	SKIPE XGP
	PUSHJ P,XGPSTR		;PUT XGP INFO OUT
>;ITS
	MOVEI T,FILMAR		;FILENAME LIST MARGIN
	MOVEM T,MARGIN
	SETZM LINCNT		;ALWAYS START NEW PAGE
FILLS1:	SKIPL LINCNT
	PUSHJ P,@-1(P)
	PUSHJ P,FBLKPN
	FETCH(B,B,FNXT)
	JUMPN B,FILLS1
FILLS2:	POP P,B
	POP P,(P)
	JRST CPOPJ1

FBLKPN:	PUSH P,A
	PUTSIX ADDR(B,FILN)
	PUTBYT 11
	SETZM TCOUNT
	FETCH(TT,B,FPPN)
NOCMU,<	JUMPE TT,NOFPPN
	HLLZ TT,TT
	PUSHJ P,LSIXOUT
	PUTBYT ","
	FETCH(TT,B,FPPN)
	HRLZ TT,TT
	PUSHJ P,LSIXOUT
>;NOCMU
CMU,<	MOVE TTT,[TT,,PPNBUF]
	DECCMU TTT,
	JRST [	PUTSIX ADDR(B,FPPN)
		JRST .+2]
	PUTSTR PPNBUF
>;CMU
NOFPPN:	MOVE T,TCOUNT
	CAIGE T,8
	PUTBYT 11
	PUTBYT 11
	FETCH(A,B,FDAT)
	SKIPE A
	PUSHJ P,PRNTDT
	PUTBYT 11
	FETCH(A,B,FMOD)
	PUSHJ P,STROUT
	FETCH(A,B,FVAR)
	JUMPE A,NPVAR
	PUTBYT "-"
	PUSHJ P,STROUT
NPVAR:	FETCH(A,B,FPRF)
	JUMPE A,NPPRF
	PUTBYT "-"
	PUSHJ P,STROUT
NPPRF:	MOVEI A,=56
	PUSHJ P,FILL
	FETCH(A,B,FREV)
	PUSHJ P,STROUT
	FETCH(A,B,AUTHOR)
	PUTBYT 11
	PUSHJ P,STROUT
	CRLF
	FETCH(A,B,TIT1)
	JUMPE A,[FETCH(A,B,FPROJ)
		JUMPE A,[FETCH(A,B,FBRD)
			JUMPE A,NOTIT1
			JRST ISFBRD]
		JRST ISPROJ]
	PUTBYT 11
	PUSHJ P,STROUT
	FETCH(A,B,FPROJ)
	JUMPE A,[FETCH(A,B,FBRD)
		JUMPE A,NOFBRD
		JRST ISFBRD]
ISPROJ:	MOVEI A,=40
	PUSHJ P,FILL
	FETCH(A,B,FPROJ)
	PUSHJ P,STROUT
NOPROJ:	FETCH(A,B,FBRD)
	JUMPE A,NOFBRD
ISFBRD:	MOVEI A,=56
	PUSHJ P,FILL
	FETCH(A,B,FBRD)
	PUSHJ P,STROUT
NOFBRD:	CRLF
NOTIT1:	FETCH(A,B,TIT2)
	JUMPE A,[FETCH(A,B,FPAGE)
		JUMPN A,ISPAGE
		FETCH(A,B,FOF)
		JUMPN A,ISPAGE
		JRST NOTIT2]
	PUTBYT 11
	PUSHJ P,STROUT
	FETCH(A,B,FPAGE)
	JUMPN A,ISPAGE
	FETCH(A,B,FOF)
	JUMPE A,NOPAGE
ISPAGE:	MOVEI A,=40
	PUSHJ P,FILL
	PUTSTR[ASCIZ/SHEET /]
	FETCH(A,B,FPAGE)
	JUMPE A,.+2
	PUSHJ P,STROUT
	FETCH(A,B,FOF)
	JUMPE A,NOPAGE
	PUTSTR[ASCIZ/ OF /]
	PUSHJ P,STROUT
NOPAGE:	CRLF
NOTIT2:	FETCH(T,B,MCRD)
	HRLZM T,SLICEL
	FETCH(T,B,MVLS)
	MOVEM T,VARLST
	SKIPN SLICEL		;IS THERE A CARD LOC
	JUMPE T,NOTIT3		;NO, JUMP IF NO VARIABLES EITHER
	FETCH(T,B,MUBN)
	MOVEM T,UPPER
	FETCH(T,B,MLBN)
	MOVEM T,LOWER
	PUSHJ P,PSLVAR
	CRLF
NOTIT3:	POP P,A
	POPJ P,
ITS,<
;PUT XGP INFO ON FIRST PAGE FOR ITS XGP SYSTEM

XGPSTR:	SETZM MARGIN		;FLUSH AGAINST THE LEFT MARGIN
	MOVNI T,STDPAG		;AVOID AN EXTRANEOUS FORM FEED
	MOVEM T,LINCNT
	PUTSTR [ASCIZ /;SKIP 1
;TOPMAR 128
;BOTMAR 0
;VSP /]
	SKIPN T,XGPVSP
	MOVEI T,6		;COUGH UP REASONABLE DEFAULT
	PUSHJ P,DECOUT
	PUTSTR [ASCIZ /
;KSET DSK:/]
	SKIPN T,XGPPPN
	MOVE T,[SIXBIT /FONTS/]
	PUTSIX T	
	PUTBYT ";"
	SKIPN T,XGPFNT
	MOVE T,[SIXBIT /25FG/]
	PUTSIX T
	PUTBYT 40
	SKIPN T,XGPEXT
	MOVE T,[SIXBIT /KST/]
	PUTSIX T
	PUTSTR [BYTE (7) 15,12]
	PUTBYT 14		;THROW IN A FORM FEED
	POPJ P,
>;ITS
;		BACK PANEL LISTING SUBRS
BAKLOP:	SETOM CONFLG	;AOS THIS FLAG IF MORE THAN ONE CARD
	MOVEM G,WIRTMP
	FETCH(G,G,WPIN)	;THERE HAS TO BE AT LEAST ONE!
	JUMPE G,BAKLP0
	MOVE T,LCOUNT
	CAIL T,8
BAKLP1:	CRLF
	PUSHJ P,SLIT
	FETCH(G,G,NXTP)
	JUMPN G,BAKLP1
BAKLP0:	SETZB A,CNTTAB
	MOVE T,[CNTTAB,,CNTTAB+1]
	BLT T,LODTAB+LODLEN-1
	TLO DOLC!BAKGAT
	MOVE G,WIRTMP
	PUSHJ P,GATHER
	PUSHJ P,LODMER		;DO LOADING CALC
 	MOVE G,WIRTMP
	FETCH(T,G,WRN)
	JUMPE T,NBAKWR
	PUSH P,A
	MOVEI A,=8
	CAMG A,LCOUNT
	CRLF
	PUSHJ P,FILL
	PUTSTR[ASCIZ/WR = /]
	FETCH(T,G,WRN)
	PUSHJ P,DECOUT
	POP P,A
NBAKWR:	TLNN A,WNULL
	TRNN A,INLD!OUTLD!TERM!PWR!GND
	JRST BAKLPN
	TLNN A,WSINGL
	JRST NONIL
	JRST NOSUMB

BAKLPN:	ERRSTR 0,[ASCIZ/THIS WIRE DOESN'T HAVE ANYTHING ON IT/]
	JRST ERDONE

NONIL:	PUSH P,A
	MOVEI A,=24
	CAMG A,LCOUNT
	CRLF
	PUSHJ P,FILL
	POP P,A
	PUSHJ P,PNTYPE
	PUSHJ P,CLOADS
NOSUMB:	SKIPG CONFLG			;MORE THAN ONE CARD?
	ERRSTR 2,[ASCIZ/ONLY USED ON ONE CARD!/]
	TRC A,ECL!TTL
	TRCN A,ECL!TTL
	ERRSTR[ASCIZ/ECL PINS CONNECTED TO TTL PINS/]
	TRNN A,ECL		;ECL RUN?
	JRST NBZERR		;NO, NO TERM CHECK
	SKIPE NTRMOUT
	JRST ILTERM
	TRC A,INLD!OUTLD
	TRCE A,INLD!OUTLD
	JRST NBZERR
	SKIPG NTERM
	JRST [	ERRSTR 1,[ASCIZ/TERMINATOR NEEDED/]
		JRST NBZERR]
	MOVE T,NTERM
	CAMN T,NTRMIN
	CAIE T,1
	JRST QTERM
	JRST NBZERR

ILTERM:	ERRSTR 2,[ASCIZ/ILLEGAL TERMINATION/]
	JRST NBZERR

QTERM:	ERRSTR 1,[ASCIZ/QUESTIONABLE TERMINATION/]
NBZERR:	SKIPLE NPULL
	ERRSTR 2,[ASCIZ/MORE THAN 1 PULLUP/]
	MOVE T,NOUT
	SUB T,NPULL
	CAILE T,1
	CAMN T,NTRI
	CAIA
	ERRSTR 3,[ASCIZ/"WIRE-OR" WARNING/]
	MOVE T,CONFLG
	TRC A,FFOUT!ECL		;ECL FLIP FLOP OUTPUT?
	TRCN A,FFOUT!ECL
	CAIG T,1		;YES, DRIVING TOO MANY BOARDS(STARTED AS -1)?
	CAIA
	ERRSTR 2,[ASCIZ/FLIP-FLOP OUTPUT DRIVING MORE THAN 1 OTHER BOARD/]
	TRC A,OUTLD!INLD
	TRCE A,OUTLD!INLD
	JRST ISHIDV
	SKIPN T,LOUTH			;HI DRIVE?
	ERRSTR 3,[ASCIZ/NO HI DRIVE/]
	SKIPN T,LOUTL			;LOW DRIVE?
	ERRSTR 3,[ASCIZ/NO LOW DRIVE/]
ISHIDV:	TRNN A,INLD!PWR!GND
	TDNN A,[SPWR!SGND,,OUTLD]
	CAIA
	ERRSTR 1,[ASCIZ/UNUSED SIGNAL/]
	TDNN A,[SPWR!SGND,,OUTLD]
	TRNN A,INLD!PWR!GND
	CAIA
	ERRSTR 1,[ASCIZ/NO DRIVE/]
	TRNN A,TERM
	TRNE A,INLD
	TRNN A,OUTLD
	JRST NOTWIR
	MOVE T,NOUT
	CAMN T,NPULL		;IF JUST PULLUPS
	JUMPN T,NOLOWD		;THEN NO LOW CHECK
	PUSHJ P,LOLOAD
	JUMPL T,NOLOWD
	ERRSTR 2,[ASCIZ/OUTPUT LOW OVERLOAD/]
NOLOWD:	PUSHJ P,HILOAD
	JUMPL T,NOTWIR
	ERRSTR 2,[ASCIZ/OUTPUT HI OVERLOAD/]
NOTWIR:	TRCN A,PWR!GND
	JRST ERDONE
	TRCN A,PWR!GND
	ERRSTR 3,[ASCIZ/POWER CONNECTED TO GROUND!/]
	TRNE A,OUTLD
	ERRSTR 3,[ASCIZ\OUTPUT(S) CONNECTED TO POWER AND/OR GROUND!\]
	TRC A,INLD!PWR
	TRCN A,INLD!PWR
	ERRSTR 3,[ASCIZ/INPUT(S) CONNECTED TO POWER!/]
	SKIPE ILVOLT
	ERRSTR 3,[ASCIZ/MIXED POWER VOLTAGES/]
	JRST ERDONE

SLIT:	FETCH(F,G,PBDY)
	PUTBYT 11
	FETCH(A,F,BLOC)			;SLOT TO A
	PUSHJ P,SLTOUT			;PRINT IT
	PUTBYT 11
	FETCH(A,G,PINN)
	PUSHJ P,PRBCN
	FETCH(T,G,PBIT)
	ANDI T,QBITS		;EXTRACT Q LETTER FROM BITS
	JUMPE T,NOBAKQ			;IS THERE ONE?
	MOVEI A,=22
	SUB A,LCOUNT
	CAIA
	PUTBYT 40
	SOJGE A,.-1
	PUTBYT @QN2L(T)
NOBAKQ:	PUTBYT 11
	FETCH(A,G,PBIT)
	TRNE A,DUP
	TDZA A,A
	FETCH(A,G,CBIT)			;CON/CARD BITS
	JUMPE A,[PUTSTR[ASCIZ/				/]
		JRST SLIT3]
	AOS CONFLG			;AOS THIS TO INDICATE ANOTHER CARD
	PUSHJ P,PNTYPE
	MOVE C,G
	PUSHJ P,LOADIT
SLIT3:	PUTBYT 11
	FETCH(A,G,FILP)
	PUTSIX ADDR(A,FILN)		;FILENAME
	PUTBYT 11
	FETCH(A,G,PBIT)
	JRST CNPNT

LOADIT:	FETCH(T,C,LILD)
	ADDM T,LINL			;GATHER LOADING FOR BS FILE OUTPUT
	PUSHJ P,SLITLH
	FETCH(T,C,HILD)
	ADDM T,LINH
	PUSHJ P,SLITLH
	FETCH(T,C,LOLD)
	ADDM T,LOUTL
	PUSHJ P,SLITLH
	FETCH(T,C,HOLD)
	ADDM T,LOUTH
	JRST SLITLH

CLOADS:	MOVE T,LINL
	PUSHJ P,SLITLH
	MOVE T,LINH
	PUSHJ P,SLITLH
	MOVE T,LOUTL
	PUSHJ P,SLITLH
	MOVE T,LOUTH
SLITLH:	PUTBYT 11
	PUSHJ P,LDOUT
	POPJ P,
SUBTTL	'WLS' 'BS' 'LES' SETUP, BODY SUMMARIES
;HERE WE GENERATE A WIRE LIST SUMMARY!
ALLWLS:	TRZA ISBACK
ALLBS:	TRO ISBACK
	TDZ [ASKHIM,,DOERRS]
	JRST SUMALL

ERRSUM:	TRZ ISBACK
	TRO DOERRS
	JRST ALLSUM

BSSEL:	TLOA ASKHIM
BACSUM:	TLZ ASKHIM
	TRO ISBACK
	JRST ALLSUM

WLSSEL:	TLOA ASKHIM
SUMLST:	TLZ ASKHIM
	TRZ ISBACK
	TRZ DOERRS
ALLSUM:	SETZM ALLNAM
	SETZM ALLCRD
SUMALL:	MOVE T,[PUSHJ P,TTYIN]
	MOVEM T,GETCHR
	MOVE L,DEFLST
	TRNE ISBACK
	JRST [	FETCH(W,L,BLST)
		JUMPE W,XBAK
		MOVSI T,'BS '
		MOVEI TT,0
		JRST DOBS]
	SKIPE T,ALLCRD
	JRST [	MOVEM T,SLICEL
		JRST GALCRD]
	PUSHJ P,LSTGET
	POPJ P,
GALCRD:	PUSHJ P,CLEARP
	TRNE DOERRS
	JRST [	MOVSI T,'LES'
		MOVEI TT,10	;FULL WORD MODE
		JRST DOBS]
	MOVSI T,'WLS'		;WIRE LIST SUMMARY FILE EXTENSION
	MOVEI TT,0		;DATA MODE
DOBS:	MOVSI TTT,'DSK'
	PUSHJ P,ALLSET
	POPJ P,
	MOVEI TTT,LESVER
	TRNE DOERRS
	PUSHJ P,WORDOUT		;WRITE VERSION #
	TRNE DOERRS
	JRST NOFLST
	PUSHJ P,FILLST
	 PUTHDR
NOFLST:
;		CARD DIP SUMMARY
	TRNE ISBACK!DOERRS
	JRST NODIPS		;SKIP FIRST PART FOR BACK PANEL
	TLNN ASKHIM
	JRST DOBSUM
	SKIPE TTYFLG
	CLOSE LST,		;FORCE OUTPUT IF TTY
	ASK[ASCIZ\DIPTYPE/BODY SUMMARY?\]
	JRST WLSQT
	JRST NODIPS
DOBSUM:	MOVEI T,[ASCIZ/DIPTYPE	BODY NAME	# SECTION	TOTAL DIPS	#SPARE SECTIONS	  MA		  @V/]
	MOVEM T,HEADER
	SETZM LINCNT
	MOVEI T,WSBMAR
	MOVEM T,MARGIN
	PUSHJ P,CLEARB		;CLEAR MARK BIT FOR BODIES
	PUSHJ P,DCLEAR		;CLEAR DIP COUNTS
	PUSHJ P,DCNTW		;COUNT ASSIGNED DIPS
	PUSHJ P,AMPRET		;RETURN CURRENT@VOLTAGE LIST
	SETZM TOTDIP		;ZERO TOTAL DIP COUNTER
	SETZM REALDV		;CLEAR REAL(.GE.14 PINS) DIP COUNT
	SETZM TMPCN1		;ASSIGNED TOTDIP
	SETZM TMPCN2		;ASSIGNED REALDV
	SKIPN H,DIPLST		;PREPARE TO RUN DOWN DIPLST
	JRST NODIPC		;NONE
NODIP1:	PUSHJ P,NXTDIP
	FETCH(H,H,NXTD)
	JUMPN H,NODIP1
	SKIPN TOTDIP		;ANY DIPS AT ALL?
	SKIPE TMPCN1
	CAIA
	JRST NTOTDP
	PUTSTR[ASCIZ/			  GRAND TOTAL =	  /]
	MOVE T,TOTDIP
	PUSHJ P,DECOUT
	PUTBYT "("
	MOVE T,REALDV
	PUSHJ P,DECOUT
	SKIPN T,TMPCN1
	JRST NODIP2
	PUTSTR[ASCIZ/)
			WITH LOCATION= 	  /]
	MOVE T,TMPCN1
	PUSHJ P,DECOUT
	PUTBYT "("
	MOVE T,TMPCN2
	PUSHJ P,DECOUT
NODIP2:	PUTSTR[ASCIZ/)
NUMBER IN PARENS IS REAL (.GE.14 PINS) DIPS.

/]
NTOTDP:	SKIPN G,AMPLST			;ANY CURRENTS?
	JRST NOCURN			;NO
	PUTSTR[ASCIZ/TOTAL CURRENTS:
/]
CURN1:	FETCH(T,G,MAMP)
	SETZM TCOUNT
	PUSHJ P,LDOUT
	PUTSTR[ASCIZ/MA/]
	MOVE T,TCOUNT
	CAIGE T,8
	PUTBYT 11
	PUTSTR[ASCIZ/	@/]
	FETCH(T,G,VLTS)
	PUSHJ P,LDOUT
	PUTSTR[ASCIZ/V
/]
	FETCH(G,G,NXTM)
	JUMPN G,CURN1
	CRLF
	CRLF
	PUSHJ P,AMPRET		;NOW GIVE BACK LIST
NOCURN:	SETZ H,
	PUSHJ P,NXTDIP		;LIST ALL UNASSIGNED DIPS!
NODIPC:	MOVEI H,RADDR(W,WBDY,NXTB)
	TLZ TFLG
	SETZM NDIPS
	SETOM NBODS
	JRST DIPC1

DIPC2:	FETCH(T,H,BBIT)
	TRNN T,CBODY
	JRST DIPC1		;NOT CON
	TLON TFLG
	PUTSTR[ASCIZ/CONS	PADDLE	# PINS	TOTAL PINS
/]
	AOSE NBODS
	CRLF
	PUTSTR[ASCIZ/	  "/]
	FETCH(A,H,BLOC)
	PUSHJ P,LOCOUT
	PUTBYT 42
	FETCH(G,H,BPIN)		;CON PIN LIST
	JUMPE G,DIPC1
	PUTSTR[ASCIZ/	  /]
	SETZ T,
DIPC3:	FETCH(TT,G,PBIT)
	TRNN TT,DUP		;DON'T COUNT DUPS
	ADDI T,1		;COUNT THEM
	FETCH(G,G,NXBP)		;NEXT PIN OF CON
	JUMPN G,DIPC3
	ADDM T,NDIPS		;ADD TO TOTAL
	PUSHJ P,DECOUT		;PRINT THESE
DIPC1:	FETCH(H,H,NXTB)		;NEXT BODY
	JUMPN H,DIPC2
	TLNN TFLG
	JRST NODIPS
	PUTSTR[ASCIZ/	  /]
	MOVE T,NDIPS
	PUSHJ P,DECOUT
	CRLF
	JRST NODIPS

NXTDIP:	TLZ TFLG		;NOTE NONE FOUND YET!
	SETZM SAVET
	SETZM NBODS
	SETZM NDIPS
	SETZM RBODS
	JUMPE H,[SETO F,	;FLAG NO DIP DEF
		JRST BLANKD]
DIPCHECK
	PUSHJ P,SECCAL		;CALC SEC/DIP
BLANKD:	MOVEM F,FACTR1		;SAVE HERE
	MOVEI G,RADDR(W,WBDY,NXTB)
	JRST NXTDP1

NXTDP2:	FETCH(T,G,BBIT)		;LOC
	TRNE T,CBODY		;CON?
	JRST NXTDP1		;YES, SKIP IT
	FETCH(T,G,DIPT)		;POINTER TO DIP TYPE
	CAME T,H		;SAME DIP?
	JRST NXTDP1		;NO
	FETCH(T,G,BBIT)		;GET BODY BITS
	TROE T,BTMP1		;MARK AND CHECK
	JRST NXTDP1		;DONE ALREADY, FIND ANOTHER
	STORE(T,G,BBIT)
	TLOE TFLG		;PUT OUT DIP NAME YET?
	JRST NXTDP4		;YES, SUMMARIZE LAST BODY
	SKIPL LINCNT
	PUSHJ P,PUTHDR
	JUMPE H,NXTDP5
	FETCH(A,H,DNAM)
	PUSHJ P,PRINT7	;NO, PRINT DIP NAME
	JRST NXTDP5

NXTDP4:	MOVE T,TCOUNT
	CAIGE T,8
	PUTBYT 11
	PUTSTR[ASCIZ/	  /]
	MOVE T,NBODS
	ADDM T,NDIPS
	PUSHJ P,DECOUT
	SETZM NBODS
	CRLF
NXTDP5:	PUTBYT 11
	MOVEI A,ADDR(G,BNAM)
	MOVEM A,SAVET		;REMEMBER THIS GUY
	SETZM TCOUNT
	PUTSTR ADDR(G,BNAM)
	MOVE F,G		;NOW GO DOWN FROM HERE
	JRST NXTDP6

NXTDP7:	FETCH(T,F,BBIT)		;LOC
	TRNE T,CBODY		;CON?
	JRST NXTDP8		;YES, SKIP IT
	FETCH(T,F,DIPT)
	CAME T,H
	JRST NXTDP8
	MOVEI T,ADDR(F,BNAM)
	MOVE TT,SAVET
	PUSHJ P,SEQMAT
	JRST NXTDP8		;NOT EQUAL
	JRST NXTDP8		;    "
	FETCH(T,F,BBIT)
	TROE T,BTMP1
	JRST NXTDP8
	STORE(T,F,BBIT)
NXTDP6:	SKIPG T,FACTR1		;1 SEC/DIP? (OR NONE?)
	JRST [	MOVEI T,1	;COUNT 1 BODY
		JRST ONESEC]
	MOVEI A,RADDR(F,BPIN,NXBP)
	SETZB T,FACTR2		;ACCUMULATE BITS HERE
	JRST SECCL1

SECCL2:	FETCH(TT,A,PTYP)
	JUMPE TT,SECCL1		;ANY TYPE POINTER
	FETCH(TTT,TT,DPBIT)
	TRNN TTT,INLD!OUTLD!TERM	;INPUT OR OUTPUT PINS?
	JRST SECCL1		;NO
	FETCH(TT,TT,SCTB)	;SECTION BITS
	MOVS TT,TT
	JFFO TT,.+2
	JRST SECCL1
	MOVE TTT,SCTTAB(TTT)
	TDOE TTT,FACTR2		;ANY IN THIS SEC YET?
	JRST SECCL1		;YES
	MOVEM TTT,FACTR2
	ADDI T,1		;COUNT ANOTHER NEW SEC IN THIS BODY
SECCL1:	FETCH(A,A,NXBP)
	JUMPN A,SECCL2
ONESEC:	ADDM T,NBODS		;ADD TO COUNT
	FETCH(TT,F,BLOC)
	JUMPE TT,NXTDP8
	ADDM T,RBODS
NXTDP8:	FETCH(F,F,NXTB)
	JUMPN F,NXTDP7
NXTDP1:	FETCH(G,G,NXTB)
	JUMPN G,NXTDP2
	TLNN TFLG		;DID WE FIND ANY?
	POPJ P,			;NO
	MOVE T,TCOUNT
	CAIGE T,=8
	PUTBYT 11
	PUTSTR[ASCIZ/	  /]
	MOVE T,NBODS
	ADDB T,NDIPS
	EXCH T,NBODS		;STORE TOTAL BODIES, GET CURRENT NUMBER
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/		  /]
	MOVE T,NDIPS
	SKIPGE FACTR1		;BODIES ONLY?
	JRST NODEF
	ADD T,FACTR1		;ADD # OF SEC'S -1
	AOS FACTR1		;MAKE IT # OF SEC'S
	IDIV T,FACTR1
	MOVEM T,NDIPS		;SAVE # OF DIPS
	ADDM T,TOTDIP		;ADD TOTAL DIPS
	JUMPE H,NTREAL
	FETCH(TTT,H,DPNN)
	CAIL TTT,=14		;IS THIS REAL (.GE.14 PINS)
	ADDM T,REALDV		;YES, COUNT IT
NTREAL:	SUB TT,FACTR1
	MOVN TT,TT
	HRLM TT,(P)
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/		  /]
	HLRZ T,(P)
	SUBI T,1
	PUSHJ P,DECOUT
	JUMPE H,NODEF0
	MOVE T,NBODS
	CAME T,RBODS		;IF ALL BODIES HAVE LOCS, PRINT LATER
	PUSHJ P,AMPL		;THEN PRINT CURRENT AND ADD TO TOTAL
	SKIPN RBODS		;ANY REAL BODIES?
	JRST NODEF0		;NO
	PUTSTR[ASCIZ/
	WITH LOCATIONS	  /]
	MOVE T,RBODS
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/		  /]
	FETCH(T,H,DTMP)
	PUSHJ P,DECOUT
	PUTSTR[ASCIZ/		  /]
	FETCH(T,H,DTMP)
	IMUL T,FACTR1
	SUB T,RBODS
	PUSHJ P,DECOUT
	FETCH(T,H,DTMP)
	MOVEM T,NDIPS			;SAVE HERE FOR AMPL
	ADDM T,TMPCN1
	FETCH(TTT,H,DPNN)
	CAIL TTT,=14			;IS THIS REAL(.GE.14 PINS)
	ADDM T,TMPCN2			;YES
	JUMPE T,NODEF0			;QUIT NOW IF NO REAL DIPS
	MOVE T,NBODS
	CAMN T,RBODS			;NO PRINT IF NOT ALL BODIES HAVE LOC
	PUSHJ P,AMPL			;LIST AMPS HERE, AND TOTAL
	JRST NODEF0

AMPL:	PUTBYT 11
	FETCH(G,H,DPIN)		;PREPARE TO SCAN FOR PWR PINS
AMPL1:	FETCH(T,G,DPBIT)
	TRNN T,PWR		;POWER PIN?
	JRST AMPL2		;NO
	FETCH(T,G,LLOW)		;GET VOLTAGE
	MOVEI B,AMPLST-ADDR(0,NXTM)
	JRST AMPL3

AMPL4:	FETCH(TT,B,VLTS)	;GET VOLTS FROM LIST
	CAMGE TT,T		;TO RIGHT ENTRY YET?
	JRST AMPL5		;PASSED IT.
	CAMN TT,T		;PERHAPS THERE NOW?
	JRST AMPL6		;YES
AMPL3:	MOVE A,B		;REMEMBER PREVIOUS
	FETCH(B,B,NXTM)		;GET NEXT
	JUMPN B,AMPL4		;LOOP IF THERE IS ONE
AMPL5:	GETFS(TT,MABLK)		;NO ENTRY FOR THIS VOLTAGE, MAKE ONE
	STORE(B,TT,NXTM)	;SET NEXT
	MOVE B,TT
	STORE(B,A,NXTM)		;MAKE US NEXT OF LAST
	STORE(T,B,VLTS)		;STORE VOLTAGE
	CLEAR(B,MAMP)		;CLEAR CURRENT
AMPL6:	FETCH(T,G,LHI)		;PWR SUPPLY CURRENT
	IMUL T,NDIPS
	FETCH(TT,B,MAMP)	;GET CURRENT
	ADD TT,T		;ADD TO TOTAL
	STORE(TT,B,MAMP)		;STORE TOTAL BACK IN BLOCK
	PUSH P,T
	PUTBYT 11		;SPACE OUT SOME
	POP P,T
	SETZM TCOUNT
	PUSHJ P,LDOUT		;PRINT CURRENT
	PUTSTR[ASCIZ/MA/]
	MOVE T,TCOUNT
	CAIGE T,8
	PUTBYT 11
	PUTSTR[ASCIZ/	@/]
	FETCH(T,G,LLOW)		;GET VOLTAGE AGAIN
	PUSHJ P,LDOUT
	PUTBYT "V"
AMPL2:	FETCH(G,G,NXTDP)	;NEXT DIP PIN
	JUMPN G,AMPL1
	POPJ P,

NODEF:	PUSHJ P,DECOUT		;AND PRINT # OF DIPS
	PUTSTR[ASCIZ/  TOTAL BODIES (NOT DIPS)/]
NODEF0:	CRLF
	CRLF
	POPJ P,

AMPRET:	SKIPN T,AMPLST
	POPJ P,
AMPRT1:	FETCH(TT,T,NXTM)
	FSTRET(T,MABLK)
	MOVE T,TT
	JUMPN T,AMPRT1
	SETZM AMPLST
	POPJ P,

DCNTW:	SETZM LSLOT
	SETZM CDSKPC
	MOVEI H,RADDR(W,WBDY,NXTB)
	JRST DCNT3

DCNT4:	FETCH(T,H,BLOC)
	JUMPE T,[AOS CDSKPC	;INC COUNT OF UNLOC'D DIPS
		JRST DCNT3]
	EXCH T,LSLOT
	CAMN T,LSLOT		;NEW SLOT?
	JRST DCNT3
	FETCH(A,H,DIPT)		;DIP POINTER
	JUMPE A,DCNT3		;ANY?
	FETCH(T,A,DTMP)
	ADDI T,1		;INC COUNT
	STORE(T,A,DTMP)
DCNT3:	FETCH(H,H,NXTB)
	JUMPN H,DCNT4
	POPJ P,

NODIPS:
;		VERIFICATION STUFF
	TRNE ISBACK!DOERRS
	JRST VERDON
	TLNN ASKHIM
	JRST VER0
	SKIPE TTYFLG
	CLOSE LST,		;FORCE OUTPUT IF TTY
	ASK[ASCIZ\BODY/DIP SOCKET MATCHING ERRORS?\]
	JRST WLSQT
	JRST VERDON
VER0:	MOVEI T,[ASCIZ\		BODY/DIP SOCKET  MATCHING ERRORS
LOC	DIP(S)	BODY(S)	FILE	POS	PIN\]
	MOVEM T,HEADER
	MOVEI T,WVMAR
	MOVEM T,MARGIN
	SETZM LINCNT		;FORCE NEW PAGE
	SETZM GOODW
	FETCH(H,W,WBDY)
VER1:	JUMPE H,VERDON
	SKIPE B,GOODW
	PUSHJ P,TRET
	SETZM GOODW		;USE THIS AS LIST HEAD
	SETZM BADW		;CURRENT DIP POINTER
	SETZM SLICEL		;CURRENT LOC
	TLZ TFLG		;FLAG LOC NOT OUT YET
VER2:	FETCH(TT,H,BBIT)
	TRNE TT,CBODY		;CON?
	JRST[	SKIPE GOODW	;YES, ANY OTHER BODIES IN LIST YET?
		JRST VER5	;YES, JUST DO THEM
		JRST VER3]	;VERIFY CONNECTOR
	FETCH(TT,H,BLOC)
	JUMPE TT,VERNLC
	SKIPN SLICEL
	JRST VER4
	CAME TT,SLICEL
	JRST VER5		;WE HAVE LIST
VER4:	MOVEM TT,SLICEL
	PGETFS(TT,TBLK)
	STORE(H,TT,TRHT)	;STORE BODY IN RIGHT HALF
	MOVE T,GOODW
	STORE(T,TT,NXTT)
	MOVEM TT,GOODW
VER3:	FETCH(H,H,NXTB)
	JUMPN H,VER2
VER5:	SKIPN G,GOODW
	JRST VERDON		;IF NOTHING IN LIST, ALL DONE!
VER6:	FETCH(TT,G,TRHT)	;GET DIP POINTER
	FETCH(TT,TT,DIPT)
	SKIPN BADW		;ANY YET?
	JRST VER7
	CAME TT,BADW		;YES, SAME?
	JRST VERER1
VER7:	MOVEM TT,BADW
	FETCH(G,G,NXTT)
	JUMPN G,VER6
	SKIPN BADW		;DID ONE OF THEM HAVE ONE?
	JRST NANYDP		;NOT ANY DIP TYPE AT ALL
VERPCK:	MOVE G,GOODW		;NOW CHECK EACH PIN OF DIP
	SETZM UPINS		;CLEAR USED PIN POINTERS
	MOVE T,[UPINS,,UPINS+1]
	BLT T,UPINS+MAXPINS-1	;WHOLE TABLE
VERP1:	FETCH(F,G,TRHT)		;BODY POINTER
	PUSHJ P,VERP6
	FETCH(G,G,NXTT)
	JUMPN G,VERP1		;ANOTHER BODY
	JRST VER1

VERP6:	MOVEM F,SAVET
	FETCH(T,F,DIPT)
	JUMPN T,.+2		;DIPTYPE?
	SKIPA T,[MAXPIN]	;NO, USE MAX
	FETCH(T,T,DPNN)		;PICKUP # OF PINS FROM DIP DEF
	CAILE T,MAXPIN
	JRST VERDBG		;TOO MANY PINS ON DIP
	MOVEM T,FACTR1
	MOVEI F,RADDR(F,BPIN,NXBP);PREPARE TO GO DOWN BODY PIN LIST
	JRST VERP3

VERP2:	SKIPN SLICEL			;ONLY CHECK PIDPIN IF WE HAVE LOC
	JRST VERPOK
	FETCH(T,F,PBIT)
	TRNE T,PIDPIN		;REALLY JUST PID?
	JRST VERPID		;YES, DIFFERENT ERROR
VERPOK:	FETCH(T,F,PINN)
	MOVE TT,F
	CAMLE T,FACTR1		;TOO BIG?
	JRST VERPBG		;YES, PRINT ERROR
	CAILE T,MAXPIN		;OVERFLOW TABLE?
	JRST VERTBX		;YES, THIS IS THE BEST WE CAN DO
	FETCH(A,F,PTYP)		;PIN TYPE BLOCK
	JUMPE A,VERP4		;ANY?
	FETCH(TTT,A,DPBIT)
	TRNE TTT,NULLD
	JRST VERP4
	TRNN TTT,INLD!OUTLD!TERM
	JRST VERP5
	FETCH(TTT,A,SCTB)
	HRL TT,TTT		;PICKUP SECTION BITS
VERP5:	EXCH TT,UPINS-1(T)	;STORE BITS AND PIN POINTER
	JUMPN TT,VERPML		;OLD POINTER HERE?
VERP3:	FETCH(F,F,NXBP)
	JUMPN F,VERP2
	MOVE F,SAVET
	FETCH(F,F,DIPT)
	JUMPE F,CPOPJ
	FETCH(F,F,DPIN)		;FIRST PIN OF DIP
	MOVEI T,1		;PIN 1
	SETOM FACTR2		;ASSUME ALL SECTIONS INCLUDED INITIALLY
VERP7:	SKIPE UPINS-1(T)
	JRST VERP8
	FETCH(TT,F,SCTB)
	ANDCAM TT,FACTR2	;CAN'T BE ANY OF THESE SECTIONS
VERP8:	FETCH(F,F,DPNXT)
	SKIPE F
	AOJA T,VERP7
	MOVEI T,MAXPIN-1
VERP9:	HLRZ TT,UPINS(T)
	JUMPE TT,VERP10
	TDNN TT,FACTR2		;THIS PIN IN ONE OF THE SECTIONS OF WHICH WE HAVE ALL?
	JRST VERP11
VERP10:	HRRZS UPINS(T)		;DONE CHECKING IT, CLEAR BITS FOR NEXT GUY
	SOJGE T,VERP9
	POPJ P,

VERP11:	HRRZS UPINS(T)			;CLEAR THE REST OF THE BITS FOR THIS BODY
	SOJGE T,VERP11
	PUSHJ P,VERLNA
	ERRSTR 2,[ASCIZ/NOT ALL PINS OF SECTIONS USED/]
	POPJ P,

VERNLC:	SKIPL LINCNT
	PUSHJ P,PUTHDR
	MOVE E,H
	PUSHJ P,VERLIN
	ERRSTR 1,[ASCIZ/NO LOCATION SET/]
	MOVE F,H		;PUT BODY POINTER INTO F
	SETZM UPINS		;CLEAR USED PIN POINTERS
	MOVE T,[UPINS,,UPINS+1]
	BLT T,UPINS+MAXPINS-1	;WHOLE TABLE
	PUSH P,SLICEL
	SETZM SLICEL
	PUSHJ P,VERP6
	POP P,SLICEL
	JRST VER3

VERP4:	FETCH(TTT,G,TRHT)	;DIP POINTER
	FETCH(TTT,TTT,DIPT)
	JUMPE TTT,VERP5		;DON'T COMPLAIN IF NO DIP
	PUSHJ P,VERLNA
	PUSH P,T
	PUSHJ P,DECOUT
	POP P,T
	ERRSTR 3,[ASCIZ/UN OR NC PIN/]
	JRST VERP5

VERPID:	FETCH(T,F,PINN)
	PUSHJ P,VERLNA
	PUSHJ P,DECOUT
	ERRSTR 1,[ASCIZ/DEFAULT NOT EXPLICIT PIN #/]
	JRST VERP3

VERTBX:	PUSHJ P,VERLNA
	PUSHJ P,DECOUT
	ERRSTR 0,[ASCIZ/PIN # EXCEEDS TABLE SPACE/]
	JRST VERP3

VERDBG:	PUSHJ P,VERLNA
	PUSHJ P,DECOUT
	ERRSTR 3,[ASCIZ/TOO MANY PINS ON DIP/]
	POPJ P,

VERPBG:	PUSHJ P,VERLNA
	PUSHJ P,DECOUT
	ERRSTR 3,[ASCIZ/PIN # TOO BIG FOR DIP!/]
	JRST VERP3

VERLNA:	HRLM T,(P)
	TLNN TFLG		;LOC OUT YET?
	PUSHJ P,VERLOC		;NO, PUT IT OUT
	MOVE E,SAVET
	PUSHJ P,VERLIN		;BODY LINE
	PUTBYT 11
	HLRZ T,(P)
	POPJ P,

VERPML:	FETCH(A,TT,HPNT)	;GET HEADER FOR ONE PIN
	FETCH(E,F,HPNT)		;AND THE OTHER
	CAMN A,E		;SAME WIRE MEANS DUP
	JRST VERP3
	PUSH P,TT		;SAVE FIRST PIN FOUND THERE
	PUSHJ P,VERLNA
	PUSHJ P,DECOUT
	CRLF
	POP P,E			;GET FIRST PIN POINTER
	FETCH(E,E,PBDY)		;BODY OF THIS PIN
	PUSHJ P,VERLIN		;DON'T REPEAT PIN #
	ERRSTR 2,[ASCIZ/PIN # USED MORE THAN ONCE/]
	JRST VERP3

VERER1:	PUSHJ P,VERALL
	ERRSTR 3,[ASCIZ/MULTIPLE DIP TYPES/]
	JRST VERPCK		;NOW CHECK FOR BAD PINS

VERALL:	PUSHJ P,VERLOC		;NO CHECK FOR LOC, WE ARE FIRST
	SKIPA G,GOODW		;SKIP FIRST CRLF
VERERB:	CRLF
	FETCH(E,G,TRHT)
	PUSHJ P,VERLIN
	FETCH(G,G,NXTT)
	JUMPN G,VERERB
	POPJ P,

NANYDP:	PUSHJ P,VERLOC
	SKIPA G,GOODW
NANYD1:	CRLF
	FETCH(E,G,TRHT)
	PUTBYT 11
	FETCH(A,E,DIPT)		;DIP TYPE POINTER
	JUMPE A,NANYD2
	FETCH(A,A,DNAM)
	PUSHJ P,STROUT
	JRST NANYD3

NANYD2:	FETCH(A,E,PRPX)		;LOOK IN EXTRA PROPERTY LIST FOR DIPTYPE
	JUMPE A,NANYD3
NANYD4:	FETCH(T,A,TLFT)
	MOVEI TT,[ASCIZ/DIPTYPE/]
	PUSHJ P,ASCMAT
	JRST NANYD5
	FETCH(A,A,TRHT)
	PUSHJ P,STROUT
	JRST NANYD3

NANYD5:	FETCH(A,A,NXTT)
	JUMPN A,NANYD4
NANYD3:	PUSHJ P,VERER3
	FETCH(G,G,NXTT)
	JUMPN G,NANYD1
	ERRSTR 3,[ASCIZ/MISSING OR UNKNOWN DIPTYPE/]
	JRST VERPCK

VERLOC:	SKIPL LINCNT
	PUSHJ P,PUTHDR
	TLO TFLG		;FLAG LOC OUT
	SKIPN A,SLICEL
	POPJ P,
	SETZ T,			;THESE BODIES DON'T HAVE ANY INTERSTING BITS
	JRST WLOCOUT

VERLIN:	PUTBYT 11
	FETCH(A,E,DIPT)		;DIP TYPE POINTER
	JUMPE A,VERER3
	FETCH(A,A,DNAM)
	PUSHJ P,STROUT
VERER3:	PUTBYT 11
	PUTSTR ADDR(E,BNAM)
	PUTBYT 11
	FETCH(A,E,FILB)
	PUTSIX ADDR(A,FILN)
	PUTBYT 11
	FETCH(A,E,BBIT)
	JRST BPPNT

TRET1:	MOVE B,T
TRET:	FETCH(T,B,NXTT)
	FSTRET(B,TBLK)
	JUMPN T,TRET1
	POPJ P,

VERDON:
;		PART NUMBER ERRORS
	TRNE ISBACK!DOERRS
	JRST NPNERR
	TLNN ASKHIM
	JRST DOPNER
	SKIPE TTYFLG
	CLOSE LST,
	ASK[ASCIZ/PART NUMBER ERRORS?/]
	JRST WLSQT
	JRST NPNERR
STANFO,<JRST NPNERR>		;SCREW PART NUMBERS (TAG)
DOPNER:	MOVEI T,[ASCIZ/		PART NUMBER ERRORS
LOC	BODY	FILE	POS	DIPTYPE		PROPERTIES/]
	MOVEM T,HEADER
	MOVEI T,PREMAR
	MOVEM T,MARGIN
	SETZM LINCNT
	MOVEI H,RADDR(W,WBDY,NXTB)
	JRST DOPN1

DOPN2:	FETCH(T,H,BBIT)
	TRNE T,CBODY
	JRST DOPN1
	FETCH(T,H,DIPT)
	JUMPE T,[FETCH(T,H,PRTN)	;IF NO DIPTYPE
		JUMPE T,DOPN1
		FETCH(T,H,PRPX)		;AND HAS PART NUMBER
		JUMPE T,DOPN1
		JRST DOPN3]		;AND EXTRA PROPERTIES, THEN ERROR
	FETCH(T,H,PRTN)
	JUMPE T,DOPN3
	FETCH(T,H,PRPX)
	JUMPE T,DOPN1
DOPN3:	SKIPL LINCNT
	PUSHJ P,PUTHDR
	FETCH(A,H,BLOC)
	FETCH(T,H,BBIT)
	PUSHJ P,WLOCOUT
	PUTBYT 11
	PUTSTR ADDR(H,BNAM)
	PUTBYT 11
	FETCH(A,H,FILB)
	PUTSIX ADDR(A,FILN)
	PUTBYT 11
	FETCH(A,H,BBIT)
	PUSHJ P,BPPNT
	FETCH(A,H,DIPT)		;DIP TYPE POINTER
	JUMPE A,DOPN4
	PUTBYT 11
	FETCH(A,A,DNAM)
	PUSHJ P,STROUT
DOPN4:	FETCH(G,H,PRPX)
	JUMPE G,DOPN5
DOPN6:	MOVE T,LWIDTH
	SUBI T,=24		;MUST BE THIS MUCH SPACE LEFT
	CAMGE T,LCOUNT
	CRLF
	MOVEI A,=48
	CAMLE A,LCOUNT
	PUSHJ P,FILL
	MOVE T,LCOUNT
	CAIE T,=48
	PUTSTR[ASCIZ/, /]
	FETCH(A,G,TLFT)
	PUSHJ P,STROUT
	PUTBYT ":"
	FETCH(A,G,TRHT)
	PUSHJ P,STROUT
	FETCH(G,G,NXTT)
	JUMPN G,DOPN6
DOPN5:	FETCH(T,H,PRTN)
	JUMPN T,DOPN7
	FETCH(T,H,PRPX)
	JUMPE T,DOPN8
	ERRSTR 2,[ASCIZ/NO PART NUMBER FOUND/]
	JRST DOPN1A

DOPN8:	ERRSTR 1,[ASCIZ/BODY HAS NO PROPERTIES, SO NO PART NUMBER/]
DOPN1A:	FETCH(T,H,DIPT)
	JUMPE T,DOPN1
	FETCH(T,T,PRPN)
	SKIPN T
	ERRSTR 3,[ASCIZ/NO PROPERTIES IN DIP DEF/]
	JRST DOPN1

DOPN7:	ERRSTR 2,[ASCIZ/EXTRA PROPERTIES/]
DOPN1:	FETCH(H,H,NXTB)
	JUMPN H,DOPN2
NPNERR:
;		REDUNDANT BACK PANEL PIN CHECK
	TRNE DOERRS
	JRST NREDUN
	TLNN ASKHIM
	JRST DOREDN
	SKIPE TTYFLG
	CLOSE LST,		;FORCE OUTPUT IF TTY
	ASK[ASCIZ/REDUNDANT CONNECTOR PIN CHECK?/]
	JRST WLSQT
	JRST NREDUN		;NO
DOREDN:	MOVEI T,[ASCIZ/		MULTIPLY USED CONNECTOR PINS
LOC	PIN	FILES	POS	SIGNALS/]
	MOVEM T,HEADER
	MOVEI T,BVMAR
	MOVEM T,MARGIN
	SETZM LINCNT
	MOVEI H,RADDR(W,WBDY,NXTB)
	JRST BV1

BV2:	TRNE ISBACK
	JRST BV6
	FETCH(T,H,BBIT)		;BODY BITS
	TRNN T,CBODY		;CONNECTOR BODY?
	JRST BV1		;NO, SKIP IT
BV6:	FETCH(G,H,BPIN)
	JUMPE G,BV1		;SKIPE IF NO PINS???????
	TLZ TFLG		;NOTE STARTING NEW LOC
	FETCH(F,G,PINN)		;PADDLE PIN SIDE
	JRST BV3

BV4:	FETCH(TT,G,PBIT)
	TRNE TT,DUP
	JRST BV3
	FETCH(TT,G,PINN)	;PADDLE PIN SIDE
	EXCH TT,F		;SET AS NEW ONE
	CAME TT,F		;SAME AS LAST??
	JRST BV3		;NO, OK
	SKIPL LINCNT
	PUSHJ P,[TLZ TFLG	;FORCE LOC OUT AGAIN
		JRST PUTHDR]
	TLOE TFLG		;LOC OUT YET?
	JRST BV5
	FETCH(A,H,BLOC)
	TRNN ISBACK
	JRST [	FETCH(T,H,BBIT)
		PUSHJ P,WLOCOUT
		JRST BV5]
	PUSHJ P,SLTOUT		;OUT OUT DAMN SLOT
BV5:	PUTBYT 11
	PUSH P,A
	MOVEI A,(F)
	TRNN ISBACK
	PUSHJ P,PRCNPN
	TRNE ISBACK
	PUSHJ P,PRBCN
	POP P,A
	PUTBYT 11
	FETCH(T,D,FILP)
	PUTSIX ADDR(T,FILN)	;FILE 1
	PUTBYT 11
	FETCH(A,D,PBIT)
	PUSHJ P,CNPNT
	PUTBYT 11
	FETCH(T,D,HPNT)
	PUTSIG ADDR(T,SPNT)	;SIGNAL 1
	CRLF
	PUTSTR[ASCIZ/		/]
	FETCH(T,G,FILP)
	PUTSIX ADDR(T,FILN)	;FILE 2
	PUTBYT 11
	FETCH(A,G,PBIT)
	PUSHJ P,CNPNT
	PUTBYT 11
	FETCH(T,G,HPNT)
	PUTSIG ADDR(T,SPNT)	;SIGNAL 2
	CRLF
BV3:	MOVE D,G
	FETCH(G,G,NXBP)		;NEXT PIN
	JUMPN G,BV4
BV1:	FETCH(H,H,NXTB)		;ANOTHER SLOT PLEASE
	JUMPN H,BV2

NREDUN:
;		COUNT OF POLARITY ERRORS FROM DWG PROG
DEC,<	TRNE ISBACK!DOERRS
	JRST NPOLCK
	TLNN ASKHIM
	JRST DOPOL
	SKIPE TTYFLG
	CLOSE LST,		;FORCE OUTPUT IF TTY
	ASK[ASCIZ/POLARITY ERROR COUNTS?/]
	JRST WLSQT
	JRST NPOLCK
DOPOL:	MOVEI T,[ASCIZ/	POLARITY ERROR COUNTS BY DRAWING
FILE	PPN		MODULE	COUNT OF ERRORS/]
	MOVEM T,HEADER
	MOVEI T,POLMAR
	MOVEM T,MARGIN
	SETZM LINCNT
	MOVEI H,RADDR(W,FILS,FNXT)
	JRST DOPOL1

DOPOL2:	FETCH(T,H,FPOL)
	JUMPE T,DOPOL1
	SKIPL LINCNT
	PUSHJ P,PUTHDR
	PUTSIX ADDR(H,FILN)
	PUTBYT 11
	FETCH(TT,H,FPPN)
	HLLZ TT,TT
	PUSHJ P,LSIXOUT
	PUTBYT ","
	FETCH(TT,H,FPPN)
	HRLZ TT,TT
	PUSHJ P,LSIXOUT
	MOVEI A,=24
	PUSHJ P,FILL
	FETCH(A,H,FMOD)
	PUSHJ P,STROUT
	PUTSTR[ASCIZ/		/]
	FETCH(T,H,FPOL)
	PUSHJ P,DECOUT
	CRLF
DOPOL1:	FETCH(H,H,FNXT)
	JUMPN H,DOPOL2
NPOLCK:
>;DEC
;		MAIN LOOP
	TRNE ISBACK
	SKIPA F,[-NBLEN,,SUMLEN]
	MOVSI F,-SUMLEN
	MOVEI T,WSWMAR
	MOVEM T,MARGIN
WIRSM1:	TLNN ASKHIM
	JRST WIRSMD
	SKIPE TTYFLG
	CLOSE LST,		;FORCE OUTPUT IF TTY
	OUTSTR @HDRTAB(F)
	ASK[ASCIZ/?/]
	JRST WLSQT
	JRST ASKNXT
WIRSMD:	MOVE T,HDRTAB(F)
	MOVEM T,HEADER
	SETZM LINCNT
	MOVEI H,RADDR(W,WIRES,ALPH)
	SETZM NBODS			;COLLECT # OF UNNAMED SIGS HERE
	JRST WIRSM3

WIRSM2:	FETCH(T,H,WBIT)
	TRNN T,NAM2ND			;IS THIS A WIRE HEADER?
	TRNE T,SNC			;YES, IS IT NC WIRE?
	JRST WIRSM3			;YES, SKIP IT
	MOVE G,H
	HLRZ TT,SUMTAB(F)		;GET CONTROL BITS
	JUMPE TT,[FETCH(TT,H,TBIT)
		HRL T,TT
		JRST WIRSMN]
	TRNN TT,NEEDLC
	TLZA DOLC
	TLO DOLC
	TRNN ISBACK			;ARE WE BACK PANELING?
	TLZA BAKGAT
	TLO BAKGAT			;INDICATE BACK PANEL
	PUSH P,G
	SETZ A,				;CLEAR BIT ACCUMULATORS
	TLNN DOLC			;DOING LOADING AND COUNTS?
	JRST NCLR			;NO, SKIP BLT
	SETZM CNTTAB
	MOVE T,[CNTTAB,,CNTTAB+1]
	BLT T,LODTAB+LODLEN-1
NCLR:	PUSHJ P,GATHER			;GATHER REQUIRED INFO
	TLNE DOLC
	PUSHJ P,LODMER			;CALC PROPER LOADING
	POP P,G
	MOVS T,A
WIRSMN:	PUSHJ P,@SUMTAB(F)
	JRST WIRSM3
	TRNE DOERRS
	JRST ERRSPN
	PUSH P,T
	SKIPL LINCNT
	PUSHJ P,WLSHDR
	POP P,T
	PUTSIG ADDR(H,SPNT)
	TRNE ISBACK
	JRST BKSMLS
	MOVE T,LCOUNT
	CAIGE T,=24			;3 TABS WORTH YET?
	JRST ISTHRE
	CRLF
	PUTSTR[ASCIZ/			/]
	JRST WASTHR

ISTHRE:	LSH T,-3			;# OF TAB STOPS WE ALREADY HAVE
	SUBI T,3
	PUTBYT 11
	AOJL T,.-1
WASTHR:	PUSH P,F
	PUSHJ P,ONECRD
	POP P,F
WIRSM3:	FETCH(H,H,ALPH)
	JUMPN H,WIRSM2
	TRNN DOERRS
	JRST ASKNXT
	SKIPE LINCNT
	PUSHJ P,WRTZERO			;END THIS CATEGORY
ASKNXT:	AOBJN F,WIRSM1
	TRNE DOERRS
	PUSHJ P,WRTZERO			;END ERROR SUMMARY WITH NULL HEADER
WLSQT:	RELEASE LST,
	POPJ P,

BKSMLS:	FETCH(E,G,WPIN)
	JUMPE E,BKSM5			;YUCK
FOR I IN(LINL,LINH,LOUTL,LOUTH)
<	SETZM I
>
	SETOM SAVET
	MOVE T,LCOUNT
	CAIL T,=16			;TWO TABS WORTH
	CRLF				;TO MUCH, CRLF
	MOVE T,LCOUNT
	CAIGE T,8
BKSM2:	PUTBYT 11
	PUTBYT 11
	FETCH(T,E,CBIT)
	JUMPE T,BKSM3
	FETCH(A,E,PBDY)
	FETCH(A,A,BLOC)
	PUSHJ P,SLTOUT
BKSM3:	PUTBYT 11
	FETCH(A,E,PINN)
	PUSHJ P,PRBCN
	MOVE C,D
	PUSHJ P,BMSMLD			;LOADING OUT
	PUTBYT 11
	FETCH(A,E,FILP)
	PUTSIX ADDR(A,FILN)
	CRLF
	FETCH(E,E,NXTP)
	JUMPN E,BKSM2
	SKIPG SAVET			;MORE THAN ONE LOADING LINE?
	JRST WIRSM3
	PUTSTR[ASCIZ/			/]
	PUSHJ P,CLOADS
BKSM5:	CRLF
	JRST WIRSM3

BMSMLD:
FOR I IN(HILD,LILD,HOLD,LOLD)
<	FETCH(T,C,I)
	JUMPN T,BMSML1
>
	PUTSTR[ASCIZ/				/]
	POPJ P,

BMSML1:	AOS SAVET
	JRST LOADIT

WLSHDR:	TRNN ISBACK
	JRST WLSHD1
	PUSHJ P,PUTHDR
	PUTSTR[ASCIZ/SIGNAL NAME	CARD	PIN	LOW IN	HI IN	LOW OUT	HI OUT	FILE

/]
	POPJ P,

WLSHD1:	PUSHJ P,PUTHDR
	PUTSTR[ASCIZ/SIGNAL NAME		LOW IN	HI IN	LOW OUT	HI OUT	FILNAM	#I,#O Z# C#

/]
	POPJ P,

ERRSPN:	FETCH(A,G,WPIN)			;NOW PINS
	JUMPE A,WIRSM3			;DON'T PRINT WIRES WITH NO PINS
	SKIPN LINCNT
	PUSHJ P,WRTWCD			;PUT OUT WIRE CODE AND HEADING
	MOVE B,G
ERRSP1:	BINSIG ADDR(B,SPNT)		;WRITE SIGNAL NAME
	FETCH(B,B,NNAM)
	JUMPN B,ERRSP1
	PUSHJ P,WRTZERO
ERRSP2:	FETCH(TTT,A,PID)
	FETCH(TT,A,PBDY)
	FETCH(T,TT,BBIT)
	TRNE T,CBODY			;IF CONNECTOR THIS IS ALL OF PID
	JRST ERRSPC
	FETCH(TT,TT,BID)
	HRL TTT,TT			;ELSE THIS IS THE REST
	MOVS TTT,TTT			;BID IN RIGHT HALF
ERRSPC:	PUSHJ P,WORDOUT
	FETCH(TTT,A,FILP)
	FETCH(TTT,TTT,FILN)
	PUSHJ P,WORDOUT			;FILENAME
	FETCH(A,A,NXTP)
	JUMPN A,ERRSP2
	PUSHJ P,WRTZERO
	JRST WIRSM3

WRTWCD:	MOVEI TTT,LESWIR
	PUSHJ P,WORDOUT
WRTHD:	AOS LINCNT			;USE THIS TO NOTE HEADER OUT
	OUTSTR @HEADER			;TELL HIM A CATEGORY IS GOING OUT
	OUTSTR[ASCIZ/
/]
	BINSTR @HEADER
	POPJ P,
;		TABLES FOR SUMMARY

DEFINE NAMESW
<
SUMMAC	NEEDT,NOOUTP,<RUNS WITH NO OUTPUTS>
SUMMAC	NEEDT!NEEDLC,OVRLOD,<RUNS WHICH ARE OVERLOADED>
SUMMAC	NEEDT,NOINP,<RUNS WITH NO INPUTS>
SUMMAC	NEEDLC,EXTRAC,<RUNS WITH MORE THAN 1 CONNECTOR PIN>
SUMMAC	NEEDT,INACTI,<INACTIVE INPUTS>
SUMMAC	NEEDT,EXOUT,<UNUSED EXTRA OUTPUTS>
SUMMAC	NEEDT,NILWIR,<RUNS WITH NO INPUTS OR OUTPUTS>
SUMMAC	NEEDT!NEEDLC,UNWIR,<RUNS WITH UN OR NC PINS>
SUMMAC	NEEDT,MLTMRL,<RUNS WITH MULTIPLE TERMINATION RULES>
SUMMAC	NEEDT!NEEDLC,ILLWOR,<RUNS WITH WIRE OR WARNINGS>
SUMMAC	NEEDT,NOOUTC,<EDGE CONNECTOR RUNS WITH NO OUTPUTS>
SUMMAC	NEEDT,NOINC,<EDGE CONNECTOR RUNS WITH OUTPUTS>
SUMMAC	NEEDT,ILBPRC,<UNNAMED BACKPANEL RUNS>
SUMMAC	NEEDT,OUTPWR,<RUNS WITH INPUTS OR OUTPUTS CONNECTED TO PWR>
SUMMAC	NEEDT,ILGNDO,<RUNS WITH OUTPUT CONNECTED TO GND>
SUMMAC	NEEDT,ILLVG,<RUNS WITH PWR CONNECTED TO GND>
SUMMAC	NEEDLC,ILLMIX,<RUNS WITH MIXED POWER VOLTAGES>
SUMMAC	NEEDT,ILLET,<RUNS WITH ECL CONNECTED TO TTL>
SUMMAC	NEEDT!NEEDLC,NEEDZ,<RUNS WHICH NEED TERMINATION>
SUMMAC	NEEDT!NEEDLC,ILLZ,<RUNS WITH ILLEGAL TERMINATION>
SUMMAC	NEEDT!NEEDLC,QUESTZ,<RUNS WITH QUESTIONABLE TERMINATION>
>

DEFINE NAMESB
<
SUMMAC	,NOOUTA,<RUNS WITH NO OUTPUTS>
SUMMAC	NEEDLC,OVRLOD,<RUNS WHICH ARE OVERLOADED>
SUMMAC	NEEDT,ILBPRB,<UNNAMED BACKPANEL RUNS>
SUMMAC	,OUTONE,<RUNS UNUSED ON BACK PANEL>
SUMMAC	,NILWIR,<RUNS WITH NO INPUTS OR OUTPUTS>
SUMMAC	NEEDLC,ILLWOR,<RUNS WITH WIRE OR WARNINGS>
SUMMAC	NEEDT!NEEDLC,ILFFO,<FLIP-FLOP OUTPUTS DRIVING MORE THAN 1 OTHER BOARD>
SUMMAC	,MORONE,<RUNS WITH MORE THAN ONE PIN ON A CARD>
SUMMAC	,OUTPWR,<RUNS WITH INPUTS OR OUTPUTS CONNECTED TO PWR>
SUMMAC	,ILGNDO,<RUNS WITH OUTPUT CONNECTED TO GND>
SUMMAC	,ILLVG,<RUNS WITH PWR CONNECTED TO GND>
SUMMAC	NEEDLC,ILLMIX,<RUNS WITH MIXED POWER VOLTAGES>
SUMMAC	NEEDT,ILLET,<RUNS WITH ECL CONNECTED TO TTL>
SUMMAC	NEEDT!NEEDLC,NEEDBZ,<RUNS WHICH NEED TERMINATION>
SUMMAC	NEEDT!NEEDLC,ILLBZ,<RUNS WITH ILLEGAL TERMINATION>
SUMMAC	NEEDT!NEEDLC,QUESBZ,<RUNS WITH QUESTIONABLE TERMINATION>
>

DEFINE SUMMAC(A,B,C)
<	A,,B
>

NEEDT__400000		;NEED TYPE BITS FROM GATHER
NEEDLC__200000		;NEED LOADING BITS FROM GATHER

SUMTAB:
	NAMESW
SUMLEN__.-SUMTAB
	NAMESB
NBLEN__.-SUMTAB-SUMLEN

DEFINE SUMMAC(A,B,C)
<	[ASCIZ/C/]
>

HDRTAB:
	NAMESW
	NAMESB
PURGE NAMESW,NAMESB
;		SUMMARY ROUTINES
NOOUTP:	TRC T,WSINGL!GENSIG
	TRCN T,WSINGL!GENSIG
	TLNE T,DRVREQ			;IS SINGLE UNNAMED PIN, NEED DRIVE?
	CAIA				;YES
	POPJ P,				;SINGLE PIN WITH NO DRIVE REQUIRED
	TLNN T,ANYCON			;NON-EDGE RUNS WITH NO OUTPUTS
	PUSHJ P,NOOUTA
	POPJ P,
	PUSHJ P,EXTRAI			;IS THIS AN INACTIVE INPUT
	AOS (P)
	POPJ P,				;YES, NOT IN THIS CATEGORY

NOOUTC:	TLNN T,ANYCON			;EDGE RUNS WITH NO OUTPUTS
	POPJ P,
NOOUTA:	TLNN T,OUTLD			;RUNS WITH NO OUTPUTS
	TLNN T,INLD!PWR!GND
	POPJ P,
	JRST CPOPJ1

;INACTIVE INPUTS
INACTI:	TRC T,WSINGL!GENSIG
	TRCN T,WSINGL!GENSIG
	TLNE T,DRVREQ			;IS SINGLE UNNAMED PIN, NEED DRIVE?
	CAIA
	POPJ P,				;SINGLE PIN WITH NO DRIVE REQUIRED
	TLNN T,ANYCON
	PUSHJ P,NOOUTA
	POPJ P,
	PUSHJ P,EXTRAI
	POPJ P,
	JRST CPOPJ1

;ECL FF OUTPUT DRIVING TOO MANY CARDS
ILFFO:	TLC T,ECL!FFOUT
	TLCE T,ECL!FFOUT
	POPJ P,				;NOT ECL FF OUTPUT
	MOVE TT,CONFLG
	CAILE TT,1			;HOW MANY CARDS(STARTED AS -1)?
	AOS (P)				;TOO MANY
	POPJ P,

;UNUSED BACK PANEL RUNS
OUTONE:	TRNN T,SPWR!SGND		;SKIP THESE
	TLNN T,OUTLD			;MUST HAVE SOME OUTPUTS ON RUN
	POPJ P,
	TLNE T,INLD!PWR!GND		;IF NO INPUTS AT ALL,
	JRST CPOPJ1			;THEN UNUSED
	SETZM TMPCN1
	FETCH(TT,G,WPIN)		;FIRST PIN (AT LEAST ONE)
OUTTWO:	FETCH(TTT,TT,CBIT)		;GET CON/CARD BITS
	JUMPE TTT,OUTTHREE		;ANY?
	CAME TTT,TMPCN1			;SAME CARD?
	SKIPN TMPCN1			;NO, SEEN ONE YET?
	CAIA				;SAME OR FIRST
	POPJ P,				;MORE THAN ONE CARD
OUTTHREE:FETCH(TT,TT,NXTP)
	JUMPN TT,OUTTWO
	JRST CPOPJ1			;ONLY OUTPUTS!

;MORE THAN ONE PIN ON RUN ON CARD
MORONE:	TRNE T,SPWR!SGND!SHI!SNC	;THESE RUNS ARE OK
	POPJ P,
	MOVEI TT,RADDR(G,WPIN,NXTP)
	SETZM SAVET
	JRST MORON2

MORON1:	FETCH(TTT,TT,PBIT)
	TRNE TTT,DUP
	JRST MORON2
	FETCH(TTT,TT,PBDY)
	FETCH(TTT,TTT,BLOC)
	EXCH TTT,SAVET
	CAMN TTT,SAVET			;SAME CARD?
	JRST CPOPJ1			;YES, BINGO
MORON2:	FETCH(TT,TT,NXTP)
	JUMPN TT,MORON1
	POPJ P,

;MORE THAN ONE TERMINATION RULE ON RUN
MLTMRL:	TLNN T,ANYCON			;ANY CONNECTORS?
	POPJ P,				;NO, OK THEN
	MOVEI TT,RADDR(G,WPIN,NXTP)
	SETZM SAVET			;CLEAR LAST RULE SEEN
	JRST MLTRL2

MLTRL1:	FETCH(TTT,TT,PBIT)
	TRNN TTT,TRMBTS			;ANY TERMINATOR RULE?
	JRST MLTRL2			;NO, SKIP IT
	LDB TTT,[POINT TRMBSZ,TTT,TRMBPS+=18]
	EXCH TTT,SAVET			;SAVE NEW, GET OLD
	JUMPE TTT,MLTRL2		;OK IF FIRST
	CAME TTT,SAVET			;SAME RULE?
	JRST CPOPJ1			;NO, LOSE
MLTRL2:	FETCH(TT,TT,NXTP)
	JUMPN TT,MLTRL1
	POPJ P,

;EDGE RUNS WITH OUTPUTS
NOINC:	TLNE T,OUTLD
	TLNN T,ANYCON
	POPJ P,
	JRST CPOPJ1

;UNNAMED BACKPANEL RUNS (CARD)
ILBPRC:	TRNE T,GENSIG
	TLNN T,ANYCON
	POPJ P,
	JRST CPOPJ1

;UNNAMED BACKPANEL RUNS (BP)
ILBPRB:	TRNE T,GENSIG
	AOS (P)
	POPJ P,

;NON-EDGE RUNS WITH NO INPUTS
NOINP:	TDNE T,[OUTLD,,SPWR!SGND]	;IS IT EVEN GENERATED?
	TLNE T,ANYCON!INLD!GND!PWR
	POPJ P,				;NO
	PUSHJ P,EXTRAO
	JRST CPOPJ1
	POPJ P,

EXTRAC:	TRNE T,SPWR!SGND!SNC!SHI
	POPJ P,
	MOVE T,NCONS
	CAILE T,1
	AOS (P)
	POPJ P,

;UNUSED EXTRA OUTPUTS
EXOUT:	TDNE T,[OUTLD,,SPWR!SGND]
	TLNE T,ANYCON!INLD!GND!PWR
	POPJ P,
	JRST EXTRAO

;RUNS WITH NOTHING
NILWIR:	TLNN T,INLD!OUTLD!PWR!GND
	AOS (P)
	POPJ P,

;RUNS WITH UN OR NC
UNWIR:	TLNN T,NULLD			;ANY NULLS?
	SKIPE NULLS			;OR UN'S
	AOS (P)
	POPJ P,

;OUTPUTS AND PWR
OUTPWR:	TLNE T,PWR
	TDNN T,[OUTLD!INLD,,SGND]
	POPJ P,
	JRST CPOPJ1

;OUTPUTS AND GND
ILGNDO:	TLNE T,GND
	TDNN T,[OUTLD,,SPWR]
	POPJ P,
	JRST CPOPJ1

;GND AND PWR
ILLVG:	TLC T,PWR!GND
	TLCN T,PWR!GND
	AOS (P)
	POPJ P,

;MIXED VOLTAGES
ILLMIX:	SKIPE ILVOLT
	AOS (P)
	POPJ P,

;ECL CONNECTED TO TTL
ILLET:	TLC T,ECL!TTL
	TLCN T,ECL!TTL
	AOS (P)
	POPJ P,

;OVERLOADS
OVRLOD:	TLNE T,INLD!TERM		;INPUTS AND
	SKIPN TT,ANYOUT		;OUTPUTS?
	POPJ P,			;NO
	CAMN TT,NPULL
	JRST CKHOVR
	SKIPN TT,LOUTL
	JRST CKHOVR
	PUSH P,T
	PUSHJ P,LOLOAD
	POP P,TT
	EXCH T,TT
	JUMPGE TT,ISOVR
CKHOVR:	PUSH P,T
	PUSHJ P,HILOAD
	POP P,TT
	EXCH T,TT
	JUMPL TT,CPOPJ
ISOVR:	AOS (P)
	POPJ P,

;WIRE OR
ILLWOR:	SKIPN TT,ANYOUT
	POPJ P,
	CAMN TT,NOUT			;ALL NORMAL OUTPUTS?
	JRST [	CAIE TT,1
		AOS (P)
		POPJ P,]
	CAMN TT,NTRI
	POPJ P,
	SUB TT,NPULL
	CAME TT,NOC
	AOS (P)
	POPJ P,

NEEDZ:	TRC A,INLD!OUTLD!ECL
	TRCN A,INLD!OUTLD!ECL
	TRNE A,ANYCON
	POPJ P,
	SKIPN NTERM
	AOS (P)
	POPJ P,

ILLZ:	TRNN A,ECL
	POPJ P,
	MOVE T,NTERM
	CAILE T,1
	JRST CPOPJ1
	TRNE A,ANYCON
	TRNN A,OUTLD
	POPJ P,
	SKIPE NTERM
	AOS (P)
	POPJ P,

QUESTZ:	TRNN A,ECL
	POPJ P,
	TRNN A,ANYCON
	JRST QNCONZ
	TRNN A,OUTLD
	SKIPE NTERM
	POPJ P,
	TRNE A,INLD
	AOS (P)
	POPJ P,

QNCONZ:	TRNN A,INLD
	TRNN A,OUTLD
	POPJ P,
	SKIPE NTERM
	AOS (P)
	POPJ P,

NEEDBZ:	SKIPN NTERM
	TRNN A,ECL
	POPJ P,
	TRC A,INLD!OUTLD
	TRCN A,INLD!OUTLD
	AOS (P)
	POPJ P,

ILLBZ:	TRNE A,ECL
	SKIPN NTRMOUT
	POPJ P,
	JRST CPOPJ1

QUESBZ:	TRNE A,ECL
	SKIPG T,NTERM
	POPJ P,
	CAMN T,NTRMIN
	CAIE T,1
	AOS (P)
	POPJ P,
