;<WIRELIST>MPG21.FAI.9, 15-NOV-75 19:03:46, EDIT BY HELLIWELL
.ADD(ALLLOCS,MPG21,LMP21V)

BEGIN MPG21

;THE TRANSFER VECTOR FOR THE AUGUAT 8136 PG21-180 BOARD

^LMP21V:
	JRST LCINIT		;INITIALIZE LETTER/NUMBER CONVERSION TABLES
	JRST LOCOUT
	JRST QUPIN
	JRST SLTOUT
	JRST GETSLT
	JRST BPINPN
	JRST RPNAM
MDPC,<
	JRST CSLTLP
	JRST MSLTLN		;USE THE CANONICAL ONE
	JRST SLTLPN
	JRST PINPLS
MD,<
	JRST CSLTLT
	JRST GTCONP
	JRST CSLTLD
>;MD
>;MDPC
MWL,<
	JRST PINPN5
	JRST TRPNAM
	JRST RPNAMA
	JRST WLOCOUT
	JRST LOCPNC
	JRST LOCPNW
	JRST PRCNPN
	JRST GETLOC
	JRST PADPRT
	JRST RAYDIP
	JRST RAYCON
	JRST LOCPIN
	JRST LOCPNM
	JRST LOCPNP
	JRST CPNSEP
	JRST CPNMER
	JRST BPNPIN
	JRST CPARTP
	JRST PRBCN
	JRST SEQLOC
	JRST CONGIN
	JRST GTSLTT
>;MWL
	[ASCIZ/#LL#/]
	[ASCIZ /#/]
MDPC,<	[ASCIZ/#LJ#-#/]
	[ASCIZ/#L#/]
	[ASCID /A01/]
>;MDPC
MWL,<	[ASCIZ/#L#/]
	[ASCIZ/#LJ#/]
>;MWL
CHECK LMP21V,LTVLEN

L2NSUB:	BLOCK L2NLEN
N2LSUB:	REPEAT N2LLEN, < "?"
>
EN2L__.

NNN__1
FOR I IN(A,B,C,D,E,F)
<	L2N2L I,0
>
FOR I IN (G,I,O,Q)
<	L2N2L I,1B0
>

N2LMAX__NNN-1

ORG EN2L

COMMENT 

AUGAT-8136-PG21 CONNECTOR PIN FORMAT PRINTS AS #LJ#(#)
WHERE L IS THE GROUP.  THE J IS LITERAL.  THE FIRST DIGIT IS THE PANEL
AND THE SECOND IS THE JACK.  PIN IS LAST

______|_____|_____|_____|_____|_____|
|    20    23                      35
|  3  |  3  |          12           |
|_____|_____|_______________________|
     |     |		|
<    |     |		|------------>J#-# PIN (1-26. FOR J1 26-52. FOR J2)
     |     |
<    |     |------------------------->L GROUP
     |
<    |-------------------------------># PANEL



LCINIT:	MOVE T,[L2NSUB,,L2N]
	BLT T,L2N+L2NLEN+N2LLEN-1
	MOVEI T,N2LMAX
	MOVEM T,MAXN2L
	POPJ P,
;THIS BLOCK STRUCTURE HIDES ALL THE STANDARD ROUTINES FOR
;ALL BUT III BOARDS FROM III


;PUT PIN ON END OF BODY LOCATION

BPINPN:	MOVE T,A
	PUSHJ P,PUTDEC
	POPJ P,

MWL,<
PINPN5:	MOVEI TT,5
	MOVEM TT,NDIG
	JRST NPUTDEC

TRPNAM:	TGETNUM
	JRST CPOPJ1

RPNAMA:	GETCH
	CAIL CHRREG,"0"
	CAILE CHRREG,"9"
	TDZA A,A
	PUSHJ P,TRPNAM
	JFCL
	JRST CPOPJ1
>;MWL

RPNAM:	GETNUM
	JRST CPOPJ1

MDPC,<
SLTLPN:PUSHJ P,SLTOUT
	HLRZ A,A
	JRST LOCOUT

PINPLS:HLRZ T,A
	PUSHJ P,PUTDEC
MD,<	TRNE A,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		POPJ P, ]
	TRNE A,L1416
	PUTBYT "+"
>;MD
	POPJ P,
>;MDPC
SUBTTL	LOCPIN -- PRINTS LOCATION AND PIN #
MWL,<

LOCPIN:PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	PUTBYT ")"
	POPJ P,

;LOCPNM -- PRINTS DIP LOC AND PIN # ON WIRE-WRAP SIDE

LOCPNM:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	MOVE TT,(P)
	TRNE TT,R1416
	JRST [	CAIL T,8
		ADDI T,2
		JRST LCPNM3 ]
	TRNE TT,L1416
	ADDI T,1
LCPNM3:	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/-2/]
		JRST LCPNM2 ]
	TRNE T,L1416
	PUTBYT "-"
LCPNM2:	PUTBYT ")"
	POPJ P,

LOCPNP:PUSH P,T
	PUSHJ P,LOCOUT
	PUTBYT "("
	HLRZ T,A
	PUSHJ P,PUTDEC
	POP P,T
	TRNE T,R1416
	JRST [	CAML A,[8,,0]
		PUTSTR [ASCIZ/+2/]
		JRST LCPNP2 ]
	TRNE T,L1416
	PUTBYT "+"
LCPNP2:	PUTBYT ")"
	POPJ P,
;MORE WL ROUTINES

;SEPARATE CONNECTOR PIN BODY LOC (PADDLE) FROM PIN # (PIN, SIDE)
CPNSEP:	MOVE TT,T
	ANDI T,770000
	ANDI TT,7777
	POPJ P,

;MERGE CONNECTOR PIN BODY LOC WITH PIN #
CPNMER:	DPB TT,[POINT 12,T,35]
	POPJ P,

;PRINT CONNECTOR PIN NAME AS SEEN FROM BACK PANEL
BPNPIN:	PUSH P,T	;SAVE OFFSET HERE
	LDB T,[POINT 4,A,3]
	SKIPE T		;CONVERT 0 TO SPACE
	ADDI T,20	;ALL THE REST TO DIGITS
	ADDI T,40
	PUTBYT (T)
	LDB T,[POINT 5,A,8]
	LDB TT,[POINT 6,A,23]
	ADDI T,-1(TT)
	CAILE T,77
	SETZ T,
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,17]
	POP P,TT
	JUMPE TT,NOOFFS
	ADD T,TT
	JUMPLE T,[OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE ZERO OR NEGATIVE.
/]
		SETZ T,
		JRST NOOFFS]
	CAIG T,=99
	JRST NOOFFS
	OUTSTR[ASCIZ/OFFSET CAUSED SLOT NUMBER TO BE MORE THAN 2 DIGITS.
/]
	MOVEI T,=99
NOOFFS:	PUSHJ P,NDECOUT
	LDB T,[POINT 6,A,29]
	PUTBYT @N2L(T)
	LDB T,[POINT 6,A,35]
	JRST DECOUT

;OUTPUT EDGE PIN TO PARTITION FILE AS NUMBER
CPARTP:	LSH T,-14
	PUTBYT "0"(T)
	MOVEI T,2
	MOVEM T,NDIG
	MOVE T,TT
	LSH T,-5
	TRNE TT,1
	SUBI T,1
	JRST NDECOUT

>;MWL
SUBTTL	CSLTLT,LOCPNC,LOCPNW,PRCNPN -- CONNECTOR PIN PRINTING STUFF

MD,<
CSLTLT:	PUSHJ P,SLTOUT
	JRST CSLTLD
>;MD

MWL,<
PRBCN:	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
	JRST PRCNPN
>;MWL
SUBTTL	DETECT IF THIS PIN NAME IS "WILD"
;CALL WITH PIN NAME IN RH(A)
QUPIN:	PUSH P,A
	LSH A,-14
	ANDI A,77
	MOVE A,N2L(A)
	CAIE A,"U"
	JRST [	POP P,A
		POPJ P,]
	POP P,A
	ANDI A,3777
	PUSH P,B
	IDIVI A,=200
	POP P,B
	CAILE A,2
	TDZA A,A
	ADDI A,1
	JRST CPOPJ1

MWL,<
;THESE ROUTINES SHOULD BE MOVED EVENTUALLY, THEY ARE A FUNCTION OF THE BOARD TYPE

;CALL WITH LESSER OF TWO LOCS IN T
;GREATER IN TT
SEQLOC:	XOR T,TT
	TRNE T,770000		;LETTERS SHOULD BE THE SAME
	POPJ P,			;ELSE NOT IN SEQUENCE
	XOR T,TT
	SUB TT,T
	CAIN TT,1
	AOS (P)
	POPJ P,

CONGIN:	PUSH P,A
	HRRZ A,A
	JUMPE A,CONG1
	TRNN A,2
	JRST [	TRC A,3
		JRST CONG3]
	LSH A,-6
	ANDI A,77
	ADDI A,1
	CAIG A,=18
	JRST [	LSH A,6
		TRO A,1
		DPB A,[POINT 12,(P),35]
		POP P,A
		JRST CPOPJ1]
	LDB A,[POINT 6,(P),23]
	ADDI A,1
	PUSH P,T
	LDB T,[POINT 3,-1(P),11]		;GET EXTENT
	CAIG A,1(T)			;DOES THIS PADDLE EXIST?
	JRST [	POP P,T
		JRST CONG2]
	POP P,T
	POP P,A
	POPJ P,

;VARIOUS FLAVORS OF RETURN FOR CONGIN

CONG1:	HRRZ A,L2N+"A"
CONG2:	LSH A,6
	IOR A,L2N+"A"
	HRRZ A,A		;CLEAR ANY LH GARBAGE
	LSH A,6
	TRO A,1
CONG3:	HRRM A,(P)
	POP P,A
	JRST CPOPJ1
>;MWL
SUBTTL	SLTOUT -- PRINTS DIP BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|				   18      21        26    29          35
|                 18		    |   4   |    5    |  3  |     6     |
|___________________________________|_______|_________|_____|___________|
		  |			|	 |	 |	  |
<		  |			|	 |	 |	  |------># SLOT
		  |			|	 |	 |
<		  |			|	 |	 |--------------->L +RACK
		  |			|	 |
<		  |			|	 |----------------------->L RACK
		  |			|
<		  |			|--------------------------------># BAY
		  |
<		  |------------------------------------------------------>NOT USED

SLTOUT:	LDB T,[POINT 4,A,21]
	JUMPE T,.+2
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
MDPC,<	JUMPE TT,CPOPJ	>
	PUTBYT @N2L(TT)
	LDB TTT,[POINT 3,A,29]
	JUMPE TTT,SLTOU1
	ADD TT,TTT
	PUTBYT @N2L(TT)
SLTOU1:	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 6,A,35]
	JRST NPUTDEC
SUBTTL	CSLTOUT -- PRINTS CONNECTOR PIN BAY/RACK/SLOT #

COMMENT 

|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|_____|
|	    |			   18      21        26    29          35
|     6     |          12	    |   4   |    5    |  3  |     6     |
|___________|_______________________|_______|_________|_____|___________|
      |			|		|	 |	 |	  |
<     |			|		|	 |	 |	  |------># SLOT
      |			|		|	 |	 |
<     |			|		|	 |	 |--------------->L +RACK
      |			|		|	 |
<     |			|		|	 |----------------------->L RACK
      |			|		|
<     |			|		|--------------------------------># BAY
      |			|
<     |			|------------------------------------------------>NOT USED
      |
<     |------------------------------------------------------------------>L PADDLE

MD,<
CSLTOUT:
	JRST SLTOUT			;DO SAME AS BODY LOC.
IFN 0,<;REMOVE BY R HELLIWELL 31 MAR 75
;CONFUSING BECAUSE DISPLAYED AND PLOTTED CONNECTOR PIN DESIGNATOR
;WERE DIFFERENT FROM TYPEIN AND TYPEOUT.
	TRNN A,-1
	POPJ P,
	LDB T,[POINT 3,A,29]
	LDB TT,[POINT 6,A,5]
	TLNN A,4000			;"U" PIN?
	CAILE TT,1(T)			;ON BACK PANEL?
	JRST SLTOUT			;NO, TREAT LIKE A BODY
	LDB T,[POINT 4,A,21]		;SHOW ACTUAL BACKPANEL RACK LETTER
	SKIPE T
	PUSHJ P,PUTDEC
	LDB TT,[POINT 5,A,26]
	LDB T,[POINT 6,A,5]
	ADDI TT,-1(T)
	PUTBYT @N2L(TT)
	LDB T,[POINT 6,A,35]
	JRST PUTDEC
>;IFN 0
>;MD
;CHECK TO MAKE SURE CHRREG IS A LETTER (CONVERTS LOWER CASE)

CHKLET:CAIL CHRREG,"a"		;CHECK FOR LOWER CASE
	CAILE CHRREG,"z"
	CAIA			;NOT LOWER CASE
	SUBI CHRREG,<"a"-"A">	;CONVERT TO UPPER CASE
	CAIL CHRREG,"A"
	CAILE CHRREG,"Z"
	POPJ P,
	JRST CPOPJ1		;ITS LEGAL

TWOLET:ROT CHRREG,-7*2	;MAKE IT ASCII
	MOVSI NUMREG,-TWOLEN
TWOLT1:	CAME CHRREG,TWOTAB(NUMREG)
	AOBJN NUMREG,TWOLT1
	JUMPGE NUMREG,CPOPJ
	MOVEI NUMREG,40(NUMREG)
	JRST CPOPJ1

TWOTAB:	ASCII/BT/
	ASCII/CB/
	ASCII/CR/
	ASCII/DL/
	ASCII/FC/
	ASCII/TP/
REPEAT 40-<.-TWOTAB>,<ASCII/??/>
TWOLEN__.-TWOTAB

MDPC,<
;THE CANONICAL DIP LOC READER WHERE DIP LOC IS OF FORM L#
;SLTLN -- READS A BAY/RACK/SLOT/DIPLOC OR JUST A DIPLOC
;CALL IS:
;	PUSHJ P,SLTLN
;	JRST ERROR
;	JRST CRLF.ONLY
;MD,<	JFCL	>	;LOC ONLY
;	(BRS,,LOC IN DESTIN)

SLTLN:	SETZM DESTIN
MPC,<	GETCH	>
MD,<	GETNUM
	CAIL NUMREG,20
	POPJ P,
	DPB NUMREG,[POINT 4,DESTIN,3]
>;MD
	CAIN CHRREG,12
	JUMPE NUMREG,CPOPJ1
	PUSHJ P,CHKLET
	POPJ P,
MD,<	PUSH P,D
	SKIPN D,L2N(CHRREG)
	JRST POPDJ
	DPB D,[POINT 5,DESTIN,8]
	GETNUM
	PUSHJ P,CHKLET
	JRST SLTLN1
	JUMPL D,SLTLN3		;GIOQ ILLEGAL FOR BRS
	POP P,D
	JUMPN NUMREG,SLTLN2
	LDB NUMREG,[POINT 5,DESTIN,8]
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	JUMPL CHRREG,[SUB CHRREG,NUMREG
		JRST SLTLN4]
	SUB CHRREG,NUMREG
	JUMPLE CHRREG,SLTLN4
	CAILE CHRREG,7	
	JRST SLTLN4
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
	JUMPE NUMREG,CPOPJ
	PUSHJ P,CHKLET
	JRST SLTLN8
SLTLN2:	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,17]
>;MD
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPE NUMREG,SLTLN5
SLTLN7:	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
MD,<	HLRZ NUMREG,DESTIN
	JUMPN NUMREG,CPOPJ3	;THIS RETURN IF BRS TYPED
>;MD
	JRST CPOPJ2		;THIS RETURN IF PC OR NO BRS

SLTLN5:	LDB NUMREG,[POINT 6,DESTIN,23]
	MOVE NUMREG,N2L(NUMREG)
	LSH NUMREG,7
	IOR CHRREG,NUMREG
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,23]
	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

MD,<
SLTLN1:	POP P,D
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,7777
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]
	LDB NUMREG,[POINT 9,DESTIN,8]
	CAIL NUMREG,40
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ2

SLTLN3:	MOVE D,N2L(D)		;GET LETTER BACK
	LSH D,7
	IOR CHRREG,D		;PUT 2 LETTERS IN CHRREG
	POP P,D			;RESTORE D
	PUSHJ P,SLTLN6
	POPJ P,
	JRST SLTLN9

SLTLN4:	PUSHJ P,SLTLN0
	POPJ P,
SLTLN9:	GETNUM
	JUMPN NUMREG,SLTLN7
	POPJ P,

SLTLN8:	PUSH P,NUMREG
	PUSH P,CHRREG
	LDB NUMREG,[POINT 5,DESTIN,8]
	LDB CHRREG,[POINT 3,DESTIN,11]
	PUSHJ P,SLTLN0
	JRST [	POP P,CHRREG
		POP P,NUMREG
		POPJ P,]
	POP P,CHRREG
	POP P,NUMREG
	JRST SLTLN7

SLTLN0:	ADD CHRREG,NUMREG	;GET NUMBER BACK
	MOVE NUMREG,N2L(NUMREG)	;GET FIRST LETTER BACK
	MOVE CHRREG,N2L(CHRREG)	;GET SECOND LETTER BACK
	LSH NUMREG,7
	IOR CHRREG,NUMREG
SLTLN6:	LDB NUMREG,[POINT 4,DESTIN,3]
	JUMPN NUMREG,CPOPJ
	PUSHJ P,TWOLET
	POPJ P,
	DPB NUMREG,[POINT 24,DESTIN,23]
	JRST CPOPJ1

POPDJ:	POP P,D
	POPJ P,
>;MD
>;MDPC

;GETSLT -- READS A BAY/RACK/SLOT #

;CALL IS:
;	PUSHJ P,GETSLT
;	JRST ERROR
;	JUMPE TT,CRLF.ONLY	;THIS IS OPTIONAL, OBVIOUSLY
;	MOVEM TT,BRS

MWL,<
GTSLTT:	SETZM DESTIN
	TGETNUM
	JRST GTSLT1
>;MWL

GETSLT:	SETZM DESTIN
	GETNUM
GTSLT1:	DPB NUMREG,[POINT 4,DESTIN,3]
	PUSHJ P,CHKLET
	JRST [	JUMPE NUMREG,CPOPJ1
		POPJ P,]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 5,DESTIN,8]
	GETNUM
	JUMPN NUMREG,GTSLT2
	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	LDB NUMREG,[POINT 5,DESTIN,8]
	SUB CHRREG,NUMREG
	JUMPL CHRREG,CPOPJ
	CAILE CHRREG,7
	POPJ P,
	DPB CHRREG,[POINT 3,DESTIN,11]
	GETNUM
GTSLT2:	DPB NUMREG,[POINT 6,DESTIN,17]
	JRST CPOPJ1
MD,<
;	PUSHJ P,RDLLN
;	    ERROR RETURN
;	    SUCCESS RETURN

RDLLN:	PUSHJ P,CHKLET
	POPJ P,
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 12,DESTIN,29]
	GETNUM
	JUMPN NUMREG,CKCR
	PUSHJ P,CHKLET
	POPJ P,
	LDB TT,[POINT 6,DESTIN,29]
	DPB TT,[POINT 6,DESTIN,23]
	SKIPG CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 6,DESTIN,29]
	GETNUM
	JUMPE NUMREG,CPOPJ
CKCR:	LDB TT,[POINT 12,DESTIN,29]
	XOR TT,L2N+"U"
	TRNN TT,-1			;SAME LETTER?
	JRST [	CAILE NUMREG,3777	;YES, LEGAL NUMBER?
		POPJ P,			;NO
		HRRZ TT,L2N+"U"
		DPB TT,[POINT 6,DESTIN,23]
		TRO NUMREG,4000
		DPB NUMREG,[POINT 12,DESTIN,35]
		JRST CPOPJ1]
	CAILE NUMREG,77
	POPJ P,
	DPB NUMREG,[POINT 6,DESTIN,35]
	JRST CPOPJ1
>;MD

LOCOUT:	LDB TTT,[POINT 6,A,23]
	JUMPE TTT,CPOPJ			;JUMP IF ILLEGAL
	LDB T,[POINT 3,A,20]		;PANEL NUMBER
	PUSHJ P,PUTDEC			;PRINT IT OUT
	LDB T,[POINT 3,A,23]		;GROUP LETTER
	PUTBYT @N2L(T)			;CONVERT TO LETTER AND PRINT
	TRNE A,7777			;IS THIS A CONNECTOR PIN?
	JRST [	MOVEI T,2		;NO, SETUP FOR 2 DIGIT NUMBER PRINT
		MOVEM T,NDIG
		LDB T,[POINT 12,A,35]	;SLOT NUMBER WITHIN GROUP
		JRST PUTDEC]		;PRINT IT OUT
	PUTBYT "J"			;YES, CONNECTOR, PRINT OUT A LITERAL J
	POPJ P,

MSLTLN:	SETZM DESTIN
	GETNUM				;GET PANEL NUMBER
	CAILE NUMREG,4			;MAX OF FOUR PANELS
	POPJ P,
	DPB NUMREG,[POINT 3,DESTIN,20]	;STORE AS PANEL NUMBER
	CAIN CHRREG,12			;END OF LINE?
	JUMPE NUMREG,CPOPJ1		;YES, AND NULL NUMBER, JUST RETURN
	JUMPE NUMREG,CPOPJ		;OTHERWISE ZERO IS IN ERROR
	PUSHJ P,CHKLET			;LETTER TYPED NEXT?
	POPJ P,				;NO, ERROR
	SKIPG D,L2N(CHRREG)		;GET NUMBER CORRESPONDING TO LETTER
	POPJ P,				;NON-EX OR GIOQ!!
	DPB D,[POINT 3,DESTIN,23]	;DEPOSIT AS GROUP LETTER
	GETNUM				;GET NEXT NUMBER
	JUMPE NUMREG,CPOPJ		;ZERO, ERROR
	CAILE NUMREG,=30		;EXCEEDS MAX SLOT NUMBER WITHIN GROUP?
	POPJ P,				;YES
	DPB NUMREG,[POINT 12,DESTIN,35]	;DEPOSIT AS SLOT NUMBER
	JRST CPOPJ2			;RETURN HAPPINESS


MDPC,<
CSLTLP:
MD,<
	PUSHJ P,CSLTOUT
CSLTLD:	HLRZS A
	PUSH P,A
	TRZ A,7777
	PUSHJ P,LOCOUT
	POP P,A
>;MD
	LDB TTT,[POINT 12,A,35]		;SLOT NUMBER
	MOVEI T,1
	CAILE TTT,=26			;IN CONNECTOR 1?
	MOVEI T,2			;NO, IN 2
	PUSHJ P,PUTDEC
	PUTBYT "-"
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,[POINT 12,A,35]
	CAILE T,=26			;IN CONNECTOR 1
	SUBI T,=26			;NO, OFFSET NUMBERS
	JRST NPUTDEC
MD,<
GTCONP:	SETZM DESTIN
	GETNUM
	CAIN CHRREG,12
	JUMPE NUMREG,CPOPJ1		;CARRIAGE RETURN ONLY
	JUMPE NUMREG,CPOPJ		;ERROR IF NO NUMBER
	CAILE NUMREG,4			;MAX OF FOUR PANELS
	POPJ P,				;ERROR OTHERWISE
	DPB NUMREG,[POINT 3,DESTIN,20]	;DEPOSIT PANEL NUMBER
	PUSHJ P,CHKLET			;CHECK FOR LETTER
	POPJ P,				;BAD LETTER, ERROR
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 3,DESTIN,23]	;DEPOSIT GROUP LETTER
	GETNUM
	JUMPN NUMREG,CPOPJ		;BETTER NOT BE A NUMBER HERE
	CAIE CHRREG,"J"			;AND LETTER BETTER BE J
	POPJ P,
	GETNUM
	JUMPE NUMREG,CPOPJ		;BETTER BE A NUMBER HERE
	CAIN CHRREG,"-"			;TERMINATED BY A -
	CAILE NUMREG,2			;AND LESS THAN OR EQUAL TO 2
	POPJ P,
	DPB NUMREG,[POINT 12,DESTIN,35]	;TEMP STORAGE FOR CONNECTOR NUMBER
	GETNUM
	JUMPE NUMREG,CPOPJ		;BETTER BE A NON ZERO NUMBER
	CAILE NUMREG,=26		;LESS THAN 27
	POPJ P,
	LDB TT,[POINT 12,DESTIN,35]	;PICK UP CONNECTOR NUMBER
	CAILE TT,1
	ADDI NUMREG,=26			;OFFSET PIN NUMBER BY 26 IF CONNECTOR 2
	DPB NUMREG,[POINT 12,DESTIN,35]
	CAIE CHRREG,12			;TERMINATION HAD BETTER BE HERE AND EOL
	POPJ P,
	JRST CPOPJ2			;ALL'S WELL THAT ENDS WELL	
>;MD
>;MDPC

MWL,<
WLOCOUT:JRST LOCOUT

GETLOC:	GETNUM
	JUMPE NUMREG,CPOPJ		;NON ZERO NUMBER
	CAILE NUMREG,4			;PANEL # MAX OF FOUR
	POPJ P,
	LSH NUMREG,=15			;INTO LEFT 3 BITS OF RIGHT HALF
	HRLM NUMREG,(P)			;INTO LH OF PDL
	PUSHJ P,CHKLET
	POPJ P,				;NOT A LETTER
	SKIPN CHRREG,L2N(CHRREG)
	POPJ P,
	DPB CHRREG,[POINT 3,(P),5]	;DEPOSIT GROUP LETTER INTO LH OF PDL
	GETNUM
	JUMPN NUMREG,[	CAILE NUMREG,=30	;HERE FOR DIP LOC
			POPJ P,			;ONLY 30 DIP SLOTS PER GROUP
			HLRZ TT,(P)
			TRO TT,(NUMREG)
			JRST CPOPJ2]
	PUSHJ P,CHKLET			;HERE TO DO CONNECTOR PIN
	POPJ P,
	CAIE CHRREG,"J"
	POPJ P,
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAIN CHRREG,"-"
	CAILE NUMREG,2			;CHECK FOR LEGAL JACK #
	POPJ P,
	DPB NUMREG,[POINT 2,(P),17]	;DEPOSIT CONNECTOR NUMBER INTO RH OF LH OF PDL
	GETNUM
	JUMPE NUMREG,CPOPJ
	CAILE NUMREG,=26		;ONLY 26 PINS PER JACK
	POPJ P,
	LDB TT,[POINT 2,(P),17]		;PICK UP CONNECTOR NUMBER
	CAILE TT,1			;CONNECTOR 1?
	ADDI TT,=26			;NO, ADD OFFSET FOR CONNECTOR 2
	HLRZ TT,(P)
	TRZ TT,7777
	TRO TT,(NUMREG)
	JRST CPOPJ1	

LOCPNW:
LOCPNC:	PUSHJ P,LOCOUT
	HLRZ A,A
	PUTBYT "-"
PRCNPN:	LDB T,[POINT 12,A,35]
RAYDP1:	MOVEI TTT,2
	MOVEM TTT,NDIG
	JRST NPUTDEC

RAYCON:	PUSHJ P,LOCOUT
	PUTSTR [ASCIZ /   /]		;3 SPACES
	HLRZS A
	JRST PRCNPN

PADPRT:	OUTSTR[ASCIZ/NOT YET!
/]
	HALT CPOPJ

RAYDIP:	PUSH P,T			;HAS SOME BITS
	PUSHJ P,LOCOUT
	PUTSTR [ASCIZ /   /]		;3 SPACES
	POP P,T
	TRZE T,L1416
	ADD A,[1,,0]
	HLRZ T,A
	JRST RAYDP1
>;MWL
BEND MPG21

MDWL,<
.ADD(ALLWW,MPG21,MP21TV)
MWL,<
SUBTTL WIRE WRAP ROUTINES  --  MULTIPLE PG21'S
BEGIN WMPG21

comment 

All calculations are done from the dip side.

(0,0) at lower left hand corner in left handed coordinate system.
Dip sockets are arranged in 5x6 groups.  These 30 dip groups come
in pairs.  Each group comes with a pair of Scotch Flex(R) connectors
labeled "J1" and "J2".  There can be up to 6 30 dip groups on one
board.  The horizontal spacing between groups is 2.700".

There are 5 panels, vertically arranged.  The vertical spacing
is 7.500".  

5A30(8) is at (0,0).  5AJ2-26 is at (500,6100).  5AJ1-26 is at (500,6400).

It follows that 5B30(8) is at (2700,0)


MXPNL__5		;max number of panels wrappable at once
NGRPS__6		;PG21-180 HAS 6 GROUPS
GRPCOL__5		;# COLS IN GROUP
GRPROW__6		;# COLUMNS IN GROUP
UMLCOL__GRPROW		;UML INTERCHANGES ROWS AND COLUMNS
UMLROW__GRPCOL
MAXDIP__GRPROW*GRPCOL	;# DIPS IN GROUP
PNLOFT__=7500		;7.500" VERTICAL SPACING BETWEEN PANELS
GXOFST__=2700		;2.700" GROUP HORIZONTAL SPACING
XDIPSP__ =500		; .500" DIP HORIZONTAL SPACING
YDIPSP__=1000		;1.000" DIP VERTICAL SPACING
XPINSP__ =300		; .300" DIP PIN HORIZONTAL SPACING
YPINSP__ =100		; .100" DIP PIN VERTICAL SPACING
PXOFST__ =500		; .500" X OFFSET FOR POWER PIN
PYOFST__ =800		; .800" Y OFFSET FOR POWER AND GROUND PINS
JXOFST__ =500		; .500" X OFFSET FOR JACKS
JYOFST__=6100		;6.100" Y OFFSET FOR J2
JYOFS1__ =300		; .300" Y OFFSET FROM J2 TO J1
PXPNSP__ =100		; .100"	CONNECTOR PIN VERTICAL SPACING
PYPNSP__ =100		; .100" CONNECTOR PIN HORIZONTAL SPACING

;SOME BYTE POINTERS FOR EXTRACTING FIELDS

DEFINE %DIPP<[POINT  6,A,17]>	;DIP PIN #
DEFINE %DIPPNL<[POINT 3,A,20]>	;PANEL NUMBER
DEFINE %DIPG<[POINT  3,A,23]>	;DIP GROUP
DEFINE %DIPS<[POINT 12,A,35]>	;DIP SLOT

DEFINE %CONP<[POINT 12,A,35]>	;CONNECTOR PIN # (AND JACK NUMBER <PINS 1-2*26>)
DEFINE %CNPG<[POINT 5,A,22]>	;<PANEL-1>*NGRPS+GROUP
DEFINE %CNJK<[POINT 1,A,23]>	;CONNECTOR JACK BIT

^MP21TV__.	;TRANSFER VECTOR FOR DOUBLE HEIGHT DEC BOARDS

	-1		;ISWW
	JRST CPOPJ	;WWINIT
	JRST MAPRC
	JRST MAPPAD
	JRST PINMAP	
	JRST PINBIT	
	JRST VGCON	
	JRST DISTPP	;POINT TO POINT WIRING
	JRST FPWR
	JRST FGND
	JRST PGPRTM
	JRST PGPRNT
	JRST GNDCLR
	JRST WAGGND
	JRST GNDOUT
	JRST VCCOUT
	JRST RAYGEN
	MXPNL*UMLROW*NGRPS	;NROWS		    (USED FOR UML ONLY)
	UMLCOL		;NCOLS		    (USED FOR UML ONLY)
	0		;NCLPRG		    (USED FOR UML ONLY)
	MXPNL*MAXDIP*NGRPS	;DIPSLT_NROWS*NCOLS (USED FOR UML ONLY)
	SETPAD(MXPNL*NGRPS*2)	;NPADS		    (USED FOR UML ONLY)
	XWD -=26,1	;PADLET		    (USED FOR UML ONLY)
	XWD -1,0	;PADPIN		    (USED FOR UML ONLY)
	=10		;FRACTN
	=200*2		;WRAPMG .200" INSULATION AROUND EACH POST
	=1500		;POSTMG .750" BARE WIRE AROUND EACH POST
	0		;NEXTR
CHECK MP21TV,WTVLEN

;TABLES FOR MAPIT

;**************************************** DIP PINS

DEFINE XY(X,Y)
<	X*XPINSP,,Y*YPINSP
>

PINTAB:
FOR Y_7, 0, -1
<	XY(1,Y)
>
FOR Y_0, 7, 1
<	XY(0,Y)
>
;**************************************** DIP LOCATIONS

DEFINE XY(X,Y)
<	X*XDIPSP,,Y*YDIPSP
>

DIPLOC:
 FOR Y_5, 0, -1
<FOR X_4, 0, -1
<	XY(X,Y)
>>
;**************************************** SCOTCH-FLEX PINS

DEFINE XY(X,Y)
<	X*PXPNSP,,Y*PYPNSP
>

FLXPIN:
 FOR Y_1, 0, -1
<FOR X_0, =12, 1
<	XY(X,Y)
>>
MAPIT:	JUMPL A,CONMAP
	LDB TTT,%DIPPNL		;GET PANEL NUMBER
	JUMPE TTT,CPOPJ		;LOSER
	MOVEI TT,MXPNL		;MAX NUMBER OF PANELS
	CAILE TTT,MXPNL		;TOO BIG
	POPJ P,
	SUBM TT,TTT
	IMULI TTT,PNLOFT	;VERTICAL OFFSET OF PANEL	
	LDB TT,%DIPG		;GROUP LETTER
	LDB T,%DIPS		;DIP #
	JUMPE T,CPOPJ
	CAILE T,MAXDIP		;TOO MANY DIPS FOR SLOT
	POPJ P,
	MOVE T,DIPLOC-1(T)	;XY OF DIP SLOT
	IMULI TT,GXOFST		;X FUDGE FOR GROUP
	MOVSI TT,-GXOFST(TT)
	ADD T,TT		;LOCATION OF DIP ON BOARD
	ADD T,TTT		;ADD IN PANEL OFFSET
	LDB TT,%DIPP		;PIN #
	TLNE A,MAPPWR!MAPGND	;WANT POWER OR GROUND?
	JRST PINPGP		;YES, RETURN FIX IT UP
	CAIN B,=16		;16 PIN DIP?
	JRST MAPIT0		;NO, DON'T NEED TO FUTZ AROUND
ITS,<
	CAIN B,=24		;24 PIN CHIP?
	JRST MAP24P
	CAIN B,=28
	JRST MAP28P
	CAIN B,=40
	JRST MAP40P
>;ITS
	CAIE B,=14		;14 PIN CHIP?
	POPJ P,			;??? I GIVE UP
	ADDI TT,1
	ADD A,[1,,0]
	TLO A,PLUS1
MAPIT0:	ADD T,PINTAB-1(TT)	;ADD EXTRA XY FOR PIN
	SETZB TT,TTT		;FLUSH AWAY THOSE GOODIES
	JRST CPOPJ2

;POWER AND GROUND PIN FOR DIP

PINPGP:	JUMPN TT,CPOPJ		;THE PIN # SHOULD BE ZERO
	ADDI T,PYOFST		;FIX THE Y
	TLNN A,MAPPWR
	JRST [	MOVSI TT,GND	;HE WANTS GND
		JRST PINPG1]
	ADD T,[PXOFST,,0]	;HE WANTS PWR
	MOVSI TT,PWR
PINPG1:	SETZ TTT,
	JRST CPOPJ2

ITS,<
;I WOULDN'T WISH THIS ON . . .

MAP24P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,2		;BETTER BE IN A SLOT 1,2,OR 3 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 24 PIN DIP
	ADD T,M24PIN-1(TT)	;ADD X,Y OFFSET FOR 24 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

MAP28P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,2		;BETTER BE IN A SLOT 1,2,OR 3 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 28 PIN DIP
	ADD T,M28PIN-1(TT)	;ADD X,Y OFFSET FOR 28 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

MAP40P:	PUSH P,TT
	LDB TT,%DIPS		;PICK UP SLOT #
	SUBI TT,1
	IDIVI TT,GRPCOL		;SLOT NUMBER MOD 5 IN TTT
	POP P,TT
	CAILE TTT,1		;BETTER BE IN A SLOT 1 OR 2 FROM LEFT SIDE OF GROUP
	POPJ P,			;ILLEGAL SLOT LOCATION FOR 40 PIN DIP
	ADD T,M40PIN-1(TT)	;ADD X,Y OFFSET FOR 40 PIN CHIPS (GIVES SOCKET PIN #)
	SETZB TT,TTT
	JRST CPOPJ2

RADIX =10

DEFINE LP(SLOT,PIN)
<	-<SLOT-1>*XDIPSP,,<8-PIN>*YPINSP
>

DEFINE RP(SLOT,PIN)
<	XPINSP-<SLOT-1>*XDIPSP,,<PIN-9>*YPINSP
>

M24PIN:	RP(1,15)	;1
	RP(1,14)	;2
	RP(1,13)	;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,3)		;8
	LP(2,2)		;9
	LP(2,4)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,4)		;13
	RP(3,11)	;14
	RP(3,12)	;15
	LP(2,6)		;16
	LP(2,5)		;17
	RP(2,12)	;18
	RP(2,11)	;19
	RP(2,13)	;20
	LP(1,6)	;21
	LP(1,5)		;22
	RP(1,12)	;23
	RP(1,11)	;24
CHECK M24PIN,=24

M28PIN:	RP(1,15)	;1
	RP(1,14)	;2
	RP(1,13)	;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,3)		;8
	LP(2,2)		;9
	LP(2,4)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,3)		;13
	LP(3,2)		;14

	LP(3,6)		;15
	LP(3,5)		;16
	LP(3,4)		;17
	RP(3,11)	;18
	RP(3,12)	;19
	LP(2,6)		;20
	LP(2,5)		;21
	RP(2,12)	;22
	RP(2,11)	;23
	RP(2,13)	;24
	LP(1,6)		;25
	LP(1,5)		;26
	RP(1,12)	;27
	RP(1,11)	;28
CHECK M28PIN,=28

M40PIN:	RP(1,15)	;1
	RP(1,14)	;2
	LP(1,4)		;3
	LP(1,2)		;4
	LP(1,3)		;5
	RP(2,15)	;6
	RP(2,14)	;7
	LP(2,4)		;8
	LP(2,2)		;9
	LP(2,3)		;10
	RP(3,15)	;11
	RP(3,14)	;12
	LP(3,4)		;13
	LP(3,2)		;14
	LP(3,3)		;15
	RP(4,15)	;16
	RP(4,14)	;17
	LP(4,4)		;18
	LP(4,2)		;19
	LP(4,3)		;20

	LP(4,5)		;21
	LP(4,6)		;22
	RP(4,13)	;23
	RP(4,12)	;24
	RP(4,11)	;25
	LP(3,5)		;26
	LP(3,6)		;27
	RP(3,13)	;28
	RP(3,12)	;29
	RP(3,11)	;30
	LP(2,5)		;31
	LP(2,6)		;32
	RP(2,13)	;33
	RP(2,12)	;34
	RP(2,11)	;35
	LP(1,5)		;36
	LP(1,6)		;37
	RP(1,13)	;38
	RP(1,12)	;39
	RP(1,11)	;40
CHECK M40PIN,=40

RADIX 8
>;ITS
;HERE FOR CONNECTOR PINS

CONMAP:	AOS (P)
	LDB TT,%CNPG		;PANEL AND GROUP NUMBER
	JUMPE TT,CPOPJ		;ILLEGAL PANEL NO
	PUSH P,TT+1
	SOS TT
	IDIVI TT,NGRPS
	CAIL TT,MXPNL
	JRST [	POP P,TT+1
		POPJ P,]			;TOO BIG A PANEL NUMBER
	MOVEI T,MXPNL-1
	SUBM T,TT		;0 FOR PANEL <MXPNL>
	IMULI TT,PNLOFT		;OFFSET BETWEEN PANELS
	MOVE T,TT+1
	POP P,TT+1
	IMULI T,GXOFST
	HRLZ T,T	;XY OF GROUP
	ADD T,[JXOFST,,JYOFST]	;LOC OF J2
	ADD T,TT		;ADD IN PANEL OFFSET
	LDB TT,%CNJK
	SKIPG TT
	ADDI T,JYOFS1		;ADD OFFSET FOR JACK 1
	LDB TT,%CONP		;PIN #
	JUMPE TT,CPOPJ
	CAILE TT,=26		;REASONABLE # OF PINS
	POPJ P,
	ADD T,FLXPIN-1(TT)
	SETZB TT,TTT
	JRST CPOPJ1

;THE ROUTINES WHICH CALL MAPIT

PINBIT:	PUSHJ P,MAPIT
	JFCL			;DIP ERROR
	SETZ TT,		;CONNECTOR ERROR
	POPJ P,			;HAPPINESS

PINMAP:	PUSHJ P,MAPIT
	JRST PINERR
	JRST CONERR
	JRST CPOPJ1

CONERR:	LDB T,%CNPG		;PANEL NO.
	PUSH P,T+1
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS T+1
	EXCH T+1,(P)
	PUSHJ P,NUMPNT
	POP P,T		;CONNECTOR GROUP
	AOS T
	OUTCHR N2L(T)
	OUTCHR ["J"]
	PUSH P,TT
	LDB TT,%CNJK
	AOS TT
	PUSHJ P,NUMPNT
	OUTCHR ["-"]
	LDB T,%CONP
	PUSHJ P,NUMPNT		;PIN
CONER1:	OUTSTR [ASCIZ / MAPPING ERROR
/]
	POP P,TT
	POPJ P,
		
PINERR:	LDB T,%DIPPNL		;PANEL NO.
	PUSHJ P,NUMPNT
	LDB T,%DIPG		;FORM A#(#)
	OUTCHR N2L(T)
	LDB T,%DIPS		;SLOT #
	PUSHJ P,NUMPNT
	OUTCHR["("]
	LDB T,%DIPP		;PIN #
	PUSHJ P,NUMPNT
	OUTCHR [")"]
	JRST CONER1

MAPRC:	HLRZ TT,T		;(1,1) IS DIP IN UPPER LEFT HAND CORNER
	SOJL TT,CPOPJ
	CAML TT,NROWS
	POPJ P,
	HRRZS T			;ROW IN TT, COL IN T
	SOJL T,CPOPJ
	CAML T,NCOLS		;(0,0) IS NOW IN UPPER LEFT
	POPJ P,
	IDIVI TT,NGRPS*GRPCOL	;FUDGE RIGHT THING INTO TT FOR MPG21
	ADDI TT,1
	LSH TT,3
	PUSH P,TT
	MOVE TT,TTT
	IDIVI TT,GRPCOL
	ADDI TT,1
	IOR TT,(P)
	LSH TT,14
	POP P,(P)
	SUBI TTT,UMLROW-1	;TRANSLATE
	MOVNS TTT		;REFLECT
	IMULI T,UMLROW
	ADDI T,1(TTT)
	IOR T,TT		;OR IN GROUP
	JRST CPOPJ1

MAPPAD:	ADDI T,1		;T HAS CONNECTOR #, STARTING AT 1 FOR FIRST
	LSH T,14
	ADDI T,(TT)		;TT HAS CONNECTOR PIN, STARTING AT 1
	POPJ P,


FPWR:	TLZ A,PLUS1!PLUS2!77
	SKIPGE A
	PUSHJ P,FPGC
	TLO A,MAPPWR
	POPJ P,

FGND:	TLZ A,PLUS1!PLUS2!77
	SKIPGE A
	PUSHJ P,FPGC
	TLO A,MAPGND
	POPJ P,

FPGC:	LDB T,[POINT 5,A,22]
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS TT
	DPB T,[POINT 3,A,20]
	DPB TT,[POINT 3,A,23]
	LDB T,[POINT 12,A,35]
	SOS T
	IDIVI T,=13
	MOVE T,[	REPEAT 4,<2>
			REPEAT 5,<3>
			REPEAT 4,<4>](TT)
	DPB T,[POINT 12,A,35]
	HRRZ A,A
	POPJ P,


PGPRTM:	SETOM PMINUS#
	CAIA
PGPRNT:	SETZM PMINUS
	JUMPL A,PGCPNT		;JUMP FOR CONNECTOR
	LDB T,%DIPPNL		;PANEL NO.
	PUSHJ P,DECOUT
	LDB T,%DIPG		;LETTER PART OF SLOT LOCATION
	PUTBYT @N2L(T)
	MOVEI T,2		;TWO DIGITS FOR SLOT #
	MOVEM T,NDIG
	LDB T,%DIPS
	PUSHJ P,NDECOUT		;SLOT #
	PUTBYT "("
	TLNN A,MAPPWR!MAPGND
	JRST NPGPNT
	TLNN A,MAPPWR
	SKIPA T,["G"]
	MOVEI T,"V"
	PUTBYT @T
PGPN1:	PUTBYT ")"
	POPJ P,

NPGPNT:	LDB T,%DIPP		;PIN #
	SKIPN PMINUS
	JRST NPGPN1
	TLNE A,PLUS1
	JRST [	PUSHJ P,DECOUT
		PUTBYT "-"
		JRST PGPN1]
NPGPN1:	TLNE A,PLUS1
	SUBI T,1		;SUBTRACT 1 FOR 14 PIN DIP #
	PUSHJ P,DECOUT
	TLNE A,PLUS1
	PUTBYT "+"
	TLNE A,PLUS2
	PUTSTR [ASCIZ /+2??????????/]
	JRST PGPN1

;PRINTS CONNECTOR PIN

PGCPNT:	LDB T,%CNPG	;PANEL NO.AND GROUP
	PUSH P,T+1
	SOS T
	IDIVI T,NGRPS
	AOS T
	AOS T+1
	EXCH T+1,(P)
	PUSHJ P,DECOUT
	POP P,T
	AOS T
	PUTBYT @N2L(T)
	PUTBYT "J"
	LDB T,%CNJK
	PUSHJ P,DECOUT
	PUTBYT "-"
	LDB T,%CONP
	JRST DECOUT

RAYGEN:	JUMPL A,RAYRP1
	TLNN A,MAPPWR!MAPGND	;ONLY DO GENERATED POWER AND GROUNDS HERE
	JRST RAYRP3		;PALM OFF THE REST ON RAYDIP!
	LDB T,%DIPPNL
	PUSHJ P,DECOUT
	LDB T,%DIPG
	PUTBYT @N2L(T)
	MOVEI T,2
	MOVEM T,NDIG
	LDB T,%DIPS		;SLOT #
	PUSHJ P,NDECOUT
	PUTSTR [ASCIZ /  /]	;2 SPACES
	MOVEI T,2		;ASSUME NOT POWER OR GROUND
	MOVEM T,NDIG
	LDB T,%DIPP
	PUTBYT "0"
	MOVEI T,"V"
	TLNN A,MAPPWR
	MOVEI T,"G"
	PUTBYT (T)
	POPJ P,

RAYRP3:	SETZM T		;CLEAR BODY BITS (14 TO 16 PIN CONVERSION ALREADY DONE
					;IN PNAMEP (WLROUT) CROCK!!!)
	JRST RAYDIP

RAYRP1:	OUTSTR [ASCIZ /CONNECTOR BECAME GENERATED PIN
/]
	JRST ERRET

VGCON:	POPJ P,

FOR NAME (GNDCLR,WAGGND,GNDOUT,VCCOUT)
<NAME:	OUTSTR [ASCIZ /NAME NOT IMPLEMENTED FOR THE MPG21 BOARD
/]
	JRST ERRET
>
BEND WMPG21
>;MWL
>;MDWL
