VERSION(REP,1)
REPLST:	SKIPE B,CORLST
	PUSHJ P,PUTFS
	SETZM CORLST
	MOVEI G,CORLST	;POINT TO IT
	MOVE A,OLDTYP
MD,<	HLRZ A,1(A)	>
MPC,<	ADDI A,1	>
	JRST CORPIN

CORNXT:	GETFS(T)
	SETZM 1(T)
	HRRM T,(G)	;LINK NEW BLOCK
	MOVE G,T
	HRLZM A,(G)	;SAVE TYPE PIN POINTER
CORPIN:	HRRZ A,(A)
	JUMPN A,CORNXT
	POPJ P,

REPSET:	MOVEM A,OLDTYP
MD,<	MOVEI T,[ASCIZ/TYPE NEW BODY NAME
/]
>;MD
	PUSHJ P,BODYGT
	POPJ P,			;ALTMODE
	POPJ P,			;NULL
	JRST OOPS1		;NX
	MOVEM A,NEWTYP
REPST1:	PUSHJ P,REPLST		;INTIALIZE CORLST
MD,<	MOVE T,[ASCID/BR/]
	MOVEM T,SPMODT
	MOVEI T,SPM
	PUSHJ P,TCHNGM
	MOVEI T,UPREP
	MOVEM T,SPDISP
	PUSHJ P,PUSHM		;SET PIN IDS ON
	PUSH P,H
	PUSHJ P,REPAGN
	CAIA
	AOS -1(P)
	POP P,H
	PUSHJ P,POPM		;RESTORE STATE OF PIN IDS
	JRST RCHNGM		;RESTORE OLD MODE
>;MD
REPAGN:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/REPLACEMENT MODE (H FOR HELP)?/]
MD,<	SETZM KEEPIN		;CLEAR KEEP PINS FLAG
	SETZM COPPRP
>;MD
MAINTP:	PUSHJ P,GETCHR
	CAIL C,"A"+40
	CAILE C,"Z"+40
	CAIA
	SUBI C,40
	CAIE C,12
	TLNE M,DSKACT!MACACT
	CAIA
	OUTSTR[ASCIZ/
/]
	CAIE C,"H"
	CAIN C,"?"
	JRST [	TLNN M,DSKACT!MACACT
MD,<		OUTSTR[ASCIZ/CHAR	MODE
E	EXACT MATCH BY DEFAULT PIN NAME
N	CLOSEST MATCH BY DEFAULT PIN NAME
L	EXACT GEOMETRIC OVERLAY
C	CLOSEST GEOMETRIC OVERLAY
<CR>	ASK ABOUT EACH PIN
<ALT>	ABORT
M	MAINTAIN PIN NAMES (PRECEEDS OTHER COMMANDS)
P	COPY BODY TEXT AND PROPERTIES
/]
>;MD
MPC,<		OUTSTR[ASCIZ/CHAR	MODE
N	MATCH BY PIN #
L	EXACT GEOMETRIC OVERLAY
C	CLOSEST GEOMETRIC OVERLAY
<CR>	ASK ABOUT EACH PIN
<ALT>	ABORT
/]
>;MPC
		JRST MAINTP]
MD,<	CAIN C,"M"
	JRST [	SETOM KEEPIN
		TLNN M,DSKACT!MACACT
		OUTSTR[ASCIZ/MAINTAINING PIN NAMES./]
		JRST MAINTP]
	CAIN C,"P"
	JRST [	SETOM COPPRP
		TLNN M,DSKACT!MACACT
		OUTSTR[ASCIZ/COPYING PROPERTIES AND TEXT./]
		JRST MAINTP]
>;MD
	CAIN C,ALTMOD
	POPJ P,
MPC,<	CAIN C,"N"
	JRST SAME
>;MPC
MD,<	CAIN C,"N"
	JRST CNMAT
	CAIN C,"E"
	JRST ENMAT
>;MD
	CAIN C,"L"
	JRST REPLOC
	CAIN C,"C"
	JRST REPCLS		;MORE FORGIVING THAN L
	CAIE C,12
	JRST [	PUSHJ P,PERRET
		JRST REPAGN]
	MOVEI G,CORLST
	TLNN M,DSKACT!MACACT
MD,<	OUTSTR[ASCIZ/TYPE CORRESPONDENCE FOR PIN ID'S.
/]
>;MD
MPC,<	OUTSTR[ASCIZ/TYPE CORRESPONDENCE FOR PIN NAMES.
/]
>;MPC
	JRST GETIN

GETPIN:	TLNE M,DSKACT!MACACT
	JRST NOTALK
MD,<	OUTSTR[ASCIZ/OLD PIN ID /]	>
MPC,<	OUTSTR[ASCIZ/OLD PIN NAME /]	>
	HLRZ T,(G)
	HLRZ T,(T)
MD,<	HRRZ T,(T)
	PUSHJ P,DECOUT		;PRINT PIN ID
>;MD
MPC,<	MOVSI A,(T)
	PUSH P,PUTCHR
	MOVE T,[OUTCHR TTT]
	MOVEM T,PUTCHR
	PUSHJ P,PINPLS
	POP P,PUTCHR
>;MPC
	OUTSTR[ASCIZ/=/]
NOTALK:
MPC,<	PUSHJ P,RPNAM
	CAIA
>;MPC
MD,<	PUSHJ P,READN	>
	CAIE C,12
	JRST [	PUSHJ P,SCARF
		JRST REPST1
		JRST GETPIN]
	SETZ A,
	JUMPE T,NEWOK
	MOVEI A,CORLST
	JRST CHKNUM

CHKNXT:	HLRZ TT,1(A)
	CAMN TT,T
	JRST [	TLNN M,DSKACT!MACACT
		OUTSTR[ASCIZ/ALREADY USED!
/]
		JRST GETPIN]
CHKNUM:	HRRZ A,(A)
	JUMPN A,CHKNXT
	MOVE A,NEWTYP
MD,<	HLRZ A,1(A)	>
MPC,<	ADDI A,1	>
	JRST NEWCHK

NEWNXT:	HLRZ TT,(A)
MD,<	HRRZ TT,(TT)	>
	CAMN TT,T
	JRST NEWOK
NEWCHK:	HRRZ A,(A)
	JUMPN A,NEWNXT
	TLNN DSKACT!MACACT
	OUTSTR[ASCIZ/NO SUCH PIN IN NEW TYPE!
/]
	JRST GETPIN

NEWOK:	HRLM T,1(G)
	HRRM A,1(G)	;SAVE PIN ID AND POINTER TO BLOCK IN TYPE
GETIN:	MOVE H,G
	HRRZ G,(G)
	JUMPN G,GETPIN
SAMEDN:	MOVE G,NEWTYP
MD,<	HLRZ G,1(G)	>
MPC,<	ADDI G,1	>
	JRST REPNW1

REPNEW:	GETFS(T)
	HRRZM G,1(T)
	EXCH T,CORLST
	HRRZM T,@CORLST
REPNW1:	HRRZ G,(G)
	JUMPE G,CPOPJ1
	MOVEI A,CORLST
	HLRZ T,(G)
MD,<	HRRZ T,(T)	>
	JRST REPNW2

REPNW3:	HLRZ TT,1(A)
	CAMN TT,T
	JRST REPNW1
REPNW2:	HRRZ A,(A)
	JUMPN A,REPNW3
	JRST REPNEW

MPC,<
SAME:	SKIPN G,CORLST	;RUN DOWN CORLST
	JRST SAMEDN
SAME4:	MOVE A,NEWTYP
	ADDI A,1
	HLRZ T,(G)	;OLD PIN ID
	HLRZ T,(T)
	JRST SAME1

SAME2:	HLRZ TT,(A)
	CAME T,TT
	JRST SAME1
	HRRZM A,1(G)	;STUFF IN NEW PIN ID AND POINTER
	HRLM T,1(G)
SAME3:	HRRZ G,(G)
	JUMPE G,SAMEDN
	JRST SAME4

SAME1:	HRRZ A,(A)
	JUMPN A,SAME2
	JRST SAME3
>;MPC
;REPLACE BY GEOMETRIC OVERLAY

REPCL1:PUSHJ P,REPLST		;INITIALIZE CORLST
MD,<	SETOM KEEPIN		;MAKE SURE WE KEEP PINS>
REPLOC:				;OFFSET CRAP GOES HERE
	PUSHJ P,REPCL4
	JRST RPLERR
	JRST SAMEDN

RPLERR:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/
SORRY, NO EXACT GEOMETRIC OVERLAY POSSIBLE.
/]
	JRST REPST1

FCLONE:	SETZM CURPIN		;NO CLOSEST YET!
	HRLOI T,77777		;LARGE NUMBER
	MOVEM T,CURDIS		;CURRENT CLOSEST DISTANCE
REPL3:	SKIPE 1(G)		;HAVE WE FOUND THIS ONE YET?
	JRST REPL2		;YES, SKIP IT
	HLRZ T,(G)		;POINTER TO OLD PIN TYPE BLOCK
	MOVE T,1(T)		;OLD PIN LOC
	ADJUST(SUB,T,<1(A)>)
	HLRE TT,T
	HRRES T
	IMUL T,T
	IMUL TT,TT
	ADD T,TT		;SQUARE OF DISTANCE
	CAML T,CURDIS		;SMALLER?
	JRST REPL2		;NO
	MOVEM T,CURDIS		;UPDATE
	MOVEM G,CURPIN
REPL2:	HRRZ G,(G)		;ANOTHER MEMBER OF CORLST
	JUMPN G,REPL3
	POPJ P,
;GEOMETIC OVERLAY FOR DIFF NUMBER OF PINS
%MAX__20	;MAX SEPARATION BETWEEN PINS IN ORDER TO MATCH!

REPCLS:
				;OFFSET STUFF GOES HERE
	PUSHJ P,REPCL4
	JFCL			;THIS VERSION DOESN'T CARE
	JRST SAMEDN

REPCL4:	SKIPN CORLST
	POPJ P,
	MOVE A,NEWTYP
MD,<	HLRZ A,1(A)
	HRRZ A,(A)
>;MD
MPC,<	HRRZ A,1(A)	>
	JUMPE A,CPOPJ
	HRLOI B,77777
	SETZ H,			;NONE FOUND YET
REPCL2:
MPC,<	HLL A,(A)	>	;GET PIN ID ALSO
MD,<	HLRZ T,(A)
	HRL A,(T)		;PIN ID ALSO
>;MD
	TRZ TFLG
	MOVE G,CORLST		;GET CORLST
REPCL5:	CAMN A,1(G)		;IS IT MAPPED YET?
	JRST REPCL3		;YES, TRY ANOTHER
	HRRZ G,(G)
	JUMPN G,REPCL5
	TRO TFLG		;FLAG UN-MAPPED NEW PIN SEEN
	MOVE G,CORLST		;NOT USED YET, CHECK DISTANCE
	PUSHJ P,FCLONE		;FIND ONE
	SKIPN CURPIN		;DID WE FIND ONE
	POPJ P,			;NO, RAN OUT OF CORLST I GUESS
	CAMG B,CURDIS		;IS IT CLOSEST YET?
	JRST REPCL3		;NO
	MOVE B,CURDIS
	MOVE H,CURPIN		;SAVE CLOSEST YET
	HRL H,A			;SAVE BOTH POINTERS
REPCL3:	HRRZ A,(A)
	JUMPN A,REPCL2		;LOOP THROUGH ALL
	JUMPE H,REPCL6
	CAILE B,%MAX*%MAX	;TO FAR APART?
	POPJ P,
	HLRZ A,H		;GET POINTER TO NEW PIN
MPC,<	HLL A,(A)	>	;AND PIN ID
MD,<	HLRZ T,(A)
	HRL A,(T)		;PIN ID
>;MD
	MOVEM A,1(H)		;AND SAVE HERE
	JRST REPCL4		;LOOP UNTIL ALL ASSIGNED

REPCL6:	TRNE TFLG		;ALL NEW PINS MAPPED?
	POPJ P,			;NO, LOSE RETURN
	MOVE H,CORLST
REPCL7:	SKIPN 1(H)		;THIS OLD PIN MAPPED?
	POPJ P,			;NO, LOSE
	HRRZ H,(H)
	JUMPN H,REPCL7
	JRST CPOPJ1		;ALL OLD PINS MAPPED, WIN
;MAP BY DEFAULT PIN NAMES
MD,<
CNMAT:	PUSHJ P,DONMAT
	JFCL
	JRST SAMEDN

ENMAT:	PUSHJ P,DONMAT
	JRST ENERR
	JRST SAMEDN

ENERR:	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/SORRY, NO EXACT PIN MATCH POSSIBLE.
/]
	JRST REPST1

DONMAT:	SKIPN CORLST
	POPJ P,
	TRZ TFLG!ATLP
	MOVE A,NEWTYP
	HLRZ A,1(A)
	HRRZ A,(A)
	JUMPE A,CPOPJ
	MOVSI TT,DPTMP1!DPTMP2
DONMT0:	HLRZ T,(A)
	ANDCAM TT,(T)
	HRRZ A,(A)
	JUMPN A,DONMT0
	MOVE A,NEWTYP
	HLRZ A,1(A)
	HRRZ A,(A)
DONMT1:	SETZM CURPIN
	HLRZ TT,(A)
	HLRZ T,(TT)
	TRNE T,DPTMP1
	JRST DONMT6
	ANDI T,BASSLH
	HRL T,1(TT)
	HRRZ B,(A)
	JUMPE B,DONMT3
	TRZ ATFP
DONMT2:	HLRZ TT,(B)
	HLRZ TTT,(TT)
	ANDI TTT,BASSLH
	HRL TTT,1(TT)
	CAMN TTT,T
	JRST [	TRO ATFP
		MOVSI TTT,DPTMP1!DPTMP2
		IORM TTT,(TT)
		JRST .+1]
	HRRZ B,(B)
	JUMPN B,DONMT2
	TRNE ATFP
	JRST DONMTE
DONMT3:	MOVE G,CORLST
DONMT4:	HLRZ TT,(G)
	HLRZ TT,(TT)
	HLRZ TTT,(TT)
	ANDI TTT,BASSLH
	HRL TTT,1(TT)
	CAME TTT,T
	JRST DONMT5
	SKIPE CURPIN
	JRST DONMTE
	MOVEM G,CURPIN
DONMT5:	HRRZ G,(G)
	JUMPN G,DONMT4
	SKIPN G,CURPIN
	JRST [	TRO ATLP
		JRST DONMT6]
	HLRZ T,(A)
	HRL A,(T)
	MOVEM A,1(G)
	JRST DONMTW

DONMTE:	TRO TFLG
	PUSHJ P,DONMRK
DONMTW:	HLRZ T,(A)
	MOVSI TT,DPTMP1
	IORM TT,(T)
DONMT6:	HRRZ A,(A)
	JUMPN A,DONMT1
	TRNN ATLP		;ANY UNMAPPED NEW PINS?
	JRST REPCL6		;NO, CHECK OLD PINS
	MOVE A,NEWTYP
	HLRZ A,1(A)
	HRRZ A,(A)
DONMA1:	SETZM CURPIN
	HLRZ TT,(A)
	HLRZ T,(TT)
	TRNE T,DPTMP2!DPTMP1
	JRST DONMA6
NODEC,<	HRRZ T,1(TT)	>
DEC,<	HLRZ T,(TT)
	ANDI T,BUSSED
	HRL T,1(TT)
>;DEC
	HRRZ B,(A)
	JUMPE B,DONMA3
	TRZ ATFP
DONMA2:	HLRZ TT,(B)
NODEC,<	HRRZ TTT,1(TT)	>
DEC,<	HLRZ TTT,(TT)
	ANDI TTT,BUSSED
	HRL TTT,1(TT)
>;DEC
	CAMN TTT,T
	JRST [	HLRZ TTT,(TT)
		TRNE TTT,DPTMP1
		JRST .+1
		TRO ATFP
		MOVSI TTT,DPTMP2
		IORM TTT,(TT)
		JRST .+1]
	HRRZ B,(B)
	JUMPN B,DONMA2
	TRNE ATFP
	JRST DONMAE
DONMA3:	MOVE G,CORLST
DONMA4:	SKIPE 1(G)
	JRST DONMA5
	HLRZ TT,(G)
	HLRZ TT,(TT)
NODEC,<	HRRZ TTT,1(TT)	>
DEC,<	HLRZ TTT,(TT)
	ANDI TTT,BUSSED
	HRL TTT,1(TT)
>;DEC
	CAME TTT,T
	JRST DONMA5
	SKIPE CURPIN
	JRST DONMA6
	MOVEM G,CURPIN
DONMA5:	HRRZ G,(G)
	JUMPN G,DONMA4
	SKIPN G,CURPIN
	JRST DONMAF
	HLRZ T,(A)
	HRL A,(T)
	MOVEM A,1(G)
	JRST DONMA6

DONMAE:	PUSHJ P,DONMRK
DONMAF:	TRO TFLG
DONMA6:	HRRZ A,(A)
	JUMPN A,DONMA1
	JRST REPCL6

DONMRK:	MOVE B,NEWTYP
	HLRZ B,1(B)
	HRRZ B,(B)
	MOVSI TT,DPTMP2
	HLRZ T,(A)
	HRRZ T,1(T)
	MOVEM T,CURPIN
DONMR1:	HLRZ TTT,(B)
	HRRZ T,1(TTT)
	CAMN T,CURPIN
	IORM TT,(TTT)
	HRRZ B,(B)
	JUMPN B,DONMR1
	POPJ P,
>;MD
REPONE:	PUSHJ P,GETCLS
	POPJ P,
	PUSH P,A
	HLRZ A,(A)
	HRRZ A,1(A)
	PUSHJ P,REPSET		;SET UP CORRESPONDENCE
	JRST [	POP P,(P)
		JRST CLRREP]
	POP P,G
	PUSHJ P,REPIT
	TRO MCHG
	JRST CLRREP
IFN 0,<	PUSHJ P,CLRREP
MD,<	JRST STRAIGHTEN	>
MPC,<	POPJ P,		>
>;IFN 0

REPALL:
MD,<	MOVEI T,[ASCIZ/TYPE OLD BODY NAME
/]
>;MD
	PUSHJ P,BODYGT
	POPJ P,			;ALTMODE
	POPJ P,			;NULL
	JRST OOPS1		;NX
	PUSHJ P,REPSET
	JRST CLRREP
REPLOP:MOVEI G,DBODPN
	JRST REPLP1
REPLP2:	HLRZ A,(G)
	HRRZ B,1(A)
	CAMN B,OLDTYP	;DOES THIS BODY HAVE THE RIGHT TYPE?
	PUSHJ P,REPIT
REPLP1:	HRRZ G,(G)
	JUMPN G,REPLP2
	TRO MCHG
	JRST CLRREP
IFN 0,<	PUSHJ P,CLRREP
MD,<	JRST STRAIGHTEN	>
MPC,<	POPJ P,		>
>;IFN 0

REPSOME:
MD,<	MOVEI T,[ASCIZ/TYPE OLD BODY NAME
/]
>;MD
	PUSHJ P,BODYGT
	POPJ P,			;ALTMODE
	POPJ P,			;NULL
	JRST OOPS1		;NX
	PUSHJ P,REPSET
	JRST CLRREP		;LET HIM OUT
	MOVEI T,UPSTAR		;WHERE TO GO IN UPCLOS
	MOVEM T,SPDISP
	MOVE T,[ASCID/BR/]
	MOVEM T,SPMODT
	MOVEI T,SPM
	PUSHJ P,TCHNGM		;GET INTO SPECIAL POINTER MODE
	MOVEI G,DBODPN
	MOVEM G,CURREP
REPSPC:	MOVE G,CURREP
	HRRZ G,(G)
	JUMPE G,REPALT
	MOVEM G,CURREP
	HLRZ A,(G)
	HRRZ A,1(A)
	CAME A,OLDTYP
	JRST REPSPC		;KEEP LOOKING
	MOVEI T,BIGPG
	MOVEM T,PGLASS
	PUSHJ P,DPYSET
	PUSHJ P,SETBRT
	MOVE T,1(G)		;LOC OF BOD.
	TLNE M,DSKACT!MACACT
	JRST ISON		;NO, ALL BODIES ARE ON
	PUSHJ P,ONSCR		;IS IT ON SCREEN?
	PUSHJ P,PICSET		;NO, GET IT ON
ISON:	MOVEM T,STARLOC
	TLNN M,DSKACT!MACACT
	OUTSTR[ASCIZ/
THIS ONE/]
	PUSHJ P,YORN
	JRST REPALT
	JRST REPSPC
	MOVE G,CURREP
	PUSHJ P,REPIT
	TRO MCHG
IFN 0,<MD,<	PUSHJ P,STRAIGHTEN	>>
	JRST REPSPC

REPALT:	PUSHJ P,RCHNGM		;RESTORE OLD MODE
CLRREP:	SKIPE B,CORLST
	PUSHJ P,PUTFS
	SETZM CORLST
	POPJ P,
REPIT:	PUSH P,G
	HLRZ G,(G)
	MOVE T,NEWTYP
	HRRM T,1(G)		;NEW TYPE POINTER FOR BODY
MD,<	CAMN T,OLDTYP		;DON'T COPY PROPERTIES IF SAME BODY
	JRST REPITP
	MOVE T,(P)
	PUSHJ P,OFFBLO		;FIX CHAR OFFSET IF BIT ON
	HLRZ A,1(G)
	MOVE T,1(A)
	TLNN T,FIXLOC
	JRST NFXBLC
	HRRZ B,(A)
	JUMPE B,NFXBLC		;JUST IN CASE
	HLRZ F,(G)
	HRRZ TT,1(G)
	PUSHJ P,STLCOF
NFXBLC:	HLRZ C,1(G)
	SKIPN COPPRP
	JRST [	PUSHJ P,PRPRET
		JRST REPITP]
	HRRZ D,1(C)		;GET OLD PROPERTY LIST
	JUMPE D,REPITP		;NOTHING TO DO IF NO LOCAL PROPS
	HLLZS 1(C)		;CLEAR POINTER TO IT
	MOVE A,(P)		;GET BODY POINTER
	PUSHJ P,COPLTP		;COPY BODY DEF TEXT/PROPERTIES INTO INDIRECT LIST
	HLRZ A,(A)
	HLRZ A,1(A)		;POINT TO TEXT/PROPERTY LIST HEADER BLOCK
PRPCP1:	MOVE C,D
	HRRZ D,(D)
	HLRZ T,(C)
	JUMPE T,[FSTRET(C)	;JUST INDIRECT POINTER, DELETE
		JRST PRPCP2]
	HLRZ T,(T)		;GET PROPERTY NAME
	JUMPE T,PRPCP3		;IF NONE, COPY TEXT
	HRRZ TTT,1(A)
	SKIPE TTT		;NOT FOUND IF NO PROPS IN NEW BODY
	PUSHJ P,FPROPX		;LOOK FOR THIS PROPERTY IN BODY DEF
	JRST PRPCP3		;NOT FOUND, INSERT NEW PROPERTY
	HLRZ TTT,(T)
	JUMPN TTT,PRPCP4	;BUG CHECK FOR DUPLICATE PROPERTY ON BODY
	HLRZ TTT,(C)		;GET POINTER TO REST OF NEW PROPERTY
	HRLM TTT,(T)		;STORE INTO INDIRECT BLOCK
	MOVE TTT,1(C)		;GET X,Y
	MOVEM TTT,1(T)		;STORE IT ALSO
	FSTRET(C)		;GIVE BACK OLD BLOCK
	JRST PRPCP2

PRPCP3:	HRRZ TTT,1(A)		;LINK THIS PROPERTY
	HRRM TTT,(C)		;INTO BODY LIST
	HRRM C,1(A)
	JRST PRPCP2

PRPCP4:	MOVSS T,(C)
	HLRZ B,(T)
	HLRZ B,(B)		;CHECK FOR DIP DEF LIST
	SKIPE B
	PUSHJ P,PUTFS
	HRRZ B,(C)
	HLRZ B,(B)
	PUSHJ P,PUTFS		;RETURN PROPERTY NAME
	MOVE B,C
	PUSHJ P,PUTFS		;AND THE REST
PRPCP2:	JUMPN D,PRPCP1		;LOOP IF MORE
REPITP:
>;MD
MPC,<	SWITCH
	PUSHJ P,REPIT1		;DO BACK SIDE FIRST
	HRRZ G,(P)
	HLRZ G,(G)
	SWITCH
>;MPC
	PUSHJ P,REPIT1		;DO IT
	HRRZ A,(P)		;BODY POINTER
	PUSHJ P,BODFIX		;FIX ALL THE PINS ON IT!
	POP P,G			;RESTORE BODY POINTER
	POPJ P,

REPIT2:	HRRZ T,1(A)
	JUMPE T,REPDEL
	HRRM T,(B)	;NEW TYPE PIN POINTER
MD,<	SKIPN KEEPIN	;KEEPING PIN #'S?
	HLLZS 1(B)	;NO, FLUSH THEM
>;MD
	MOVE B,-1(P)
	HLRZ F,(B)
	HLRZ F,(F)
	MOVE T,1(T)	;X,Y FROM PIN TYPE BLOCK
	PUSHJ P,ORIENT	;ORIENT IT
	ADJUST(ADD,T,<1(B)>)
	MOVE A,SAVEG
MD,<	PUSH P,G
	PUSHJ P,PMOVRL	;MOVE TO NEW POS!
	POP P,G
>;MD
REPIT1:	MOVE H,G
	HRRZ G,(G)
REPDL4:	JUMPE G,REPMAK
	MOVEM G,SAVEG
	HLRZ G,(G)
	HRRZ B,1(G)
MPC,<	MOVE TT,1(B)		;BITS
	EQV TT,SID
	JUMPGE TT,REPIT1	;SAME SIDE?
>;MPC
	HRRZ T,(B)
	MOVEI A,CORLST
	JRST REPIT3

REPIT4:	HLRZ TT,(A)
	CAMN TT,T		;SAME TYPE POINTER?
	JRST REPIT2
REPIT3:	HRRZ A,(A)
	JUMPN A,REPIT4
	OUTSTR[ASCIZ/POINT NOT FOUND AT REPIT4!!
/]
	PUSHJ P,FUCKUP
	JRST REPIT1		;THIS SHOULDN'T HAVE HAPPENED

REPMAK:	MOVEI G,CORLST
	JRST REPMK1

REPMK2:	HLRZ A,(G)
	JUMPN A,REPMK1		;NOT A NEW PIN?
	HRRZ A,1(G)		;PIN ID & X,Y BLOCK
	MOVE B,-1(P)		;BODY POINTER
	HRLI B,ISPIN		;PIN
	HLRZ TT,(B)
	HLLZ TT,(TT)		;ORI AND X,Y BLOCK
	PUSHJ P,PUTPNT		;MAKE IT
MPC,<	HLRZ TT,(A)		;PIN #
	CAIE TT,1		;FOR PIN 1
	JRST XPIN1
	AOS 1(T)
	AOS 1(T)
XPIN1:	AOS 1(T)
>;MPC
	HRRM D,(H)		;LINK INTO PIN LIST
	MOVE H,TTT
REPMK1:	HRRZ G,(G)
	JUMPN G,REPMK2
	POPJ P,

REPDEL:	HRRZ G,(G)
	HRRM G,(H)	;LINK THIS PIN OUT
	TRO TFLG	;DELETE PINS
	PUSH P,H
	PUSH P,G
	MOVE B,SAVEG
	PUSHJ P,DELPNL
	POP P,G
	POP P,H
	JRST REPDL4
